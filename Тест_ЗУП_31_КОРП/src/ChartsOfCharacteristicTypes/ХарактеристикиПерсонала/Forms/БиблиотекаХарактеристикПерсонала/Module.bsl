
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗакрыватьПриВыборе = Ложь;
	ЗаполнитьТаблицуХарактеристик();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыХарактеристики

&НаКлиенте
Процедура ХарактеристикиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыборВСпискеХарактеристик(СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ОбработатьВыборВСпискеХарактеристик();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуХарактеристик()
	
	БиблиотекаXML = ПланыВидовХарактеристик.ХарактеристикиПерсонала.ПолучитьМакет("Библиотека").ПолучитьТекст();
	
	БиблиотекаТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(БиблиотекаXML).Данные;
	
	Для Каждого ТекущаяСтрока Из БиблиотекаТаблица Цикл
		НоваяСтрока = ЗначенияХарактеристик.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
	КонецЦикла;
	
	БиблиотекаТаблица.Свернуть("Наименование, ВидХарактеристики");
	Для Каждого ТекущаяСтрока Из БиблиотекаТаблица Цикл
		НоваяСтрока = Характеристики.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		НоваяСтрока.ВидХарактеристики = Перечисления.ВидыХарактеристикПерсонала[ТекущаяСтрока.ВидХарактеристики];
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьВыбранныеСтроки(Знач ВыбранныеСтроки, ТекущаяСсылка)
	
	Для каждого НомерСтроки Из ВыбранныеСтроки Цикл
		ТекущиеДанные = Характеристики[НомерСтроки];
		
		СтрокаВБазе = ПланыВидовХарактеристик.ХарактеристикиПерсонала.НайтиПоНаименованию(ТекущиеДанные.Наименование, Истина);
		Если ЗначениеЗаполнено(СтрокаВБазе) Тогда
			Если НомерСтроки = Элементы.Характеристики.ТекущаяСтрока Или ТекущаяСсылка = Неопределено Тогда
				ТекущаяСсылка = СтрокаВБазе;
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ПланыВидовХарактеристик.ХарактеристикиПерсонала.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущиеДанные);
		НоваяСтрока.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.ЗначенияХарактеристикПерсонала");
		НоваяСтрока.Записать();
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Наименование", ТекущиеДанные.Наименование);
		СтрокиЗначений = ЗначенияХарактеристик.НайтиСтроки(СтруктураПоиска);
		Для Каждого ТекущаяСтрока Из СтрокиЗначений Цикл
			Если Не ЗначениеЗаполнено(ТекущаяСтрока.НаименованиеЗначенияХарактеристики) Тогда
				Продолжить;
			КонецЕсли;
			
			НовоеЗначение = Справочники.ЗначенияХарактеристикПерсонала.СоздатьЭлемент();
			НовоеЗначение.Владелец = НоваяСтрока.Ссылка;
			НовоеЗначение.Наименование = ТекущаяСтрока.НаименованиеЗначенияХарактеристики;
			НовоеЗначение.РеквизитДопУпорядочивания = ТекущаяСтрока.РеквизитДопУпорядочивания;
			НовоеЗначение.Записать();
		КонецЦикла;
		
		Если НомерСтроки = Элементы.Характеристики.ТекущаяСтрока Или ТекущаяСсылка = Неопределено Тогда
			ТекущаяСсылка = НоваяСтрока.Ссылка;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборВСпискеХарактеристик(СтандартнаяОбработка = Неопределено)
	
	СтандартнаяОбработка = Ложь;
	ТекущаяСсылка = Неопределено;
	СохранитьВыбранныеСтроки(Элементы.Характеристики.ВыделенныеСтроки, ТекущаяСсылка);
	ПоказатьОповещениеПользователя(НСтр("ru = 'Добавлены характеристики'"));
	ОповеститьОВыборе(ТекущаяСсылка);
	Закрыть();
	
КонецПроцедуры

#КонецОбласти