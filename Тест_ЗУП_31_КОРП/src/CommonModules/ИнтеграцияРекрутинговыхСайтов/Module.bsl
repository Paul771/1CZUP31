
#Область СлужебныйПрограммныйИнтерфейс

// См. описание этой же процедуры в РаботаВБезопасномРежимеПереопределяемый
//  из СтандартныеПодсистемы.БазоваяФункциональность.
//
// Параметры:
//	ЗапросыРазрешений - заполняемые разрешения.
//
Процедура ПриЗаполненииРазрешенийНаДоступКВнешнимРесурсам(ЗапросыРазрешений) Экспорт
	
	Разрешения = Новый Массив;
	
	Для Каждого КлючИЗначение Из АдресаРекрутинговыхСайтов() Цикл
		
		Адрес = КлючИЗначение.Значение;
		Описание = СтрШаблон(НСтр("ru = 'Веб-сервис доступа к вакансиям и кандидатам ""%1.""'"), КлючИЗначение.Ключ);
		
		Разрешения.Добавить(
			РаботаВБезопасномРежиме.РазрешениеНаИспользованиеИнтернетРесурса("HTTPS", Адрес, , Описание));
		
	КонецЦикла;
	
	ЗапросыРазрешений.Добавить(РаботаВБезопасномРежиме.ЗапросНаИспользованиеВнешнихРесурсов(Разрешения));
	
КонецПроцедуры

// Формирует подменю для публикации вакансий.
//
// Параметры:
//  Форма - УправляемаяФорма - форма вакансии.
//
Процедура ВакансииПриПолученииДанныхНаСервере(Форма) Экспорт
	
	Если Не ПравоДоступа("Чтение", Метаданные.РегистрыСведений.НастройкиИнтеграцииРекрутинговыхСайтов) Тогда
		Возврат;
	КонецЕсли;
	
	МассивАктуальныхСайтов = ИспользуемыеСайты();
	
	Если МассивАктуальныхСайтов = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если МассивАктуальныхСайтов.Найти(ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter()) <> Неопределено 
		И ТекущаяДатаСеанса() < ДатаЗакрытияRabotaMail() Тогда
		МассивАктуальныхСайтов.Добавить(ИнтеграцияРекрутинговыхСайтовКлиентСервер.RabotaMail());
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Форма.СоответствиеКомандИСайтов) Тогда
		СоответствиеКомандИСайтовПромежуточное = ИнтеграцияРекрутинговыхСайтовКлиентСервер.СоответствиеПоФиксированномуСоответствию(Форма.СоответствиеКомандИСайтов);
	Иначе
		СоответствиеКомандИСайтовПромежуточное = Новый Соответствие;
	КонецЕсли;
	
	Для Каждого Сайт Из МассивАктуальныхСайтов Цикл
		
		ЕстьМенюДляСайта = Ложь;
		Если ЗначениеЗаполнено(Форма.СоответствиеКомандИСайтов) Тогда
			Для Каждого ЭлементСоответствия Из Форма.СоответствиеКомандИСайтов Цикл
				Если ЭлементСоответствия.Значение = Сайт Тогда
					ЕстьМенюДляСайта = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если Не ЕстьМенюДляСайта Тогда
			СоздатьКомандуПубликацииНаСайте(Форма, Сайт, СоответствиеКомандИСайтовПромежуточное);
		КонецЕсли;
		
	КонецЦикла;
	
	Форма.СоответствиеКомандИСайтов = Новый ФиксированноеСоответствие(СоответствиеКомандИСайтовПромежуточное);
	
КонецПроцедуры

// Формирует подменю для загрузки вакансий.
//
// Параметры:
//  Форма - УправляемаяФорма - форма списка вакансии.
//
Процедура ВакансииСписокПриСозданииНаСервере(Форма) Экспорт
	
	Если Не ПравоДоступа("Чтение", Метаданные.РегистрыСведений.НастройкиИнтеграцииРекрутинговыхСайтов) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не КонстантаИспользоватьИнтеграциюРекрутинговыхСайтов() Тогда
		Возврат;
	КонецЕсли;
	
	МассивАктуальныхСайтов = ИспользуемыеСайты();
	
	Если МассивАктуальныхСайтов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Форма.СоответствиеКомандИСайтов) Тогда
		СоответствиеКомандИСайтовПромежуточное = ИнтеграцияРекрутинговыхСайтовКлиентСервер.СоответствиеПоФиксированномуСоответствию(Форма.СоответствиеКомандИСайтов);
	Иначе
		СоответствиеКомандИСайтовПромежуточное = Новый Соответствие;
	КонецЕсли;
	
	Для Каждого Сайт Из МассивАктуальныхСайтов Цикл
		
		ЕстьМенюДляСайта = Ложь;
		Если ЗначениеЗаполнено(Форма.СоответствиеКомандИСайтов) Тогда
			Для Каждого ЭлементСоответствия Из Форма.СоответствиеКомандИСайтов Цикл
				Если ЭлементСоответствия.Значение = Сайт Тогда
					ЕстьМенюДляСайта = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если Не ЕстьМенюДляСайта Тогда
			СоздатьКомандуЗагрузкиССайта(Форма, Сайт, СоответствиеКомандИСайтовПромежуточное);
		КонецЕсли;
		
	КонецЦикла;
	
	Форма.СоответствиеКомандИСайтов = Новый ФиксированноеСоответствие(СоответствиеКомандИСайтовПромежуточное);
	
КонецПроцедуры

// Записывает данные в РС ДанныеПубликацииРезюме.
// 
// Параметры:
//  МестоПубликации - СправочникСсылка.ИсточникиИнформацииОКандидатах.
//  ДанныеПубликацииРезюмеНаборЗаписей - РегистрСведенийНаборЗаписей.ДанныеПубликацииРезюме - Набор записей регистра.
//  СтруктураРезюме - Структура - Структура резюме, полученная с сайта. Состав структуры отличается в зависимости от источника.
//  Кандидат - СправочникСсылка.Кандидаты - Кандидат, чье резюме сохраняется в регистре.
//
Процедура ЗаполнитьДанныеПубликацииРезюмеНаборЗаписей(МестоПубликации, ДанныеПубликацииРезюмеНаборЗаписей, СтруктураРезюме, Кандидат) Экспорт
	
	ДанныеПубликацииРезюмеНаборЗаписей.Очистить();
	
	Для Каждого КлючИЗначение Из СтруктураРезюме Цикл
		Если КлючИЗначение.Значение = Неопределено 
			Или ТипЗнч(КлючИЗначение.Значение) = Тип("Массив") 
			Или ТипЗнч(КлючИЗначение.Значение) = Тип("Картинка") Тогда
			Продолжить;
		Иначе
			НоваяЗапись = ДанныеПубликацииРезюмеНаборЗаписей.Добавить();
			НоваяЗапись.Кандидат = Кандидат;
			НоваяЗапись.МестоПубликации = МестоПубликации;
			НоваяЗапись.ПолеДанных = КлючИЗначение.Ключ;
			Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(КлючИЗначение.Значение)) Тогда
				НоваяЗапись.Значение = Строка(КлючИЗначение.Значение);
			Иначе
				НоваяЗапись.Значение = ЗаписьJSON(КлючИЗначение.Значение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает структуру данных кандидата по структуре резюме (для заполнения карточки кандидата).
// 
// Параметры:
//  Сайт - СправочникСсылка.ИсточникиИнформацииОКандидатах - Сайт, с которого была произведена загрузка резюме.
//  СтруктураРезюме - Структура - Структура резюме, полученная с сайта. Состав структуры отличается в зависимости от источника.
//
// Возвращаемое значение:
//  Структура (см. описание в функции Справочники.Кандидаты.СтруктураДанныхКандидата()).
//
Функция СтруктураДанныхКандидата(Сайт, СтруктураРезюме) Экспорт
	
	Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
		Возврат СтруктураДанныхКандидатаHeadHunter(СтруктураРезюме);
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
		Возврат СтруктураДанныхКандидатаRabota(СтруктураРезюме);
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
		Возврат СтруктураДанныхКандидатаSuperJob(СтруктураРезюме);
	ИначеЕсли Сайт = ПредопределенноеЗначение("Справочник.ИсточникиИнформацииОКандидатах.ПустаяСсылка") Тогда
		
		СтруктураДанныхКандидата = Справочники.Кандидаты.СтруктураДанныхКандидата();
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(СтруктураДанныхКандидата, СтруктураРезюме, Истина);
		СтруктураДанныхКандидата.Вставить("Ответственный", Пользователи.ТекущийПользователь());
		СтруктураДанныхКандидата.Вставить("ДатаРегистрации", ТекущаяДатаСеанса());
		СтруктураДанныхКандидата.Вставить("ДатаОткрытия", ТекущаяДатаСеанса());
		
		Возврат СтруктураДанныхКандидата;
		
	КонецЕсли;
	
КонецФункции

// Устанавливает функциональную опцию ИспользоватьИнтеграциюРекрутинговыхСайтовДляПользователя.
//
Процедура УстановитьФОИспользуетсяВзаимодействиеСРекрутинговымиСайтами() Экспорт
	
	ИнтеграцияРекрутинговыхСайтовВнутренний.УстановитьФОИспользуетсяВзаимодействиеСРекрутинговымиСайтами();
	
КонецПроцедуры

Процедура УстановитьВидимостьЭлементаФормы(Элемент) Экспорт
	
	Элемент.Видимость = ЕстьДоступКРекрутинговымСайтам();
	
КонецПроцедуры

Функция ПоляВакансииHeadHunter(ПервичнаяПубликация = Истина) Экспорт
	
	ТипЗначенияJSONСтрока = ТипЗначенияJSONСтрока();
	ТипЗначенияJSONНачалоМассива = ТипЗначенияJSONНачалоМассива();
	ТипЗначенияJSONНачалоОбъекта = ТипЗначенияJSONНачалоОбъекта();
	ТипЗначенияJSONЧисло = ТипЗначенияJSONЧисло();
	ТипЗначенияJSONБулево = ТипЗначенияJSONБулево();

	МассивПолей = Новый Массив;
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "name";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'Название'");
	СтруктураПоля.Обязательное = Истина;
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "description";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'Описание (не менее 200 символов)'");
	СтруктураПоля.Обязательное = Истина;
	МассивПолей.Добавить(СтруктураПоля);
	
	// Ключевые навыки
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "key_skills";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоМассива;
	СтруктураПоля.Описание = НСтр("ru = 'Список ключевых навыков (не более 30)'");
	
	ПодчиненныеЭлементы = Новый Массив;
	СтруктураПоля_key_skills = СтруктураПоля();
	СтруктураПоля_key_skills.Путь = "name";
	СтруктураПоля_key_skills.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля_key_skills.Описание = НСтр("ru = 'Название ключевого навыка'");
	ПодчиненныеЭлементы.Добавить(СтруктураПоля_key_skills);
	СтруктураПоля.ПодчиненныеЭлементы = ПодчиненныеЭлементы;
	МассивПолей.Добавить(СтруктураПоля);
	
	// Специализации
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "specializations";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоМассива;
	СтруктураПоля.Описание = НСтр("ru = 'Список специализаций (не более 6)'");
	СтруктураПоля.Обязательное = Истина;
	
	ПодчиненныеЭлементы = Новый Массив;
	СтруктураПоля_specializations = СтруктураПоля();
	СтруктураПоля_specializations.Путь = "id";
	СтруктураПоля_specializations.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля_specializations.Описание = НСтр("ru = 'Специализация (не более 6)'");
	СтруктураПоля_specializations.Обязательное = Истина;
	ПодчиненныеЭлементы.Добавить(СтруктураПоля_specializations);
	СтруктураПоля.ПодчиненныеЭлементы = ПодчиненныеЭлементы;
	МассивПолей.Добавить(СтруктураПоля);
	
	// Регион публикации
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "area";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Регион публикации'");
	Если Не ПервичнаяПубликация Тогда
		СтруктураПоля.Использование = Ложь;
	КонецЕсли;
	СтруктураПоля.Обязательное = Истина;
	
	ПодчиненныеЭлементы = Новый Массив;
	СтруктураПоля_area = СтруктураПоля();
	СтруктураПоля_area.Путь = "id";
	СтруктураПоля_area.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля_area.Описание = НСтр("ru = 'Регион публикации'");
	СтруктураПоля_area.Обязательное = Истина;
	ПодчиненныеЭлементы.Добавить(СтруктураПоля_area);
	СтруктураПоля.ПодчиненныеЭлементы = ПодчиненныеЭлементы;
	МассивПолей.Добавить(СтруктураПоля);
	
	// Тип вакансии
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "type";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Тип вакансии'");
	Если Не ПервичнаяПубликация Тогда
		СтруктураПоля.Использование = Ложь;
	КонецЕсли;
	СтруктураПоля.Обязательное = Истина;
	
	ПодчиненныеЭлементы = Новый Массив;
	СтруктураПоля_type = СтруктураПоля();
	СтруктураПоля_type.Путь = "id";
	СтруктураПоля_type.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля_type.Описание = НСтр("ru = 'Тип вакансии'");
	СтруктураПоля_type.Обязательное = Истина;
	ПодчиненныеЭлементы.Добавить(СтруктураПоля_type);
	СтруктураПоля.ПодчиненныеЭлементы = ПодчиненныеЭлементы;
	МассивПолей.Добавить(СтруктураПоля);
	
	// Биллинговый тип вакансии
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "billing_type";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Биллинговый тип вакансии'");
	СтруктураПоля.Обязательное = Истина;
	
	ПодчиненныеЭлементы = Новый Массив;
	СтруктураПоля_billing_type = СтруктураПоля();
	СтруктураПоля_billing_type.Путь = "id";
	СтруктураПоля_billing_type.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля_billing_type.Описание = НСтр("ru = 'Биллинговый тип вакансии'");
	СтруктураПоля_billing_type.Обязательное = Истина;
	ПодчиненныеЭлементы.Добавить(СтруктураПоля_billing_type);
	СтруктураПоля.ПодчиненныеЭлементы = ПодчиненныеЭлементы;
	МассивПолей.Добавить(СтруктураПоля);
	
	// Внутренний код вакансии
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "code";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'Внутренний код'");
	МассивПолей.Добавить(СтруктураПоля);
	
	// Департамент
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "department";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Департамент'");
	
	ПодчиненныеЭлементы = Новый Массив;
	СтруктураПоля_department = СтруктураПоля();
	СтруктураПоля_department.Путь = "id";
	СтруктураПоля_department.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля_department.Описание = НСтр("ru = 'Департамент'");
	ПодчиненныеЭлементы.Добавить(СтруктураПоля_department);
	СтруктураПоля.ПодчиненныеЭлементы = ПодчиненныеЭлементы;
	МассивПолей.Добавить(СтруктураПоля);
	
	// Зарплата
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "salary";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Зарплата'");
	
	ПодчиненныеЭлементы = Новый Массив;
	
	СтруктураПоля_salary = СтруктураПоля();
	СтруктураПоля_salary.Путь = "from";
	СтруктураПоля_salary.ТипJSON = ТипЗначенияJSONЧисло;
	СтруктураПоля_salary.Описание = НСтр("ru = 'Нижняя граница зарплаты'");
	ПодчиненныеЭлементы.Добавить(СтруктураПоля_salary);
	
	СтруктураПоля_salary = СтруктураПоля();
	СтруктураПоля_salary.Путь = "to";
	СтруктураПоля_salary.ТипJSON = ТипЗначенияJSONЧисло;
	СтруктураПоля_salary.Описание = НСтр("ru = 'Верхняя граница зарплаты'");
	ПодчиненныеЭлементы.Добавить(СтруктураПоля_salary);
	
	СтруктураПоля_salary = СтруктураПоля();
	СтруктураПоля_salary.Путь = "currency";
	СтруктураПоля_salary.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля_salary.Описание = НСтр("ru = 'Валюта'");
	СтруктураПоля_salary.Обязательное = Истина;
	ПодчиненныеЭлементы.Добавить(СтруктураПоля_salary);
	
	СтруктураПоля.ПодчиненныеЭлементы = ПодчиненныеЭлементы;
	МассивПолей.Добавить(СтруктураПоля);
	
	// Адрес
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "address";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Адрес'");
	
	ПодчиненныеЭлементы = Новый Массив;
	
	СтруктураПоля_address = СтруктураПоля();
	СтруктураПоля_address.Путь = "id";
	СтруктураПоля_address.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля_address.Описание = НСтр("ru = 'Адрес'");
	ПодчиненныеЭлементы.Добавить(СтруктураПоля_address);
	
	СтруктураПоля_address = СтруктураПоля();
	СтруктураПоля_address.Путь = "show_metro_only";
	СтруктураПоля_address.ТипJSON = ТипЗначенияJSONБулево;
	СтруктураПоля_address.Описание = НСтр("ru = 'Показывать только метро'");
	ПодчиненныеЭлементы.Добавить(СтруктураПоля_address);
	
	СтруктураПоля.ПодчиненныеЭлементы = ПодчиненныеЭлементы;
	МассивПолей.Добавить(СтруктураПоля);
	
	// Опыт работы
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "experience";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Требуемый опыт работы'");

	ПодчиненныеЭлементы = Новый Массив;
	СтруктураПоля_experience = СтруктураПоля();
	СтруктураПоля_experience.Путь = "id";
	СтруктураПоля_experience.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля_experience.Описание = НСтр("ru = 'Требуемый опыт работы'");
	ПодчиненныеЭлементы.Добавить(СтруктураПоля_experience);
	СтруктураПоля.ПодчиненныеЭлементы = ПодчиненныеЭлементы;
	МассивПолей.Добавить(СтруктураПоля);
	
	// График работы
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "schedule";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'График работы'");
	
	ПодчиненныеЭлементы = Новый Массив;
	СтруктураПоля_schedule = СтруктураПоля();
	СтруктураПоля_schedule.Путь = "id";
	СтруктураПоля_schedule.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля_schedule.Описание = НСтр("ru = 'График работы'");
	ПодчиненныеЭлементы.Добавить(СтруктураПоля_schedule);
	СтруктураПоля.ПодчиненныеЭлементы = ПодчиненныеЭлементы;
	МассивПолей.Добавить(СтруктураПоля);
	
	// Тип занятости
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "employment";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Тип занятости'");
	
	ПодчиненныеЭлементы = Новый Массив;
	СтруктураПоля_employment = СтруктураПоля();
	СтруктураПоля_employment.Путь = "id";
	СтруктураПоля_employment.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля_employment.Описание = НСтр("ru = 'Тип занятости'");
	ПодчиненныеЭлементы.Добавить(СтруктураПоля_employment);
	СтруктураПоля.ПодчиненныеЭлементы = ПодчиненныеЭлементы;
	МассивПолей.Добавить(СтруктураПоля);
	
	// Контактная информация (для вакансий рабочих специальностей) 
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "contacts";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Контактная информация (для вакансий рабочих специальностей)'");
	
	ПодчиненныеЭлементы = Новый Массив;
	
	СтруктураПоля_contacts = СтруктураПоля();
	СтруктураПоля_contacts.Путь = "name";
	СтруктураПоля_contacts.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля_contacts.Описание = НСтр("ru = 'Контактное лицо'");
	ПодчиненныеЭлементы.Добавить(СтруктураПоля_contacts);
	
	СтруктураПоля_contacts = СтруктураПоля();
	СтруктураПоля_contacts.Путь = "email";
	СтруктураПоля_contacts.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля_contacts.Описание = НСтр("ru = 'Электронная почта'");
	ПодчиненныеЭлементы.Добавить(СтруктураПоля_contacts);
	
	СтруктураПоля_contacts = СтруктураПоля();
	СтруктураПоля_contacts.Путь = "phones";
	СтруктураПоля_contacts.ТипJSON = ТипЗначенияJSONНачалоМассива;
	СтруктураПоля_contacts.Описание = НСтр("ru = 'Список телефонов для связи'");
	
	ПодчиненныеЭлементы.Добавить(СтруктураПоля_contacts);
	
	СтруктураПоля.ПодчиненныеЭлементы = ПодчиненныеЭлементы;
	МассивПолей.Добавить(СтруктураПоля);
	
	// Тест
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "test";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Тест'");
	СтруктураПоля.Использование = Ложь;
	
	ПодчиненныеЭлементы = Новый Массив;
	
	СтруктураПоля_test = СтруктураПоля();
	СтруктураПоля_test.Путь = "id";
	СтруктураПоля_test.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля_test.Описание = НСтр("ru = 'Тест'");
	СтруктураПоля_test.Использование = Ложь;
	ПодчиненныеЭлементы.Добавить(СтруктураПоля_test);
	
	СтруктураПоля_test = СтруктураПоля();
	СтруктураПоля_test.Путь = "required";
	СтруктураПоля_test.ТипJSON = ТипЗначенияJSONБулево;
	СтруктураПоля_test.Описание = НСтр("ru = 'Требовать прохождение теста для отклика на вакансию'");
	СтруктураПоля_test.Использование = Ложь;
	ПодчиненныеЭлементы.Добавить(СтруктураПоля_test);
	
	СтруктураПоля.ПодчиненныеЭлементы = ПодчиненныеЭлементы;
	МассивПолей.Добавить(СтруктураПоля);
	
	// URL отклика для прямых вакансий
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "response_url";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'URL отклика для рекламных вакансий'");
	МассивПолей.Добавить(СтруктураПоля);
	Если Не ПервичнаяПубликация Тогда
		СтруктураПоля.Использование = Ложь;
	КонецЕсли;
	
	// Название компании для анонимных вакансий
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "custom_employer_name";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'Название компании (для анонимных вакансий)'");
	МассивПолей.Добавить(СтруктураПоля);
	
	// Контактное лицо
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "manager";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Контактное лицо'");
	
	ПодчиненныеЭлементы = Новый Массив;
	СтруктураПоля_manager = СтруктураПоля();
	СтруктураПоля_manager.Путь = "id";
	СтруктураПоля_manager.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля_manager.Описание = НСтр("ru = 'Контактное лицо'");
	ПодчиненныеЭлементы.Добавить(СтруктураПоля_manager);
	СтруктураПоля.ПодчиненныеЭлементы = ПодчиненныеЭлементы;
	МассивПолей.Добавить(СтруктураПоля);
	
	// Уведомлять о новых откликах
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "response_notifications";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONБулево;
	СтруктураПоля.Описание = НСтр("ru = 'Уведомлять о новых откликах'");
	МассивПолей.Добавить(СтруктураПоля);
	
	// Возможность переписки с кандидатами
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "allow_messages";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONБулево;
	СтруктураПоля.Описание = НСтр("ru = 'Возможность переписки с кандидатами'");
	МассивПолей.Добавить(СтруктураПоля);
	
	// Требовать сопроводительное письмо
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "response_letter_required";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONБулево;
	СтруктураПоля.Описание = НСтр("ru = 'Требовать сопроводительное письмо'");
	МассивПолей.Добавить(СтруктураПоля);
	
	// Вакансия доступна для соискателей с инвалидностью
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "accept_handicapped";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONБулево;
	СтруктураПоля.Описание = НСтр("ru = 'Вакансия доступна для соискателей с инвалидностью'");
	МассивПолей.Добавить(СтруктураПоля);
	
	Возврат МассивПолей;
	
КонецФункции

Функция ПоляВакансииRabota(ПервичнаяПубликация = Истина) Экспорт
	
	ТипЗначенияJSONСтрока = ТипЗначенияJSONСтрока();
	ТипЗначенияJSONНачалоМассива = ТипЗначенияJSONНачалоМассива();
	ТипЗначенияJSONНачалоОбъекта = ТипЗначенияJSONНачалоОбъекта();
	ТипЗначенияJSONЧисло = ТипЗначенияJSONЧисло();
	ТипЗначенияJSONБулево = ТипЗначенияJSONБулево();
	
	МассивПолей = Новый Массив;
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "name";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'Название вакансии'");
	СтруктураПоля.Обязательное = Истина;
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "isStudyCourse";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONБулево;
	СтруктураПоля.Описание = НСтр("ru = 'Это стажировка'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "offerTrades";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоМассива;
	СтруктураПоля.Описание = НСтр("ru = 'Рубрика размещения (не более 3)'");
	СтруктураПоля.Обязательное = Истина;
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "salaryFrom"; // может быть не указано, если agreementSalary = true
	СтруктураПоля.ТипJSON = ТипЗначенияJSONЧисло;
	СтруктураПоля.Описание = НСтр("ru = 'Зарплата от'");
	СтруктураПоля.СвязанноеПолеПуть = "agreementSalary";
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "salaryTo";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONЧисло;
	СтруктураПоля.Описание = НСтр("ru = 'Зарплата до'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "agreementSalary"; // может быть не указано, если указано salaryFrom
	СтруктураПоля.ТипJSON = ТипЗначенияJSONБулево;
	СтруктураПоля.Описание = НСтр("ru = 'Зарплата по договоренности'");
	СтруктураПоля.СвязанноеПолеПуть = "salaryFrom";
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "noSalary"; // значение true допустимо при isStudyCourse = true
	СтруктураПоля.ТипJSON = ТипЗначенияJSONБулево;
	СтруктураПоля.Описание = НСтр("ru = 'Без оплаты (только для стажировок)'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "currency";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Валюта'");
	СтруктураПоля.Обязательное = Истина;
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "isNonresident"; 
	СтруктураПоля.ТипJSON = ТипЗначенияJSONБулево;
	СтруктураПоля.Описание = НСтр("ru = 'Рассматриваются иногородние'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "region";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоМассива;
	СтруктураПоля.Описание = НСтр("ru = 'Регион показа'");
	СтруктураПоля.Обязательное = Истина;
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "description";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'Описание вакансии'");
	СтруктураПоля.Обязательное = Истина;
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "operatingSchedule";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'График работы'");
	СтруктураПоля.Обязательное = Истина;
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "operatingScheduleComment";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'Комментарий к графику работы'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "experienceYearCount";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Опыт работы'");
	СтруктураПоля.Обязательное = Истина;
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "education";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Образование'");
	СтруктураПоля.Обязательное = Истина;
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "gender";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Предпочтительный пол'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "ageFrom";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONЧисло;
	СтруктураПоля.Описание = НСтр("ru = 'Возраст от'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "ageTo";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONЧисло;
	СтруктураПоля.Описание = НСтр("ru = 'Возраст до'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "contactFio";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'Контактное лицо'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "contactEmail";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'E-mail'");
	СтруктураПоля.Обязательное = Истина;
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "wantResponseByMail";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONБулево;
	СтруктураПоля.Описание = НСтр("ru = 'Получать отклики на e-mail'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "address";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоМассива;
	СтруктураПоля.Описание = НСтр("ru = 'Адреса работы'");
	СтруктураПоля.Обязательное = Истина;
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "phones";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоМассива;
	СтруктураПоля.Описание = НСтр("ru = 'Телефоны'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "language";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоМассива;
	СтруктураПоля.Описание = НСтр("ru = 'Значение языков'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "additionalInformation";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Доп. информация'");
	
	ПодчиненныеЭлементы = Новый Массив;
	
	СтруктураПоля_auto = СтруктураПоля();
	СтруктураПоля_auto.Путь = "auto";
	СтруктураПоля_auto.ТипJSON = ТипЗначенияJSONБулево;
	СтруктураПоля_auto.Описание = НСтр("ru = 'Есть личный автомобиль'");
	ПодчиненныеЭлементы.Добавить(СтруктураПоля_auto);
	
	СтруктураПоля_drivingLicense = СтруктураПоля();
	СтруктураПоля_drivingLicense.Путь = "drivingLicense";
	СтруктураПоля_drivingLicense.ТипJSON = ТипЗначенияJSONНачалоМассива;
	СтруктураПоля_drivingLicense.Описание = НСтр("ru = 'Категории водительских прав'");
	ПодчиненныеЭлементы.Добавить(СтруктураПоля_drivingLicense);
	
	СтруктураПоля.ПодчиненныеЭлементы = ПодчиненныеЭлементы;
	МассивПолей.Добавить(СтруктураПоля);
	
	Возврат МассивПолей;
	
КонецФункции

Функция ПоляВакансииSuperJob() Экспорт
	
	ТипЗначенияJSONСтрока = ТипЗначенияJSONСтрока();
	ТипЗначенияJSONНачалоМассива = ТипЗначенияJSONНачалоМассива();
	ТипЗначенияJSONНачалоОбъекта = ТипЗначенияJSONНачалоОбъекта();
	ТипЗначенияJSONЧисло = ТипЗначенияJSONЧисло();
	ТипЗначенияJSONБулево = ТипЗначенияJSONБулево();
	
	МассивПолей = Новый Массив;
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "profession";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'Название'");
	СтруктураПоля.Обязательное = Истина;
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "town";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Город'");
	СтруктураПоля.Обязательное = Истина;
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "published";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Тип доступа'");
	СтруктураПоля.Обязательное = Истина;
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "id_user";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Пользователь'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "catalogues";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоМассива;
	СтруктураПоля.Описание = НСтр("ru = 'Сфера деятельности (не более 5)'");
	СтруктураПоля.Обязательное = Истина;
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "code";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'Код вакансии'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "external_url";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'URL внешнего сайта'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "work";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'Должностные обязанности'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "compensation";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'Условия работы'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "candidat";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'Требования к кандидату'");
	МассивПолей.Добавить(СтруктураПоля);

	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "type_of_work";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Тип занятости'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "place_of_work";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Место работы'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "education";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Образование'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "experience";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Опыт работы'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "languages";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоМассива;
	СтруктураПоля.Описание = НСтр("ru = 'Наименование и уровень владения иностранными языками'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "contact";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'Имя контактного лица'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "email";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'Адрес электронной почты'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "url";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'Адрес сайта компании'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "phones";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоМассива;
	СтруктураПоля.Описание = НСтр("ru = 'Номер телефона'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "driving_licence";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоМассива;
	СтруктураПоля.Описание = НСтр("ru = 'Наличие водительских прав'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "payment_from";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONЧисло;
	СтруктураПоля.Описание = НСтр("ru = 'Сумма оклада от'");
	МассивПолей.Добавить(СтруктураПоля);

	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "payment_to";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONЧисло;
	СтруктураПоля.Описание = НСтр("ru = 'Сумма оклада до'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "firm_name";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'Название компании'");
	СтруктураПоля.Обязательное = Истина;
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "firm_activity";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'Описание деятельности компании'");
	СтруктураПоля.Обязательное = Истина;
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "address";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'Адрес компании'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "anonymous";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONБулево;
	СтруктураПоля.Описание = НСтр("ru = 'Анонимная вакансия'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "refresh_vac";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONБулево;
	СтруктураПоля.Описание = НСтр("ru = 'Обновлять вакансию автоматически'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "extend_vac";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONБулево;
	СтруктураПоля.Описание = НСтр("ru = 'Продлевать вакансию автоматически'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "resumesubscription_status";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONБулево;
	СтруктураПоля.Описание = НСтр("ru = 'Включить подписку на резюме'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "resumesubscription_keywords";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONСтрока;
	СтруктураПоля.Описание = НСтр("ru = 'Ключевые слова подписки на резюме'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "resumesubscription_kwc";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Как искать слова в подписке на резюме'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "resumesubscription_rws";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Где искать слова в подписке на резюме'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "moveable";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONБулево;
	СтруктураПоля.Описание = НСтр("ru = 'Рассматриваются соискатели из других городов'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "gender";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Пол'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "age_from";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONЧисло;
	СтруктураПоля.Описание = НСтр("ru = 'Возраст от'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "age_to";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONЧисло;
	СтруктураПоля.Описание = НСтр("ru = 'Возраст до'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "maritalstatus";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Семейное положение'");
	МассивПолей.Добавить(СтруктураПоля);
	
	СтруктураПоля = СтруктураПоля();
	СтруктураПоля.Путь = "children";
	СтруктураПоля.ТипJSON = ТипЗначенияJSONНачалоОбъекта;
	СтруктураПоля.Описание = НСтр("ru = 'Наличие детей'");
	МассивПолей.Добавить(СтруктураПоля);
	
	Возврат МассивПолей;
	
КонецФункции

Функция ОпубликоватьВакансию(Вакансия, Сайт, Дерево) Экспорт
	
	ИдентификаторВакансии = ПубликацияВакансии(Сайт, Дерево);
	
	Если ИдентификаторВакансии = Неопределено Тогда
		Возврат Ложь;
	ИначеЕсли ЗначениеЗаполнено(ИдентификаторВакансии) Тогда
		ИдентификаторВакансии = Формат(ИдентификаторВакансии, "ЧГ=0");
	КонецЕсли;
	
	Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter()
		Или Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.RabotaMail() Тогда
		АдресПубликации = ИнтеграцияРекрутинговыхСайтовКлиентСервер.АдресВакансииHeadHunter(ИдентификаторВакансии);
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
		АдресПубликации = ИнтеграцияРекрутинговыхСайтовКлиентСервер.АдресВакансииRabota(ИдентификаторВакансии);
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
		АдресПубликации = ИнтеграцияРекрутинговыхСайтовКлиентСервер.АдресВакансииSuperJob(ИдентификаторВакансии);
	КонецЕсли;
	
	ЗаписьВРегистрИнформацииОПубликации(Вакансия, Сайт, ИдентификаторВакансии, АдресПубликации);
	
	ЗаписьВРегистрДанныеПубликации(Вакансия, Сайт, Дерево);
	
	Возврат Истина;
	
КонецФункции

Функция ОбновитьВакансиюНаСайте(Вакансия, Сайт, Дерево, ДопПараметры = Неопределено) Экспорт
	
	Успешно = ОбновлениеВакансииНаСайте(Вакансия, Сайт, Дерево);
	
	Если Успешно Тогда
		
		ЗаписьВРегистрИнформацииОПубликации(Вакансия, Сайт);
		ЗаписьВРегистрДанныеПубликации(Вакансия, Сайт, Дерево);
		
	КонецЕсли;
	
	Возврат Успешно;
	
КонецФункции

Процедура ЗаписьВРегистрИнформацииОПубликации(Вакансия, Сайт, ИдентификаторВакансии = Неопределено, Адрес = Неопределено) Экспорт
	
	ПубликацияВакансийМенеджерЗаписи = РегистрыСведений.ПубликацияВакансий.СоздатьМенеджерЗаписи();
	
	ПубликацияВакансийМенеджерЗаписи.Вакансия = Вакансия;
	ПубликацияВакансийМенеджерЗаписи.МестоПубликации = Сайт;
	
	ПубликацияВакансийМенеджерЗаписи.Прочитать();
	
	Если Не ПубликацияВакансийМенеджерЗаписи.Выбран() Тогда
		ПубликацияВакансийМенеджерЗаписи.Вакансия = Вакансия;
		ПубликацияВакансийМенеджерЗаписи.МестоПубликации = Сайт;
		ПубликацияВакансийМенеджерЗаписи.ДатаПубликации = ТекущаяДатаСеанса();
		ПубликацияВакансийМенеджерЗаписи.АдресПубликации = Адрес;
		Если ЗначениеЗаполнено(ИдентификаторВакансии) Тогда
			ПубликацияВакансийМенеджерЗаписи.ИдентификаторВакансии = ИдентификаторВакансии;
		КонецЕсли;
		
	КонецЕсли;
	
	ПубликацияВакансийМенеджерЗаписи.ДатаОбновления = ТекущаяДатаСеанса();
	
	ПубликацияВакансийМенеджерЗаписи.Записать();
	
КонецПроцедуры

Процедура ЗагрузитьКлассификаторы(Сайт) Экспорт
	
	Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
		ЗагрузитьКлассификаторыHeadHunter(Сайт);
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
		ЗагрузитьКлассификаторыRabota(Сайт);
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
		ЗагрузитьКлассификаторыSuperJob(Сайт);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДерево(Классификатор, Дерево, ИспользуемыеСайты = Неопределено) Экспорт 
	
	Если Не ЗначениеЗаполнено(Классификатор) Тогда
		Возврат;
	КонецЕсли;
	
	Если Классификатор = "specializations" Тогда
		ЗаполнитьДеревоСпециализацийHeadHunter(Дерево);
	ИначеЕсли Классификатор = "areas" Тогда
		ЗаполнитьДеревоРегионовHeadHunter(Дерево);
	ИначеЕсли Классификатор = "offer_trades" Тогда
		ЗаполнитьДеревоРубрикRabota(Дерево);
	ИначеЕсли Классификатор = "regions" Тогда
		ЗаполнитьДеревоРегионовRabota(Дерево);
	ИначеЕсли Классификатор = "towns" Тогда
		ЗаполнитьДеревоРегионовSuperJob(Дерево);
	ИначеЕсли Классификатор = "catalogues" Тогда
		ЗаполнитьДеревоСпециализацийSuperJob(Дерево);
	ИначеЕсли Классификатор = "areas_towns" Тогда
		ЗаполнитьОбъединенноеДеревоРегионов(Дерево, ИспользуемыеСайты);
	КонецЕсли;
	
КонецПроцедуры

Функция СтруктураМаркеров(Сайт, КодАвторизации, Обновление = Ложь, Знач ИмяПользователя = Неопределено, Знач Пароль = Неопределено) Экспорт
	
	Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
		
		ID_Программы		= ID_ПрограммыHeadHunter();
		Пароль_Программы	= ПарольПрограммыHeadHunter();
		АдресАутентификации	= "hh.ru";
		РесурсНаСервере  	= "/oauth/token";
		КодировкаПротокола 	= "windows-1251";
		URL_Редиректа		= "http://www.v8.1c.ru/hrm";
		
		Если Обновление Тогда
			СтрокаЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"grant_type=refresh_token&refresh_token=%1",
						КодАвторизации); 
		Иначе
			СтрокаЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"grant_type=authorization_code&client_id=%1&client_secret=%2&code=%3&redirect_uri=%4",
						ID_Программы, 
						Пароль_Программы, 
						КодАвторизации, 
						URL_Редиректа); 
		КонецЕсли;
				
		// Создаем соединение.
		Соединение = НовоеHTTPСоединение(АдресАутентификации, Истина);
		
		// Формируем общие заголовки.
		ЗаголовокHTTP = Новый Соответствие();
		ЗаголовокHTTP.Вставить("User-Agent", "1С:Зарплата и управление персоналом 8 КОРП (v8@1c.ru)");
		ЗаголовокHTTP.Вставить("Content-Type", "application/x-www-form-urlencoded");
		
		Запрос = Новый HTTPЗапрос(РесурсНаСервере, ЗаголовокHTTP);
		Запрос.УстановитьТелоИзСтроки(СтрокаЗапроса, КодировкаПротокола);
		
		// Отправляем POST запрос.
		Ответ = Соединение.ОтправитьДляОбработки(Запрос);
		
		// Разбираем ответ сервера.
		ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку(КодировкаПротокола);
		Если Ответ.КодСостояния <> 200 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Ошибка авторизации %1: %2'"), Ответ.КодСостояния, ТекущаяСтрока));
			Возврат Неопределено;
		Иначе
			
			СтруктураМаркеров = ПарсерJSON(ТекущаяСтрока);
			
			Если ЗначениеЗаполнено(СтруктураМаркеров) Тогда
				СохранитьНатройкиАвторизации(Сайт, СтруктураМаркеров, ИмяПользователя, Пароль);
				Возврат СтруктураМаркеров;
			Иначе
				Возврат Неопределено;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
		
		УточнитьИмяПользователяИПарольНаСайт(Сайт, ИмяПользователя, Пароль);
		
		Соединение = HTTPСоединениеRabota();
		Запрос = HTTPЗапросRabota(СтрШаблон("login?mail=%1", ИмяПользователя));
		Ответ = Соединение.Получить(Запрос);
		
		ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку();
		Если Ответ.КодСостояния <> 200 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Ошибка авторизации %1: %2'"), Ответ.КодСостояния, ТекущаяСтрока));
			Возврат Неопределено;
		Иначе
			
			Ключ = ПарсерJSON(ТекущаяСтрока).key;
			
			ПараметрыЗапроса = Новый Структура;
			ПараметрыЗапроса.Вставить("mail", ИмяПользователя);
			ПараметрыЗапроса.Вставить("password", НРег(ПарольАвторизацииRabota(Ключ, Пароль)));
			ПараметрыЗапроса.Вставить("akey", ID_ПрограммыRabota());
			
			Запрос = HTTPЗапросRabota("login");
			Запрос.УстановитьТелоИзСтроки(ЗаписьJSON(ПараметрыЗапроса));
			
			Ответ = Соединение.ОтправитьДляОбработки(Запрос);
			
			ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку();
			Если Ответ.КодСостояния <> 200 Тогда
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Ошибка авторизации %1: %2'"), Ответ.КодСостояния, ТекущаяСтрока));
				Возврат Неопределено;
				
			Иначе
				
				СтруктураМаркеров = ПарсерJSON(ТекущаяСтрока);
				
				Если ЗначениеЗаполнено(СтруктураМаркеров) Тогда
					СохранитьНатройкиАвторизации(Сайт, СтруктураМаркеров, ИмяПользователя, Пароль);
					Возврат СтруктураМаркеров;
				Иначе
					Возврат Неопределено;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
		
		ID_Программы		= ID_ПрограммыSuperJob();
		Пароль_Программы	= ПарольПрограммыSuperJob();
		КодировкаПротокола 	= "UTF-8";
		
		УточнитьИмяПользователяИПарольНаСайт(Сайт, ИмяПользователя, Пароль);
		
		Если Обновление Тогда
			
			СтрокаЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"/2.0/oauth2/refresh_token/?refresh_token=%1&client_id=%2&client_secret=%3",
						КодАвторизации,
						ID_Программы, 
						Пароль_Программы);
		Иначе
			СтрокаЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"/2.0/oauth2/password/?login=%1&password=%2&client_id=%3&client_secret=%4",
						ИмяПользователя, 
						Пароль, 
						ID_Программы, 
						Пароль_Программы);
						
		КонецЕсли;
		
		Соединение = HTTPСоединениеSuperJob();
		
		ЗаголовокHTTP = Новый Соответствие();
		ЗаголовокHTTP.Вставить("X-User-Type", " hr_user");
		
		Запрос = Новый HTTPЗапрос(СтрокаЗапроса, ЗаголовокHTTP);
		Ответ = Соединение.Получить(Запрос);
		
		ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку(КодировкаПротокола);
		Если Ответ.КодСостояния <> 200 Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Ошибка авторизации %1: %2'"), Ответ.КодСостояния, ТекущаяСтрока));
			Возврат Неопределено;
			
		Иначе
			
			ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку(КодировкаПротокола);
			СтруктураМаркеров = ПарсерJSON(ТекущаяСтрока);
			
			Если ЗначениеЗаполнено(СтруктураМаркеров) Тогда
				СохранитьНатройкиАвторизации(Сайт, СтруктураМаркеров, ИмяПользователя, Пароль);
				Возврат СтруктураМаркеров;
			Иначе
				Возврат Неопределено;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

Функция МаркерДоступаАктивен(Сайт, ИмяПользователя) Экспорт
	
	МаркерАктивен = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиИнтеграцииРекрутинговыхСайтов.Пользователь,
	|	НастройкиИнтеграцииРекрутинговыхСайтов.МаркерДоступа,
	|	НастройкиИнтеграцииРекрутинговыхСайтов.МаркерОбновления,
	|	НастройкиИнтеграцииРекрутинговыхСайтов.ДатаОкончанияДействияМаркераДоступа,
	|	НастройкиИнтеграцииРекрутинговыхСайтов.ИмяПользователя
	|ИЗ
	|	РегистрСведений.НастройкиИнтеграцииРекрутинговыхСайтов КАК НастройкиИнтеграцииРекрутинговыхСайтов
	|ГДЕ
	|	НастройкиИнтеграцииРекрутинговыхСайтов.Пользователь = &Пользователь
	|	И НастройкиИнтеграцииРекрутинговыхСайтов.РекрутинговыйСайт = &РекрутинговыйСайт
	|	И НастройкиИнтеграцииРекрутинговыхСайтов.Использовать
	|	И (НастройкиИнтеграцииРекрутинговыхСайтов.ИмяПользователя = &ИмяПользователя
	|			ИЛИ &НеИспользоватьОтборПоИмениПользователя)";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	Запрос.УстановитьПараметр("РекрутинговыйСайт", Сайт);
	Запрос.УстановитьПараметр("ИмяПользователя", ИмяПользователя);
	Запрос.УстановитьПараметр("НеИспользоватьОтборПоИмениПользователя", НЕ ЗначениеЗаполнено(ИмяПользователя));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		МаркерДействителен = ЗначениеЗаполнено(Выборка.МаркерДоступа) 
			И Выборка.ДатаОкончанияДействияМаркераДоступа > ТекущаяДатаСеанса();
			
		Если МаркерДействителен Тогда
			
			МаркерАктивен = ПроверитьВалидностьМаркера(Сайт, Выборка.МаркерДоступа);
			
		ИначеЕсли ЗначениеЗаполнено(Выборка.МаркерОбновления) Тогда
			
			СтруктураМаркеров = СтруктураМаркеров(Сайт, Выборка.МаркерОбновления, Истина);
			МаркерАктивен = СтруктураМаркеров <> Неопределено И ЗначениеЗаполнено(СтруктураМаркеров.access_token);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат МаркерАктивен;
	
КонецФункции

Функция ВакансииССайта(Сайт) Экспорт
	
	Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
		Возврат ВакансииССайтаHeadHunter(Сайт);
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
		Возврат ВакансииССайтаRabota(Сайт);
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
		Возврат ВакансииССайтаSuperJob(Сайт);
	КонецЕсли;
	
КонецФункции

Функция СтруктураДанныхКандидатаССайта(Сайт, ИдентификаторРезюме) Экспорт
	Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
		Возврат СтруктураДанныхКандидатаССайтаHeadHunter(ИдентификаторРезюме);
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
		Возврат СтруктураДанныхКандидатаССайтаRabota(ИдентификаторРезюме);
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
		Возврат СтруктураДанныхКандидатаССайтаSuperJob(ИдентификаторРезюме);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

Процедура ВыполнитьПоискКандидатов(Параметры, АдресРезультата) Экспорт
	
	СтруктураОтбора = Параметры.СтруктураОтбора;
	ИспользуемыеСайты = Параметры.ИспользуемыеСайты;
	ТаблицаНайденныхКандидатов = Параметры.ТаблицаНайденныхКандидатов;
	
	Если ИспользуемыеСайты = Неопределено Тогда
		ПоместитьВоВременноеХранилище(Неопределено, АдресРезультата);
		Возврат;
	КонецЕсли;
	
	Если Параметры.НомерПорцииHeadHunter = 0 
		И Параметры.НомерПорцииRabota = 0
		И Параметры.НомерПорцииSuperJob = 0 Тогда
		ТаблицаНайденныхКандидатов.Очистить();
		ПолеСортировки = 0;
	Иначе
		ПолеСортировки = ТаблицаНайденныхКандидатов[ТаблицаНайденныхКандидатов.Количество() - 1].ПолеСортировки + 1;
	КонецЕсли;
	
	ТаблицаНайденныхКандидатовHeadHunter = ТаблицаНайденныхКандидатов.СкопироватьКолонки();
	ТаблицаНайденныхКандидатовRabota = ТаблицаНайденныхКандидатов.СкопироватьКолонки();
	ТаблицаНайденныхКандидатовSuperJob = ТаблицаНайденныхКандидатов.СкопироватьКолонки();
	
	Для Каждого Сайт Из ИспользуемыеСайты Цикл
			
		Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
			
			ВсегоКандидатовHeadHunter = Параметры.ВсегоКандидатовHeadHunter;
			Если Параметры.ВсегоКандидатовHeadHunter > 0 
				И Параметры.ВсегоКандидатовHeadHunter = Параметры.ЗагруженоКандидатовHeadHunter Тогда
				Продолжить;
			КонецЕсли;
			
			НайтиКандидатовHeadHunter(СтруктураОтбора, ТаблицаНайденныхКандидатовHeadHunter, Параметры.НомерПорцииHeadHunter, ПолеСортировки, ВсегоКандидатовHeadHunter);
			
		ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
			
			ВсегоКандидатовRabota = Параметры.ВсегоКандидатовRabota;
			Если Параметры.ВсегоКандидатовRabota > 0 
				И Параметры.ВсегоКандидатовRabota = Параметры.ЗагруженоКандидатовRabota Тогда
				Продолжить;
			КонецЕсли;
			
			НайтиКандидатовRabota(СтруктураОтбора, ТаблицаНайденныхКандидатовRabota, Параметры.НомерПорцииRabota, ПолеСортировки, ВсегоКандидатовRabota);
			
		ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
			
			ВсегоКандидатовSuperJob = Параметры.ВсегоКандидатовSuperJob;
			Если Параметры.ВсегоКандидатовSuperJob > 0 
				И Параметры.ВсегоКандидатовSuperJob = Параметры.ЗагруженоКандидатовSuperJob Тогда
				Продолжить;
			КонецЕсли;
			
			НайтиКандидатовSuperJob(СтруктураОтбора, ТаблицаНайденныхКандидатовSuperJob, Параметры.НомерПорцииSuperJob, ПолеСортировки, ВсегоКандидатовSuperJob);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТаблицаНайденныхКандидатовHeadHunter.Количество() > 0
		Или ТаблицаНайденныхКандидатовRabota.Количество() > 0
		Или ТаблицаНайденныхКандидатовSuperJob.Количество() > 0 Тогда
		
		Для Каждого СтрокаТаблицыHeadHunter Из ТаблицаНайденныхКандидатовHeadHunter Цикл
			
			НоваяСтрока = ТаблицаНайденныхКандидатов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицыHeadHunter);
			
		КонецЦикла;
		
		Для Каждого СтрокаТаблицыRabota Из ТаблицаНайденныхКандидатовRabota Цикл
			
			НоваяСтрока = ТаблицаНайденныхКандидатов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицыRabota);
			
		КонецЦикла;
		
		Для Каждого СтрокаТаблицыSuperJob Из ТаблицаНайденныхКандидатовSuperJob Цикл
			
			НоваяСтрока = ТаблицаНайденныхКандидатов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицыSuperJob);
			
		КонецЦикла;
		
		ТаблицаНайденныхКандидатов.Сортировать("ПолеСортировки");
		
		Результат = Новый Структура;
		Результат.Вставить("ТаблицаНайденныхКандидатов", ТаблицаНайденныхКандидатов);
		Результат.Вставить("ВсегоКандидатовHeadHunter", ВсегоКандидатовHeadHunter);
		Результат.Вставить("ВсегоКандидатовRabota", ВсегоКандидатовRabota);
		Результат.Вставить("ВсегоКандидатовSuperJob", ВсегоКандидатовSuperJob);
		Результат.Вставить("НомерПорцииHeadHunter", ?(ТаблицаНайденныхКандидатовHeadHunter.Количество() > 0, Параметры.НомерПорцииHeadHunter + 1, Параметры.НомерПорцииHeadHunter));
		Результат.Вставить("НомерПорцииRabota", ?(ТаблицаНайденныхКандидатовRabota.Количество() > 0, Параметры.НомерПорцииRabota + 1, Параметры.НомерПорцииRabota));
		Результат.Вставить("НомерПорцииSuperJob", ?(ТаблицаНайденныхКандидатовSuperJob.Количество() > 0, Параметры.НомерПорцииSuperJob + 1, Параметры.НомерПорцииSuperJob));
		Результат.Вставить("ЗагруженоКандидатовHeadHunter", ТаблицаНайденныхКандидатовHeadHunter.Количество());
		Результат.Вставить("ЗагруженоКандидатовRabota", ТаблицаНайденныхКандидатовRabota.Количество());
		Результат.Вставить("ЗагруженоКандидатовSuperJob", ТаблицаНайденныхКандидатовSuperJob.Количество());
		
		ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	Иначе
		ПоместитьВоВременноеХранилище(Неопределено, АдресРезультата);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьОтклики(Вакансия, Сайт) Экспорт
	
	Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
		ПолучитьОткликиHeadHunter(Вакансия, Сайт);
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
		ПолучитьОткликиRabota(Вакансия, Сайт);
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
		ПолучитьОткликиSuperJob(Вакансия, Сайт);
	КонецЕсли;
	
КонецПроцедуры

Функция ПервичнаяПубликация(Вакансия, Сайт) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПубликацияВакансий.Вакансия,
	|	ПубликацияВакансий.МестоПубликации
	|ИЗ
	|	РегистрСведений.ПубликацияВакансий КАК ПубликацияВакансий
	|ГДЕ
	|	ПубликацияВакансий.Вакансия = &Вакансия
	|	И ПубликацияВакансий.МестоПубликации = &МестоПубликации";
	
	Запрос.УстановитьПараметр("Вакансия", Вакансия);
	Запрос.УстановитьПараметр("МестоПубликации", Сайт);
	
	Возврат Запрос.Выполнить().Пустой();
	
КонецФункции

Функция СписокЗначенийПоМассивуСтруктур(МассивСтруктур) Экспорт
	
	Список = Новый СписокЗначений;
	
	Для Каждого ЭлементМассива Из МассивСтруктур Цикл
		Список.Добавить(ЭлементМассива.Значение, ЭлементМассива.Представление);
	КонецЦикла;
	
	Возврат Список;
	
КонецФункции

Функция АвторизацияRabota(Сайт, ИмяПользователя = Неопределено, Пароль = Неопределено) Экспорт
	
	Возврат СтруктураМаркеров(Сайт, "", Ложь, ИмяПользователя, Пароль) <> Неопределено;
	
КонецФункции

Функция АвторизацияSuperJob(Сайт, ИмяПользователя = Неопределено, Пароль = Неопределено) Экспорт
	
	Возврат СтруктураМаркеров(Сайт, "", Ложь, ИмяПользователя, Пароль) <> Неопределено;
	
КонецФункции

Функция СписокВыбораПоКлассификатору(Сайт, Путь) Экспорт
	
	Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter()
		Или Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.RabotaMail() Тогда
		Возврат СписокВыбораПоКлассификаторуHeadHunter(Сайт, Путь);
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
		Возврат СписокВыбораПоКлассификаторуRabota(Путь);
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
		Возврат СписокВыбораПоКлассификаторуSuperJob(Путь);
	КонецЕсли;
	
КонецФункции

Процедура ЗаписьВРегистрДанныеПубликацииВакансий(Вакансии, Вакансия, Сайт, ИдентификаторВакансии) Экспорт
	
	СтруктураВакансииССайта = Неопределено;
	
	Для Каждого ЭлементМассиваВакансий Из Вакансии Цикл
		
		Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
			Если Формат(ЭлементМассиваВакансий.Получить("id"), "ЧГ=0") = ИдентификаторВакансии Тогда
				СтруктураВакансииССайта = ЭлементМассиваВакансий;
				Прервать;
			КонецЕсли;
		Иначе
			Если Формат(ЭлементМассиваВакансий.id, "ЧГ=0") = ИдентификаторВакансии Тогда
				СтруктураВакансииССайта = ЭлементМассиваВакансий;
				Прервать;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Если СтруктураВакансииССайта = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Дерево = Новый ДеревоЗначений;
	
	Дерево.Колонки.Добавить("Показатель");
	Дерево.Колонки.Добавить("ПубликацияВакансии");
	Дерево.Колонки.Добавить("Путь");
	
	Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
		ЗаполнитьДеревоВакансииHeadHunter(Вакансия, Дерево, СтруктураВакансииССайта, Сайт);
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
		ЗаполнитьДеревоВакансииRabota(Вакансия, Дерево, СтруктураВакансииССайта, Сайт);
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
		ЗаполнитьДеревоВакансииSuperJob(Вакансия, Дерево, СтруктураВакансииССайта, Сайт);
	КонецЕсли;
	
	ЗаписьВРегистрДанныеПубликации(Вакансия, Сайт, Дерево);
	
КонецПроцедуры

Функция ИспользуемыеСайты() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиИнтеграцииРекрутинговыхСайтов.РекрутинговыйСайт КАК РекрутинговыйСайт,
	|	НастройкиИнтеграцииРекрутинговыхСайтов.Пользователь,
	|	НастройкиИнтеграцииРекрутинговыхСайтов.Использовать
	|ИЗ
	|	РегистрСведений.НастройкиИнтеграцииРекрутинговыхСайтов КАК НастройкиИнтеграцииРекрутинговыхСайтов
	|ГДЕ
	|	НастройкиИнтеграцииРекрутинговыхСайтов.Пользователь = &Пользователь
	|	И НастройкиИнтеграцииРекрутинговыхСайтов.Использовать
	|
	|УПОРЯДОЧИТЬ ПО
	|	РекрутинговыйСайт
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("РекрутинговыйСайт");	
	КонецЕсли;
	
КонецФункции

Функция ОбъединенныйСписокВыбораПоКлассификаторам(СтруктураПутей, ИспользуемыеСайты) Экспорт
	
	СписокВыбора = Новый СписокЗначений;
	Если Не ЗначениеЗаполнено(ИспользуемыеСайты) Тогда
		Возврат СписокВыбора;
	КонецЕсли;
	
	Если ИспользуемыеСайты.Количество() = 1 Тогда
		Если ИспользуемыеСайты[0] = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
			Возврат СписокВыбораПоКлассификаторуHeadHunter(ИспользуемыеСайты[0], СтруктураПутей.ПутьHeadHunter);
		ИначеЕсли ИспользуемыеСайты[0] = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
			Возврат СписокВыбораПоКлассификаторуRabota(СтруктураПутей.ПутьRabota);
		ИначеЕсли ИспользуемыеСайты[0] = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
			Возврат СписокВыбораПоКлассификаторуSuperJob(СтруктураПутей.ПутьSuperJob);
		Иначе
			Возврат СписокВыбора;
		КонецЕсли;
	КонецЕсли;
	
	КлассификаторыОбщие = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыОбщие");
	КлассификаторыСайтов = КлассификаторыСайтов(ИспользуемыеСайты);
	
	Если КлассификаторыСайтов = Неопределено Тогда
		Возврат СписокВыбора;
	КонецЕсли;
	
	Для Каждого ЭлементПоиска Из КлассификаторыОбщие[СтруктураПутей.Путь] Цикл
		
		Идентификаторы = ИдентификаторыЗначенияКлассификаторов(КлассификаторыСайтов, СтруктураПутей, ЭлементПоиска.Значение);
		Если Идентификаторы <> Неопределено Тогда
			СписокВыбора.Добавить(Идентификаторы, ЭлементПоиска.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СписокВыбора;
	
КонецФункции

Функция ЗначениеСвойстваУзла(Узел, ИмяСвойства, ИмяСвойстваВторогоУровня = Неопределено, ИмяСвойстваТретьегоУровня = Неопределено, ЗначениеПоУмолчанию = "") Экспорт

	Значение = ЗначениеПоУмолчанию;
	
	Если ТипЗнч(Узел) = Тип("Структура") Тогда
		
		Если Не Узел.Свойство(ИмяСвойства, Значение) Тогда
			Возврат ЗначениеПоУмолчанию;
		КонецЕсли;

		Если Значение = "null" Тогда
			Возврат ЗначениеПоУмолчанию;
		КонецЕсли;
				
	ИначеЕсли ТипЗнч(Узел) = Тип("Соответствие") Тогда
		
		Значение = Узел.Получить(ИмяСвойства);
		
		Если Значение = Неопределено Тогда
			Возврат ЗначениеПоУмолчанию;
		КонецЕсли;

		Если Значение = "null" Тогда
			Возврат ЗначениеПоУмолчанию;
		КонецЕсли;
				
	КонецЕсли;	

	Если ИмяСвойстваВторогоУровня = Неопределено Тогда
		Возврат Значение;
	Иначе
		Возврат ЗначениеСвойстваУзла(Значение, ИмяСвойстваВторогоУровня, ИмяСвойстваТретьегоУровня, Неопределено, ЗначениеПоУмолчанию);
	КонецЕсли;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьКомандуПубликацииНаСайте(Форма, Заголовок, СоответствиеКомандИСайтовПромежуточное)
	
	ИдентификаторКоманды = ИнтеграцияРекрутинговыхСайтовКлиентСервер.УникальноеИмяРеквизита();
	
	// Добавляем команду
	НоваяКоманда = Форма.Команды.Добавить("ПубликацияНаСайт_" + ИдентификаторКоманды);
	НоваяКоманда.Заголовок	= Заголовок;
	НоваяКоманда.Действие	= "Подключаемый_ОпубликоватьНаСайте";
	
	// Определяем в какое подменю, добавить кнопку
	ГруппаКнопок = Форма.Элементы.Публикации.КоманднаяПанель.ПодчиненныеЭлементы.ГруппаОпубликовать;
	
	// Добавляем кнопку
	Кнопка = Форма.Элементы.Добавить("ПубликацияНаСайт_" + ИдентификаторКоманды, Тип("КнопкаФормы"), ГруппаКнопок); 
	Кнопка.ИмяКоманды	= НоваяКоманда.Имя;
	Кнопка.Заголовок	= Заголовок;
	
	СоответствиеКомандИСайтовПромежуточное.Вставить(НоваяКоманда.Имя, Заголовок);
		
КонецПроцедуры

Процедура СоздатьКомандуЗагрузкиССайта(Форма, Заголовок, СоответствиеКомандИСайтовПромежуточное)
	
	ИдентификаторКоманды = ИнтеграцияРекрутинговыхСайтовКлиентСервер.УникальноеИмяРеквизита();
	
	// Добавляем команду
	НоваяКоманда = Форма.Команды.Добавить("ЗагрузкаССайта_" + ИдентификаторКоманды);
	НоваяКоманда.Заголовок	= Заголовок;
	НоваяКоманда.Действие	= "Подключаемый_КомандаЗагрузитьССайта";
	
	// Определяем в какое подменю, добавить кнопку
	ГруппаКнопок = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ГруппаКнопокДляЗагрузкиССайта(Форма);
	
	// Добавляем кнопку
	Кнопка = Форма.Элементы.Добавить("ЗагрузкаССайта_" + ИдентификаторКоманды, Тип("КнопкаФормы"), ГруппаКнопок); 
	Кнопка.ИмяКоманды	= НоваяКоманда.Имя;
	Кнопка.Заголовок	= Заголовок;
	
	СоответствиеКомандИСайтовПромежуточное.Вставить(НоваяКоманда.Имя, Заголовок);
	
КонецПроцедуры

Функция СтруктураПоля()
	
	СтруктураПоляHeadHunter = Новый Структура;
	
	СтруктураПоляHeadHunter.Вставить("Путь");
	СтруктураПоляHeadHunter.Вставить("ТипJSON");
	СтруктураПоляHeadHunter.Вставить("Описание");
	СтруктураПоляHeadHunter.Вставить("ПодчиненныеЭлементы");
	СтруктураПоляHeadHunter.Вставить("Использование", Истина);
	СтруктураПоляHeadHunter.Вставить("Обязательное", Ложь);
	СтруктураПоляHeadHunter.Вставить("СвязанноеПолеПуть");
	
	Возврат СтруктураПоляHeadHunter;
	
КонецФункции

Функция ПредставлениеСпискаВВидеМассиваСтруктур(Список)
	
	МассивЗначений = Новый Массив;
	
	Для Каждого ЭлементСписка Из Список Цикл
		
		СтруктураЗначений = Новый Структура;
		СтруктураЗначений.Вставить("Значение", ЭлементСписка.Значение);
		СтруктураЗначений.Вставить("Представление", ЭлементСписка.Представление);
		
		МассивЗначений.Добавить(СтруктураЗначений);
		
	КонецЦикла;
	
	Возврат МассивЗначений;
	
КонецФункции

Процедура ЗаписьВРегистрДанныеПубликации(Вакансия, Сайт, Дерево)
	
	ПоляСТекстом = ПоляСТекстом(Сайт);
	ЕстьКолонкаСТекстом = Дерево.Колонки.Найти("ПубликацияВакансииТекст") <> Неопределено;
	
	Для Каждого СтрокаВакансии Из Дерево.Строки Цикл
		
		Если СтрокаВакансии.Показатель <> Вакансия Тогда 
			Продолжить;
		КонецЕсли;
		
		Для Каждого Поле Из СтрокаВакансии.Строки Цикл
			
			Если ЗначениеЗаполнено(Поле.ПубликацияВакансии) Тогда
				
				ДанныеПубликацииВакансийНаборЗаписей = РегистрыСведений.ДанныеПубликацииВакансий.СоздатьНаборЗаписей();
				
				ДанныеПубликацииВакансийНаборЗаписей.Отбор.Вакансия.Установить(Вакансия);
				ДанныеПубликацииВакансийНаборЗаписей.Отбор.МестоПубликации.Установить(Сайт);
				ДанныеПубликацииВакансийНаборЗаписей.Отбор.ПолеДанных.Установить(Поле.Путь);
				
				ДанныеПубликацииВакансийНаборЗаписей.Прочитать();
				ДанныеПубликацииВакансийНаборЗаписей.Очистить();
				
				НоваяЗапись = ДанныеПубликацииВакансийНаборЗаписей.Добавить();
				НоваяЗапись.Вакансия = Вакансия;
				НоваяЗапись.МестоПубликации = Сайт;
				НоваяЗапись.ПолеДанных = Поле.Путь;
				
				Если ТипЗнч(Поле.ПубликацияВакансии) = Тип("СписокЗначений") Тогда
					ПолеJSON = ПредставлениеСпискаВВидеМассиваСтруктур(Поле.ПубликацияВакансии);
				Иначе
					ПолеJSON = ?(ЕстьКолонкаСТекстом И ПоляСТекстом.Найти(Поле.Путь) <> Неопределено, Поле.ПубликацияВакансииТекст, Поле.ПубликацияВакансии);
				КонецЕсли;
				
				НоваяЗапись.Значение = ЗаписьJSON(ПолеJSON);
				
				ДанныеПубликацииВакансийНаборЗаписей.Записать();
				
			КонецЕсли;
			
			Для Каждого ПодчиненноеПоле Из Поле.Строки Цикл
				
				Если ЗначениеЗаполнено(ПодчиненноеПоле.ПубликацияВакансии) Тогда
					
					ДанныеПубликацииВакансийНаборЗаписей = РегистрыСведений.ДанныеПубликацииВакансий.СоздатьНаборЗаписей();
					
					ДанныеПубликацииВакансийНаборЗаписей.Отбор.Вакансия.Установить(Вакансия);
					ДанныеПубликацииВакансийНаборЗаписей.Отбор.МестоПубликации.Установить(Сайт);
					ДанныеПубликацииВакансийНаборЗаписей.Отбор.ПолеДанных.Установить(ПодчиненноеПоле.Путь);
					
					ДанныеПубликацииВакансийНаборЗаписей.Прочитать();
					ДанныеПубликацииВакансийНаборЗаписей.Очистить();
					
					НоваяЗапись = ДанныеПубликацииВакансийНаборЗаписей.Добавить();
					НоваяЗапись.Вакансия = Вакансия;
					НоваяЗапись.МестоПубликации = Сайт;
					НоваяЗапись.ПолеДанных = ПодчиненноеПоле.Путь;
					
					Если ТипЗнч(ПодчиненноеПоле.ПубликацияВакансии) = Тип("СписокЗначений") Тогда
						ПолеJSON = ПредставлениеСпискаВВидеМассиваСтруктур(ПодчиненноеПоле.ПубликацияВакансии);
					Иначе
						ПолеJSON = ?(ЕстьКолонкаСТекстом И ПоляСТекстом.Найти(Поле.Путь) <> Неопределено, ПодчиненноеПоле.ПубликацияВакансииТекст, ПодчиненноеПоле.ПубликацияВакансии);
					КонецЕсли;
					
					НоваяЗапись.Значение = ЗаписьJSON(ПолеJSON);
					
					ДанныеПубликацииВакансийНаборЗаписей.Записать();
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция СтрокаАвторизацииHeadHunter() Экспорт
	
	Client_ID 			= "TQHP8O8KSN8PQICJQHHF3EI0HTS2IQS9R744R2SOC4KN26NMGCJG9NDLOVCNISON";
	АдресАутентификации	= "hh.ru";
	Redirect_uri		= "http://www.v8.1c.ru/hrm";
	
	СтрокаАвторизации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"https://%1/oauth/authorize?response_type=code&client_id=%2&redirect_uri=%3",
				АдресАутентификации,
				Client_ID,
				Redirect_uri);
				
	Возврат СтрокаАвторизации
	
КонецФункции

Процедура ЗагрузитьКлассификаторыHeadHunter(Сайт) 
	
	Если ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыHeadHunter") <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КодировкаОтвета = "UTF-8";
	Соединение = HTTPСоединениеHeadHunter();
	Запрос = HTTPЗапросHeadHunter("/dictionaries");
	Ответ = Соединение.Получить(Запрос);
	
	ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку(КодировкаОтвета);
	Если Ответ.КодСостояния <> 200 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru = 'Ошибка загрузки классификаторов %1: %2'"), 
											Ответ.КодСостояния, 
											ТекущаяСтрока));
		
	Иначе
		КлассификаторыHeadHunter = ПарсерJSON(ТекущаяСтрока);
	КонецЕсли;
	
	Запрос = HTTPЗапросHeadHunter("/specializations");
	Ответ = Соединение.Получить(Запрос);
	
	ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку(КодировкаОтвета);
	Если Ответ.КодСостояния <> 200 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru = 'Ошибка загрузки классификатора специализаций %1: %2'"), 
											Ответ.КодСостояния, 
											ТекущаяСтрока));
		
	Иначе
		СправочникСпециализацийHeadHunter = ПарсерJSON(ТекущаяСтрока);
	КонецЕсли;
	
	Запрос = HTTPЗапросHeadHunter("/areas");
	Ответ = Соединение.Получить(Запрос);
	
	ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку(КодировкаОтвета);
	Если Ответ.КодСостояния <> 200 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru = 'Ошибка загрузки классификатора регионов %1: %2'"), 
											Ответ.КодСостояния, 
											ТекущаяСтрока));
		
	Иначе
		СправочникРегионовHeadHunter = ПарсерJSON(ТекущаяСтрока);
	КонецЕсли;
	
	ИдентификаторПользователя = ИдентификаторПользователяИзРегистра(Сайт);
	МаркерДоступа = МаркерДоступа(Сайт);
	
	Если ЗначениеЗаполнено(ИдентификаторПользователя) 
		И ЗначениеЗаполнено(МаркерДоступа) Тогда
		
		// Адреса
		Запрос = HTTPЗапросHeadHunter("/employers/" + ИдентификаторПользователя + "/addresses", МаркерДоступа);
		Ответ = Соединение.Получить(Запрос);
		
		ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку(КодировкаОтвета);
		Если Ответ.КодСостояния <> 200 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
												НСтр("ru = 'Ошибка загрузки адресов пользователя %1: %2'"), 
												Ответ.КодСостояния, 
												ТекущаяСтрока));
		Иначе
			АдресаПользователя = ПарсерJSON(ТекущаяСтрока);
		КонецЕсли;
		
		// Менеджеры
		Запрос = HTTPЗапросHeadHunter("/employers/" + ИдентификаторПользователя + "/managers", МаркерДоступа);
		Ответ = Соединение.Получить(Запрос);
		
		ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку(КодировкаОтвета);
		Если Ответ.КодСостояния <> 200 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
												НСтр("ru = 'Ошибка загрузки менеджеров пользователя %1: %2'"), 
												Ответ.КодСостояния, 
												ТекущаяСтрока));
		Иначе
			МенеджерыПользователя = ПарсерJSON(ТекущаяСтрока);
		КонецЕсли;
		
		// Департаменты
		Запрос = HTTPЗапросHeadHunter("/employers/" + ИдентификаторПользователя + "/departments", МаркерДоступа);
		Ответ = Соединение.Получить(Запрос);
		
		ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку(КодировкаОтвета);
		Если Ответ.КодСостояния <> 200 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
												НСтр("ru = 'Ошибка загрузки департаментов пользователя %1: %2'"), 
												Ответ.КодСостояния, 
												ТекущаяСтрока));
		Иначе
			ДепартаментыПользователя = ПарсерJSON(ТекущаяСтрока);
		КонецЕсли;
		
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Классификаторы пользователя (адреса, менеджеры, департаменты) не загружены.'"));
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КлассификаторыHeadHunter) Тогда
		Возврат;
	КонецЕсли;
	
	КлассификаторыHeadHunterНовый = Новый Структура;
	Для Каждого ЭлементСтруктуры Из КлассификаторыHeadHunter Цикл
		НовыйФиксированныйМассив = ОбщегоНазначения.ФиксированныеДанные(ЭлементСтруктуры.Значение);
		КлассификаторыHeadHunterНовый.Вставить(ЭлементСтруктуры.Ключ, НовыйФиксированныйМассив);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(СправочникСпециализацийHeadHunter) Тогда
		НовыйФиксированныйМассив = ОбщегоНазначения.ФиксированныеДанные(СправочникСпециализацийHeadHunter);
		КлассификаторыHeadHunterНовый.Вставить("specializations", НовыйФиксированныйМассив);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СправочникРегионовHeadHunter) Тогда
		НовыйФиксированныйМассив = ОбщегоНазначения.ФиксированныеДанные(СправочникРегионовHeadHunter);
		КлассификаторыHeadHunterНовый.Вставить("areas", НовыйФиксированныйМассив);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(АдресаПользователя) Тогда
		
		НовыйМассив = Новый Массив;
		Для Каждого ЭлементМассива Из АдресаПользователя.items Цикл
			
			НоваяСтруктура = СтруктураДанных();
			НоваяСтруктура.id = ЭлементМассива.id;
			НоваяСтруктура.name = ?(ЗначениеЗаполнено(ЭлементМассива.city), ЭлементМассива.city, "") 
				+ ?(ЗначениеЗаполнено(ЭлементМассива.street), ", ", "") + ?(ЗначениеЗаполнено(ЭлементМассива.street), ЭлементМассива.street, "") 
				+ ?(ЗначениеЗаполнено(ЭлементМассива.building), ", ", "") + ?(ЗначениеЗаполнено(ЭлементМассива.building), ЭлементМассива.building, ""); 
				
			Если Не ЗначениеЗаполнено(НоваяСтруктура.name) 
				И ЗначениеЗаполнено(ЭлементМассива.raw) Тогда
				
				НоваяСтруктура.name = ЭлементМассива.raw;
			КонецЕсли;
				
			НоваяФиксированнаяСтруктура = Новый ФиксированнаяСтруктура(НоваяСтруктура);
			НовыйМассив.Добавить(НоваяФиксированнаяСтруктура);
			
		КонецЦикла;
		
		НовыйФиксированныйМассив = Новый ФиксированныйМассив(НовыйМассив);
		
		КлассификаторыHeadHunterНовый.Вставить("addresses", НовыйФиксированныйМассив);
		
	КонецЕсли;

	Если ЗначениеЗаполнено(МенеджерыПользователя) Тогда
		НовыйФиксированныйМассив = ОбщегоНазначения.ФиксированныеДанные(МенеджерыПользователя.items);
		КлассификаторыHeadHunterНовый.Вставить("managers", НовыйФиксированныйМассив);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДепартаментыПользователя) Тогда
		НовыйФиксированныйМассив = ОбщегоНазначения.ФиксированныеДанные(ДепартаментыПользователя.items);
		КлассификаторыHeadHunterНовый.Вставить("departments", НовыйФиксированныйМассив);
	КонецЕсли;
	
	Периоды = Новый Массив;
	Периоды.Добавить(СтруктураДанных(0, НСтр("ru = 'За все время'")));
	Периоды.Добавить(СтруктураДанных(1, НСтр("ru = 'За последние 24 часа'")));
	Периоды.Добавить(СтруктураДанных(3, НСтр("ru = 'За последние 3 дня'")));
	Периоды.Добавить(СтруктураДанных(7, НСтр("ru = 'За последнюю неделю'")));
	Периоды.Добавить(СтруктураДанных(14, НСтр("ru = 'За последние 2 недели'")));
	Периоды.Добавить(СтруктураДанных(30, НСтр("ru = 'За последний 1 месяц'")));
	Периоды.Добавить(СтруктураДанных(60, НСтр("ru = 'За последние 2 месяца'")));
	
	КлассификаторыHeadHunterНовый.Вставить("periods", ОбщегоНазначения.ФиксированныеДанные(Периоды));
	
	КлассификаторыHeadHunterФикс = Новый ФиксированнаяСтруктура(КлассификаторыHeadHunterНовый);
	
	ТекущиеПараметры = ИнтеграцияРекрутинговыхСайтовКлиентСервер.СоответствиеПоФиксированномуСоответствию(ПараметрыСеанса.ПараметрыКлиентаНаСервере);
	ТекущиеПараметры.Вставить("КлассификаторыHeadHunter", КлассификаторыHeadHunterФикс);
	УстановитьПривилегированныйРежим(Истина);
	ПараметрыСеанса.ПараметрыКлиентаНаСервере = Новый ФиксированноеСоответствие(ТекущиеПараметры);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ЗагрузитьКлассификаторыRabota(Сайт)
	
	Если ЗначениеЗаполнено(ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыRabota")) Тогда
		Возврат;
	КонецЕсли;
	
	КлассификаторыRabota = СправочникиWebApi("Rabota");
	
	Если Не ЗначениеЗаполнено(КлассификаторыRabota) Тогда
		Возврат;
	КонецЕсли;
	
	Соединение = HTTPСоединениеRabota();
	КлассификаторыRabotaНовый = Новый Структура;
	МаркерДоступа = МаркерДоступа(ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota());
	
	Для Каждого КлючИЗначение Из КлассификаторыRabota Цикл
		
		Запрос = HTTPЗапросRabota(КлючИЗначение.Ключ, МаркерДоступа);
		Ответ = Соединение.Получить(Запрос);
		
		ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку();
		Если Ответ.КодСостояния <> 200 Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон(
				НСтр("ru = 'Ошибка загрузки классификатора ""%1"" %2: %3'"),
				КлючИЗначение.Значение,
				Ответ.КодСостояния,
				ТекущаяСтрока));
			
		Иначе
			
			ОписаниеЭлементов = ПарсерJSON(ТекущаяСтрока);
			ОписаниеЭлементовФикс = ОбщегоНазначения.ФиксированныеДанные(ОписаниеЭлементов);
			
			Если КлючИЗначение.Ключ = "offer-trades" Тогда
				
				НовоеОписаниеЭлементов = Новый Массив;
				
				Для Каждого Элемент Из ОписаниеЭлементовФикс Цикл
					Если Не Элемент.hidden Тогда
						НовоеОписаниеЭлементов.Добавить(Элемент);
					КонецЕсли;
				КонецЦикла;
				
				ОписаниеЭлементовФикс = ОбщегоНазначения.ФиксированныеДанные(НовоеОписаниеЭлементов);
				
			КонецЕсли;
			КлассификаторыRabotaНовый.Вставить(СтрЗаменить(КлючИЗначение.Ключ, "-", "_"), ОписаниеЭлементовФикс);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ВариантыОпытаРаботы = Новый Массив;
	ВариантыОпытаРаботы.Добавить(СтруктураДанных(Неопределено, НСтр("ru = 'Не имеет значения'")));
	ВариантыОпытаРаботы.Добавить(СтруктураДанных(-1, НСтр("ru = 'Без опыта'")));
	ВариантыОпытаРаботы.Добавить(СтруктураДанных(0, НСтр("ru = 'Менее года'")));
	ВариантыОпытаРаботы.Добавить(СтруктураДанных(1, НСтр("ru = 'от 1 года'")));
	
	КолВоЛет = 2;
	Пока КолВоЛет < 11 Цикл
		ВариантыОпытаРаботы.Добавить(СтруктураДанных(КолВоЛет, СтрШаблон(НСтр("ru = 'от %1 лет'"), КолВоЛет)));
		КолВоЛет = КолВоЛет + 1;
	КонецЦикла;
	
	КлассификаторыRabotaНовый.Вставить("experience", ОбщегоНазначения.ФиксированныеДанные(ВариантыОпытаРаботы));
	
	ВариантыПола = Новый Массив;
	ВариантыПола.Добавить(СтруктураДанных(Неопределено, НСтр("ru = 'Не важно'")));
	ВариантыПола.Добавить(СтруктураДанных("female", НСтр("ru = 'Женский'")));
	ВариантыПола.Добавить(СтруктураДанных("male", НСтр("ru = 'Мужской'")));
	
	КлассификаторыRabotaНовый.Вставить("gender", ОбщегоНазначения.ФиксированныеДанные(ВариантыПола));
	
	КатегорииПрав = Новый Массив;
	КатегорииПрав.Добавить(СтруктураДанных("A", "A"));
	КатегорииПрав.Добавить(СтруктураДанных("B", "B"));
	КатегорииПрав.Добавить(СтруктураДанных("C", "C"));
	КатегорииПрав.Добавить(СтруктураДанных("D", "D"));
	КатегорииПрав.Добавить(СтруктураДанных("E", "E"));
	
	КлассификаторыRabotaНовый.Вставить("driving_licence", ОбщегоНазначения.ФиксированныеДанные(КатегорииПрав));
	
	Периоды = Новый Массив;
	Периоды.Добавить(СтруктураДанных(-2005, НСтр("ru = 'За все время'")));
	Периоды.Добавить(СтруктураДанных(1, НСтр("ru = 'За последние 24 часа'")));
	Периоды.Добавить(СтруктураДанных(3, НСтр("ru = 'За последние 3 дня'")));
	Периоды.Добавить(СтруктураДанных(7, НСтр("ru = 'За последнюю неделю'")));
	Периоды.Добавить(СтруктураДанных(14, НСтр("ru = 'За последние 2 недели'")));
	Периоды.Добавить(СтруктураДанных(30, НСтр("ru = 'За последний 1 месяц'")));
	Периоды.Добавить(СтруктураДанных(60, НСтр("ru = 'За последние 2 месяца'")));
	
	КлассификаторыRabotaНовый.Вставить("periods", ОбщегоНазначения.ФиксированныеДанные(Периоды));
	
	КлассификаторыRabotaФикс = Новый ФиксированнаяСтруктура(КлассификаторыRabotaНовый);
	
	ТекущиеПараметры = ИнтеграцияРекрутинговыхСайтовКлиентСервер.СоответствиеПоФиксированномуСоответствию(ПараметрыСеанса.ПараметрыКлиентаНаСервере);
	ТекущиеПараметры.Вставить("КлассификаторыRabota", КлассификаторыRabotaФикс);
	
	УстановитьПривилегированныйРежим(Истина);
	ПараметрыСеанса.ПараметрыКлиентаНаСервере = Новый ФиксированноеСоответствие(ТекущиеПараметры);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ЗагрузитьКлассификаторыSuperJob(Сайт) 
	
	Если ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыSuperJob") <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КодировкаОтвета = "UTF-8";
	Соединение = HTTPСоединениеSuperJob();
	Запрос = HTTPЗапросSuperJob("/2.0/references/");
	Ответ = Соединение.Получить(Запрос);
	
	ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку(КодировкаОтвета);
	Если Ответ.КодСостояния <> 200 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru = 'Ошибка загрузки классификаторов %1: %2'"), 
											Ответ.КодСостояния, 
											ТекущаяСтрока));
		
	Иначе
		
		КлассификаторыSuperJob = ПарсерJSON(ТекущаяСтрока, Истина);
		
	КонецЕсли;
	
	Запрос = HTTPЗапросSuperJob("/2.0/regions/combined/");
	Ответ = Соединение.Получить(Запрос);
	
	ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку(КодировкаОтвета);
	Если Ответ.КодСостояния <> 200 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru = 'Ошибка загрузки классификатора городов %1: %2'"), 
											Ответ.КодСостояния, 
											ТекущаяСтрока));
		
	Иначе
		
		СправочникРегионовSuperJob = ПарсерJSON(ТекущаяСтрока);
		
	КонецЕсли;
	
	Запрос = HTTPЗапросSuperJob("/2.0/catalogues/");
	Ответ = Соединение.Получить(Запрос);
	
	ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку(КодировкаОтвета);
	Если Ответ.КодСостояния <> 200 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru = 'Ошибка загрузки классификатора сфер деятельности %1: %2'"),
											Ответ.КодСостояния, 
											ТекущаяСтрока));
		
	Иначе
		
		СправочникСферДеятельностиSuperJob = ПарсерJSON(ТекущаяСтрока);
		
	КонецЕсли;
	
	МаркерДоступа = МаркерДоступа(Сайт);
	
	Если ЗначениеЗаполнено(МаркерДоступа) Тогда
		
		Запрос = HTTPЗапросSuperJob("/2.0/user/list/", МаркерДоступа);
		Ответ = Соединение.Получить(Запрос);
		
		ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку(КодировкаОтвета);
		Если Ответ.КодСостояния <> 200 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
												НСтр("ru = 'Ошибка загрузки классификатора пользователей %1: %2'"), 
												Ответ.КодСостояния,
												ТекущаяСтрока));
		Иначе
			
			СправочникПользователейSuperJob = ПарсерJSON(ТекущаяСтрока, Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КлассификаторыSuperJob) Тогда
		Возврат;
	КонецЕсли;
	
	КлассификаторыSuperJobНовый = Новый Структура;
	Для Каждого ЭлементСтруктуры Из КлассификаторыSuperJob Цикл
		НоваяСтруктура = Новый Структура;
		Для Каждого ЭлементВложеннойСтруктуры Из ЭлементСтруктуры.Значение Цикл
			Если ТипЗнч(ЭлементВложеннойСтруктуры.Значение) = Тип("Строка") Тогда
				Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ЭлементВложеннойСтруктуры.Ключ) Тогда
					НоваяСтруктура.Вставить("Ключ" + ЭлементВложеннойСтруктуры.Ключ, ЭлементВложеннойСтруктуры.Значение);
				Иначе
					НоваяСтруктура.Вставить(ЭлементВложеннойСтруктуры.Ключ, ЭлементВложеннойСтруктуры.Значение);
				КонецЕсли;
			ИначеЕсли ТипЗнч(ЭлементВложеннойСтруктуры.Значение) = Тип("Структура") Тогда
				НоваяВложеннаяСтруктура = Новый Структура;
				Для Каждого ЭлементВложеннойСтруктурыВторогоУровня Из ЭлементВложеннойСтруктуры.Значение Цикл
					Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ЭлементВложеннойСтруктурыВторогоУровня.Ключ) Тогда
						НоваяВложеннаяСтруктура.Вставить("Ключ" + ЭлементВложеннойСтруктурыВторогоУровня.Ключ, ЭлементВложеннойСтруктурыВторогоУровня.Значение);
					Иначе
						НоваяВложеннаяСтруктура.Вставить(ЭлементВложеннойСтруктурыВторогоУровня.Ключ, ЭлементВложеннойСтруктурыВторогоУровня.Значение);
					КонецЕсли;
				КонецЦикла;
				НоваяВложеннаяСтруктураФикс = Новый ФиксированнаяСтруктура(НоваяВложеннаяСтруктура);
				НоваяСтруктура.Вставить(ЭлементВложеннойСтруктуры.Ключ, НоваяВложеннаяСтруктураФикс);
			КонецЕсли;
		КонецЦикла;
		НоваяСтруктураФикс = Новый ФиксированнаяСтруктура(НоваяСтруктура);
		КлассификаторыSuperJobНовый.Вставить(ЭлементСтруктуры.Ключ, НоваяСтруктураФикс);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(СправочникРегионовSuperJob) Тогда
	
		НовыйМассив = Новый Массив;
		
		Для Каждого ЭлементМассива Из СправочникРегионовSuperJob Цикл
			
			НовыйВложенныйМассив = Новый Массив;
			Если ЭлементМассива.Свойство("towns") 
				И ЭлементМассива.towns.Количество() > 0 Тогда
				
				Для Каждого Город Из ЭлементМассива.towns Цикл
								
					НоваяСтруктура = СтруктураДанныхSuperJob();
					ЗаполнитьЗначенияСвойств(НоваяСтруктура, Город);
								
					НоваяФиксированнаяСтруктура = Новый ФиксированнаяСтруктура(НоваяСтруктура);
					Если ЭлементМассива.Свойство("title") Тогда
						НовыйВложенныйМассив.Добавить(НоваяФиксированнаяСтруктура);
					Иначе
						НовыйМассив.Добавить(НоваяФиксированнаяСтруктура);
					КонецЕсли;
					
				КонецЦикла;
			КонецЕсли;
			
			Если ЭлементМассива.Свойство("regions") 
				И ЭлементМассива.regions.Количество() > 0 Тогда
				
				Для Каждого Регион Из ЭлементМассива.regions Цикл
					
					НовыйВложенныйМассивВторогоУровня = Новый Массив;
					Если Регион.Свойство("towns") 
						И Регион.towns.Количество() > 0 Тогда
						
						Для Каждого Город Из Регион.towns Цикл
							
							НоваяСтруктураТретьегоУровня = СтруктураДанныхSuperJob();
							ЗаполнитьЗначенияСвойств(НоваяСтруктураТретьегоУровня, Город);
							
							НоваяФиксированнаяСтруктураТретьегоУровня = Новый ФиксированнаяСтруктура(НоваяСтруктураТретьегоУровня);
							НовыйВложенныйМассивВторогоУровня.Добавить(НоваяФиксированнаяСтруктураТретьегоУровня);
							
						КонецЦикла;
					КонецЕсли;
					
					НовыйВложенныйФиксированныйМассивВторогоУровня = Новый ФиксированныйМассив(НовыйВложенныйМассивВторогоУровня);
					
					НоваяСтруктураВторогоУровня = Новый Структура("id, title, towns");
					ЗаполнитьЗначенияСвойств(НоваяСтруктураВторогоУровня, Регион, "id, title");
					НоваяСтруктураВторогоУровня.towns = НовыйВложенныйФиксированныйМассивВторогоУровня;
					
					НоваяФиксированнаяСтруктураВторогоУровня = Новый ФиксированнаяСтруктура(НоваяСтруктураВторогоУровня);
					НовыйВложенныйМассив.Добавить(НоваяФиксированнаяСтруктураВторогоУровня);
				КонецЦикла;
				
			КонецЕсли;
			
			НовыйВложенныйФиксированныйМассив = Новый ФиксированныйМассив(НовыйВложенныйМассив);
			
			Если ЭлементМассива.Свойство("title") Тогда
				НоваяСтруктура = Новый Структура("id, title, regions");
				ЗаполнитьЗначенияСвойств(НоваяСтруктура, ЭлементМассива, "id, title");
				НоваяСтруктура.regions = НовыйВложенныйФиксированныйМассив;
				
				НоваяФиксированнаяСтруктура = Новый ФиксированнаяСтруктура(НоваяСтруктура);
				НовыйМассив.Добавить(НоваяФиксированнаяСтруктура);
			КонецЕсли;
			
		КонецЦикла;
		
		НовыйФиксированныйМассив = Новый ФиксированныйМассив(НовыйМассив);
		
		КлассификаторыSuperJobНовый.Вставить("towns", НовыйФиксированныйМассив);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СправочникСферДеятельностиSuperJob) Тогда
		
		НовыйМассив = Новый Массив;
		Для Каждого ЭлементМассива Из СправочникСферДеятельностиSuperJob Цикл
			
			НовыйВложенныйМассив = Новый Массив;
			Для Каждого ЭлементВложенногоМассива Из ЭлементМассива.positions Цикл
				ВложеннаяСтруктура = Новый Структура("key, title");
				ЗаполнитьЗначенияСвойств(ВложеннаяСтруктура, ЭлементВложенногоМассива);
				ВложеннаяСтруктураФикс = Новый ФиксированнаяСтруктура(ВложеннаяСтруктура);
				НовыйВложенныйМассив.Добавить(ВложеннаяСтруктураФикс);
			КонецЦикла;
			НовыйВложенныйФиксированныйМассив = Новый ФиксированныйМассив(НовыйВложенныйМассив);
			
			НоваяСтруктура = Новый Структура("key, title, positions");
			ЗаполнитьЗначенияСвойств(НоваяСтруктура, ЭлементМассива, "key, title");
			НоваяСтруктура.positions = НовыйВложенныйФиксированныйМассив;
			
			НоваяФиксированнаяСтруктура = Новый ФиксированнаяСтруктура(НоваяСтруктура);
			НовыйМассив.Добавить(НоваяФиксированнаяСтруктура);
			
		КонецЦикла;
		
		НовыйФиксированныйМассив = Новый ФиксированныйМассив(НовыйМассив);
		
		КлассификаторыSuperJobНовый.Вставить("catalogues", НовыйФиксированныйМассив);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СправочникПользователейSuperJob) Тогда
		
		СтруктураПользователей = Новый Структура;
			
		Для Каждого ЭлементСтруктуры Из СправочникПользователейSuperJob.Получить("objects") Цикл
			Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ЭлементСтруктуры.Ключ) Тогда
				СтруктураПользователей.Вставить("Ключ" + ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение.Получить("firstname") + " " + ЭлементСтруктуры.Значение.Получить("lastname"));	
			Иначе
				СтруктураПользователей.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение.Получить("firstname") + " " + ЭлементСтруктуры.Значение.Получить("lastname"));	
			КонецЕсли;
		КонецЦикла;
		
		НовыйФиксированнаяСтруктура = Новый ФиксированнаяСтруктура(СтруктураПользователей);
		
		КлассификаторыSuperJobНовый.Вставить("users", НовыйФиксированнаяСтруктура);
		
	КонецЕсли;
	
	resumesubscription_kwc = Новый Структура;
	
	resumesubscription_kwc.Вставить("or", НСтр("ru = 'Хотя бы одно слово'"));
	resumesubscription_kwc.Вставить("and", НСтр("ru = 'Все слова'"));
	resumesubscription_kwc.Вставить("particular", НСтр("ru = 'Точная фраза'"));
	
	КлассификаторыSuperJobНовый.Вставить("resumesubscription_kwc", Новый ФиксированнаяСтруктура(resumesubscription_kwc));
	
	resumesubscription_rws = Новый Структура;
	
	resumesubscription_rws.Вставить("Ключ7",  НСтр("ru = 'По опыту'"));
	resumesubscription_rws.Вставить("Ключ50", НСтр("ru = 'По всему резюме'"));
	resumesubscription_rws.Вставить("Ключ60", НСтр("ru = 'По желаемой должности'"));
	resumesubscription_rws.Вставить("Ключ8",  НСтр("ru = 'По названию организации'"));
	resumesubscription_rws.Вставить("Ключ3",  НСтр("ru = 'По ключевым навыкам'"));
	resumesubscription_rws.Вставить("Ключ6",  НСтр("ru = 'По образованию и курсам'"));
	resumesubscription_rws.Вставить("Ключ2",  НСтр("ru = 'По месту проживания'"));
	
	КлассификаторыSuperJobНовый.Вставить("resumesubscription_rws", Новый ФиксированнаяСтруктура(resumesubscription_rws));
	
	driving_licence = Новый Структура;
	
	driving_licence.Вставить("A", "A");
	driving_licence.Вставить("B", "B");
	driving_licence.Вставить("C", "C");
	driving_licence.Вставить("D", "D");
	driving_licence.Вставить("E", "E");
	
	КлассификаторыSuperJobНовый.Вставить("driving_licence", Новый ФиксированнаяСтруктура(driving_licence));
	
	published = Новый Структура;
	
	published.Вставить("Ключ0", НСтр("ru = 'Закрытый доступ'"));
	published.Вставить("Ключ1", НСтр("ru = 'Открытый доступ'"));
	
	КлассификаторыSuperJobНовый.Вставить("published", Новый ФиксированнаяСтруктура(published));
	
	periods = Новый Структура;
	
	periods.Вставить("Ключ0",  НСтр("ru = 'За все время'"));
	periods.Вставить("Ключ1",  НСтр("ru = 'За последние 24 часа'"));
	periods.Вставить("Ключ3",  НСтр("ru = 'За последние 3 дня'"));
	periods.Вставить("Ключ7",  НСтр("ru = 'За последнюю неделю'"));
	periods.Вставить("Ключ14", НСтр("ru = 'За последние 2 недели'"));
	periods.Вставить("Ключ30", НСтр("ru = 'За последний 1 месяц'"));
	periods.Вставить("Ключ60", НСтр("ru = 'За последние 2 месяца'"));
	
	КлассификаторыSuperJobНовый.Вставить("periods", Новый ФиксированнаяСтруктура(periods));
	
	КлассификаторыSuperJobФикс = Новый ФиксированнаяСтруктура(КлассификаторыSuperJobНовый);
	
	ТекущиеПараметры = ИнтеграцияРекрутинговыхСайтовКлиентСервер.СоответствиеПоФиксированномуСоответствию(ПараметрыСеанса.ПараметрыКлиентаНаСервере);
	ТекущиеПараметры.Вставить("КлассификаторыSuperJob", КлассификаторыSuperJobФикс);
	УстановитьПривилегированныйРежим(Истина);
	ПараметрыСеанса.ПараметрыКлиентаНаСервере = Новый ФиксированноеСоответствие(ТекущиеПараметры);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ЗаполнитьДеревоСпециализацийHeadHunter(Дерево)
	
	КлассификаторыHeadHunter = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыHeadHunter");
		
	Если КлассификаторыHeadHunter = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Специализации = КлассификаторыHeadHunter.specializations;
	
	Для Каждого Раздел Из Специализации Цикл
		
		НоваяСтрока = Дерево.Строки.Добавить();
		НоваяСтрока.Идентификатор = Раздел.id;
		НоваяСтрока.Значение = Раздел.name;
		
		Если ЗначениеЗаполнено(Раздел.specializations) 
			И Раздел.specializations.Количество() > 0 Тогда
			
			Для Каждого Специализация Из Раздел.specializations Цикл
				
				НоваяПодчиненнаяСтрока = НоваяСтрока.Строки.Добавить();
				НоваяПодчиненнаяСтрока.Идентификатор = Специализация.id;
				НоваяПодчиненнаяСтрока.Значение = Специализация.name;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;	
	
	Дерево.Строки.Сортировать("Значение", Истина);
	
КонецПроцедуры

Процедура ЗаполнитьДеревоСпециализацийSuperJob(Дерево)
	
	КлассификаторыSuperJob = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыSuperJob");
	
	Если КлассификаторыSuperJob = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СферыДеятельности = КлассификаторыSuperJob.catalogues;
	
	Для Каждого Раздел Из СферыДеятельности Цикл
		
		НоваяСтрока = Дерево.Строки.Добавить();
		НоваяСтрока.Идентификатор = Раздел.key;
		НоваяСтрока.Значение = Раздел.title;
		
		Если ЗначениеЗаполнено(Раздел.positions) 
			И Раздел.positions.Количество() > 0 Тогда
			
			Для Каждого Специализация Из Раздел.positions Цикл
				
				НоваяПодчиненнаяСтрока = НоваяСтрока.Строки.Добавить();
				НоваяПодчиненнаяСтрока.Идентификатор = Специализация.key;
				НоваяПодчиненнаяСтрока.Значение = Специализация.title;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Дерево.Строки.Сортировать("Значение", Истина);
	
КонецПроцедуры

Процедура ЗаполнитьДеревоРегионовОбщее(Дерево)
	
	КлассификаторыОбщие = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыОбщие");
	
	Если КлассификаторыОбщие = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РегионыГорода = КлассификаторыОбщие.Получить("regions");
	ИдентификаторыСтрокДерева = Новый Соответствие;
	
	ТаблицаРегионыГорода = Новый ТаблицаЗначений;
	ТаблицаРегионыГорода.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаРегионыГорода.Колонки.Добавить("Значение", Новый ОписаниеТипов("Строка"));
	
	Для Каждого КлючИЗначение Из РегионыГорода Цикл
		НоваяСтрока = ТаблицаРегионыГорода.Добавить();
		НоваяСтрока.Идентификатор = КлючИЗначение.Ключ;
		НоваяСтрока.Значение = КлючИЗначение.Значение;
	КонецЦикла;
	
	ТаблицаРегионыГорода.Сортировать("Идентификатор");
	
	ПерваяСтрока = Истина;
	Для Каждого Строка Из ТаблицаРегионыГорода Цикл
		
		Если ПерваяСтрока Тогда
			ПерваяСтрока = Ложь;
		Иначе
			
			ИдентификаторРодителя = Лев(Строка.Идентификатор, СтрНайти(Строка.Идентификатор, ".", НаправлениеПоиска.СКонца) - 1);
			СтрокаРодителя = ИдентификаторыСтрокДерева.Получить(ИдентификаторРодителя);
			
			Если СтрокаРодителя = Неопределено Тогда
				СтрокаРодителя = Дерево;
			КонецЕсли;
			
			НоваяСтрока = СтрокаРодителя.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			ИдентификаторыСтрокДерева.Вставить(НоваяСтрока.Идентификатор, НоваяСтрока);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДеревоРегионовHeadHunter(Дерево)
	
	КлассификаторыHeadHunter = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыHeadHunter");
	
	Если КлассификаторыHeadHunter = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Страны = КлассификаторыHeadHunter.areas;
	
	Для Каждого Страна Из Страны Цикл
		
		НоваяСтрока = Дерево.Строки.Добавить();
		НоваяСтрока.Идентификатор = Страна.id;
		НоваяСтрока.Значение = Страна.name;
		
		Если Страна.areas.Количество() > 0 Тогда 
			
			Для Каждого Регион Из Страна.areas Цикл
				
				НоваяПодчиненнаяСтрока = НоваяСтрока.Строки.Добавить();
				НоваяПодчиненнаяСтрока.Идентификатор = Регион.id;
				НоваяПодчиненнаяСтрока.Значение = УдалитьУточнениеВСкобках(Регион.name);
				
				Если Регион.areas.Количество() > 0 Тогда
					
					Для Каждого Город Из Регион.areas Цикл
						
						НоваяПодчиненнаяСтрокаВторогоУровня = НоваяПодчиненнаяСтрока.Строки.Добавить();
						НоваяПодчиненнаяСтрокаВторогоУровня.Идентификатор = Город.id;
						НоваяПодчиненнаяСтрокаВторогоУровня.Значение = УдалитьУточнениеВСкобках(Город.name);
						
						Для Каждого Район Из Город.areas Цикл
							
							НоваяПодчиненнаяСтрокаТретьегоУровня = НоваяПодчиненнаяСтрокаВторогоУровня.Строки.Добавить();
							НоваяПодчиненнаяСтрокаТретьегоУровня.Идентификатор = Район.id;
							НоваяПодчиненнаяСтрокаТретьегоУровня.Значение = УдалитьУточнениеВСкобках(Район.name);
							
						КонецЦикла;
						
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Дерево.Строки.Сортировать("Значение", Истина);
	
КонецПроцедуры

Процедура ЗаполнитьДеревоРубрикRabota(Дерево)
	
	КлассификаторыRabota = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыRabota");
	
	Если КлассификаторыRabota = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Рубрики = КлассификаторыRabota.offer_trades;
	СтрокиДереваПоId = Новый Соответствие;
	
	Для Каждого Рубрика Из Рубрики Цикл
		
		СтрокаРодителя = СтрокиДереваПоId.Получить(Рубрика.parentId);
		
		Если СтрокаРодителя <> Неопределено Тогда
			НоваяСтрока = СтрокаРодителя.Строки.Добавить();
		Иначе
			НоваяСтрока = Дерево.Строки.Добавить();
		КонецЕсли;
		
		НоваяСтрока.Идентификатор = Рубрика.id;
		НоваяСтрока.Значение = Рубрика.name;
		
		СтрокиДереваПоId.Вставить(Рубрика.id, НоваяСтрока);
		
	КонецЦикла;
	
	Дерево.Строки.Сортировать("Значение", Истина);
	
КонецПроцедуры

Процедура ЗаполнитьДеревоРегионовRabota(Дерево)
	
	КлассификаторыRabota = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыRabota");
	
	Если КлассификаторыRabota = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Страны = КлассификаторыRabota.countries;
	
	IdsДоступныхСтран = Новый Массив;
	IdsДоступныхСтран.Добавить(1);
	IdsДоступныхСтран.Добавить(5);
	IdsДоступныхСтран.Добавить(2);
	IdsДоступныхСтран.Добавить(6);
	
	IdsСтрокСтран = Новый Соответствие;
	
	Для Каждого Страна Из Страны Цикл
		
		Если IdsДоступныхСтран.Найти(Страна.id) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Дерево.Строки.Добавить();
		НоваяСтрока.Идентификатор = Страна.id;
		НоваяСтрока.Значение = Страна.name;
		
		IdsСтрокСтран.Вставить(Страна.id, НоваяСтрока);
		
	КонецЦикла;
	
	Регионы = КлассификаторыRabota.regions;
	IdsРегионов = Новый Соответствие;
	IdsСтрокРодителей = Новый Соответствие;
	
	Для Каждого Регион Из Регионы Цикл
		IdsРегионов.Вставить(Регион.id, Регион);
	КонецЦикла;
	
	Для Каждого Регион Из Регионы Цикл
		
		Страна = IdsСтрокСтран.Получить(Регион.countryId);
		Если Страна = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Путь = Регион.path;
		
		Если Регион.regionTypeId <> 2 // город
			И Регион.regionTypeId <> 10 // поселок городского типа
			И Регион.regionTypeId <> 13 Тогда // станица
			Продолжить;
		КонецЕсли;
			
		Если Путь.Количество() = 1 Тогда 
			СтрокаРодителя = Страна;
		Иначе
			
			СтрокаРодителя = IdsСтрокРодителей.Получить(Путь[0]);
			
			Если СтрокаРодителя = Неопределено Тогда
				
				СтрокаРегиона = IdsРегионов.Получить(Путь[0]);
				
				СтрокаРодителя = Страна.Строки.Добавить();
				СтрокаРодителя.Идентификатор = СтрокаРегиона.id;
				СтрокаРодителя.Значение = ПолноеНазваниеРегиона(СтрокаРегиона.name, СтрокаРегиона.regionTypeId);
				
				IdsСтрокРодителей.Вставить(СтрокаРегиона.id, СтрокаРодителя);
				
			КонецЕсли;
			
		КонецЕсли;
		
		НоваяСтрока = СтрокаРодителя.Строки.Добавить();
		НоваяСтрока.Идентификатор = Регион.id;
		НоваяСтрока.Значение = ПолноеНазваниеРегиона(Регион.name, Регион.regionTypeId);
		
	КонецЦикла;
	
	Для Каждого Строка Из Дерево.Строки Цикл
		Строка.Строки.Сортировать("Значение", Истина);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДеревоРегионовSuperJob(Дерево)
	
	КлассификаторыSuperJob = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыSuperJob");
	
	Если КлассификаторыSuperJob = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Страны = КлассификаторыSuperJob.towns;
	
	Для Каждого Страна Из Страны Цикл
		
		НоваяСтрока = Дерево.Строки.Добавить();
		НоваяСтрока.Идентификатор = Формат(Страна.id, "ЧГ=0");
		НоваяСтрока.Значение = Страна.title;
		
		Если Страна.Свойство("regions") 
			И Страна.regions.Количество() > 0 Тогда 
			
			Для Каждого Регион Из Страна.regions Цикл
				
				НоваяПодчиненнаяСтрока = НоваяСтрока.Строки.Добавить();
				НоваяПодчиненнаяСтрока.Идентификатор = Регион.id;
				НоваяПодчиненнаяСтрока.Значение = Регион.title;
				
				Если Регион.Свойство("towns") 
					И Регион.towns.Количество() > 0 Тогда 
					
					Для Каждого Город Из Регион.towns Цикл
						
						НоваяПодчиненнаяСтрокаВторогоУровня = НоваяПодчиненнаяСтрока.Строки.Добавить();
						НоваяПодчиненнаяСтрокаВторогоУровня.Идентификатор = Формат(Город.id, "ЧГ=0");
						НоваяПодчиненнаяСтрокаВторогоУровня.Значение = Город.title;
						
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Дерево.Строки.Сортировать("Значение", Истина);
	
КонецПроцедуры

Процедура ЗаполнитьОбъединенноеДеревоРегионов(ДеревоОбщее, ИспользуемыеСайты)
	
	Если ИспользуемыеСайты = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ИспользуемыеСайты.Количество() = 1 Тогда
		Если ИспользуемыеСайты[0] = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
			ЗаполнитьДеревоРегионовHeadHunter(ДеревоОбщее);
		ИначеЕсли ИспользуемыеСайты[0] = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
			ЗаполнитьДеревоРегионовRabota(ДеревоОбщее);
		ИначеЕсли ИспользуемыеСайты[0] = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
			ЗаполнитьДеревоРегионовSuperJob(ДеревоОбщее);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	КлассификаторыОбщие = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыОбщие");
	КлассификаторыСайтов = КлассификаторыСайтов(ИспользуемыеСайты);
	
	Если КлассификаторыСайтов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДеревьяПоКлассификаторам = ДеревьяРегионовПоКлассификаторамСайтов(ИспользуемыеСайты, ДеревоОбщее);
	ЗаполнитьДеревоРегионовОбщее(ДеревоОбщее);
	
	ВыбираемыеСтроки = Новый Массив;
	УдаляемыеСтроки = Новый Массив;
	
	Для Каждого СтрокаДерева Из ДеревоОбщее.Строки Цикл
		ДополнитьВыбираемыеСтроки(ВыбираемыеСтроки, СтрокаДерева);
	КонецЦикла;
	
	Для Каждого СтрокаДерева Из ВыбираемыеСтроки Цикл
		
		Родитель = СтрокаДерева.Родитель;
		Если ЗначениеЗаполнено(Родитель) Тогда
			ЗначениеРодителя = Родитель.Значение;
		Иначе
			ЗначениеРодителя = "";
		КонецЕсли;
		
		Идентификаторы = ИдентификаторыЗначенияИзДеревьевПоКлассификаторам(ДеревьяПоКлассификаторам, СтрокаДерева.Значение, ЗначениеРодителя);
		
		Если Идентификаторы <> Неопределено Тогда
			СтрокаДерева.ИдентификаторДоп = ОбщегоНазначения.ФиксированныеДанные(Идентификаторы);
		Иначе
			 УдаляемыеСтроки.Добавить(СтрокаДерева);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаДерева Из УдаляемыеСтроки Цикл
		
		Если ЗначениеЗаполнено(СтрокаДерева.Родитель) Тогда
			СтрокаДерева.Родитель.Строки.Удалить(СтрокаДерева);
		Иначе
			ДеревоОбщее.Строки.Удалить(СтрокаДерева);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДополнитьВыбираемыеСтроки(ВыбираемыеСтроки, ТекущаяСтрока)
	
	Если ТекущаяСтрока.Строки.Количество() = 0 Тогда
		ВыбираемыеСтроки.Добавить(ТекущаяСтрока);
	Иначе
		Для Каждого ПодСтрока Из ТекущаяСтрока.Строки Цикл
			ДополнитьВыбираемыеСтроки(ВыбираемыеСтроки, ПодСтрока);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ПубликацияВакансии(Сайт, Дерево)
	
	Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() 
		Или Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.RabotaMail() Тогда
		Возврат ПубликацияВакансииHeadHunter(Дерево, Сайт);
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
		Возврат ПубликацияВакансииRabota(Дерево);
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
		Возврат ПубликацияВакансииSuperJob(Дерево);
	КонецЕсли;
	
КонецФункции

Функция ОбновлениеВакансииНаСайте(Вакансия, Сайт, Дерево)
	
	Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() 
		Или Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.RabotaMail() Тогда
		ИдентификаторВакансии = ИдентификаторВакансии(Вакансия, Сайт);
		Если Не ЗначениеЗаполнено(ИдентификаторВакансии) Тогда
			Возврат Ложь;
		Иначе
			Возврат ОбновлениеВакансииHeadHunter(ИдентификаторВакансии, Дерево, Сайт);
		КонецЕсли;
		
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
		ИдентификаторВакансии = ИдентификаторВакансии(Вакансия, Сайт);
		Если Не ЗначениеЗаполнено(ИдентификаторВакансии) Тогда
			Возврат Ложь;
		Иначе
			Возврат ОбновлениеВакансииRabota(ИдентификаторВакансии, Дерево);
		КонецЕсли;
		
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
		ИдентификаторВакансии = ИдентификаторВакансии(Вакансия, Сайт);
		Если Не ЗначениеЗаполнено(ИдентификаторВакансии) Тогда
			Возврат Ложь;
		Иначе
			Возврат ОбновлениеВакансииSuperJob(ИдентификаторВакансии, Дерево);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция ПубликацияВакансииHeadHunter(Дерево, Сайт)
	
	Возврат ИнтеграцияРекрутинговыхСайтовВнутренний.ПубликацияВакансииHeadHunter(Дерево, Сайт);
	
КонецФункции

Функция ПубликацияВакансииRabota(Дерево)
	
	Возврат ИнтеграцияРекрутинговыхСайтовВнутренний.ПубликацияВакансииRabota(Дерево);
	
КонецФункции

Функция ПубликацияВакансииSuperJob(Дерево)
	
	Возврат ИнтеграцияРекрутинговыхСайтовВнутренний.ПубликацияВакансииSuperJob(Дерево);
	
КонецФункции

Функция ОбновлениеВакансииHeadHunter(ИдентификаторВакансии, Дерево, Сайт)
	
	Возврат ИнтеграцияРекрутинговыхСайтовВнутренний.ОбновлениеВакансииHeadHunter(ИдентификаторВакансии, Дерево, Сайт);
	
КонецФункции

Функция ОбновлениеВакансииRabota(ИдентификаторВакансии, Дерево)
	
	Возврат ИнтеграцияРекрутинговыхСайтовВнутренний.ОбновлениеВакансииRabota(ИдентификаторВакансии, Дерево);
	
КонецФункции

Функция ОбновлениеВакансииSuperJob(ИдентификаторВакансии, Дерево)
	
	Возврат ИнтеграцияРекрутинговыхСайтовВнутренний.ОбновлениеВакансииSuperJob(ИдентификаторВакансии, Дерево);
	
КонецФункции

Функция JSONОписаниеВакансииHeadHunter(Дерево, Сайт, Обновление = Ложь) Экспорт
	
	СтрокаJSON = "";
	
	СтруктураОписанияВакансииHeadHunter = СтруктураОписанияВакансииHeadHunter(Дерево, Сайт, Обновление);
	СтрокаJSON = ЗаписьJSON(СтруктураОписанияВакансииHeadHunter);
	
	Возврат СтрокаJSON;
	
КонецФункции

Функция СтруктураОписанияВакансииHeadHunter(Дерево, Сайт, Обновление = Ложь)
	
	ПоляВакансии = Новый Структура;
	ПоляСТекстомВHTML = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ПоляВакансииСТекстомВHTMLHeadHunter();
	
	Для Каждого СтрокаДерева Из Дерево.Строки Цикл
		Если ТипЗнч(СтрокаДерева.Показатель) = Тип("СправочникСсылка.Вакансии") Тогда
			Для Каждого СтрокаДереваПодчиненная Из СтрокаДерева.Строки Цикл
				Если СтрокаДереваПодчиненная.ТипРеквизита = "Строка" Тогда
					Если Обновление 
						И Не СтрокаДереваПодчиненная.СтрокаИзменена Тогда
						Продолжить;
					КонецЕсли;
					
					Если ЗначениеЗаполнено(СтрокаДереваПодчиненная.ПубликацияВакансии) Тогда
						
						Если ПоляСТекстомВHTML.Найти(СтрокаДереваПодчиненная.Путь) <> Неопределено Тогда
							ЗначениеПоля = СтрокаДереваПодчиненная.ПубликацияВакансииТекст;
						Иначе
							ЗначениеПоля = СтрокаДереваПодчиненная.ПубликацияВакансии;
						КонецЕсли;
						
						ПоляВакансии.Вставить(СтрокаДереваПодчиненная.Путь, ЗначениеПоля);
						
					КонецЕсли;
					
				ИначеЕсли СтрокаДереваПодчиненная.ТипРеквизита = "Булево" Тогда
					Если Обновление 
						И Не СтрокаДереваПодчиненная.СтрокаИзменена Тогда
						Продолжить;
					КонецЕсли;
					
					ПоляВакансии.Вставить(СтрокаДереваПодчиненная.Путь, СтрокаДереваПодчиненная.ПубликацияВакансии);
					
				ИначеЕсли СтрокаДереваПодчиненная.ТипРеквизита = "Начало объекта" Тогда
					НомерВхожденияТочки = ИнтеграцияРекрутинговыхСайтовКлиентСервер.НайтиВСтроке(СтрокаДереваПодчиненная.Путь, ".");
					Если НомерВхожденияТочки > 0 Тогда
						Если Обновление 
							И Не СтрокаДереваПодчиненная.СтрокаИзменена Тогда
							Продолжить;
						КонецЕсли;
						
						ИмяОбъекта = Лев(СтрокаДереваПодчиненная.Путь, НомерВхожденияТочки - 1);
						РеквизитОбъекта = Сред(СтрокаДереваПодчиненная.Путь, НомерВхожденияТочки + 1, СтрДлина(СтрокаДереваПодчиненная.Путь) - НомерВхожденияТочки);
						
						СтруктураОбъекта = Новый Структура;
						Если ТипЗнч(СтрокаДереваПодчиненная.ПубликацияВакансии) = Тип("СписокЗначений") 
							И СтрокаДереваПодчиненная.ПубликацияВакансии.Количество() = 1 
							И ЗначениеЗаполнено(СтрокаДереваПодчиненная.ПубликацияВакансии[0].Значение) Тогда
							
							СтруктураОбъекта.Вставить(РеквизитОбъекта, СтрокаДереваПодчиненная.ПубликацияВакансии[0].Значение);
							ПоляВакансии.Вставить(ИмяОбъекта, СтруктураОбъекта);
						КонецЕсли;
						
					Иначе
						
						ИмяОбъекта = СтрокаДереваПодчиненная.Путь;
						
						СтруктураОбъекта = Новый Структура;
						ОбъектИзменен = Ложь;
						Для Каждого СтрокаОбъекта Из СтрокаДереваПодчиненная.Строки Цикл
							
							ОбъектИзменен = ОбъектИзменен Или СтрокаОбъекта.СтрокаИзменена;
							
							НомерВхожденияТочки = ИнтеграцияРекрутинговыхСайтовКлиентСервер.НайтиВСтроке(СтрокаОбъекта.Путь, ".");
							Если НомерВхожденияТочки > 0 Тогда
								РеквизитОбъекта = Сред(СтрокаОбъекта.Путь, НомерВхожденияТочки + 1, СтрДлина(СтрокаОбъекта.Путь) - НомерВхожденияТочки);
							Иначе	
								РеквизитОбъекта = СтрокаОбъекта.Путь;
							КонецЕсли;
							
							Если ТипЗнч(СтрокаОбъекта.ПубликацияВакансии) = Тип("СписокЗначений") Тогда 
								Если СтрокаОбъекта.ПубликацияВакансии.Количество() = 1 
									И ЗначениеЗаполнено(СтрокаОбъекта.ПубликацияВакансии[0].Значение) Тогда
									
									Если ТипЗнч(СтрокаОбъекта.ПубликацияВакансии[0].Значение) = Тип("Структура") Тогда
										
										МассивОбъектов = Новый Массив;
										МассивОбъектов.Добавить(СтрокаОбъекта.ПубликацияВакансии[0].Значение);
										СтруктураОбъекта.Вставить(РеквизитОбъекта, МассивОбъектов);
										
									Иначе
										СтруктураОбъекта.Вставить(РеквизитОбъекта, СтрокаОбъекта.ПубликацияВакансии[0].Значение);
									КонецЕсли;
									
								ИначеЕсли  СтрокаОбъекта.ПубликацияВакансии.Количество() > 1 Тогда
									
									МассивОбъектов = Новый Массив;
									Для Каждого ЭлементСписка Из СтрокаОбъекта.ПубликацияВакансии Цикл
										
										Если Тип(ЭлементСписка.Значение) = Тип("Структура") Тогда
											МассивОбъектов.Добавить(ЭлементСписка.Значение);
										КонецЕсли;
										
									КонецЦикла;
									
									СтруктураОбъекта.Вставить(РеквизитОбъекта, МассивОбъектов);
								КонецЕсли;
							Иначе
								СтруктураОбъекта.Вставить(РеквизитОбъекта, СтрокаОбъекта.ПубликацияВакансии);
							КонецЕсли;
							
						КонецЦикла;
						
						Если Обновление 
							И Не ОбъектИзменен Тогда
							Продолжить;
						КонецЕсли;
						
						ПоляВакансии.Вставить(ИмяОбъекта, СтруктураОбъекта);
						
					КонецЕсли;
					
				ИначеЕсли СтрокаДереваПодчиненная.ТипРеквизита = "Начало массива" Тогда
					Если Обновление
						И Не СтрокаДереваПодчиненная.СтрокаИзменена Тогда
						Продолжить;
					КонецЕсли;
					
					МассивЭлементов = Новый Массив;
					НомерВхожденияТочки = ИнтеграцияРекрутинговыхСайтовКлиентСервер.НайтиВСтроке(СтрокаДереваПодчиненная.Путь, ".");
					Если НомерВхожденияТочки > 0 Тогда
						ИмяМассива = Лев(СтрокаДереваПодчиненная.Путь, НомерВхожденияТочки - 1);
						ИмяЭлементаМассива = Сред(СтрокаДереваПодчиненная.Путь, НомерВхожденияТочки + 1, СтрДлина(СтрокаДереваПодчиненная.Путь) - НомерВхожденияТочки);
						
						Если ТипЗнч(СтрокаДереваПодчиненная.ПубликацияВакансии) = Тип("СписокЗначений") 
							И СтрокаДереваПодчиненная.ПубликацияВакансии.Количество() > 0 Тогда
							
							Для Каждого ЭлементСписка Из СтрокаДереваПодчиненная.ПубликацияВакансии Цикл
								Если ЗначениеЗаполнено(ЭлементСписка.Значение) Тогда
									СтруктураЭлементаМассива = Новый Структура;
									СтруктураЭлементаМассива.Вставить(ИмяЭлементаМассива, ЭлементСписка.Значение);
									
									МассивЭлементов.Добавить(СтруктураЭлементаМассива);
								КонецЕсли;
							КонецЦикла;
						КонецЕсли;
						
						ПоляВакансии.Вставить(ИмяМассива, МассивЭлементов);
						
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			Если Не Обновление Тогда
				СтруктураИнформацииОРаботодателе = Новый Структура;
				СтруктураИнформацииОРаботодателе.Вставить("id", ИдентификаторПользователяИзРегистра(ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter()));
				ПоляВакансии.Вставить("employer", СтруктураИнформацииОРаботодателе);
			КонецЕсли;
			
			Если Не Обновление Тогда
				СтруктураИнформацииОСайте = Новый Структура;
				ПредставлениеСайта = "";
				Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
					ПредставлениеСайта = "hh";
				ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.RabotaMail() Тогда
					ПредставлениеСайта = "rmr";
				КонецЕсли;
				СтруктураИнформацииОСайте.Вставить("id", ПредставлениеСайта);
				ПоляВакансии.Вставить("site", СтруктураИнформацииОСайте);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПоляВакансии;
	
КонецФункции

Функция JSONОписаниеВакансииRabota(Дерево, Обновление = Ложь) Экспорт
	
	СтрокаJSON = "";
	
	СтруктураОписанияВакансииRabota = СтруктураОписанияВакансииRabota(Дерево, Обновление);
	СтрокаJSON = ЗаписьJSON(СтруктураОписанияВакансииRabota);
	
	Возврат СтрокаJSON;
	
КонецФункции

Функция СтруктураОписанияВакансииRabota(Дерево, Обновление = Ложь)
	
	ПоляВакансии = Новый Структура;
	ПоляСТекстомВHTML = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ПоляВакансииСТекстомВHTMLRabota();
	КлассификаторыRabota = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыRabota");
	Представительства = КлассификаторыRabota.representations;
	
	Если Обновление Тогда
		ПоляВакансии.Вставить("action", "edit");
	КонецЕсли;
	
	Для Каждого СтрокаДерева Из Дерево.Строки Цикл
		
		Если ТипЗнч(СтрокаДерева.Показатель) <> Тип("СправочникСсылка.Вакансии") Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого СтрокаДереваПодчиненная Из СтрокаДерева.Строки Цикл
			
			ИмяПоля = СтрокаДереваПодчиненная.Путь;
			ИмяПоля = СтрЗаменить(ИмяПоля, "language", "languages");
			
			Если СтрокаДереваПодчиненная.ТипРеквизита <> "Начало объекта"
				И СтрокаДереваПодчиненная.ПубликацияВакансии = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрокаДереваПодчиненная.ТипРеквизита = "Строка" Тогда
				
				Если ПоляСТекстомВHTML.Найти(ИмяПоля) <> Неопределено Тогда
					ЗначениеПоля = СтрокаДереваПодчиненная.ПубликацияВакансииТекст;
				Иначе
					ЗначениеПоля = СтрокаДереваПодчиненная.ПубликацияВакансии;
				КонецЕсли;
				
				ПоляВакансии.Вставить(ИмяПоля, ЗначениеПоля);
				
			ИначеЕсли СтрокаДереваПодчиненная.ТипРеквизита = "Булево" 
				Или СтрокаДереваПодчиненная.ТипРеквизита = "Число" Тогда
				
				ПоляВакансии.Вставить(ИмяПоля, СтрокаДереваПодчиненная.ПубликацияВакансии);
				
			ИначеЕсли СтрокаДереваПодчиненная.ТипРеквизита = "Начало объекта" Тогда
				
				СвойстваОбъекта = СтрокаДереваПодчиненная.Строки;
				
				Если ИмяПоля = "additionalInformation" Тогда
					
					Для Каждого Свойство Из СвойстваОбъекта Цикл
						
						ЗначениеСвойства = ЗначениеСвойстваОбъекта(Свойство);
						
						Если ЗначениеЗаполнено(ЗначениеСвойства) Тогда
							ИмяСвойства = Сред(Свойство.Путь, СтрДлина(ИмяПоля) + 2);
							ПоляВакансии.Вставить(ИмяСвойства, ЗначениеСвойства);
						КонецЕсли;
						
					КонецЦикла;
					
				ИначеЕсли СвойстваОбъекта.Количество() > 0 Тогда
					
					СтруктураСвойств = Новый Структура;
					Для Каждого Свойство Из СвойстваОбъекта Цикл
						
						ЗначениеСвойства = ЗначениеСвойстваОбъекта(Свойство);
						
						Если ЗначениеЗаполнено(ЗначениеСвойства) Тогда
							ИмяСвойства = Сред(Свойство.Путь, СтрДлина(ИмяПоля) + 2);
							СтруктураСвойств.Вставить(ИмяСвойства, ЗначениеСвойства);
						КонецЕсли;
						
					КонецЦикла;
					
					Если ЗначениеЗаполнено(СтруктураСвойств) Тогда
						ПоляВакансии.Вставить(ИмяПоля, СтруктураСвойств);
					КонецЕсли;
					
				Иначе
					
					Если ТипЗнч(СтрокаДереваПодчиненная.ПубликацияВакансии) = Тип("СписокЗначений") Тогда 
						ЗначениеПоля = СтрокаДереваПодчиненная.ПубликацияВакансии[0].Значение;
					Иначе
						ЗначениеПоля = СтрокаДереваПодчиненная.ПубликацияВакансии;
					КонецЕсли;
					
					Если ЗначениеЗаполнено(ЗначениеПоля) Тогда
						ПоляВакансии.Вставить(ИмяПоля, ЗначениеПоля);
					КонецЕсли;
					
				КонецЕсли;
				
			ИначеЕсли СтрокаДереваПодчиненная.ТипРеквизита = "Начало массива" Тогда
				
				МассивЭлементов = Новый Массив;
				Для Каждого ЭлементСписка Из СтрокаДереваПодчиненная.ПубликацияВакансии Цикл
					
					Если ЗначениеЗаполнено(ЭлементСписка.Значение) Тогда
						
						Если ИмяПоля = "address" Тогда
							
							ВыбранноеЗначение = ЭлементСписка.Значение;
							ДанныеДляПередачи = Новый Структура("name,region,address,comment,metro");
							
							Для Каждого ДанныеПредставительства Из Представительства Цикл
								
								Если ВыбранноеЗначение = ДанныеПредставительства.id Тогда
									
									ЗаполнитьЗначенияСвойств(ДанныеДляПередачи, ДанныеПредставительства, "name, address, comment");
									ДанныеДляПередачи.region = ДанныеПредставительства.region.id;
									
									СтанцииМетро = Новый Массив;
									Для Каждого Метро Из ДанныеПредставительства.metro Цикл
										СтанцииМетро.Добавить(Метро.id);
										ДанныеДляПередачи.metro = СтанцииМетро;
									КонецЦикла;
									
									Прервать;
									
								КонецЕсли;
								
							КонецЦикла;
								
							ДанныеДляПередачи.Вставить("representation", ВыбранноеЗначение);
							
							МассивЭлементов.Добавить(ДанныеДляПередачи);
							
						ИначеЕсли ИмяПоля = "languages" Тогда
							
							ДанныеДляПередачи = Новый Структура;
							ДанныеДляПередачи.Вставить("offerLanguage", ЭлементСписка.Значение.offerLanguage.id);
							ДанныеДляПередачи.Вставить("offerLanguageLevel", ЭлементСписка.Значение.offerLanguageLevel.id);
							
							МассивЭлементов.Добавить(ДанныеДляПередачи);
							
						Иначе
							МассивЭлементов.Добавить(ЭлементСписка.Значение);
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЦикла;
				
				ПоляВакансии.Вставить(ИмяПоля, МассивЭлементов);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ПоляВакансии;
	
КонецФункции

Функция ОписаниеВакансииДляПубликацииSuperJob(Дерево) Экспорт 
	
	ОписаниеВакансииДляПубликацииSuperJob = "";
	ПоляСМногострочнымВводом = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ПоляВакансииСМногострочнымВводомSuperJob();
	
	Для Каждого СтрокаДерева Из Дерево.Строки Цикл
	
		Если ТипЗнч(СтрокаДерева.Показатель) = Тип("СправочникСсылка.Вакансии") Тогда
			Для Каждого СтрокаДереваПодчиненная Из СтрокаДерева.Строки Цикл
				
				Если Не ЗначениеЗаполнено(СтрокаДереваПодчиненная.ПубликацияВакансии) Тогда
					Продолжить;
				КонецЕсли;
				
				Если ТипЗнч(СтрокаДереваПодчиненная.ПубликацияВакансии) = Тип("Строка") Тогда
					
					Если ПоляСМногострочнымВводом.Найти(ТипЗнч(СтрокаДереваПодчиненная.Путь)) <> Неопределено Тогда
						ЗначениеПоля = СтрокаДереваПодчиненная.ПубликацияВакансииТекст;
					Иначе
						ЗначениеПоля = СтрокаДереваПодчиненная.ПубликацияВакансии;
					КонецЕсли;
					
					ОписаниеВакансииДляПубликацииSuperJob = ОписаниеВакансииДляПубликацииSuperJob + СтрокаДереваПодчиненная.Путь + "=" + ЗначениеПоля + "&";
					
				ИначеЕсли ТипЗнч(СтрокаДереваПодчиненная.ПубликацияВакансии) = Тип("Число") Тогда
					ОписаниеВакансииДляПубликацииSuperJob = ОписаниеВакансииДляПубликацииSuperJob + СтрокаДереваПодчиненная.Путь + "=" + Формат(СтрокаДереваПодчиненная.ПубликацияВакансии, "ЧГ=0") + "&";
				ИначеЕсли ТипЗнч(СтрокаДереваПодчиненная.ПубликацияВакансии) = Тип("Булево") Тогда
					ОписаниеВакансииДляПубликацииSuperJob = ОписаниеВакансииДляПубликацииSuperJob + СтрокаДереваПодчиненная.Путь + "=" + ?(СтрокаДереваПодчиненная.ПубликацияВакансии = Истина, 1, 0) + "&";
				ИначеЕсли ТипЗнч(СтрокаДереваПодчиненная.ПубликацияВакансии) = Тип("СписокЗначений") Тогда
					Если СтрокаДереваПодчиненная.ТипРеквизита = "Начало объекта" Тогда
						ОписаниеВакансииДляПубликацииSuperJob = ОписаниеВакансииДляПубликацииSuperJob + СтрокаДереваПодчиненная.Путь + "=" + СтрЗаменить(СтрокаДереваПодчиненная.ПубликацияВакансии[0].Значение, "Ключ", "") + "&";		
					ИначеЕсли СтрокаДереваПодчиненная.ТипРеквизита = "Начало массива" Тогда
						Индекс = 0;
						Для Каждого Элемент Из СтрокаДереваПодчиненная.ПубликацияВакансии Цикл
							Если ТипЗнч(Элемент.Значение) = Тип("Строка") Тогда
								ОписаниеВакансииДляПубликацииSuperJob = ОписаниеВакансииДляПубликацииSuperJob + СтрокаДереваПодчиненная.Путь + "[" + Индекс + "]" + "=" + СтрЗаменить(Элемент.Значение, "Ключ", "") + "&";				
							ИначеЕсли ТипЗнч(Элемент.Значение) = Тип("Структура") Тогда
								Если СтрокаДереваПодчиненная.Путь = "phones" Тогда
									Для Каждого ЭлементСтруктуры Из Элемент.Значение Цикл
										ОписаниеВакансииДляПубликацииSuperJob = ОписаниеВакансииДляПубликацииSuperJob + СтрокаДереваПодчиненная.Путь + "[" + Индекс + "]" + "[" + ЭлементСтруктуры.Ключ + "]" + "=" + ЭлементСтруктуры.Значение + "&";					
									КонецЦикла;
								ИначеЕсли СтрокаДереваПодчиненная.Путь = "languages" Тогда
									Для Каждого ЭлементСтруктуры Из Элемент.Значение Цикл
										ОписаниеВакансииДляПубликацииSuperJob = ОписаниеВакансииДляПубликацииSuperJob + СтрокаДереваПодчиненная.Путь + "[" + Индекс + "]" + "[" + ЭлементСтруктуры.Ключ + "]" + "=" + СтрЗаменить(ЭлементСтруктуры.Значение.id, "Ключ", "") + "&";					
									КонецЦикла;
								КонецЕсли;
							КонецЕсли;
							Индекс = Индекс + 1;
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ПустаяСтрока(ОписаниеВакансииДляПубликацииSuperJob) Тогда
		ОписаниеВакансииДляПубликацииSuperJob = Лев(ОписаниеВакансииДляПубликацииSuperJob, СтрДлина(ОписаниеВакансииДляПубликацииSuperJob) - 1);
	КонецЕсли;
	
	Возврат ОписаниеВакансииДляПубликацииSuperJob;
	
КонецФункции

Процедура СохранитьНатройкиАвторизации(Сайт, СтруктураМаркеров, ИмяПользователя = Неопределено, Пароль = Неопределено)
	
	НастройкиИнтеграцииРекрутинговыхСайтовМенеджерЗаписи = РегистрыСведений.НастройкиИнтеграцииРекрутинговыхСайтов.СоздатьМенеджерЗаписи();
	
	НастройкиИнтеграцииРекрутинговыхСайтовМенеджерЗаписи.Пользователь = Пользователи.ТекущийПользователь();
	НастройкиИнтеграцииРекрутинговыхСайтовМенеджерЗаписи.РекрутинговыйСайт = Сайт;
	НастройкиИнтеграцииРекрутинговыхСайтовМенеджерЗаписи.Прочитать();
	
	Если Не НастройкиИнтеграцииРекрутинговыхСайтовМенеджерЗаписи.Выбран() Тогда
		Если Не ЗначениеЗаполнено(ИмяПользователя) 
			И Не ЗначениеЗаполнено(Пароль) Тогда
			
			Возврат;
		Иначе
			НастройкиИнтеграцииРекрутинговыхСайтовМенеджерЗаписи.РекрутинговыйСайт = Сайт;
			НастройкиИнтеграцииРекрутинговыхСайтовМенеджерЗаписи.Пользователь = Пользователи.ТекущийПользователь();
			НастройкиИнтеграцииРекрутинговыхСайтовМенеджерЗаписи.Использовать = Истина;
			НастройкиИнтеграцииРекрутинговыхСайтовМенеджерЗаписи.ИмяПользователя = ИмяПользователя;
			
			УстановитьПривилегированныйРежим(Истина);
			Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
				ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(Пользователи.ТекущийПользователь(), Пароль, "ПарольRabota");
			ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
				ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(Пользователи.ТекущийПользователь(), Пароль, "ПарольSuperJob");
			КонецЕсли;
			УстановитьПривилегированныйРежим(Ложь);

		КонецЕсли;
	КонецЕсли;
	
	Если Не НастройкиИнтеграцииРекрутинговыхСайтовМенеджерЗаписи.Использовать Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИмяПользователя) Тогда
		НастройкиИнтеграцииРекрутинговыхСайтовМенеджерЗаписи.ИмяПользователя = ИмяПользователя;
	КонецЕсли;
	
	Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
		НастройкиИнтеграцииРекрутинговыхСайтовМенеджерЗаписи.МаркерДоступа = СтруктураМаркеров.sessionId;
		НастройкиИнтеграцииРекрутинговыхСайтовМенеджерЗаписи.ДатаОкончанияДействияМаркераДоступа = ТекущаяДатаСеанса() + 1800;
	Иначе
		НастройкиИнтеграцииРекрутинговыхСайтовМенеджерЗаписи.МаркерДоступа = СтруктураМаркеров.access_token;
		НастройкиИнтеграцииРекрутинговыхСайтовМенеджерЗаписи.МаркерОбновления = СтруктураМаркеров.refresh_token;
		НастройкиИнтеграцииРекрутинговыхСайтовМенеджерЗаписи.ДатаОкончанияДействияМаркераДоступа = ТекущаяДатаСеанса() + СтруктураМаркеров.expires_in;
	КонецЕсли;
	
	Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
		ИнформацияОПользователе = ИнформацияОПользователе(Сайт, НастройкиИнтеграцииРекрутинговыхСайтовМенеджерЗаписи.МаркерДоступа);
		НастройкиИнтеграцииРекрутинговыхСайтовМенеджерЗаписи.ИдентификаторПользователя = ИнформацияОПользователе.ИдентификаторКомпании;
	КонецЕсли;
	
	НастройкиИнтеграцииРекрутинговыхСайтовМенеджерЗаписи.Записать();
	
КонецПроцедуры

Функция ИнформацияОПользователе(Сайт, МаркерДоступа)
	
	СтруктураИнформацияОПользователе = СтруктураИнформацияОПользователе();
	Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
		
		ИнформацияОПользователе = ИнформацияОПользователеHeadHunter(МаркерДоступа);
		Если ЗначениеЗаполнено(ИнформацияОПользователе) Тогда
			СтруктураИнформацияОПользователе.ИдентификаторКомпании = ИнформацияОПользователе.employer.id;
			СтруктураИнформацияОПользователе.ИдентификаторПользователя = ИнформацияОПользователе.employer.manager_id;
		КонецЕсли;
		
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
		
		ИнформацияОПользователе = ИнформацияОПользователеSuperJob(МаркерДоступа);
		Если ЗначениеЗаполнено(ИнформацияОПользователе) Тогда
			
			СтруктураИнформацияОПользователе.ИдентификаторКомпании = Формат(ИнформацияОПользователе.id_client, "ЧГ=0");
			СтруктураИнформацияОПользователе.ИдентификаторПользователя = Формат(ИнформацияОПользователе.id, "ЧГ=0");
			
			Для Каждого Тариф Из ИнформацияОПользователе.tariffs Цикл
				
				Если Тариф.is_unlimited Тогда
					СтруктураИнформацияОПользователе.БезлимитныйТариф = Истина;
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтруктураИнформацияОПользователе;
	
КонецФункции

Функция ИнформацияОПользователеHeadHunter(МаркерДоступа, СообщатьОбОшибке = Истина)
	
	ИнформацияОПользователе = Неопределено;
	
	Соединение = HTTPСоединениеHeadHunter();
	Запрос = HTTPЗапросHeadHunter("/me", МаркерДоступа);
	Ответ = Соединение.Получить(Запрос);
	
	ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку("UTF-8");
	
	Если Ответ.КодСостояния = 200 Тогда
		ИнформацияОПользователе = ПарсерJSON(ТекущаяСтрока);
	ИначеЕсли СообщатьОбОшибке Тогда
		СообщитьОбОшибкеПолученияДанныхПользователя(Ответ.КодСостояния, ТекущаяСтрока);
	КонецЕсли;
	
	Возврат ИнформацияОПользователе;
	
КонецФункции

Функция ИнформацияОПользователеSuperJob(МаркерДоступа, СообщатьОбОшибке = Истина)
	
	ИнформацияОПользователе = Неопределено;
	
	Соединение = HTTPСоединениеSuperJob();
	Запрос = HTTPЗапросSuperJob("/2.0/user/current/", МаркерДоступа);
	Ответ = Соединение.Получить(Запрос);
	
	ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку("UTF-8");
	
	Если Ответ.КодСостояния = 200 Тогда
		ИнформацияОПользователе = ПарсерJSON(ТекущаяСтрока);
	ИначеЕсли СообщатьОбОшибке Тогда
		СообщитьОбОшибкеПолученияДанныхПользователя(Ответ.КодСостояния, ТекущаяСтрока);
	КонецЕсли;
	
	Возврат ИнформацияОПользователе;
	
КонецФункции

Функция ИнформацияОбАдресахКомпанииRabota(МаркерДоступа)
	
	ИнформацияОбАдресах = Неопределено;
	
	Соединение = HTTPСоединениеRabota();
	Запрос = HTTPЗапросRabota("representations", МаркерДоступа);
	Ответ = Соединение.Получить(Запрос);
	
	ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку();
	
	Если Ответ.КодСостояния = 200 Тогда
		ИнформацияОбАдресах = ПарсерJSON(ТекущаяСтрока);
	КонецЕсли;
	
	Возврат ИнформацияОбАдресах;
	
КонецФункции

Функция ИдентификаторПользователяИзРегистра(Сайт)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиИнтеграцииРекрутинговыхСайтов.Пользователь,
	|	НастройкиИнтеграцииРекрутинговыхСайтов.ИдентификаторПользователя
	|ИЗ
	|	РегистрСведений.НастройкиИнтеграцииРекрутинговыхСайтов КАК НастройкиИнтеграцииРекрутинговыхСайтов
	|ГДЕ
	|	НастройкиИнтеграцииРекрутинговыхСайтов.Пользователь = &Пользователь
	|	И НастройкиИнтеграцииРекрутинговыхСайтов.РекрутинговыйСайт = &РекрутинговыйСайт
	|	И НастройкиИнтеграцииРекрутинговыхСайтов.Использовать";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	Запрос.УстановитьПараметр("РекрутинговыйСайт", Сайт);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ИдентификаторПользователя;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция МаркерДоступа(Сайт) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиИнтеграцииРекрутинговыхСайтов.Пользователь,
		|	НастройкиИнтеграцииРекрутинговыхСайтов.МаркерДоступа
		|ИЗ
		|	РегистрСведений.НастройкиИнтеграцииРекрутинговыхСайтов КАК НастройкиИнтеграцииРекрутинговыхСайтов
		|ГДЕ
		|	НастройкиИнтеграцииРекрутинговыхСайтов.Пользователь = &Пользователь
		|	И НастройкиИнтеграцииРекрутинговыхСайтов.РекрутинговыйСайт = &РекрутинговыйСайт
		|	И НастройкиИнтеграцииРекрутинговыхСайтов.Использовать";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	Запрос.УстановитьПараметр("РекрутинговыйСайт", Сайт);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.МаркерДоступа;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ПроверитьВалидностьМаркера(Сайт, МаркерДоступа)
	
	Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
		Возврат ЗначениеЗаполнено(ИнформацияОПользователеHeadHunter(МаркерДоступа, Ложь));
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
		Возврат ЗначениеЗаполнено(ИнформацияОбАдресахКомпанииRabota(МаркерДоступа));
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
		Возврат ЗначениеЗаполнено(ИнформацияОПользователеSuperJob(МаркерДоступа, Ложь));
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ИдентификаторВакансии(Вакансия, Сайт)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПубликацияВакансий.ИдентификаторВакансии,
	|	ПубликацияВакансий.Вакансия
	|ИЗ
	|	РегистрСведений.ПубликацияВакансий КАК ПубликацияВакансий
	|ГДЕ
	|	ПубликацияВакансий.Вакансия = &Вакансия
	|	И ПубликацияВакансий.МестоПубликации = &МестоПубликации";
	
	Запрос.УстановитьПараметр("Вакансия", Вакансия);
	Запрос.УстановитьПараметр("МестоПубликации", Сайт);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ИдентификаторВакансии;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ПараметрыАвторизации(Сайт)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиИнтеграцииРекрутинговыхСайтов.ИмяПользователя
	|ИЗ
	|	РегистрСведений.НастройкиИнтеграцииРекрутинговыхСайтов КАК НастройкиИнтеграцииРекрутинговыхСайтов
	|ГДЕ
	|	НастройкиИнтеграцииРекрутинговыхСайтов.Пользователь = &Пользователь
	|	И НастройкиИнтеграцииРекрутинговыхСайтов.РекрутинговыйСайт = &РекрутинговыйСайт
	|	И НастройкиИнтеграцииРекрутинговыхСайтов.Использовать";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	Запрос.УстановитьПараметр("РекрутинговыйСайт", Сайт);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		УстановитьПривилегированныйРежим(Истина);
		Пароли = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Пользователи.ТекущийПользователь(), "ПарольRabota, ПарольSuperJob");
		УстановитьПривилегированныйРежим(Ложь);
		
		ПараметрыАвторизации = Новый Структура("ИмяПользователя, Пароль", Выборка.ИмяПользователя, "");
		Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
			ПараметрыАвторизации.Пароль = Пароли.ПарольRabota;
		ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
			ПараметрыАвторизации.Пароль = Пароли.ПарольSuperJob;
		КонецЕсли;
		
		Возврат ПараметрыАвторизации;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Процедура УточнитьИмяПользователяИПарольНаСайт(Сайт, ИмяПользователя, Пароль)
	
	Если Не ЗначениеЗаполнено(ИмяПользователя)
		Или Не ЗначениеЗаполнено(Пароль) Тогда
		
		ПараметрыАвторизации = ПараметрыАвторизации(Сайт);
		
		Если ПараметрыАвторизации = Неопределено Тогда
			ВызватьИсключение НСтр("ru = 'Не указаны параметры доступа на сайт'");
		Иначе
			ИмяПользователя = ПараметрыАвторизации.ИмяПользователя;
			Пароль = ПараметрыАвторизации.Пароль;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПарольАвторизацииRabota(Ключ, Пароль)
	
	Строки = Новый Массив;
	Строки.Добавить(Ключ);
	Строки.Добавить(Пароль);
	
	ХешMD5 = Новый ХешированиеДанных(ХешФункция.MD5);
	ХешMD5.Добавить(СтрСоединить(Строки));
	
	Результат = ХешMD5.ХешСумма;
	Результат = СтрЗаменить(Результат, " ", "");
	
	Возврат Результат;
	
КонецФункции

Функция ВакансииССайтаHeadHunter(Сайт)
	
	МассивВакансий = Новый Массив;
	МаркерДоступа = МаркерДоступа(Сайт);
	
	Если Не ЗначениеЗаполнено(МаркерДоступа) Тогда
		Возврат МассивВакансий;
	КонецЕсли;
	
	ИнформацияОПользователе = ИнформацияОПользователе(Сайт, МаркерДоступа);
	
	ИдентификаторКомпании = ИнформацияОПользователе.ИдентификаторКомпании;
	ИдентификаторПользователя = ИнформацияОПользователе.ИдентификаторПользователя;
	
	Если Не ЗначениеЗаполнено(ИдентификаторКомпании) 
		Или Не ЗначениеЗаполнено(ИдентификаторПользователя) Тогда 
		
		Возврат МассивВакансий;
	КонецЕсли;
	
	ЗагрузитьКлассификаторыHeadHunter(Сайт);
	
	Соединение = HTTPСоединениеHeadHunter();
	Запрос = HTTPЗапросHeadHunter("/employers/" + ИдентификаторКомпании + "/vacancies/active?per_page=50&page=0&manager_id=" + ИдентификаторПользователя, МаркерДоступа);
	Ответ = Соединение.Получить(Запрос);
	
	ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку();
	Если Ответ.КодСостояния <> 200 Тогда
		СообщитьОбОшибкеПолученияВакансий(Ответ.КодСостояния, ТекущаяСтрока);
	Иначе
		
		СчитанныеДанные = ПарсерJSON(ТекущаяСтрока);
		Если ПарсерJSON(ТекущаяСтрока).found > 0 Тогда
			
			Вакансии = СчитанныеДанные.items;
			Для Каждого Вакансия Из Вакансии Цикл
				
				Запрос = HTTPЗапросHeadHunter(СтрШаблон("vacancies/%1", Формат(Вакансия.id, "ЧГ=0")), МаркерДоступа);
				Ответ = Соединение.Получить(Запрос);
				ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку();
				
				Если Ответ.КодСостояния <> 200 Тогда
					СообщитьОбОшибкеПолученияВакансии(Ответ.КодСостояния, ТекущаяСтрока);
				Иначе
					
					ОписаниеВакансии = ПарсерJSON(ТекущаяСтрока, Истина);
					МассивВакансий.Добавить(ОписаниеВакансии);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат МассивВакансий;
	
КонецФункции

Функция ВакансииССайтаRabota(Сайт)
	
	МассивВакансий = Новый Массив;
	МаркерДоступа = МаркерДоступа(Сайт);
	
	Если Не ЗначениеЗаполнено(МаркерДоступа) Тогда
		Возврат МассивВакансий;
	КонецЕсли;
	
	ЗагрузитьКлассификаторыRabota(Сайт);
	
	Соединение = HTTPСоединениеRabota();
	
	Запрос = HTTPЗапросRabota("vacancies", МаркерДоступа);
	Ответ = Соединение.Получить(Запрос);
	ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку();
	
	Если Ответ.КодСостояния <> 200 Тогда
		СообщитьОбОшибкеПолученияВакансий(Ответ.КодСостояния, ТекущаяСтрока);
	Иначе
		
		Вакансии = ПарсерJSON(ТекущаяСтрока);
		Для Каждого Вакансия Из Вакансии Цикл
			
			Запрос = HTTPЗапросRabota(СтрШаблон("vacancies/%1", Формат(Вакансия.id, "ЧГ=0")), МаркерДоступа);
			Ответ = Соединение.Получить(Запрос);
			ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку();
			
			Если Ответ.КодСостояния <> 200 Тогда
				СообщитьОбОшибкеПолученияВакансии(Ответ.КодСостояния, ТекущаяСтрока);
			Иначе
				
				ОписаниеВакансии = ПарсерJSON(ТекущаяСтрока);
				МассивВакансий.Добавить(ОписаниеВакансии);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат МассивВакансий;
	
КонецФункции

Функция ВакансииССайтаSuperJob(Сайт)
	
	МассивВакансий = Неопределено;
	
	МаркерДоступа = МаркерДоступа(Сайт);
	
	Если Не ЗначениеЗаполнено(МаркерДоступа) Тогда
		Возврат МассивВакансий;
	КонецЕсли;
	
	ИнформацияОПользователе = ИнформацияОПользователе(Сайт, МаркерДоступа);
	
	ИдентификаторКомпании  = ИнформацияОПользователе.ИдентификаторКомпании;
	ИдентификаторПользователя = ИнформацияОПользователе.ИдентификаторПользователя;
	
	Если Не ЗначениеЗаполнено(ИдентификаторКомпании) 
		Или Не ЗначениеЗаполнено(ИдентификаторПользователя) Тогда 
		
		Возврат МассивВакансий;
	КонецЕсли;
	
	ЗагрузитьКлассификаторыSuperJob(Сайт);
	
	Соединение = HTTPСоединениеSuperJob();
	Запрос = HTTPЗапросSuperJob("/2.0/hr/vacancies/?status=all_not_archive&id_user=" + ИдентификаторПользователя, МаркерДоступа);
	Ответ = Соединение.Получить(Запрос);
	
	ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку("UTF-8");

	Если Ответ.КодСостояния <> 200 Тогда
		СообщитьОбОшибкеПолученияВакансий(Ответ.КодСостояния, ТекущаяСтрока);
	Иначе
		
		Вакансии = ПарсерJSON(ТекущаяСтрока);
		МассивВакансий = Вакансии.objects;
		
	КонецЕсли;
	
	Возврат МассивВакансий;
	
КонецФункции

Функция КартинкаПоАдресу(АдресКартинки, ИмяСервераПоУмолчанию = "")
	
	URI = ПолучениеФайловИзИнтернетаКлиентСервер.РазделитьURL(АдресКартинки);
	Если ПустаяСтрока(URI.ИмяСервера) Тогда
		URI.ИмяСервера = ИмяСервераПоУмолчанию;
	КонецЕсли;
	
	ВременныйФайл = ПолучитьИмяВременногоФайла();
	
	Запрос = Новый HTTPЗапрос(URI.ПутьКФайлуНаСервере);
	Соединение = НовоеHTTPСоединение(URI.ИмяСервера);
	Соединение.Получить(Запрос, ВременныйФайл);
	
	Картинка = Новый Картинка(ВременныйФайл);
	УдалитьФайлы(ВременныйФайл);
	
	Возврат Картинка;
	
КонецФункции

Функция СтруктураДанныхКандидатаССайтаHeadHunter(ИдентификаторРезюме)
	
	МаркерДоступа = МаркерДоступа(ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter());
	
	Если Не ЗначениеЗаполнено(МаркерДоступа) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Соединение = HTTPСоединениеHeadHunter();
	Запрос = HTTPЗапросHeadHunter("/resumes/" + ИдентификаторРезюме, МаркерДоступа);
	
	Попытка
		Ответ = ВыполнитьЗапросКРекрутинговомуСайту(Соединение, Запрос);
	Исключение
		ЗарегистрироватьОшибкуОбращенияКРекрутинговомуСайту(ИнформацияОбОшибке());
		Возврат Неопределено;
	КонецПопытки;
	
	ТелоОтветаСтрокой = Ответ.ПолучитьТелоКакСтроку("UTF-8");
	Резюме = ПарсерJSON(ТелоОтветаСтрокой, Истина);
	
	ИнформацияПользователя = ИнформацияОПользователе(ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter(), МаркерДоступа);
	АдресКомментариев = Резюме["owner"]["comments"]["url"];
	КомментарииКандидата = МассивКомментариевКандидатаHeadHunter(АдресКомментариев, ИнформацияПользователя.ИдентификаторКомпании);
	
	Резюме.Вставить("Комментарии", КомментарииКандидата);
	
	Если Не ЗначениеЗаполнено(ЗначениеСвойстваУзла(Резюме, "photo")) Тогда
		Возврат Резюме;
	КонецЕсли;
	
	АдресФото = Резюме.Получить("photo").Получить("medium");
	Резюме.Вставить("Фото", КартинкаПоАдресу(АдресФото, "hh.ru"));
	
	Возврат Резюме;
	
КонецФункции

Функция СтруктураДанныхКандидатаССайтаRabota(ИдентификаторРезюме)
	
	МаркерДоступа = МаркерДоступа(ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota());
	
	Если Не ЗначениеЗаполнено(МаркерДоступа) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Соединение = HTTPСоединениеRabota();
	Запрос = HTTPЗапросRabota(СтрШаблон("search-resumes/%1", ИдентификаторРезюме), МаркерДоступа);
	
	Попытка
		Ответ = ВыполнитьЗапросКРекрутинговомуСайту(Соединение, Запрос);
	Исключение
		ЗарегистрироватьОшибкуОбращенияКРекрутинговомуСайту(ИнформацияОбОшибке());
		Возврат Неопределено;
	КонецПопытки;
	
	ТелоОтветаСтрокой = Ответ.ПолучитьТелоКакСтроку();
	Резюме = ПарсерJSON(ТелоОтветаСтрокой, Истина);
	
	КомментарииКандидата = МассивКомментариевКандидатаRabota(ИдентификаторРезюме);
	Резюме.Вставить("Комментарии", КомментарииКандидата);
	
	Если Не ЗначениеЗаполнено(ЗначениеСвойстваУзла(Резюме, "photo")) Тогда
		Возврат Резюме;
	КонецЕсли;
	
	АдресФото = Резюме.Получить("photo");
	Резюме.Вставить("Фото", КартинкаПоАдресу(АдресФото));
	
	Возврат Резюме;
	
КонецФункции

Функция СтруктураДанныхКандидатаССайтаSuperJob(ИдентификаторРезюме)
	
	МаркерДоступа = МаркерДоступа(ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob());
	
	Если Не ЗначениеЗаполнено(МаркерДоступа) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Соединение = HTTPСоединениеSuperJob();
	Запрос = HTTPЗапросSuperJob("/2.0/resumes/" + ИдентификаторРезюме + "/", МаркерДоступа);
	
	Попытка
		Ответ = ВыполнитьЗапросКРекрутинговомуСайту(Соединение, Запрос);
	Исключение
		ЗарегистрироватьОшибкуОбращенияКРекрутинговомуСайту(ИнформацияОбОшибке());
		Возврат Неопределено;
	КонецПопытки;
	
	ТелоОтветаСтрокой = Ответ.ПолучитьТелоКакСтроку("UTF-8");
	Резюме = ПарсерJSON(ТелоОтветаСтрокой);
	
	Если Не ЗначениеЗаполнено(ЗначениеСвойстваУзла(Резюме, "firstname"))
		Или (Не ЗначениеЗаполнено(ЗначениеСвойстваУзла(Резюме, "phone1"))
			И Не ЗначениеЗаполнено(ЗначениеСвойстваУзла(Резюме, "email"))) Тогда
		
		ИнформацияОПользователе = ИнформацияОПользователе(ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob(), МаркерДоступа);
		Если ИнформацияОПользователе.БезлимитныйТариф Тогда
			ОткрытоеРезюме = ОткрытоеРезюмеSuperJob(ИдентификаторРезюме, Соединение, МаркерДоступа);
			Резюме = ?(ОткрытоеРезюме <> Неопределено, ОткрытоеРезюме, Резюме);
		КонецЕсли;
		
	КонецЕсли;
	
	КомментарииКандидата = МассивКомментариевКандидатаSuperJob(ИдентификаторРезюме);
	Резюме.Вставить("Комментарии", КомментарииКандидата);
	
	Если Не ЗначениеЗаполнено(ЗначениеСвойстваУзла(Резюме, "photo")) Тогда
		Возврат Резюме;
	КонецЕсли;
	
	АдресФото = СтрЗаменить(Резюме.photo, "\", "");
	Резюме.Вставить("Фото", КартинкаПоАдресу(АдресФото, "superjob.ru"));
	
	Возврат Резюме;
	
КонецФункции

Функция СтрокаОтбораHeadHunter(СтруктураОтбора, НомерСтраницы)
	
	СтрокаОтбора = "";
	
	Для Каждого КлючИЗначение Из СтруктураОтбора Цикл
		
		Если Не ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
			Продолжить;
		КонецЕсли;
		
		Значение = КлючИЗначение.Значение;
		
		Если ТипЗнч(Значение) = Тип("Строка") Тогда
			Значение = Значение;
		ИначеЕсли ТипЗнч(Значение) = Тип("Число") Тогда
			Значение = Формат(Значение, "ЧГ=0");
		ИначеЕсли ТипЗнч(Значение) = Тип("Дата") Тогда
			Значение = Формат(Значение, "ДЛФ=Д");
		ИначеЕсли ТипЗнч(Значение) = Тип("Булево") Тогда
			Если КлючИЗначение.Ключ = "ТолькоВНазвании" И Значение Тогда
				СтрокаОтбора = СтрокаОтбора + "text.logic=phrase&text.field=title&text.period=all_time&";
				Продолжить;
			КонецЕсли;
			Если КлючИЗначение.Ключ = "СкрыватьРезюмеБезЗарплаты" И Значение Тогда
				СтрокаОтбора = СтрокаОтбора + "label=only_with_salary&";
				Продолжить;
			КонецЕсли;
		ИначеЕсли ТипЗнч(Значение) = Тип("СписокЗначений") Тогда
			
			Для Каждого ЭлементСписка Из Значение Цикл
				Если ТипЗнч(ЭлементСписка.Значение) = Тип("Структура") Тогда
					Идентификатор = ЭлементСписка.Значение.IDHeadHunter;
				Иначе
					Идентификатор = ЭлементСписка.Значение;
				КонецЕсли;
				СтрокаОтбора = СтрокаОтбора + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1=%2&", ИнтеграцияРекрутинговыхСайтовКлиентСервер.СоответствиеПолейПоискаКандидатовПрограммыИHeadHunter().Получить(КлючИЗначение.Ключ), Строка(Идентификатор));
			КонецЦикла;
			
			Продолжить;
			
		КонецЕсли;
		
		СтрокаОтбора = СтрокаОтбора + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1=%2&", ИнтеграцияРекрутинговыхСайтовКлиентСервер.СоответствиеПолейПоискаКандидатовПрограммыИHeadHunter().Получить(КлючИЗначение.Ключ), Значение);
		
	КонецЦикла;
	
	СтрокаОтбора = СтрокаОтбора + "order_by=relevance&per_page=50&page=" + НомерСтраницы;
		
	Возврат СтрокаОтбора;
	
КонецФункции

Функция СтрокаОтбораRabota(СтруктураОтбора, НомерСтраницы)
	
	СтрокаОтбора = "qot[]=1&";
	
	Для Каждого КлючИЗначение Из СтруктураОтбора Цикл
		
		Если Не ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
			Продолжить;
		КонецЕсли;
		
		Значение = КлючИЗначение.Значение;
		
		Если ТипЗнч(Значение) = Тип("Строка") Тогда
			Значение = КодироватьСтроку(Значение, СпособКодированияСтроки.КодировкаURL);
		ИначеЕсли ТипЗнч(Значение) = Тип("Число") Тогда
			Значение = Формат(Значение, "ЧГ=0");
		ИначеЕсли ТипЗнч(Значение) = Тип("Дата") Тогда
			Значение = Формат(Значение, "ДЛФ=Д");
		ИначеЕсли ТипЗнч(Значение) = Тип("Булево") Тогда
			
			Если КлючИЗначение.Ключ = "ТолькоВНазвании" Тогда
				СтрокаОтбора = СтрШаблон("%1qsa[0][]=%2&", СтрокаОтбора, ?(Значение, 2, 1));
			ИначеЕсли КлючИЗначение.Ключ = "СкрыватьРезюмеБезЗарплаты" И Значение Тогда
				СтрокаОтбора = СтрШаблон("%1cs=%2&",  СтрокаОтбора, "t");
			КонецЕсли;
			
			Продолжить;
			
		ИначеЕсли ТипЗнч(Значение) = Тип("СписокЗначений") Тогда
			
			Для Каждого ЭлементСписка Из Значение Цикл
				Если ТипЗнч(ЭлементСписка.Значение) = Тип("Структура") Тогда
					Идентификатор = ЭлементСписка.Значение.IDRabota;
				Иначе
					Идентификатор = ЭлементСписка.Значение;
				КонецЕсли;
				СтрокаОтбора = СтрокаОтбора + СтрШаблон("%1=%2&", ИнтеграцияРекрутинговыхСайтовКлиентСервер.СоответствиеПолейПоискаКандидатовПрограммыИRabota().Получить(КлючИЗначение.Ключ), Строка(Идентификатор));
			КонецЦикла;
			
			Продолжить;
			
		КонецЕсли;
		
		СтрокаОтбора = СтрокаОтбора + СтрШаблон("%1=%2&", ИнтеграцияРекрутинговыхСайтовКлиентСервер.СоответствиеПолейПоискаКандидатовПрограммыИRabota().Получить(КлючИЗначение.Ключ), Значение);
		
	КонецЦикла;
	
	СтрокаОтбора = СтрокаОтбора + "sm=2&pp=50&start=" + НомерСтраницы * 50;
	
	Возврат СтрокаОтбора;
	
КонецФункции

Функция СтрокаОтбораSuperJob(СтруктураОтбора, НомерСтраницы)
		
	СтрокаОтбора = "";
	
	Для Каждого КлючИЗначение Из СтруктураОтбора Цикл
		
		Если Не ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
			Продолжить;
		КонецЕсли;
		
		Значение = КлючИЗначение.Значение;
		
		Если ТипЗнч(Значение) = Тип("Строка") Тогда
			Значение = Значение;
		ИначеЕсли ТипЗнч(Значение) = Тип("Число") Тогда
			Значение = Формат(Значение, "ЧГ=0");
		ИначеЕсли ТипЗнч(Значение) = Тип("Дата") Тогда
			Значение = Формат(Значение, "ДЛФ=Д");
		ИначеЕсли ТипЗнч(Значение) = Тип("Булево") Тогда
			Если КлючИЗначение.Ключ = "ТолькоВНазвании" И Значение Тогда
				СтрокаОтбора = СтрокаОтбора + "keywords[0][keys]=" + СтруктураОтбора.КлючевоеСлово + "&keywords[0][skwc]=and&keywords[0][srws]=60&";
				Продолжить;
			КонецЕсли;
			Если КлючИЗначение.Ключ = "СкрыватьРезюмеБезЗарплаты" И Значение Тогда
				СтрокаОтбора = СтрокаОтбора + "payment_no_agreement=1&";   	
				Продолжить;
			КонецЕсли;
		ИначеЕсли ТипЗнч(Значение) = Тип("СписокЗначений") Тогда
			
			Если КлючИЗначение.Ключ = "Регион" Тогда
				Индекс = 0;
				
				Для Каждого ЭлементСписка Из Значение Цикл
					Если ТипЗнч(ЭлементСписка.Значение) = Тип("Структура") Тогда
						Идентификатор = ЭлементСписка.Значение.IDSuperJob;
					Иначе
						Идентификатор = ЭлементСписка.Значение;
					КонецЕсли;
					СтрокаОтбора = СтрокаОтбора + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1=%2&", ИнтеграцияРекрутинговыхСайтовКлиентСервер.СоответствиеПолейПоискаКандидатовПрограммыИSuperJob().Получить(КлючИЗначение.Ключ) + "[" + Индекс + "]", Формат(Идентификатор, "ЧГ=0"));
					Индекс = Индекс + 1;
				КонецЦикла;
				
			Иначе
				
				Для Каждого ЭлементСписка Из Значение Цикл
					Если ТипЗнч(ЭлементСписка.Значение) = Тип("Структура") Тогда
						Идентификатор = ЭлементСписка.Значение.IDSuperJob;
					Иначе
						Идентификатор = ЭлементСписка.Значение;
					КонецЕсли;
					СтрокаОтбора = СтрокаОтбора + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1=%2&", ИнтеграцияРекрутинговыхСайтовКлиентСервер.СоответствиеПолейПоискаКандидатовПрограммыИSuperJob().Получить(КлючИЗначение.Ключ), СтрЗаменить(Строка(Идентификатор), "Ключ", ""));
				КонецЦикла;
				
			КонецЕсли;
			
			Продолжить;
			
		КонецЕсли;
		
		Если КлючИЗначение.Ключ = "КлючевоеСлово" Тогда
			Если Не СтруктураОтбора.ТолькоВНазвании Тогда
				СтрокаОтбора = СтрокаОтбора + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1=%2&", ИнтеграцияРекрутинговыхСайтовКлиентСервер.СоответствиеПолейПоискаКандидатовПрограммыИSuperJob().Получить(КлючИЗначение.Ключ), СтрЗаменить(Значение, "Ключ", ""));
			КонецЕсли;
		Иначе
			СтрокаОтбора = СтрокаОтбора + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1=%2&", ИнтеграцияРекрутинговыхСайтовКлиентСервер.СоответствиеПолейПоискаКандидатовПрограммыИSuperJob().Получить(КлючИЗначение.Ключ), СтрЗаменить(Значение, "Ключ", ""));
		КонецЕсли;
		
	КонецЦикла;
	
	СтрокаОтбора = СтрокаОтбора + "order_field=rank&count=50&page=" + НомерСтраницы;
	
	Возврат СтрокаОтбора;
	
КонецФункции

Процедура ПолучитьОткликиHeadHunter(Вакансия, Сайт)
	
	ИдентификаторВакансии = ИдентификаторВакансии(Вакансия, Сайт);
	
	Если Не ЗначениеЗаполнено(ИдентификаторВакансии) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Вакансия не опубликована на сайте'") + " " + Сайт);
		Возврат;
	КонецЕсли;
	
	МаркерДоступа = МаркерДоступа(Сайт);
		
	Если Не ЗначениеЗаполнено(МаркерДоступа) Тогда
		Возврат;
	КонецЕсли;
	
	Соединение = HTTPСоединениеHeadHunter();
	Запрос = HTTPЗапросHeadHunter("/negotiations/?per_page=50&page=0&vacancy_id=" + ИдентификаторВакансии, МаркерДоступа);
	Ответ = Соединение.Получить(Запрос);
	
	ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку("UTF-8");
	Если Ответ.КодСостояния <> 200 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru = 'Ошибка получения откликов %1: %2'"), 
											Ответ.КодСостояния, 
											ТекущаяСтрока));
		Возврат;
		
	Иначе
		
		КоллекцииОткликов = ПарсерJSON(ТекущаяСтрока);
		
	КонецЕсли;
	
	МаксимальныйНомерОтклика = МаксимальныйНомерОтклика(Вакансия);
	ЗагруженныеОткликиВакансии = ЗагруженныеОткликиВакансии(Вакансия);
	
	НаборЗаписейОтклики = РегистрыСведений.ОткликиКандидатов.СоздатьНаборЗаписей();
	НаборЗаписейОтклики.Отбор.Вакансия.Установить(Вакансия);
	НаборЗаписейОтклики.Прочитать();
	
	Для Каждого КоллекцияОткликов Из КоллекцииОткликов.states Цикл
		
		Соединение = HTTPСоединениеHeadHunter();
		Запрос = HTTPЗапросHeadHunter("/negotiations/" + КоллекцияОткликов.id + "?vacancy_id=" + ИдентификаторВакансии, МаркерДоступа);
		Ответ = Соединение.Получить(Запрос);
		
		ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку("UTF-8");
		Если Ответ.КодСостояния <> 200 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
												НСтр("ru = 'Ошибка получения откликов %1: %2'"), 
												Ответ.КодСостояния, 
												ТекущаяСтрока));
			Возврат;
			
		Иначе
			
			Отклики = ПарсерJSON(ТекущаяСтрока, Истина);
			
			Для Каждого Отклик Из Отклики.Получить("items") Цикл
				
				Если Отклик = Неопределено 
					Или Отклик = "null" Тогда
					Продолжить;
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(ЗначениеСвойстваУзла(Отклик, "resume")) Тогда
					Продолжить;
				КонецЕсли;
				
				ИдентификаторРезюме = ЗначениеСвойстваУзла(Отклик, "resume", "id");
				Если ЗагруженныеОткликиВакансии.Найти(ИдентификаторРезюме) = Неопределено Тогда
					
					НоваяЗапись = НаборЗаписейОтклики.Добавить();
					НоваяЗапись.Вакансия = Вакансия;
					НоваяЗапись.НомерОтклика = МаксимальныйНомерОтклика + 1;
					НоваяЗапись.Фамилия = ЗначениеСвойстваУзла(Отклик, "resume", "last_name");
					НоваяЗапись.Имя = ЗначениеСвойстваУзла(Отклик, "resume", "first_name");
					НоваяЗапись.Отчество = ЗначениеСвойстваУзла(Отклик, "resume", "middle_name");
					НоваяЗапись.Источник = Сайт;
					НоваяЗапись.ДатаОтклика = Дата(СтрЗаменить(Лев(ЗначениеСвойстваУзла(Отклик, "updated_at"), 10), "-", ""));
					НоваяЗапись.ИдентификаторРезюме = ИдентификаторРезюме;
					НоваяЗапись.Адрес = ЗначениеСвойстваУзла(Отклик, "resume", "area", "name");
					НоваяЗапись.АдресРезюме = ЗначениеСвойстваУзла(Отклик, "resume", "alternate_url");
					
					Запрос = HTTPЗапросHeadHunter("/negotiations/" + ЗначениеСвойстваУзла(Отклик, "id") + "/messages?with_text_only=true", МаркерДоступа);
					Ответ = Соединение.Получить(Запрос);
					
					ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку("UTF-8");
					Если Ответ.КодСостояния = 200 Тогда
						
						Сообщения = ИнтеграцияРекрутинговыхСайтовВнутренний.ПарсерJSON(ТекущаяСтрока, Истина);
						Для Каждого Сообщение Из Сообщения.Получить("items") Цикл
							
							Если ЗначениеСвойстваУзла(Сообщение, "author", "participant_type") = "applicant" Тогда
								
								НоваяЗапись.Сообщение = УдалитьЛишнееИзСтроки(ЗначениеСвойстваУзла(Сообщение, "text"));
								Прервать;
								
							КонецЕсли;
							
						КонецЦикла;
						
					КонецЕсли;
					
					МаксимальныйНомерОтклика = МаксимальныйНомерОтклика + 1;
					
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Если НаборЗаписейОтклики.Модифицированность() Тогда
		НаборЗаписейОтклики.Записать();
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не найдено откликов для загрузки на сайте'") + " " + Сайт);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьОткликиRabota(Вакансия, Сайт)
	
	ИдентификаторВакансии = ИдентификаторВакансии(Вакансия, Сайт);
	
	Если Не ЗначениеЗаполнено(ИдентификаторВакансии) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Вакансия не опубликована на сайте'") + " " + Сайт);
		Возврат;
	КонецЕсли;
	
	МаркерДоступа = МаркерДоступа(Сайт);
	
	Если Не ЗначениеЗаполнено(МаркерДоступа) Тогда
		Возврат;
	КонецЕсли;
	
	Соединение = HTTPСоединениеRabota();
	Запрос = HTTPЗапросRabota(СтрШаблон("responses/vacancy/%1?period=365", ИдентификаторВакансии), МаркерДоступа);
	Ответ = Соединение.Получить(Запрос);
	
	ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку();
	Если Ответ.КодСостояния <> 200 Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон(
			НСтр("ru = 'Ошибка получения откликов %1: %2'"), 
			Ответ.КодСостояния, 
			ТекущаяСтрока));
		Возврат;
		
	Иначе
		КоллекцияОткликов = ПарсерJSON(ТекущаяСтрока);
	КонецЕсли;
	
	МаксимальныйНомерОтклика = МаксимальныйНомерОтклика(Вакансия);
	ЗагруженныеОткликиВакансии = ЗагруженныеОткликиВакансии(Вакансия);
	
	НаборЗаписейОтклики = РегистрыСведений.ОткликиКандидатов.СоздатьНаборЗаписей();
	НаборЗаписейОтклики.Отбор.Вакансия.Установить(Вакансия);
	НаборЗаписейОтклики.Прочитать();
	
	Для Каждого Отклик Из КоллекцияОткликов Цикл
		
		Если Формат(Отклик.vacancy.id,"ЧГ=") <> ИдентификаторВакансии Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Отклик.resume) Тогда
			Продолжить;
		КонецЕсли;
		
		Запрос = HTTPЗапросRabota(СтрШаблон("search-resumes/%1", Формат(Отклик.resume.id,"ЧГ=")), МаркерДоступа);
		Ответ = Соединение.Получить(Запрос);
		
		ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку();
		Если Ответ.КодСостояния <> 200 Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон(
				НСтр("ru = 'Ошибка получения резюме%1: %2'"), 
				Ответ.КодСостояния, 
				ТекущаяСтрока));
			
			Возврат;
			
		Иначе
			
			ОписаниеРезюме = ПарсерJSON(ТекущаяСтрока);
			ИдентификаторРезюме = Формат(ОписаниеРезюме.id, "ЧГ=");
			
			Если ЗагруженныеОткликиВакансии.Найти(ИдентификаторРезюме) = Неопределено Тогда
				
				НоваяЗапись = НаборЗаписейОтклики.Добавить();
				НоваяЗапись.Вакансия = Вакансия;
				НоваяЗапись.НомерОтклика = МаксимальныйНомерОтклика + 1;
				НоваяЗапись.Имя = ЗначениеСвойстваУзла(ОписаниеРезюме, "personName");
				НоваяЗапись.Фамилия = ЗначениеСвойстваУзла(ОписаниеРезюме, "surname");
				НоваяЗапись.Отчество = ЗначениеСвойстваУзла(ОписаниеРезюме, "secondName");
				НоваяЗапись.Источник = Сайт;
				НоваяЗапись.ИдентификаторРезюме = ИдентификаторРезюме;
				НоваяЗапись.EMail = ЗначениеСвойстваУзла(ОписаниеРезюме, "contactEmail");
				
				Если ЗначениеЗаполнено(ОписаниеРезюме.birthDate) Тогда
					НоваяЗапись.ДатаРождения = Дата(СтрЗаменить(Лев(ОписаниеРезюме.birthDate, 10), "-", ""));
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ОписаниеРезюме.phones) Тогда
					Контакт = ОписаниеРезюме.phones[0];
					НоваяЗапись.Телефон = СтрШаблон("%1 (%2) %3", Контакт.countryCode, Контакт.Code, Контакт.Number);
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ОписаниеРезюме.region) Тогда
					НоваяЗапись.Адрес = ОписаниеРезюме.city.name;
				КонецЕсли;
				
				// Получим текст сопроводительного письма
				Запрос = HTTPЗапросRabota(СтрШаблон("responses/%1/messages", Формат(Отклик.id,"ЧГ=")), МаркерДоступа);
				Ответ = Соединение.Получить(Запрос);
				
				ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку();
				Если Ответ.КодСостояния = 200 Тогда
					
					СообщенияПоОтклику = ПарсерJSON(ТекущаяСтрока);
					Если ЗначениеЗаполнено(СообщенияПоОтклику) И Не СообщенияПоОтклику[0].fromEmployer Тогда
						НоваяЗапись.Сообщение = СообщенияПоОтклику[0].text;
						НоваяЗапись.ДатаОтклика = Дата(СтрЗаменить(Лев(СообщенияПоОтклику[0].date, 10), "-", ""));
					КонецЕсли;
					
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(НоваяЗапись.ДатаОтклика) Тогда
					НоваяЗапись.ДатаОтклика = Дата(СтрЗаменить(Лев(Отклик.lastModified, 10), "-", ""));
				КонецЕсли;
				
				НоваяЗапись.Рассмотрено = (Отклик.status.id > 0);
				НоваяЗапись.АдресРезюме = Отклик.resume.link;
				МаксимальныйНомерОтклика = МаксимальныйНомерОтклика + 1;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
		
	Если НаборЗаписейОтклики.Модифицированность() Тогда
		НаборЗаписейОтклики.Записать();
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не найдено откликов для загрузки на сайте'") + " " + Сайт);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьОткликиSuperJob(Вакансия, Сайт)
	
	ИдентификаторВакансии = ИдентификаторВакансии(Вакансия, Сайт);
	
	Если Не ЗначениеЗаполнено(ИдентификаторВакансии) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Вакансия не опубликована на сайте'") + " " + Сайт);
		Возврат;
	КонецЕсли;
	
	МаркерДоступа = МаркерДоступа(Сайт);
		
	Если Не ЗначениеЗаполнено(МаркерДоступа) Тогда
		Возврат;
	КонецЕсли;
	
	МаксимальныйНомерОтклика = МаксимальныйНомерОтклика(Вакансия);
	ЗагруженныеОткликиВакансии = ЗагруженныеОткликиВакансии(Вакансия);
	
	НаборЗаписейОтклики = РегистрыСведений.ОткликиКандидатов.СоздатьНаборЗаписей();
	НаборЗаписейОтклики.Отбор.Вакансия.Установить(Вакансия);
	НаборЗаписейОтклики.Прочитать();

	Соединение = HTTPСоединениеSuperJob();
	Запрос = HTTPЗапросSuperJob("/2.0/resumes/received/" + ИдентификаторВакансии + "/", МаркерДоступа);
	Ответ = Соединение.Получить(Запрос);
	
	ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку("UTF-8");

	Если Ответ.КодСостояния <> 200 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru = 'Ошибка загрузки резюме %1: %2'"), 
											Ответ.КодСостояния, 
											ТекущаяСтрока));
		Возврат;
		
	Иначе
		
		Отклики = ПарсерJSON(ТекущаяСтрока);
		
	КонецЕсли;
	
	Для Каждого Отклик Из Отклики.objects Цикл
		
		Если Отклик = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ИдентификаторРезюме = Формат(ЗначениеСвойстваУзла(Отклик, "resume", "id"), "ЧГ=0");
		Если ЗагруженныеОткликиВакансии.Найти(ИдентификаторРезюме) = Неопределено Тогда
			
			НоваяЗапись = НаборЗаписейОтклики.Добавить();
			НоваяЗапись.Вакансия = Вакансия;
			НоваяЗапись.НомерОтклика = МаксимальныйНомерОтклика + 1;
			НоваяЗапись.Фамилия = ЗначениеСвойстваУзла(Отклик, "resume", "lastname");
			НоваяЗапись.Имя = ЗначениеСвойстваУзла(Отклик, "resume", "firstname");
			НоваяЗапись.Отчество = ЗначениеСвойстваУзла(Отклик, "resume", "middlename");
			НоваяЗапись.Источник = Сайт;
			НоваяЗапись.ДатаОтклика = Дата(1970, 1, 1) + Отклик.events[0].date_event;
			НоваяЗапись.ИдентификаторРезюме = ИдентификаторРезюме;
			Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(Отклик, "resume", "birthyear")) 
				И ЗначениеЗаполнено(ЗначениеСвойстваУзла(Отклик, "resume", "birthmonth")) 
				И ЗначениеЗаполнено(ЗначениеСвойстваУзла(Отклик, "resume", "birthday")) Тогда
				
				НоваяЗапись.ДатаРождения = Дата(ЗначениеСвойстваУзла(Отклик, "resume", "birthyear"), ЗначениеСвойстваУзла(Отклик, "resume", "birthmonth"), ЗначениеСвойстваУзла(Отклик, "resume", "birthday"));
			КонецЕсли;
			НоваяЗапись.Адрес = ЗначениеСвойстваУзла(Отклик, "resume", "town", "title");
			НоваяЗапись.Телефон = ЗначениеСвойстваУзла(Отклик, "resume", "phone1");
			НоваяЗапись.EMail = ЗначениеСвойстваУзла(Отклик, "resume", "email");
			НоваяЗапись.АдресРезюме = СтрЗаменить(ЗначениеСвойстваУзла(Отклик, "resume", "link"), "\", "");
			
			СопроводительноеПисьмо = ЗначениеСвойстваУзла(Отклик, "cover_letter");
			Если ТипЗнч(СопроводительноеПисьмо) = Тип("Массив") 
				И ЗначениеЗаполнено(СопроводительноеПисьмо) Тогда
				НоваяЗапись.Сообщение = УдалитьЛишнееИзСтроки(СопроводительноеПисьмо[0]);
			КонецЕсли;
			
			МаксимальныйНомерОтклика = МаксимальныйНомерОтклика + 1;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НаборЗаписейОтклики.Модифицированность() Тогда
		НаборЗаписейОтклики.Записать();
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не найдено откликов для загрузки на сайте'") + " " + Сайт);
	КонецЕсли;
	
КонецПроцедуры

Функция МаксимальныйНомерОтклика(Вакансия) 
	Возврат РегистрыСведений.ОткликиКандидатов.НомерПоследнегоОтклика(Вакансия); 
КонецФункции

Функция ЗагруженныеОткликиВакансии(Вакансия)
	
	МассивЗагруженныхОткликов = Новый Массив;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОткликиКандидатов.Вакансия,
	|	ОткликиКандидатов.ИдентификаторРезюме
	|ИЗ
	|	РегистрСведений.ОткликиКандидатов КАК ОткликиКандидатов
	|ГДЕ
	|	ОткликиКандидатов.Вакансия = &Вакансия";
	
	Запрос.УстановитьПараметр("Вакансия", Вакансия);
	
	МассивЗагруженныхОткликов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИдентификаторРезюме");
	
	Возврат МассивЗагруженныхОткликов;
	
КонецФункции

Функция СписокВыбораПоКлассификаторуHeadHunter(Сайт, Путь)  
	
	СписокВыбора = Новый СписокЗначений;
	
	Если Не ЗначениеЗаполнено(Путь) Тогда
		Возврат СписокВыбора;
	КонецЕсли;
	
	КлассификаторыHeadHunter = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыHeadHunter");
	Если КлассификаторыHeadHunter = Неопределено Или Не КлассификаторыHeadHunter.Свойство(Путь) Тогда
		Возврат СписокВыбора;
	КонецЕсли;
	
	МассивДанных = КлассификаторыHeadHunter[Путь];
	Если Путь = "currency" Тогда
		Для Каждого ЭлементМассива Из МассивДанных Цикл
			Если Не ЭлементМассива.in_use Тогда
				Продолжить;
			КонецЕсли;
			СписокВыбора.Добавить(ЭлементМассива.code, ЭлементМассива.abbr);
		КонецЦикла;
	Иначе
		Для Каждого ЭлементМассива Из МассивДанных Цикл
			Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.RabotaMail()
				И Путь = "vacancy_billing_type"
				И ЭлементМассива.id = "free" Тогда
				Продолжить;
			КонецЕсли;
			СписокВыбора.Добавить(ЭлементМассива.id, ЭлементМассива.name);
		КонецЦикла;
	КонецЕсли;
	
	Возврат СписокВыбора;

КонецФункции

Функция СписокВыбораПоКлассификаторуRabota(Путь)
	
	СписокВыбора = Новый СписокЗначений;
	
	Если Не ЗначениеЗаполнено(Путь) Тогда
		Возврат СписокВыбора;
	КонецЕсли;
	
	КлассификаторыRabota = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыRabota");
	Если КлассификаторыRabota = Неопределено Или Не КлассификаторыRabota.Свойство(Путь) Тогда
		Возврат СписокВыбора;
	КонецЕсли;
	
	МассивДанных = КлассификаторыRabota[Путь];
	
	Для Каждого ЭлементМассива Из МассивДанных Цикл
		СписокВыбора.Добавить(ЭлементМассива.id, ЭлементМассива.name);
	КонецЦикла;
	
	Возврат СписокВыбора;
	
КонецФункции

Функция СписокВыбораПоКлассификаторуSuperJob(Путь)  
	
	СписокВыбора = Новый СписокЗначений;
	
	Если Не ЗначениеЗаполнено(Путь) Тогда
		Возврат СписокВыбора;
	КонецЕсли;
	
	КлассификаторыSuperJob = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыSuperJob");
	Если КлассификаторыSuperJob = Неопределено Тогда
		Возврат СписокВыбора;
	КонецЕсли;
	
	СтруктураДанных = КлассификаторыSuperJob[Путь];
	Для Каждого ЭлементСтруктурыДанных Из СтруктураДанных Цикл
		Если ЭлементСтруктурыДанных.Значение = "Не имеет значения" Тогда
			Продолжить;
		КонецЕсли;
		СписокВыбора.Добавить(ЭлементСтруктурыДанных.Ключ, ЭлементСтруктурыДанных.Значение);
	КонецЦикла;
		
	Возврат СписокВыбора;

КонецФункции

Функция СтруктураИнформацияОПользователе()
	
	СтруктураИнформацияОПользователе = Новый Структура;
	
	СтруктураИнформацияОПользователе.Вставить("ИдентификаторКомпании", "");
	СтруктураИнформацияОПользователе.Вставить("ИдентификаторПользователя", "");
	СтруктураИнформацияОПользователе.Вставить("БезлимитныйТариф", Ложь);
	
	Возврат СтруктураИнформацияОПользователе;
	
КонецФункции

Процедура ЗаполнитьДеревоВакансииHeadHunter(Вакансия, Дерево, СтруктураВакансииССайта, Сайт)
	
	НоваяСтрока = Дерево.Строки.Добавить();
	НоваяСтрока.Показатель = Вакансия;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "address")) Тогда 
				
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "address.id";
		СписокАдрес = Новый СписокЗначений;
		ПредставлениеАдреса = "";
		СправочникАдресов = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыHeadHunter").addresses;
		Для Каждого ЭлементСправочникаАдресов Из СправочникАдресов Цикл
			Если ЭлементСправочникаАдресов.id = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "address", "id") Тогда
				ПредставлениеАдреса = ЭлементСправочникаАдресов.name;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		СписокАдрес.Добавить(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "address", "id"), ПредставлениеАдреса);
		НовоеПоле.ПубликацияВакансии = СписокАдрес;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "area")) Тогда
		
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "area.id";
		СписокРегион = Новый СписокЗначений;
		СписокРегион.Добавить(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "area", "id"), ЗначениеСвойстваУзла(СтруктураВакансииССайта, "area", "name"));
		НовоеПоле.ПубликацияВакансии = СписокРегион;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "name")) Тогда
		
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "name";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "name");
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "response_letter_required")) Тогда
		
    	НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "response_letter_required";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "response_letter_required") = "true"
			Или ЗначениеСвойстваУзла(СтруктураВакансииССайта, "response_letter_required") = Истина;
		
	КонецЕсли;

	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "salary")) Тогда
		
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "salary.to";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "salary", "to");
				
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "salary.from";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "salary", "from");
				
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "salary.currency";
		СписокВалюта = Новый СписокЗначений;
		ПредставлениеВалюты = "";
		СправочникВалют = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыHeadHunter").currency;
		Для Каждого ЭлементСправочникаВалют Из СправочникВалют Цикл
			Если ЭлементСправочникаВалют.code = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "salary", "currency") Тогда
				ПредставлениеВалюты = ЭлементСправочникаВалют.abbr;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		СписокВалюта.Добавить(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "salary", "currency"), ПредставлениеВалюты);
		НовоеПоле.ПубликацияВакансии = СписокВалюта;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "type")) Тогда
		
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "type.id";
		СписокТип = Новый СписокЗначений;
		СписокТип.Добавить(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "type", "id"), ЗначениеСвойстваУзла(СтруктураВакансииССайта, "type", "name"));
		НовоеПоле.ПубликацияВакансии = СписокТип; 
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДеревоВакансииRabota(Вакансия, Дерево, СтруктураВакансииССайта, Сайт)
	
	НоваяСтрока = Дерево.Строки.Добавить();
	НоваяСтрока.Показатель = Вакансия;
	
	НовоеПоле = НоваяСтрока.Строки.Добавить();
	НовоеПоле.Путь = "name";
	НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "name");
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "isStudyCourse")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "isStudyCourse";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "isStudyCourse");
	КонецЕсли;
	
	Профессии = Новый СписокЗначений;
	Для Каждого Профессия Из ЗначениеСвойстваУзла(СтруктураВакансииССайта, "offerTrades") Цикл
		Профессии.Добавить(Профессия.id, Профессия.name);
	КонецЦикла;
	
	НовоеПоле = НоваяСтрока.Строки.Добавить();
	НовоеПоле.Путь = "offerTrades";
	НовоеПоле.ПубликацияВакансии = Профессии;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "salaryFrom")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "salaryFrom";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "salaryFrom");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "salaryTo")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "salaryTo";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "salaryTo");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "agreementSalary")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "agreementSalary";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "agreementSalary");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "noSalary")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "noSalary";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "noSalary");
	КонецЕсли;
	
	Валюты = Новый СписокЗначений;
	Валюты.Добавить(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "currency", "id"), ЗначениеСвойстваУзла(СтруктураВакансииССайта, "currency", "name"));
	
	НовоеПоле = НоваяСтрока.Строки.Добавить();
	НовоеПоле.Путь = "currency";
	НовоеПоле.ПубликацияВакансии = Валюты;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "isNonresident")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "isNonresident";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "isNonresident");
	КонецЕсли;
	
	Регионы = Новый СписокЗначений;
	Для Каждого Регион Из ЗначениеСвойстваУзла(СтруктураВакансииССайта, "region") Цикл
		Регионы.Добавить(Регион.id, Регион.name);
	КонецЦикла;
	
	НовоеПоле = НоваяСтрока.Строки.Добавить();
	НовоеПоле.Путь = "region";
	НовоеПоле.ПубликацияВакансии = Регионы;
	
	НовоеПоле = НоваяСтрока.Строки.Добавить();
	НовоеПоле.Путь = "description";
	НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "description");
	
	Графики = Новый СписокЗначений;
	Графики.Добавить(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "operatingSchedule", "id"), ЗначениеСвойстваУзла(СтруктураВакансииССайта, "operatingSchedule", "name"));
	
	НовоеПоле = НоваяСтрока.Строки.Добавить();
	НовоеПоле.Путь = "operatingSchedule";
	НовоеПоле.ПубликацияВакансии = Графики;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "operatingScheduleComment")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "operatingScheduleComment";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "operatingScheduleComment");
	КонецЕсли;
	
	ИдентификаторОпытаРаботы = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "experienceYearCount");
	Если ТипЗнч(ИдентификаторОпытаРаботы) = Тип("Число") Тогда
		
		ВариантыОпытаРаботы = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыRabota").experience;
		
		СведенияОбОпыте = Новый СписокЗначений;
		СведенияОбОпыте.Добавить(ИдентификаторОпытаРаботы, ПредставлениеДанныхПоИдентификатору(ВариантыОпытаРаботы, ИдентификаторОпытаРаботы));
		
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "experienceYearCount";
		НовоеПоле.ПубликацияВакансии = СведенияОбОпыте;
		
	КонецЕсли;
	
	Образование = Новый СписокЗначений;
	Образование.Добавить(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "education", "id"), ЗначениеСвойстваУзла(СтруктураВакансииССайта, "education", "name"));
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "education")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "education";
		НовоеПоле.ПубликацияВакансии = Образование;
	КонецЕсли;
	
	ИдентификаторПола = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "gender");
	ВариантыПола = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыRabota").gender;
	
	СведенияОПоле = Новый СписокЗначений;
	СведенияОПоле.Добавить(ИдентификаторПола, ПредставлениеДанныхПоИдентификатору(ВариантыПола, ИдентификаторПола));
	
	НовоеПоле = НоваяСтрока.Строки.Добавить();
	НовоеПоле.Путь = "gender";
	НовоеПоле.ПубликацияВакансии = СведенияОПоле;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "ageFrom")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "ageFrom";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "ageFrom");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "ageTo")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "ageTo";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "ageTo");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "contactFio")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "contactFio";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "contactFio");
	КонецЕсли;
	
	НовоеПоле = НоваяСтрока.Строки.Добавить();
	НовоеПоле.Путь = "contactEmail";
	НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "contactEmail");
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "wantResponseByMail")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "wantResponseByMail";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "wantResponseByMail");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "address")) Тогда
		
		Адреса = Новый СписокЗначений;
		Для Каждого Адрес Из ЗначениеСвойстваУзла(СтруктураВакансииССайта, "address") Цикл
			
			Если ЗначениеЗаполнено(Адрес.representation) Тогда 
				Адреса.Добавить(Адрес.representation, Адрес.name);
			КонецЕсли;
			
		КонецЦикла;
		
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "address";
		НовоеПоле.ПубликацияВакансии = Адреса;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "phones")) Тогда
		
		Телефоны = Новый СписокЗначений;
		Для Каждого Телефон Из ЗначениеСвойстваУзла(СтруктураВакансииССайта, "phones") Цикл
			
			ДанныеТелефона = Новый Структура("countryCode,code,number,comment");
			ЗаполнитьЗначенияСвойств(ДанныеТелефона, Телефон);
			
			Если ЗначениеЗаполнено(Телефон.comment) Тогда
				Представление = СтрШаблон("%1 (%2) %3 (%4)", Телефон.countryCode, Телефон.code, Телефон.number, Телефон.comment);
			Иначе
				Представление = СтрШаблон("%1 (%2) %3", Телефон.countryCode, Телефон.code, Телефон.number);
			КонецЕсли;
			Телефоны.Добавить(ДанныеТелефона, Представление);
			
		КонецЦикла;
		
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "phones";
		НовоеПоле.ПубликацияВакансии = Телефоны;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "language")) Тогда
		
		ЗнанияЯзыков = Новый СписокЗначений;
		
		Для Каждого ЗнаниеЯзыка Из ЗначениеСвойстваУзла(СтруктураВакансииССайта, "language") Цикл
			
			СтруктураДанных = Новый Структура("offerLanguage, offerLanguageLevel");
			ЗаполнитьЗначенияСвойств(СтруктураДанных, ЗнаниеЯзыка);
			
			Представление= СтрШаблон("%1 (%2)", ЗнаниеЯзыка.offerLanguage.name, ЗнаниеЯзыка.offerLanguageLevel.name);
			ЗнанияЯзыков.Добавить(СтруктураДанных, Представление);
			
		КонецЦикла;
		
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "language";
		НовоеПоле.ПубликацияВакансии = ЗнанияЯзыков;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "additionalInformation")) Тогда
		
		Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "additionalInformation", "auto")) Тогда
			
			НовоеПоле = НоваяСтрока.Строки.Добавить();
			НовоеПоле.Путь = "additionalInformation.auto";
			НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "additionalInformation", "auto");
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "additionalInformation", "drivingLicense")) Тогда
			
			КатегорииПрав = Новый СписокЗначений;
			
			Для Каждого КатегорияПрав Из ЗначениеСвойстваУзла(СтруктураВакансииССайта, "additionalInformation", "drivingLicense") Цикл
				КатегорииПрав.Добавить(КатегорияПрав, КатегорияПрав);
			КонецЦикла;
			
			НовоеПоле = НоваяСтрока.Строки.Добавить();
			НовоеПоле.Путь = "additionalInformation.drivingLicense";
			НовоеПоле.ПубликацияВакансии = КатегорииПрав;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДеревоВакансииSuperJob(Вакансия, Дерево, СтруктураВакансииССайта, Сайт)
	
	НоваяСтрока = Дерево.Строки.Добавить();
	НоваяСтрока.Показатель = Вакансия;

	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "address")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "address";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "address");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "age_from")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "age_from";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "age_from");
	КонецЕсли;

	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "age_to")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "age_to";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "age_to");
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "anonymous")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "anonymous";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "anonymous") = "true"
			Или ЗначениеСвойстваУзла(СтруктураВакансииССайта, "anonymous") = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "candidat")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "candidat";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "candidat");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "compensation")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "compensation";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "compensation");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "contact")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "contact";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "contact");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "email")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "email";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "email");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "extend_vac")) Тогда
    	НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "extend_vac";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "extend_vac") = "true"
			Или ЗначениеСвойстваУзла(СтруктураВакансииССайта, "extend_vac") = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "firm_name")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "firm_name";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "firm_name");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "firm_activity")) Тогда
   		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "firm_activity";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "firm_activity");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "moveable")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "moveable";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "moveable") = "true"
			Или ЗначениеСвойстваУзла(СтруктураВакансииССайта, "moveable") = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "payment_from")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "payment_from";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "payment_from");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "payment_to")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "payment_to";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "payment_to");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "profession")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "profession";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "profession");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "refresh_vac")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "refresh_vac";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "refresh_vac") = "true"
			Или ЗначениеСвойстваУзла(СтруктураВакансииССайта, "refresh_vac") = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "resumesubscription_keywords"))  Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "resumesubscription_keywords";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "resumesubscription_keywords");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "resumesubscription_status")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "resumesubscription_status";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "resumesubscription_status") <> "0";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "work")) Тогда 
	 	НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "work";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "work");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "code")) Тогда 
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "code";
		НовоеПоле.ПубликацияВакансии = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "code");
	КонецЕсли;

	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "children")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "children";
		Список = Новый СписокЗначений;
		Список.Добавить("Ключ" + ЗначениеСвойстваУзла(СтруктураВакансииССайта, "children", "id"), ЗначениеСвойстваУзла(СтруктураВакансииССайта, "children", "title"));
		НовоеПоле.ПубликацияВакансии = Список;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "education")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "education";
		Список = Новый СписокЗначений;
		Список.Добавить("Ключ" + ЗначениеСвойстваУзла(СтруктураВакансииССайта, "education", "id"), ЗначениеСвойстваУзла(СтруктураВакансииССайта, "education", "title"));
		НовоеПоле.ПубликацияВакансии = Список;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "experience")) Тогда
		
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "experience";
		Список = Новый СписокЗначений;
		Список.Добавить("Ключ" + ЗначениеСвойстваУзла(СтруктураВакансииССайта, "experience", "id"), ЗначениеСвойстваУзла(СтруктураВакансииССайта, "experience", "title"));
		НовоеПоле.ПубликацияВакансии = Список;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "gender")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "gender";
		Список = Новый СписокЗначений;
		Список.Добавить("Ключ" + ЗначениеСвойстваУзла(СтруктураВакансииССайта, "gender", "id"), ЗначениеСвойстваУзла(СтруктураВакансииССайта, "gender", "title"));
		НовоеПоле.ПубликацияВакансии = Список;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "maritalstatus")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "maritalstatus";
		Список = Новый СписокЗначений;
		Список.Добавить("Ключ" + ЗначениеСвойстваУзла(СтруктураВакансииССайта, "maritalstatus", "id"), ЗначениеСвойстваУзла(СтруктураВакансииССайта, "maritalstatus", "title"));
		НовоеПоле.ПубликацияВакансии = Список;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "place_of_work")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "place_of_work";
		Список = Новый СписокЗначений;
		Список.Добавить("Ключ" + ЗначениеСвойстваУзла(СтруктураВакансииССайта, "place_of_work", "id"), ЗначениеСвойстваУзла(СтруктураВакансииССайта, "place_of_work", "title"));
		НовоеПоле.ПубликацияВакансии = Список;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "type_of_work")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "type_of_work";
		Список = Новый СписокЗначений;
		Список.Добавить("Ключ" + ЗначениеСвойстваУзла(СтруктураВакансииССайта, "type_of_work", "id"), ЗначениеСвойстваУзла(СтруктураВакансииССайта, "type_of_work", "title"));
		НовоеПоле.ПубликацияВакансии = Список;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "town")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "town";
		Список = Новый СписокЗначений;
		Список.Добавить(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "town", "id"), ЗначениеСвойстваУзла(СтруктураВакансииССайта, "town", "title"));
		НовоеПоле.ПубликацияВакансии = Список;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "catalogues")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "catalogues";
		Список = Новый СписокЗначений;
		
		Для Каждого СфераДеятельности Из ЗначениеСвойстваУзла(СтруктураВакансииССайта, "catalogues") Цикл
			Если СфераДеятельности.Свойство("positions") 
				И ТипЗнч(СфераДеятельности.positions) = Тип("Массив") Тогда
				
				Для Каждого Специализация Из СфераДеятельности.positions Цикл
					Список.Добавить(Формат(Специализация.key, "ЧГ=0"), Специализация.title);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
			
		НовоеПоле.ПубликацияВакансии = Список;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "driving_licence")) Тогда
		
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "driving_licence";
		Список = Новый СписокЗначений;
		
		Для Каждого Категория Из ЗначениеСвойстваУзла(СтруктураВакансииССайта, "driving_licence") Цикл
			Если Категория = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Список.Добавить(Категория, Категория);
		КонецЦикла;
		
		Если Список.Количество() > 0 Тогда
			НовоеПоле.ПубликацияВакансии = Список;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "languages")) Тогда
		НовоеПоле = НоваяСтрока.Строки.Добавить();
		НовоеПоле.Путь = "languages";
		Список = Новый СписокЗначений;
		
		Для Каждого ВложенныйМассив Из ЗначениеСвойстваУзла(СтруктураВакансииССайта, "languages") Цикл
			
			Если ВложенныйМассив = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Индекс = 0;
			Для Каждого ЭлементМассива Из ВложенныйМассив Цикл
				
				Если ЭлементМассива = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				Если Индекс = 0 Тогда
					
					СтруктураЯзыка = Новый Структура;
					СтруктураЯзыка.Вставить("id", "Ключ" + ЭлементМассива.id);
					СтруктураЯзыка.Вставить("title", ЭлементМассива.title);
					
				КонецЕсли;
				
				Если Индекс = 1 Тогда
			
					СтруктураУровня = Новый Структура;
					СтруктураУровня.Вставить("id", "Ключ" + ЭлементМассива.id);
					СтруктураУровня.Вставить("title", ЭлементМассива.title);
					
				КонецЕсли;
				
				Индекс = Индекс + 1;
				
			КонецЦикла;
			
			СтруктураУровняЯзыков = Новый Структура;
			СтруктураУровняЯзыков.Вставить("id_language", СтруктураЯзыка);
			СтруктураУровняЯзыков.Вставить("level", СтруктураУровня);
			
			Список.Добавить(СтруктураУровняЯзыков, СтруктураУровняЯзыков.id_language.title + " (" + СтруктураУровняЯзыков.level.title + ")");
			
		КонецЦикла;
		
		НовоеПоле.ПубликацияВакансии = Список;
	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "id_user")) Тогда
		
		Список = Новый СписокЗначений;
		ПредставлениеПользователя = "";
		СправочникПользователи = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыSuperJob").users;
		Для Каждого ЭлементСправочникаПользователи Из СправочникПользователи Цикл
			Если ЭлементСправочникаПользователи.Ключ = "Ключ" + Формат(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "id_user"), "ЧГ=0") Тогда
				ПредставлениеПользователя = ЭлементСправочникаПользователи.Значение;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ЗначениеЗаполнено(ПредставлениеПользователя) Тогда
			НовоеПоле = НоваяСтрока.Строки.Добавить();
			НовоеПоле.Путь = "id_user";
			Список.Добавить("Ключ" + Формат(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "id_user"), "ЧГ=0"), ПредставлениеПользователя);
			НовоеПоле.ПубликацияВакансии = Список;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "resumesubscription_kwc")) Тогда 
		
		Список = Новый СписокЗначений;
		Представление = "";
		Справочник = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыSuperJob").resumesubscription_kwc;
		Для Каждого ЭлементСправочника Из Справочник Цикл
			Если ЭлементСправочника.Ключ = ЗначениеСвойстваУзла(СтруктураВакансииССайта, "resumesubscription_kwc") Тогда
				Представление = ЭлементСправочника.Значение;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ЗначениеЗаполнено(Представление) Тогда
			НовоеПоле = НоваяСтрока.Строки.Добавить();
			НовоеПоле.Путь = "resumesubscription_kwc";
			Список.Добавить(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "resumesubscription_kwc"), Представление);
			НовоеПоле.ПубликацияВакансии = Список;
		КонецЕсли;
			
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "resumesubscription_rws")) Тогда 
	
		Список = Новый СписокЗначений;
		Представление = "";
		Справочник = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыSuperJob").resumesubscription_rws;
		Для Каждого ЭлементСправочника Из Справочник Цикл
			Если ЭлементСправочника.Ключ = "Ключ" + Формат(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "resumesubscription_rws"), "ЧГ=0") Тогда
				Представление = ЭлементСправочника.Значение;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ЗначениеЗаполнено(Представление) Тогда
			НовоеПоле = НоваяСтрока.Строки.Добавить();
			НовоеПоле.Путь = "resumesubscription_rws";
			Список.Добавить("Ключ" + Формат(ЗначениеСвойстваУзла(СтруктураВакансииССайта, "resumesubscription_rws"), "ЧГ=0"), Представление);
			НовоеПоле.ПубликацияВакансии = Список;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция СтруктураДанныхКандидатаHeadHunter(СтруктураРезюме) 
	
	СтруктураДанныхКандидата = Справочники[ИнтеграцияРекрутинговыхСайтовКлиентСервер.ИмяСправочникаКандидатов()].СтруктураДанныхКандидата();
	
	ДатаРождения = ЗначениеСвойстваУзла(СтруктураРезюме, "birth_date");
	СтруктураДанныхКандидата.ДатаРождения = ?(ЗначениеЗаполнено(ДатаРождения), Дата(СтрЗаменить(ДатаРождения, "-", "")), Дата(1, 1, 1));
	
	МассивГражданство = Новый Массив;
	Для Каждого Гражданство Из ЗначениеСвойстваУзла(СтруктураРезюме, "citizenship") Цикл
		СтруктураГражданство = Новый Структура("Страна, Дата");
		СтруктураГражданство.Страна = ЗначениеСвойстваУзла(Гражданство, "name");
		МассивГражданство.Добавить(СтруктураГражданство);
	КонецЦикла;
	СтруктураДанныхКандидата.Гражданство = МассивГражданство;
	
	МассивКонтактнаяИнформация = Новый Массив;
	ЗаполнитьМассивКонтактнойИнформацииHeadHunter(СтруктураРезюме, МассивКонтактнаяИнформация);
	
	СтруктураДанныхКандидата.Вставить("КонтактнаяИнформация", МассивКонтактнаяИнформация);
	
	МассивОбразование = Новый Массив;
	ЗаполнитьМассивОбразованиеHeadHunter(СтруктураРезюме, МассивОбразование);
	
	СтруктураДанныхКандидата.Вставить("Образование", МассивОбразование);
	
	МассивТрудоваяДеятельность = Новый Массив;
	Для Каждого ТрудоваяДеятельность Из ЗначениеСвойстваУзла(СтруктураРезюме, "experience") Цикл
		
		Если ТрудоваяДеятельность = Неопределено 
			Или ТрудоваяДеятельность = "null" Тогда
			
			Продолжить;
		КонецЕсли;
		
		СтруктураТрудоваяДеятельность = СтруктураТрудоваяДеятельность();
		СтруктураТрудоваяДеятельность.Организация = ЗначениеСвойстваУзла(ТрудоваяДеятельность, "company");
		СтруктураТрудоваяДеятельность.ДатаНачала = МесяцСтрокойВДату(ЗначениеСвойстваУзла(ТрудоваяДеятельность, "start"));
		СтруктураТрудоваяДеятельность.ДатаОкончания = МесяцСтрокойВДату(ЗначениеСвойстваУзла(ТрудоваяДеятельность, "end"));
		СтруктураТрудоваяДеятельность.Должность = ЗначениеСвойстваУзла(ТрудоваяДеятельность, "position"); 
		СтруктураТрудоваяДеятельность.Обязанности = УдалитьЛишнееИзСтроки(ЗначениеСвойстваУзла(ТрудоваяДеятельность, "description"));
		СфераДеятельностиКомпании = "";
		Для Каждого СфераДеятельности Из ЗначениеСвойстваУзла(ТрудоваяДеятельность, "industries") Цикл
			Если СфераДеятельности = Неопределено 
				Или СфераДеятельности = "null" Тогда
				Продолжить;
			КонецЕсли;
			СфераДеятельностиКомпании = СфераДеятельностиКомпании + ?(ЗначениеЗаполнено(СфераДеятельностиКомпании), ", ", "") + ЗначениеСвойстваУзла(СфераДеятельности, "name");	
		КонецЦикла;
		СтруктураТрудоваяДеятельность.СфераДеятельностиКомпании = СфераДеятельностиКомпании;	
		
		МассивТрудоваяДеятельность.Добавить(СтруктураТрудоваяДеятельность);			
		
	КонецЦикла;
	СтруктураДанныхКандидата.Вставить("ТрудоваяДеятельность", МассивТрудоваяДеятельность);
	
	СтруктураДанныхКандидата.Вставить("Фамилия", ЗначениеСвойстваУзла(СтруктураРезюме, "last_name"));
	СтруктураДанныхКандидата.Вставить("Имя", ЗначениеСвойстваУзла(СтруктураРезюме, "first_name"));
	СтруктураДанныхКандидата.Вставить("Отчество", ЗначениеСвойстваУзла(СтруктураРезюме, "middle_name"));
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураРезюме, "gender")) Тогда

		Если ЗначениеСвойстваУзла(СтруктураРезюме, "gender", "id") = "male" Тогда
			СтруктураДанныхКандидата.Вставить("Пол", ИнтеграцияРекрутинговыхСайтовКлиентСервер.МужскойПол());
		ИначеЕсли ЗначениеСвойстваУзла(СтруктураРезюме, "gender", "id") = "female" Тогда
			СтруктураДанныхКандидата.Вставить("Пол", ИнтеграцияРекрутинговыхСайтовКлиентСервер.ЖенскийПол());
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураРезюме, "salary")) Тогда
		СтруктураДанныхКандидата.Вставить("ОжидаемыйДоход", ЗначениеСвойстваУзла(СтруктураРезюме, "salary", "amount"));
	КонецЕсли;
	
	МассивЗнаниеЯзыков = Новый Массив;
	Для Каждого ЗнаниеЯзыка Из ЗначениеСвойстваУзла(СтруктураРезюме, "language") Цикл
		
		СтруктураЗнаниеЯзыков = СтруктураЗнаниеЯзыков();
		СтруктураЗнаниеЯзыков.Язык = ЗначениеСвойстваУзла(ЗнаниеЯзыка, "name");
		СтруктураЗнаниеЯзыков.СтепеньЗнанияЯзыка = СтепеньЗнанияЯзыкаПоИдентификаторуHeadHunter(ЗначениеСвойстваУзла(ЗнаниеЯзыка, "level", "id"));
		
		МассивЗнаниеЯзыков.Добавить(СтруктураЗнаниеЯзыков);
		
	КонецЦикла;
	СтруктураДанныхКандидата.Вставить("ЗнаниеЯзыков", МассивЗнаниеЯзыков);
	
	СтруктураДанныхКандидата.Вставить("Ответственный", Пользователи.ТекущийПользователь());
	СтруктураДанныхКандидата.Вставить("ДатаРегистрации", ТекущаяДатаСеанса());
	СтруктураДанныхКандидата.Вставить("ДатаОткрытия", ТекущаяДатаСеанса());
	СтруктураДанныхКандидата.Вставить("ИсточникИнформации", ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter());
	СтруктураДанныхКандидата.Вставить("АдресРезюме", ЗначениеСвойстваУзла(СтруктураРезюме, "alternate_url"));
	СтруктураДанныхКандидата.Вставить("Фото", ЗначениеСвойстваУзла(СтруктураРезюме, "Фото"));
	
	СтруктураДанныхКандидата.Вставить("Комментарии", СтруктураРезюме["Комментарии"]);
	
	Возврат СтруктураДанныхКандидата;
	
КонецФункции

Функция СтруктураДанныхКандидатаRabota(СтруктураРезюме)
	
	СтруктураДанныхКандидата = Справочники[ИнтеграцияРекрутинговыхСайтовКлиентСервер.ИмяСправочникаКандидатов()].СтруктураДанныхКандидата();
	
	СтруктураДанныхКандидата.Фамилия = ЗначениеСвойстваУзла(СтруктураРезюме, "surname");
	СтруктураДанныхКандидата.Имя = ЗначениеСвойстваУзла(СтруктураРезюме, "personName");
	СтруктураДанныхКандидата.Отчество = ЗначениеСвойстваУзла(СтруктураРезюме, "secondName");
	
	СтруктураДанныхКандидата.ДатаРождения = ПрочитатьДатуJSON(ЗначениеСвойстваУзла(СтруктураРезюме, "birthDate"), ФорматДатыJSON.ISO);
	
	МассивКонтактнаяИнформация = Новый Массив;
	ЗаполнитьМассивКонтактнойИнформацииRabota(СтруктураРезюме, МассивКонтактнаяИнформация);
	СтруктураДанныхКандидата.Вставить("КонтактнаяИнформация", МассивКонтактнаяИнформация);
	
	МассивГражданство = Новый Массив;
	МассивСостояниеВБраке = Новый Массив;
	Женат = Ложь;
	
	Для Каждого ДопИнформация Из ЗначениеСвойстваУзла(СтруктураРезюме, "additional") Цикл
		
		Страна = ЗначениеСвойстваУзла(ДопИнформация, "citizenship", "name");
		Если ЗначениеЗаполнено(Страна) Тогда
			МассивГражданство.Добавить(Новый Структура("Страна, Дата", Страна));
		КонецЕсли;
		
		Женат = ЗначениеСвойстваУзла(ДопИнформация, "isMarried");
		
	КонецЦикла;
	
	МассивСостояниеВБраке.Добавить(Новый Структура("СостояниеВБраке, Дата", ?(ЗначениеЗаполнено(Женат) И Женат, "Женат", "Не женат")));
	
	СтруктураДанныхКандидата.СостояниеВБраке = МассивСостояниеВБраке;
	СтруктураДанныхКандидата.Гражданство = МассивГражданство;
	
	МассивОбразование = Новый Массив;
	ЗаполнитьМассивОбразованиеRabota(СтруктураРезюме, МассивОбразование);
	СтруктураДанныхКандидата.Вставить("Образование", МассивОбразование);
	
	МассивТрудоваяДеятельность = Новый Массив;
	Для Каждого ТрудоваяДеятельность Из ЗначениеСвойстваУзла(СтруктураРезюме, "experience") Цикл
		
		Если Не ЗначениеЗаполнено(ТрудоваяДеятельность) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураТрудоваяДеятельность = СтруктураТрудоваяДеятельность();
		СтруктураТрудоваяДеятельность.Организация = ЗначениеСвойстваУзла(ТрудоваяДеятельность, "name");
		
		ДатаНачала = ЗначениеСвойстваУзла(ТрудоваяДеятельность, "dateFrom");
		ДатаНачала = ?(ЗначениеЗаполнено(ДатаНачала), ПрочитатьДатуJSON(ЗначениеСвойстваУзла(ТрудоваяДеятельность, "dateFrom"), ФорматДатыJSON.ISO), Дата(1,1,1));
		
		ДатаОкончания = ЗначениеСвойстваУзла(ТрудоваяДеятельность, "dateTo");
		ДатаОкончания = ?(ЗначениеЗаполнено(ДатаОкончания), ПрочитатьДатуJSON(ЗначениеСвойстваУзла(ТрудоваяДеятельность, "dateTo"), ФорматДатыJSON.ISO), Дата(1,1,1));
		
		СтруктураТрудоваяДеятельность.ДатаНачала = ДатаНачала;
		СтруктураТрудоваяДеятельность.ДатаОкончания = ДатаОкончания;
		
		СтруктураТрудоваяДеятельность.Должность = ЗначениеСвойстваУзла(ТрудоваяДеятельность, "position"); 
		СтруктураТрудоваяДеятельность.Обязанности = ЗначениеСвойстваУзла(ТрудоваяДеятельность, "comment");
		
		СфераДеятельностиКомпании = "";
		Для Каждого СфераДеятельности Из ЗначениеСвойстваУзла(ТрудоваяДеятельность, "branch") Цикл
			Если ЗначениеЗаполнено(СфераДеятельности) Тогда
				СфераДеятельностиКомпании = СфераДеятельностиКомпании + ?(ЗначениеЗаполнено(СфераДеятельностиКомпании), ", ", "") + ЗначениеСвойстваУзла(СфераДеятельности, "name");
			КонецЕсли;
		КонецЦикла;
		СтруктураТрудоваяДеятельность.СфераДеятельностиКомпании = СфераДеятельностиКомпании;
		
		МассивТрудоваяДеятельность.Добавить(СтруктураТрудоваяДеятельность);
		
	КонецЦикла;
	
	СтруктураДанныхКандидата.Вставить("ТрудоваяДеятельность", МассивТрудоваяДеятельность);
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураРезюме, "gender")) Тогда
		
		Если ЗначениеСвойстваУзла(СтруктураРезюме, "gender") = "male" Тогда
			СтруктураДанныхКандидата.Вставить("Пол", ИнтеграцияРекрутинговыхСайтовКлиентСервер.МужскойПол());
		Иначе
			СтруктураДанныхКандидата.Вставить("Пол", ИнтеграцияРекрутинговыхСайтовКлиентСервер.ЖенскийПол());
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураРезюме, "salaryFrom")) Тогда
		СтруктураДанныхКандидата.Вставить("ОжидаемыйДоход", ЗначениеСвойстваУзла(СтруктураРезюме, "salaryFrom"));
	КонецЕсли;
	
	МассивЗнаниеЯзыков = Новый Массив;
	Для Каждого ЗнаниеЯзыка Из ЗначениеСвойстваУзла(СтруктураРезюме, "language") Цикл
		
		СтруктураЗнаниеЯзыков = СтруктураЗнаниеЯзыков();
		СтруктураЗнаниеЯзыков.Язык = ЗначениеСвойстваУзла(ЗнаниеЯзыка, "name");
		СтруктураЗнаниеЯзыков.СтепеньЗнанияЯзыка = СтепеньЗнанияЯзыкаПоИдентификаторуRabota(ЗначениеСвойстваУзла(ЗнаниеЯзыка, "offerLanguageLevel"));
		
		МассивЗнаниеЯзыков.Добавить(СтруктураЗнаниеЯзыков);
		
	КонецЦикла;
	
	СтруктураДанныхКандидата.Вставить("ЗнаниеЯзыков", МассивЗнаниеЯзыков);
	
	СтруктураДанныхКандидата.Вставить("Ответственный", Пользователи.ТекущийПользователь());
	СтруктураДанныхКандидата.Вставить("ДатаРегистрации", ТекущаяДатаСеанса());
	СтруктураДанныхКандидата.Вставить("ДатаОткрытия", ПрочитатьДатуJSON(ЗначениеСвойстваУзла(СтруктураРезюме, "publishDate"), ФорматДатыJSON.ISO));
	СтруктураДанныхКандидата.Вставить("ИсточникИнформации", ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota());
	СтруктураДанныхКандидата.Вставить("АдресРезюме", ЗначениеСвойстваУзла(СтруктураРезюме, "link"));
	СтруктураДанныхКандидата.Вставить("Фото", ЗначениеСвойстваУзла(СтруктураРезюме, "Фото"));
	
	Возврат СтруктураДанныхКандидата;
	
КонецФункции

Функция СтруктураДанныхКандидатаSuperJob(СтруктураРезюме) 
	
	СтруктураДанныхКандидата = Справочники[ИнтеграцияРекрутинговыхСайтовКлиентСервер.ИмяСправочникаКандидатов()].СтруктураДанныхКандидата();
	
	СтруктураДанныхКандидата.Фамилия = ЗначениеСвойстваУзла(СтруктураРезюме, "lastname");
	СтруктураДанныхКандидата.Имя = ЗначениеСвойстваУзла(СтруктураРезюме, "firstname");
	СтруктураДанныхКандидата.Отчество = ЗначениеСвойстваУзла(СтруктураРезюме, "middlename");
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураРезюме, "birthyear")) 
		И ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураРезюме, "birthmonth")) 
		И ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураРезюме, "birthday")) Тогда
		
		СтруктураДанныхКандидата.ДатаРождения = Дата(ЗначениеСвойстваУзла(СтруктураРезюме, "birthyear"), ЗначениеСвойстваУзла(СтруктураРезюме, "birthmonth"), ЗначениеСвойстваУзла(СтруктураРезюме, "birthday"));
	КонецЕсли;
		
	Если Не СтруктураРезюме.agreement Тогда
		СтруктураДанныхКандидата.Вставить("ОжидаемыйДоход", ЗначениеСвойстваУзла(СтруктураРезюме, "payment"));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураРезюме, "gender")) Тогда
		Если Строка(ЗначениеСвойстваУзла(СтруктураРезюме, "gender", "id")) = "2" Тогда
			СтруктураДанныхКандидата.Вставить("Пол", ИнтеграцияРекрутинговыхСайтовКлиентСервер.МужскойПол());
		ИначеЕсли Строка(ЗначениеСвойстваУзла(СтруктураРезюме, "gender", "id")) = "3" Тогда
			СтруктураДанныхКандидата.Вставить("Пол", ИнтеграцияРекрутинговыхСайтовКлиентСервер.ЖенскийПол());
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураРезюме, "citizenship")) Тогда
		МассивГражданство = Новый Массив;
		СтруктураГражданство = Новый Структура("Страна, Дата");
		СтруктураГражданство.Страна = ЗначениеСвойстваУзла(СтруктураРезюме, "citizenship", "title");
		МассивГражданство.Добавить(СтруктураГражданство);
		СтруктураДанныхКандидата.Гражданство = МассивГражданство;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(СтруктураРезюме, "maritalstatus")) Тогда
		МассивСостояниеВБраке = Новый Массив;
		СтруктураСостояниеВБраке = Новый Структура("СостояниеВБраке, Дата");
		СтруктураСостояниеВБраке.СостояниеВБраке = ЗначениеСвойстваУзла(СтруктураРезюме, "maritalstatus", "title");
		МассивСостояниеВБраке.Добавить(СтруктураСостояниеВБраке);
		СтруктураДанныхКандидата.СостояниеВБраке = МассивСостояниеВБраке;
	КонецЕсли;
	
	МассивОбразование = Новый Массив;
	ЗаполнитьМассивОбразованиеSuperJob(СтруктураРезюме, МассивОбразование);
	
	СтруктураДанныхКандидата.Вставить("Образование", МассивОбразование);
	
	МассивТрудоваяДеятельность = Новый Массив;
	Для Каждого ТрудоваяДеятельность Из ЗначениеСвойстваУзла(СтруктураРезюме, "work_history") Цикл
		
		Если ТрудоваяДеятельность = Неопределено 
			Или ТрудоваяДеятельность = "null" Тогда
			
			Продолжить;
		КонецЕсли;
		
		СтруктураТрудоваяДеятельность = СтруктураТрудоваяДеятельность();
		СтруктураТрудоваяДеятельность.Организация = ЗначениеСвойстваУзла(ТрудоваяДеятельность, "name");
		СтруктураТрудоваяДеятельность.Должность = ЗначениеСвойстваУзла(ТрудоваяДеятельность, "profession");    
		
		ГодНачала = ЗначениеСвойстваУзла(ТрудоваяДеятельность, "yearbeg");
		МесяцНачала = ЗначениеСвойстваУзла(ТрудоваяДеятельность, "monthbeg");
		МесяцНачалаСтрокой = ?(ЗначениеЗаполнено(ГодНачала), Формат(ГодНачала, "ЧГ=0"), "0001") + ?(ЗначениеЗаполнено(МесяцНачала), ?(Число(МесяцНачала) < 10, "0" + МесяцНачала, МесяцНачала), "01") + "01";
		
		СтруктураТрудоваяДеятельность.ДатаНачала = МесяцСтрокойВДату(МесяцНачалаСтрокой);
		
		ГодОкончания = ЗначениеСвойстваУзла(ТрудоваяДеятельность, "yearend");
		МесяцОкончания = ЗначениеСвойстваУзла(ТрудоваяДеятельность, "monthend");
		МесяцОкончанияСтрокой = ?(ЗначениеЗаполнено(ГодОкончания), Формат(ГодОкончания, "ЧГ=0"), "0001") + ?(ЗначениеЗаполнено(МесяцОкончания), ?(Число(МесяцОкончания) < 10, "0" + МесяцОкончания, МесяцОкончания), "01") + "01";
		
		СтруктураТрудоваяДеятельность.ДатаОкончания = МесяцСтрокойВДату(МесяцОкончанияСтрокой);
		
		СтруктураТрудоваяДеятельность.Обязанности = УдалитьЛишнееИзСтроки(ЗначениеСвойстваУзла(ТрудоваяДеятельность, "work"));
		СтруктураТрудоваяДеятельность.СфераДеятельностиКомпании = "";
		
		МассивТрудоваяДеятельность.Добавить(СтруктураТрудоваяДеятельность);
		
	КонецЦикла;
	СтруктураДанныхКандидата.Вставить("ТрудоваяДеятельность", МассивТрудоваяДеятельность);
	
	МассивКонтактнаяИнформация = Новый Массив;
	ЗаполнитьМассивКонтактнойИнформацииSuperJob(СтруктураРезюме, МассивКонтактнаяИнформация);
	
	СтруктураДанныхКандидата.Вставить("КонтактнаяИнформация", МассивКонтактнаяИнформация);
	
	МассивЗнаниеЯзыков = Новый Массив;
	Если СтруктураРезюме.Свойство("languages") Тогда
		Для Каждого ЗнаниеЯзыка Из ЗначениеСвойстваУзла(СтруктураРезюме, "languages") Цикл
			
			Если Не ЗначениеЗаполнено(ЗнаниеЯзыка) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЗнаниеЯзыка.Количество() = 2 Тогда
				СтруктураЗнаниеЯзыков = СтруктураЗнаниеЯзыков();
				СтруктураЗнаниеЯзыков.Язык = ЗнаниеЯзыка[0].title;
				СтруктураЗнаниеЯзыков.СтепеньЗнанияЯзыка = СтепеньЗнанияЯзыкаПоИдентификаторуSuperJob(ЗнаниеЯзыка[1].id);
				МассивЗнаниеЯзыков.Добавить(СтруктураЗнаниеЯзыков);
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	СтруктураДанныхКандидата.Вставить("ЗнаниеЯзыков", МассивЗнаниеЯзыков);
	
	СтруктураДанныхКандидата.Вставить("Ответственный", Пользователи.ТекущийПользователь());
	СтруктураДанныхКандидата.Вставить("ДатаРегистрации", ТекущаяДатаСеанса());
	СтруктураДанныхКандидата.Вставить("ИсточникИнформации", ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob());
	СтруктураДанныхКандидата.Вставить("АдресРезюме", СтрЗаменить(ЗначениеСвойстваУзла(СтруктураРезюме, "link"), "\", ""));
	СтруктураДанныхКандидата.Вставить("ДатаОткрытия", ТекущаяДатаСеанса());
	
	Если СтруктураРезюме.Свойство("Фото") Тогда
		СтруктураДанныхКандидата.Вставить("Фото", ЗначениеСвойстваУзла(СтруктураРезюме, "Фото"));
	КонецЕсли;
	
	СтруктураДанныхКандидата.Вставить("Комментарии", СтруктураРезюме["Комментарии"]);
	
	Возврат СтруктураДанныхКандидата;
	
КонецФункции

Функция СтепеньЗнанияЯзыкаПоИдентификаторуHeadHunter(Идентификатор)
	
	СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.ПустаяСсылка();
	Код = Неопределено;
	
	Если Идентификатор = "basic" Тогда // базовые знания
		СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.НайтиПоКоду("1");
		Если Не ЗначениеЗаполнено(СтепеньЗнанияЯзыка) Тогда
			СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.НайтиПоКоду("01");
		КонецЕсли;
		Код = "1";
	ИначеЕсли Идентификатор = "can_read" Тогда // читаю профессиональную литературу
		СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.НайтиПоКоду("1");
		Если Не ЗначениеЗаполнено(СтепеньЗнанияЯзыка) Тогда
			СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.НайтиПоКоду("01");
		КонецЕсли;
		Код = "1";
	ИначеЕсли Идентификатор = "can_pass_interview" Тогда  // могу проходить интервью
		СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.НайтиПоКоду("2");
		Если Не ЗначениеЗаполнено(СтепеньЗнанияЯзыка) Тогда
			СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.НайтиПоКоду("02");
		КонецЕсли;
		Код = "2";
	ИначеЕсли Идентификатор = "fluent" Тогда // свободно владею
		СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.НайтиПоКоду("3");
		Если Не ЗначениеЗаполнено(СтепеньЗнанияЯзыка) Тогда
			СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.НайтиПоКоду("03");
		КонецЕсли;
		Код = "3";
	ИначеЕсли Идентификатор = "native" Тогда // родной
		СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.НайтиПоКоду("3");
		Если Не ЗначениеЗаполнено(СтепеньЗнанияЯзыка) Тогда
			СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.НайтиПоКоду("03");
		КонецЕсли;
		Код = "3";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтепеньЗнанияЯзыка)
		И Код <> Неопределено Тогда
		
		СтепеньЗнанияЯзыка = НоваяСтепеньЗнанияЯзыка(Код);
	КонецЕсли;
	
	Возврат СтепеньЗнанияЯзыка;
	
КонецФункции

Функция СтепеньЗнанияЯзыкаПоИдентификаторуRabota(Идентификатор)
	
	СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.ПустаяСсылка();
	
	Если Идентификатор = 1 Тогда // начальный
		СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.НайтиПоКоду("1");
	ИначеЕсли Идентификатор = 2
		Или Идентификатор = 3 Тогда // средний или продвинутый
		СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.НайтиПоКоду("2");
	Иначе
		СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.НайтиПоКоду("3");
	КонецЕсли;
	
	Возврат СтепеньЗнанияЯзыка;
	
КонецФункции

Функция СтепеньЗнанияЯзыкаПоИдентификаторуSuperJob(Идентификатор) 
	
	СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.ПустаяСсылка();
	Код = Неопределено;
	
	Если Строка(Идентификатор) = "3" Тогда // базовый
		СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.НайтиПоКоду("1");
		Если Не ЗначениеЗаполнено(СтепеньЗнанияЯзыка) Тогда
			СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.НайтиПоКоду("01");
		КонецЕсли;
		Код = "1";
	ИначеЕсли Строка(Идентификатор) = "5" Тогда // технический
		СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.НайтиПоКоду("1");
		Если Не ЗначениеЗаполнено(СтепеньЗнанияЯзыка) Тогда
			СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.НайтиПоКоду("01");
		КонецЕсли;
		Код = "1";
	ИначеЕсли Строка(Идентификатор) = "7" Тогда // разговорный
		СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.НайтиПоКоду("2");
		Если Не ЗначениеЗаполнено(СтепеньЗнанияЯзыка) Тогда
			СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.НайтиПоКоду("02");
		КонецЕсли;
		Код = "2";
	ИначеЕсли Строка(Идентификатор) = "9" Тогда // свободно владею	
		СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.НайтиПоКоду("3");
		Если Не ЗначениеЗаполнено(СтепеньЗнанияЯзыка) Тогда
			СтепеньЗнанияЯзыка = Справочники.СтепениЗнанияЯзыка.НайтиПоКоду("03");
		КонецЕсли;
		Код = "3";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтепеньЗнанияЯзыка) 
		И Код <> Неопределено Тогда
		
		СтепеньЗнанияЯзыка = НоваяСтепеньЗнанияЯзыка(Код);
	КонецЕсли;
	
	Возврат СтепеньЗнанияЯзыка;
	
КонецФункции

Функция ЭлементКлассификатораHeadHunter(КлассификаторHeadHunter, Представление)
	
	ЭлементСправочника = Неопределено;
	ЗначениеПоиска = НРег(Представление);
	
	СоответствиеЗамены = Новый Соответствие;
	СоответствиеЗамены.Вставить("среднее", "среднее общее");
	СоответствиеЗамены.Вставить("среднее специальное", "среднее профессиональное");
	СоответствиеЗамены.Вставить("неоконченное высшее", "неполное высшее");
	СоответствиеЗамены.Вставить("доктор наук", "докторантура");
	СоответствиеЗамены.Вставить("кандидат наук", "аспирантура");
	
	Для Каждого ЭлементHeadHunter Из КлассификаторHeadHunter Цикл
		
		ЗначениеСравнения = НРег(СокрЛП(ЭлементHeadHunter.name));
		
		ЗначениеЗамены = СоответствиеЗамены.Получить(ЗначениеСравнения);
		Если ЗначениеЗаполнено(ЗначениеЗамены) Тогда
			ЗначениеСравнения = ЗначениеЗамены;
		КонецЕсли;
		
		Если ЗначениеСравнения = ЗначениеПоиска Тогда
			ЭлементСправочника = ЭлементHeadHunter;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ЭлементСправочника;
	
КонецФункции

Функция ЭлементКлассификатораRabota(КлассификаторRabota, Представление)
	
	ЭлементСправочника = Неопределено;
	ЗначениеПоиска = НРег(Представление);
	
	СоответствиеЗамены = Новый Соответствие;
	СоответствиеЗамены.Вставить("среднее", "среднее общее");
	
	Для Каждого ЭлементRabota Из КлассификаторRabota Цикл
		
		ЗначениеСравнения = НРег(СокрЛП(ЭлементRabota.name));
		
		ЗначениеЗамены = СоответствиеЗамены.Получить(ЗначениеСравнения);
		Если ЗначениеЗаполнено(ЗначениеЗамены) Тогда
			ЗначениеСравнения = ЗначениеЗамены;
		КонецЕсли;
		
		Если ЗначениеСравнения = ЗначениеПоиска Тогда
			ЭлементСправочника = ЭлементRabota;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ЭлементСправочника;
	
КонецФункции

Функция ЭлементКлассификатораSuperJob(КлассификаторSuperJob, Представление)
	
	ЭлементСправочника = Неопределено;
	ЗначениеПоиска = НРег(Представление);
	
	СоответствиеЗамены = Новый Соответствие;
	СоответствиеЗамены.Вставить("среднее", "среднее общее");
	СоответствиеЗамены.Вставить("среднее специальное", "среднее профессиональное");
	
	Для Каждого ЭлементSuperJob Из КлассификаторSuperJob Цикл
		
		ЗначениеСравнения = НРег(СокрЛП(ЭлементSuperJob.Значение));
		
		ЗначениеЗамены = СоответствиеЗамены.Получить(ЗначениеСравнения);
		Если ЗначениеЗаполнено(ЗначениеЗамены) Тогда
			ЗначениеСравнения = ЗначениеЗамены;
		КонецЕсли;
		
		Если ЗначениеСравнения = ЗначениеПоиска Тогда
			ЭлементСправочника = ЭлементSuperJob;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ЭлементСправочника;
	
КонецФункции

Функция СтрокаДереваПоКлассификатору(СтрокиДереваПоКлассификатору, Значение, Знач ЗначениеРодителя, ЗаменяемыеЗначения)
	
	СтрокаДерева = Неопределено;
	СтрокиДерева = СтрокиДереваПоКлассификатору.НайтиСтроки(Новый Структура("Значение", Значение), Истина);
	
	Если ЗначениеЗаполнено(СтрокиДерева) Тогда
		
		Если СтрокиДерева.Количество() = 1 Тогда
			СтрокаДерева = СтрокиДерева[0];
		Иначе
			
			Для Каждого Строка Из СтрокиДерева Цикл
				
				ЗначениеРодителяИзКлассификатора = ?(ЗначениеЗаполнено(Строка.Родитель), Строка.Родитель.Значение, "");
				
				ЗначениеЗамены = ЗаменяемыеЗначения.Получить(ЗначениеРодителяИзКлассификатора);
				Если ЗначениеЗаполнено(ЗначениеЗамены) Тогда
					ЗначениеРодителяИзКлассификатора = ЗначениеЗамены;
				КонецЕсли;
				
				Если ЗначениеРодителяИзКлассификатора = ЗначениеРодителя Тогда
					СтрокаДерева = Строка;
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтрокаДерева;
	
КонецФункции

Функция СтрокаДереваHeadHunter(СтрокиДереваHeadHunter, Значение, ЗначениеРодителя)
	
	ЗаменяемыеЗначения = Новый Соответствие;
	ЗаменяемыеЗначения.Вставить("Удмуртская Республика", "Республика Удмуртия");
	ЗаменяемыеЗначения.Вставить("Чувашская Республика", "Чувашская республика");
	ЗаменяемыеЗначения.Вставить("Карачаево-Черкесская Республика", "Карачаево-Черкесская республика");
	ЗаменяемыеЗначения.Вставить("Балахт","Балахта");
	
	Возврат СтрокаДереваПоКлассификатору(СтрокиДереваHeadHunter, Значение, ЗначениеРодителя, ЗаменяемыеЗначения);
	
КонецФункции

Функция СтрокаДереваRabota(СтрокиДереваRabota, Значение, ЗначениеРодителя)
	
	ЗаменяемыеЗначения = Новый Соответствие;
	ЗаменяемыеЗначения.Вставить("Еврейская автономная область", "Еврейская АО");
	ЗаменяемыеЗначения.Вставить("Чувашия / Чувашская республика", "Чувашская республика");
	ЗаменяемыеЗначения.Вставить("Ханты-Мансийский Автономный округ - Югра АО", "Ханты-Мансийский АО - Югра");
	ЗаменяемыеЗначения.Вставить("Республика Чеченская", "Чеченская республика");
	ЗаменяемыеЗначения.Вставить("Республика Саха /Якутия/", "Республика Саха");
	ЗаменяемыеЗначения.Вставить("Республика Карачаево-Черкессия","Карачаево-Черкесская республика");
	ЗаменяемыеЗначения.Вставить("Республика Кабардино-Балкария", "Кабардино-Балкарская республика");
	
	ЗначениеЗамены = ЗаменяемыеЗначения.Получить(Значение);
	Если ЗначениеЗаполнено(ЗначениеЗамены) Тогда
		Значение = ЗначениеЗамены;
	КонецЕсли;
	
	Возврат СтрокаДереваПоКлассификатору(СтрокиДереваRabota, Значение, ЗначениеРодителя, ЗаменяемыеЗначения);
	
КонецФункции

Функция СтрокаДереваSuperJob(СтрокиДереваSuperJob, Значение, ЗначениеРодителя)
	
	ЗаменяемыеЗначения = Новый Соответствие;
	ЗаменяемыеЗначения.Вставить("Цели́нное", "Целинное");
	ЗаменяемыеЗначения.Вставить("Костерёво", "Костерево");
	ЗаменяемыеЗначения.Вставить("Еврейская автономная область", "Еврейская АО");
	ЗаменяемыеЗначения.Вставить("Улеты", "Улёты");
	ЗаменяемыеЗначения.Вставить("Берёзовский", "Березовский");
	ЗаменяемыеЗначения.Вставить("Брюховецкая", "Брюховецкая станица");
	ЗаменяемыеЗначения.Вставить("Динская", "Динская станица");
	ЗаменяемыеЗначения.Вставить("Крыловская", "Крыловская станица");
	ЗаменяемыеЗначения.Вставить("Кущевская", "Кущевская станица");
	ЗаменяемыеЗначения.Вставить("Ленинградская", "Ленинградская станица");
	ЗаменяемыеЗначения.Вставить("Северская", "Северская станица");
	ЗаменяемыеЗначения.Вставить("поселок имени Карла Либкнехта", "Посёлок имени Карла Либкнехта");
	ЗаменяемыеЗначения.Вставить("имени Свердлова", "пгт имени Свердлова");
	ЗаменяемыеЗначения.Вставить("Лев Толстой", "поселок Лев Толстой");
	ЗаменяемыеЗначения.Вставить("Белоозерский", "Белоозёрский");
	ЗаменяемыеЗначения.Вставить("Володарское", "Володарского");
	ЗаменяемыеЗначения.Вставить("Воскресенское", "Воскресенск");
	ЗаменяемыеЗначения.Вставить("Милютинское сельское поселение", "Милютинская");
	ЗаменяемыеЗначения.Вставить("Шахтёрск", "Шахтерск");
	ЗаменяемыеЗначения.Вставить("Берёзовский", "Березовский");
	ЗаменяемыеЗначения.Вставить("Озёрный", "Озерный");
	ЗаменяемыеЗначения.Вставить("Богашёв", "Богашёво");
	ЗаменяемыеЗначения.Вставить("Тёплое", "Теплое");
	ЗаменяемыеЗначения.Вставить("Тюменская область, включая Ханты-Мансийский АО и Ямало-Ненецкий АО", "Тюменская область");
	ЗаменяемыеЗначения.Вставить("Дебесы", "Дебёсы");
	ЗаменяемыеЗначения.Вставить("Чукотский автономный округ", "Чукотский АО");
	ЗаменяемыеЗначения.Вставить("Белёв", "Белев");
	ЗаменяемыеЗначения.Вставить("Щёкино", "Щекино");
	
	ЗначениеЗамены = ЗаменяемыеЗначения.Получить(Значение);
	Если ЗначениеЗаполнено(ЗначениеЗамены) Тогда
		Значение = ЗначениеЗамены;
	КонецЕсли;
	
	Возврат СтрокаДереваПоКлассификатору(СтрокиДереваSuperJob, Значение, ЗначениеРодителя, ЗаменяемыеЗначения);
	
КонецФункции

Процедура НайтиКандидатовHeadHunter(СтруктураОтбора, ТаблицаНайденныхКандидатов, НомерСтраницы, Знач ПолеСортировки, ВсегоКандидатов)
	
	МаркерДоступа = МаркерДоступа(ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter());
	
	Если Не ЗначениеЗаполнено(МаркерДоступа) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаОтбора = СтрокаОтбораHeadHunter(СтруктураОтбора, НомерСтраницы);
	
	Соединение = HTTPСоединениеHeadHunter();
	Запрос = HTTPЗапросHeadHunter("/resumes/?" + СтрокаОтбора, МаркерДоступа);
	Ответ = Соединение.Получить(Запрос);
	
	ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку("UTF-8");
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru = 'Ошибка поиска резюме %1: %2'"), 
											Ответ.КодСостояния, 
											ТекущаяСтрока); 
	Иначе
		
		РезультатПоискаРезюме = ПарсерJSON(ТекущаяСтрока, Истина);
		
		Если РезультатПоискаРезюме.Получить("items") = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ВсегоКандидатов = РезультатПоискаРезюме.Получить("found");
		ВсегоКандидатов = Мин(Число(ВсегоКандидатов), 2000);
		
		ДозаполнитьТаблицуРезюмеHeadHunter(ТаблицаНайденныхКандидатов, РезультатПоискаРезюме, ПолеСортировки);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура НайтиКандидатовRabota(СтруктураОтбора, ТаблицаНайденныхКандидатов, НомерСтраницы, Знач ПолеСортировки, ВсегоКандидатов)
	
	МаркерДоступа = МаркерДоступа(ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota());
	
	Если Не ЗначениеЗаполнено(МаркерДоступа) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаОтбора = СтрокаОтбораRabota(СтруктураОтбора, НомерСтраницы);
	
	Соединение = HTTPСоединениеRabota();
	Запрос = HTTPЗапросRabota("search-resumes/?" + СтрокаОтбора, МаркерДоступа);
	Ответ = Соединение.Получить(Запрос);
	
	ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку("UTF-8");
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка поиска резюме %1: %2'"), 
			Ответ.КодСостояния, 
			ТекущаяСтрока);
		
	Иначе
		
		РезультатПоискаРезюме = ПарсерJSON(ТекущаяСтрока);
		
		Если РезультатПоискаРезюме.data = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ВсегоКандидатов = РезультатПоискаРезюме.count;
		ВсегоКандидатов = Мин(Число(ВсегоКандидатов), 2000);
		
		ДозаполнитьТаблицуРезюмеRabota(ТаблицаНайденныхКандидатов, РезультатПоискаРезюме, ПолеСортировки);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура НайтиКандидатовSuperJob(СтруктураОтбора, ТаблицаНайденныхКандидатов, НомерСтраницы, Знач ПолеСортировки, ВсегоКандидатов)
	
	МаркерДоступа = МаркерДоступа(ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob());
	
	Если Не ЗначениеЗаполнено(МаркерДоступа) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаОтбора = СтрокаОтбораSuperJob(СтруктураОтбора, НомерСтраницы);
	
	Соединение = HTTPСоединениеSuperJob();
	Запрос = HTTPЗапросSuperJob("/2.0/resumes/resumes/?" + СтрокаОтбора, МаркерДоступа);
	Ответ = Соединение.Получить(Запрос);
	
	ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку("UTF-8");
	
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru = 'Ошибка загрузки резюме %1: %2'"), 
											Ответ.КодСостояния, 
											ТекущаяСтрока);
		
		
	Иначе
		
		РезультатПоискаРезюме = ПарсерJSON(ТекущаяСтрока);
		
		Если РезультатПоискаРезюме.objects = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если РезультатПоискаРезюме.Свойство("total") 
			И Число(РезультатПоискаРезюме.total) > 0 Тогда
		
			ВсегоКандидатов = РезультатПоискаРезюме.total;
		Иначе
			ВсегоКандидатов = 0;
		КонецЕсли;
		
		ВсегоКандидатов = Мин(Число(ВсегоКандидатов), 500);
		
		ДозаполнитьТаблицуРезюмеSuperJob(ТаблицаНайденныхКандидатов, РезультатПоискаРезюме, ПолеСортировки, Соединение, МаркерДоступа);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДозаполнитьТаблицуРезюмеHeadHunter(ТаблицаНайденныхКандидатов, РезультатПоискаРезюме, ПолеСортировки)
	
	Для Каждого Резюме Из РезультатПоискаРезюме.Получить("items") Цикл
		
		Если Резюме = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаНайденныхКандидатов.Добавить();
		ФИО = "" + ЗначениеСвойстваУзла(Резюме, "last_name") + " " + ЗначениеСвойстваУзла(Резюме, "first_name") + " " + ЗначениеСвойстваУзла(Резюме, "middle_name");
		НоваяСтрока.НазваниеРезюме = ЗначениеСвойстваУзла(Резюме, "title");
		НоваяСтрока.ФИО = ?(ЗначениеЗаполнено(ФИО), ФИО, "-");
		НоваяСтрока.Регион = ЗначениеСвойстваУзла(Резюме, "area", "name", , "-");
		НоваяСтрока.ГдеНайден = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter();
		НоваяСтрока.УровеньЗарплаты = "" + ЗначениеСвойстваУзла(Резюме, "salary", "amount") + " " + ЗначениеСвойстваУзла(Резюме, "salary", "currency");
		Если Не ЗначениеЗаполнено(НоваяСтрока.УровеньЗарплаты) Тогда
			НоваяСтрока.УровеньЗарплаты = "-";
		КонецЕсли;
		Возраст = ЗначениеСвойстваУзла(Резюме, "age");
		Если ЗначениеЗаполнено(Возраст) Тогда
			НоваяСтрока.Возраст = Число(Возраст);
		Иначе
			НоваяСтрока.Возраст = "-";
		КонецЕсли;
		НоваяСтрока.ИдентификаторРезюме = ЗначениеСвойстваУзла(Резюме, "id");
		Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(Резюме, "created_at")) Тогда 
			НоваяСтрока.ДатаРазмещения = ДатаПоСтроке(ЗначениеСвойстваУзла(Резюме, "created_at"));
		ИначеЕсли ЗначениеЗаполнено(ЗначениеСвойстваУзла(Резюме, "updated_at")) Тогда
			НоваяСтрока.ДатаРазмещения = ДатаПоСтроке(ЗначениеСвойстваУзла(Резюме, "updated_at"));;
		КонецЕсли;
		НоваяСтрока.СсылкаНаРезюме = ЗначениеСвойстваУзла(Резюме, "alternate_url");
		МестаРаботы = ЗначениеСвойстваУзла(Резюме, "experience");
		Если ЗначениеЗаполнено(МестаРаботы) 
			И МестаРаботы.Количество() > 0 Тогда
			ПоследнееМестоРаботы = МестаРаботы[0];
			Если ПоследнееМестоРаботы = Неопределено 
				Или ПоследнееМестоРаботы = "null" Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока.ПоследнееМестоРаботы = "" + ЗначениеСвойстваУзла(ПоследнееМестоРаботы, "company") + ", " + ЗначениеСвойстваУзла(ПоследнееМестоРаботы, "position") + " " + НСтр("ru = 'с'") + " " + Формат(Дата(СтрЗаменить(ЗначениеСвойстваУзла(ПоследнееМестоРаботы, "start"), "-", "")), "ДЛФ=Д") + " " + НСтр("ru = 'по'") + " " + ?(ЗначениеЗаполнено(ЗначениеСвойстваУзла(ПоследнееМестоРаботы, "end")), Формат(Дата(СтрЗаменить(ЗначениеСвойстваУзла(ПоследнееМестоРаботы, "end"), "-", "")), "ДЛФ=Д"), НСтр("ru = 'настоящее время'"));
		КонецЕсли;
		
		Пол = "-";
		Если Не ЗначениеЗаполнено(ЗначениеСвойстваУзла(Резюме, "gender")) Тогда
			Пол = "-";
		ИначеЕсли ЗначениеСвойстваУзла(Резюме, "gender", "id") = "male" Тогда
			Пол = ИнтеграцияРекрутинговыхСайтовКлиентСервер.МужскойПол();
		ИначеЕсли ЗначениеСвойстваУзла(Резюме, "gender", "id") = "female" Тогда
			Пол = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ЖенскийПол();
		КонецЕсли;
		
		НоваяСтрока.Пол = Пол; 
		НоваяСтрока.ПолеСортировки = ПолеСортировки;
		
		ПолеСортировки = ПолеСортировки + 1;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДозаполнитьТаблицуРезюмеRabota(ТаблицаНайденныхКандидатов, РезультатПоискаРезюме, ПолеСортировки)
	
	Для Каждого Резюме Из РезультатПоискаРезюме.data Цикл
		
		НоваяСтрока = ТаблицаНайденныхКандидатов.Добавить();
		
		ФИО = СтрШаблон("%1 %2 %3", ЗначениеСвойстваУзла(Резюме, "surname"), ЗначениеСвойстваУзла(Резюме, "personName"), ЗначениеСвойстваУзла(Резюме, "secondName"));
		УровеньЗарплаты = ЗначениеСвойстваУзла(Резюме, "salaryFrom");
		Возраст = ЗначениеСвойстваУзла(Резюме, "age");
		
		НоваяСтрока.НазваниеРезюме = ЗначениеСвойстваУзла(Резюме, "name");
		НоваяСтрока.ФИО = ?(ЗначениеЗаполнено(ФИО), СокрЛП(ФИО), "-");
		НоваяСтрока.Регион = ЗначениеСвойстваУзла(Резюме, "region", "name");
		НоваяСтрока.ГдеНайден = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota();
		НоваяСтрока.УровеньЗарплаты = ?(ЗначениеЗаполнено(УровеньЗарплаты), СтрШаблон("%1 %2", УровеньЗарплаты, ЗначениеСвойстваУзла(Резюме, "currency", "name")), "-");
		НоваяСтрока.Возраст = ?(ЗначениеЗаполнено(Возраст), Возраст, "-");
		НоваяСтрока.ИдентификаторРезюме = Формат(ЗначениеСвойстваУзла(Резюме, "id"), "ЧГ=0");
		Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(Резюме, "publishDate")) Тогда
			НоваяСтрока.ДатаРазмещения = ПрочитатьДатуJSON(ЗначениеСвойстваУзла(Резюме, "publishDate"), ФорматДатыJSON.ISO);
		ИначеЕсли ЗначениеЗаполнено(ЗначениеСвойстваУзла(Резюме, "modifiedDate")) Тогда
			НоваяСтрока.ДатаРазмещения = ПрочитатьДатуJSON(ЗначениеСвойстваУзла(Резюме, "modifiedDate"), ФорматДатыJSON.ISO);
		КонецЕсли;
		НоваяСтрока.СсылкаНаРезюме = ЗначениеСвойстваУзла(Резюме, "link");
		
		ПоследнееМестоРаботы = ЗначениеСвойстваУзла(Резюме, "lastExperience");
		Если ЗначениеЗаполнено(ПоследнееМестоРаботы) Тогда 
			
			Организация =  ПодставитьСпецСимволы(ПоследнееМестоРаботы.organizationName);
			Должность = ПоследнееМестоРаботы.positionName;
			ДатаНачала = Формат(ПрочитатьДатуJSON(ПоследнееМестоРаботы.dateStart, ФорматДатыJSON.ISO), "ДЛФ=Д");
			ДатаОкончания = ?(ПоследнееМестоРаботы.dateEnd = "по наст. время", НСтр("ru = 'по настоящее время'"), Формат(ПрочитатьДатуJSON(ПоследнееМестоРаботы.dateEnd, ФорматДатыJSON.ISO), "ДЛФ=Д"));
			
			НоваяСтрока.ПоследнееМестоРаботы = СтрШаблон("%1, %2 с %3 %4", Организация, Должность, ДатаНачала, ДатаОкончания);
			
		КонецЕсли;
		
		НоваяСтрока.Пол = ?(Резюме.isMale,  ИнтеграцияРекрутинговыхСайтовКлиентСервер.МужскойПол(), ИнтеграцияРекрутинговыхСайтовКлиентСервер.ЖенскийПол());
		НоваяСтрока.ПолеСортировки = ПолеСортировки;
		
		ПолеСортировки = ПолеСортировки + 1;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДозаполнитьТаблицуРезюмеSuperJob(ТаблицаНайденныхКандидатов, РезультатПоискаРезюме, ПолеСортировки, Соединение, МаркерДоступа)
	
	ИнформацияОПользователе = ИнформацияОПользователе(ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob(), МаркерДоступа);
	
	Для Каждого Резюме Из РезультатПоискаРезюме.objects Цикл
		
		Если Резюме = Неопределено 
			Или ТипЗнч(Резюме) <> Тип("Структура") Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаНайденныхКандидатов.Добавить();
		НоваяСтрока.ИдентификаторРезюме = Формат(ЗначениеСвойстваУзла(Резюме, "id"), "ЧГ=0");
		
		Если Не ЗначениеЗаполнено(ЗначениеСвойстваУзла(Резюме, "firstname")) 
			И ИнформацияОПользователе.БезлимитныйТариф Тогда
			
			ОткрытоеРезюме = ОткрытоеРезюмеSuperJob(НоваяСтрока.ИдентификаторРезюме, Соединение, МаркерДоступа);
			Резюме = ?(ОткрытоеРезюме <> Неопределено, ОткрытоеРезюме, Резюме);
			
		КонецЕсли;
		
		ФИО = "" + ЗначениеСвойстваУзла(Резюме, "lastname") + " " + ЗначениеСвойстваУзла(Резюме, "firstname") + " " + ЗначениеСвойстваУзла(Резюме, "middlename");
		
		НоваяСтрока.ФИО = ?(ЗначениеЗаполнено(ФИО), СокрЛП(ФИО), "-");
		НоваяСтрока.НазваниеРезюме = ЗначениеСвойстваУзла(Резюме, "profession");
		НоваяСтрока.Регион = ЗначениеСвойстваУзла(Резюме, "town", "title", , "-");
		НоваяСтрока.ГдеНайден = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob();
		НоваяСтрока.УровеньЗарплаты = "" + ЗначениеСвойстваУзла(Резюме, "payment") + " " + ЗначениеСвойстваУзла(Резюме, "currency");
		Если Не ЗначениеЗаполнено(НоваяСтрока.УровеньЗарплаты) Тогда
			НоваяСтрока.УровеньЗарплаты = "-";
		КонецЕсли;
		Возраст = ЗначениеСвойстваУзла(Резюме, "age");
		Если ЗначениеЗаполнено(Возраст) Тогда
			НоваяСтрока.Возраст = Число(Возраст);
		Иначе
			НоваяСтрока.Возраст = "-";
		КонецЕсли;
		Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(Резюме, "date_published")) Тогда
			НоваяСтрока.ДатаРазмещения = Дата(1970, 1, 1) + ЗначениеСвойстваУзла(Резюме, "date_published");
		ИначеЕсли ЗначениеЗаполнено(ЗначениеСвойстваУзла(Резюме, "date_last_modified")) Тогда
			НоваяСтрока.ДатаРазмещения = Дата(1970, 1, 1) + ЗначениеСвойстваУзла(Резюме, "date_last_modified");
		КонецЕсли;
		НоваяСтрока.СсылкаНаРезюме = СтрЗаменить(ЗначениеСвойстваУзла(Резюме, "link"), "\", "");
		МестаРаботы = ЗначениеСвойстваУзла(Резюме, "work_history");
		Если ЗначениеЗаполнено(МестаРаботы) 
			И МестаРаботы.Количество() > 0 Тогда
			ПоследнееМестоРаботы = МестаРаботы[0];
			
			Если ПоследнееМестоРаботы = Неопределено 
				Или ПоследнееМестоРаботы = "null" Тогда
				Продолжить;
			КонецЕсли;
			
			ГодНачала = ЗначениеСвойстваУзла(ПоследнееМестоРаботы, "yearbeg");
			МесяцНачала = ЗначениеСвойстваУзла(ПоследнееМестоРаботы, "monthbeg");
			ДатаНачала = "" + ?(ЗначениеЗаполнено(ГодНачала), Формат(ГодНачала, "ЧГ=0"), "0001") + ?(ЗначениеЗаполнено(МесяцНачала), ?(Число(МесяцНачала) < 10, "0" + МесяцНачала, МесяцНачала), "01") + "01";
			
			ГодОкончания = ЗначениеСвойстваУзла(ПоследнееМестоРаботы, "yearend");
			МесяцОкончания = ЗначениеСвойстваУзла(ПоследнееМестоРаботы, "monthend");
			ДатаОкончания = "" + ?(ЗначениеЗаполнено(ГодОкончания), Формат(ГодОкончания, "ЧГ=0"), "0001") + ?(ЗначениеЗаполнено(МесяцОкончания), ?(Число(МесяцОкончания) < 10, "0" + МесяцОкончания, МесяцОкончания), "01") + "01";
			
			Если ЗначениеЗаполнено(ЗначениеСвойстваУзла(ПоследнееМестоРаботы, "name"))
				И ЗначениеЗаполнено(ЗначениеСвойстваУзла(ПоследнееМестоРаботы, "profession")) Тогда
				НоваяСтрока.ПоследнееМестоРаботы = ЗначениеСвойстваУзла(ПоследнееМестоРаботы, "name") + ", " + ЗначениеСвойстваУзла(ПоследнееМестоРаботы, "profession") + " " + НСтр("ru = 'с'") + " " + Формат(Дата(ДатаНачала), "ДЛФ=Д") + " " + НСтр("ru = 'по'") + " " + ?(ЗначениеЗаполнено(Дата(ДатаОкончания)), Формат(Дата(ДатаОкончания), "ДЛФ=Д"), НСтр("ru = 'настоящее время'"));
			КонецЕсли;
		КонецЕсли;
		
		Пол = "-";
		Если Строка(ЗначениеСвойстваУзла(Резюме, "gender", "id")) = "2" Тогда
			Пол = ИнтеграцияРекрутинговыхСайтовКлиентСервер.МужскойПол();
		ИначеЕсли Строка(ЗначениеСвойстваУзла(Резюме, "gender", "id")) = "3" Тогда
			Пол = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ЖенскийПол();
		КонецЕсли;
		
		НоваяСтрока.Пол = Пол;
		НоваяСтрока.ПолеСортировки = ПолеСортировки;
		
		ПолеСортировки = ПолеСортировки + 1;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ДатаЗакрытияRabotaMail()
	
	Возврат Дата(2016, 11, 1);
	
КонецФункции

Функция ДатаПоСтроке(ДатаСтрока)
	Возврат Дата(СтрЗаменить(Лев(ДатаСтрока, 10), "-", ""));
КонецФункции

Функция СтруктураДанных(id = Неопределено, name = Неопределено);
	Возврат Новый Структура("id, name", id, name);
КонецФункции

Функция СтруктураДанныхSuperJob()
	Возврат Новый Структура("id, title");
КонецФункции

Функция СтруктураТрудоваяДеятельность()
	
	Возврат ИнтеграцияРекрутинговыхСайтовВнутренний.СтруктураТрудоваяДеятельность();
	
КонецФункции

Функция СтруктураЗнаниеЯзыков()
	
	Возврат ИнтеграцияРекрутинговыхСайтовВнутренний.СтруктураЗнаниеЯзыков();
	
КонецФункции

Функция HTTPСоединениеHeadHunter() Экспорт
	
	Возврат НовоеHTTPСоединение(АдресЗапросаHeadHunter(), Истина);
	
КонецФункции

Функция HTTPСоединениеRabota() Экспорт
	
	Возврат НовоеHTTPСоединение(АдресЗапросаRabota(), Истина);
	
КонецФункции

Функция HTTPСоединениеSuperJob() Экспорт
	
	Возврат НовоеHTTPСоединение(АдресЗапросаSuperJob(), Истина);
	
КонецФункции

Функция НовоеHTTPСоединение(Адрес, Защищенное = Ложь)
	
	Прокси = ПолучениеФайловИзИнтернетаКлиентСервер.ПолучитьПрокси("http://" + Адрес);
	ТаймаутСоединения = 10;
	
	Если Защищенное Тогда
		
		ЗащищенноеСоединение = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение(, Новый СертификатыУдостоверяющихЦентровОС);
		Соединение = Новый HTTPСоединение(Адрес, , , , Прокси, ТаймаутСоединения, ЗащищенноеСоединение);
		
	Иначе
		Соединение = Новый HTTPСоединение(Адрес, , , , Прокси, ТаймаутСоединения);
	КонецЕсли;
	
	Возврат Соединение;
	
КонецФункции

Функция HTTPЗапросHeadHunter(ТекстЗапроса, МаркерДоступа = "") Экспорт
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("User-Agent", "1С:Зарплата и управление персоналом 8 КОРП (v8@1c.ru)");
	
	Если ЗначениеЗаполнено(МаркерДоступа) Тогда
		ЗаголовокHTTP.Вставить("Authorization", "Bearer " + МаркерДоступа);
	КонецЕсли;
	
	Запрос = Новый HTTPЗапрос(ТекстЗапроса, ЗаголовокHTTP);
	
	Возврат Запрос;
	
КонецФункции

Функция HTTPЗапросRabota(ТекстЗапроса, МаркерДоступа = "") Экспорт
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type", "application/json");
	
	Если ЗначениеЗаполнено(МаркерДоступа) Тогда
		ЗаголовокHTTP.Вставить("X-Authorization", МаркерДоступа);
	КонецЕсли;
	
	Запрос = Новый HTTPЗапрос(СтрШаблон("/v2/%1",ТекстЗапроса), ЗаголовокHTTP);
	
	Возврат Запрос;
	
КонецФункции

Функция HTTPЗапросSuperJob(ТекстЗапроса, МаркерДоступа = "") Экспорт
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("X-Api-App-Id", ПарольПрограммыSuperJob());
	ЗаголовокHTTP.Вставить("Content-Type", "application/x-www-form-urlencoded");
	
	Если ЗначениеЗаполнено(МаркерДоступа) Тогда
		ЗаголовокHTTP.Вставить("Authorization", "Bearer " + МаркерДоступа);
	КонецЕсли;
	
	Запрос = Новый HTTPЗапрос(ТекстЗапроса, ЗаголовокHTTP);
	
	Возврат Запрос;
	
КонецФункции

Функция ВыполнитьЗапросКРекрутинговомуСайту(Соединение, Запрос)
	
	Попытка
		Ответ = Соединение.Получить(Запрос);
	Исключение
		РезультатДиагностики = ОбщегоНазначенияКлиентСервер.ДиагностикаСоединения(Соединение.Сервер);
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1
			           |
			           |Результат диагностики:
			           |%2'"),
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке()),
			РезультатДиагностики.ОписаниеОшибки);
	КонецПопытки;
	
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка при обработке запроса к ресурсу:
			           |%1'"),
			Ответ.ПолучитьТелоКакСтроку());
	КонецЕсли;
		
	Возврат Ответ;
	
КонецФункции

Процедура ЗарегистрироватьОшибкуОбращенияКРекрутинговомуСайту(ИнформацияОбОшибке, ПоказыватьСообщения = Истина)
	
	ИмяСобытия = НСтр("ru = 'Обращение к рекрутинговому сайту'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, 
		ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
	
	Если Не ПоказыватьСообщения Тогда
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Ошибка при обращении к рекрутинговому сайту. Обратитесь к администратору.
			       |Техническая информация:
			       |%1'"), 
		КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

Функция АдресЗапросаHeadHunter()
	Возврат "api.hh.ru";
КонецФункции

Функция АдресЗапросаRabota()
	Возврат "api-rest.rabota.ru";
КонецФункции

Функция АдресЗапросаSuperJob()
	Возврат "api.superjob.ru";
КонецФункции

Функция АдресаРекрутинговыхСайтов()
	
	АдресаСайтов = Новый Соответствие;
	АдресаСайтов.Вставить(ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter(), АдресЗапросаHeadHunter());
	АдресаСайтов.Вставить(ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota(), АдресЗапросаRabota());
	АдресаСайтов.Вставить(ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob(), АдресЗапросаSuperJob());
	
	Возврат АдресаСайтов;
	
КонецФункции

Функция ID_ПрограммыHeadHunter()
	Возврат "TQHP8O8KSN8PQICJQHHF3EI0HTS2IQS9R744R2SOC4KN26NMGCJG9NDLOVCNISON";
КонецФункции

Функция ПарольПрограммыHeadHunter()
	Возврат "R3ELKPR595457UTD99DLUUDNJJBMGK9MOB4RQERNJU0I4LIA9LL00DE4TNT08F04";
КонецФункции

Функция ID_ПрограммыSuperJob()
	Возврат "281";	
КонецФункции

Функция ПарольПрограммыSuperJob()
	Возврат "v1.h002a1c6e8b6cd8456fa1db3922371bc1025a152965d380b4a97304e42cf37eba4ad90f7b.f3523790948436b2d9d94e9d09955cf01468db5e";
КонецФункции

Функция ID_ПрограммыRabota()
	Возврат СтрШаблон("%1%2", "f82585a6d7eb7d44e670880d3033d276b6da", "1234");
КонецФункции

Функция ДатаПоСтрокеHeadHunter(СтрокаJSON)
	
	Возврат Дата(СтрЗаменить(Лев(СтрокаJSON, 10), "-", "") + СтрЗаменить(Сред(СтрокаJSON, 12, 8), ":", ""));
	
КонецФункции

Функция ДатаПоСтрокеSuperJob(СтрокаJSON)
	
	Возврат Дата(1970, 1, 1) + Число(СтрокаJSON);
	
КонецФункции

Функция ЗначениеСвойстваОбъекта(Свойство)
	
	Если ТипЗнч(Свойство.ПубликацияВакансии) = Тип("СписокЗначений") Тогда
		
		МассивЭлементов = Новый Массив;
		Для Каждого ЭлементСписка Из Свойство.ПубликацияВакансии Цикл
			Если ЗначениеЗаполнено(ЭлементСписка.Значение) Тогда
				МассивЭлементов.Добавить(ЭлементСписка.Значение);
			КонецЕсли;
		КонецЦикла;
		
		ЗначениеСвойства = МассивЭлементов;
		
	Иначе
		ЗначениеСвойства = Свойство.ПубликацияВакансии;
	КонецЕсли;
	
	Возврат ЗначениеСвойства;
	
КонецФункции

#Область КомментарииКандидатов

Процедура ЗагрузитьКомментарииКандидата(Кандидат) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюРекрутинговыхСайтов") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиИнтеграцииРекрутинговыхСайтов) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Пользователь", Пользователи.АвторизованныйПользователь());
	Запрос.УстановитьПараметр("Кандидат", Кандидат);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеПубликации.МестоПубликации,
		|	НастройкиИнтеграции.ИдентификаторПользователя,
		|	ДанныеПубликации.ПолеДанных КАК ИмяПоля,
		|	ДанныеПубликации.Значение КАК СодержимоеПоля
		|ИЗ
		|	РегистрСведений.ДанныеПубликацииРезюме КАК ДанныеПубликации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиИнтеграцииРекрутинговыхСайтов КАК НастройкиИнтеграции
		|		ПО ДанныеПубликации.МестоПубликации = НастройкиИнтеграции.РекрутинговыйСайт
		|			И (ДанныеПубликации.Кандидат = &Кандидат)
		|			И (НастройкиИнтеграции.Пользователь = &Пользователь)
		|			И (НастройкиИнтеграции.Использовать = ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеРезюме = РезультатЗапроса.Выгрузить();
	ЗагрузитьКомментарииКандидатаHeadHunter(Кандидат, ДанныеРезюме);
	ЗагрузитьКомментарииКандидатаRabota(Кандидат, ДанныеРезюме);
	ЗагрузитьКомментарииКандидатаSuperJob(Кандидат, ДанныеРезюме);
	
КонецПроцедуры

Процедура ЗагрузитьКомментарииКандидатаHeadHunter(Кандидат, ДанныеРезюме)
	
	МестоПубликации = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter();
	
	ОтборСтрок = Новый Структура(
		"МестоПубликации, 
		|ИмяПоля");
	ОтборСтрок.МестоПубликации = МестоПубликации;
	ОтборСтрок.ИмяПоля = "owner";
	
	НайденныеСтроки = ДанныеРезюме.НайтиСтроки(ОтборСтрок);
	Если НайденныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не МаркерДоступаАктивен(МестоПубликации, "") Тогда
		Возврат;
	КонецЕсли;
	
	СодержимоеПоляOwner = НайденныеСтроки[0].СодержимоеПоля;
	ИдентификаторКомпании = НайденныеСтроки[0].ИдентификаторПользователя;
	
	ПакетOwner = ПарсерJSON(СодержимоеПоляOwner);
	АдресКомментариев = ПакетOwner["comments"]["url"];
	
	Комментарии = КомментарииКандидатаHeadHunter(АдресКомментариев, ИдентификаторКомпании);
	
	ЗаписатьТаблицуКомментариевКандидата(Комментарии, Кандидат, МестоПубликации);
	
КонецПроцедуры

Процедура ЗагрузитьКомментарииКандидатаRabota(Кандидат, ДанныеРезюме)
	
	МестоПубликации = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota();
	
	ОтборСтрок = Новый Структура(
		"МестоПубликации, 
		|ИмяПоля");
	ОтборСтрок.МестоПубликации = МестоПубликации;
	ОтборСтрок.ИмяПоля = "id";
	
	НайденныеСтроки = ДанныеРезюме.НайтиСтроки(ОтборСтрок);
	Если НайденныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не МаркерДоступаАктивен(МестоПубликации, "") Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторРезюме = НайденныеСтроки[0].СодержимоеПоля;
	
	Комментарии = КомментарииКандидатаRabota(ИдентификаторРезюме, Кандидат);
	
	ЗаписатьТаблицуКомментариевКандидата(Комментарии, Кандидат, МестоПубликации);
	
КонецПроцедуры

Процедура ЗагрузитьКомментарииКандидатаSuperJob(Кандидат, ДанныеРезюме)
	
	МестоПубликации = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob();
	
	ОтборСтрок = Новый Структура(
		"МестоПубликации, 
		|ИмяПоля");
	ОтборСтрок.МестоПубликации = МестоПубликации;
	ОтборСтрок.ИмяПоля = "id";
	
	НайденныеСтроки = ДанныеРезюме.НайтиСтроки(ОтборСтрок);
	Если НайденныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не МаркерДоступаАктивен(МестоПубликации, "") Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторРезюме = НайденныеСтроки[0].СодержимоеПоля;
	
	Комментарии = КомментарииКандидатаSuperJob(ИдентификаторРезюме, Кандидат);
	
	ЗаписатьТаблицуКомментариевКандидата(Комментарии, Кандидат, МестоПубликации);
	
КонецПроцедуры

Процедура ЗаписатьТаблицуКомментариевКандидата(Комментарии, Кандидат, МестоПубликации)
	
	НаборЗаписей = РегистрыСведений.КомментарииКандидатов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Кандидат.Установить(Кандидат);
	УстановитьПривилегированныйРежим(Истина);
	НаборЗаписей.Прочитать();
	УстановитьПривилегированныйРежим(Ложь);
	
	// Удалим комментарии с идентификаторами, которых более нет на сайте.
	УдаляемыеСтроки = Новый Массив;
	ОтборСтрок = Новый Структура("Идентификатор");
	Для Каждого СтрокаНабора Из НаборЗаписей Цикл
		Если СтрокаНабора.МестоПубликации <> МестоПубликации Тогда
			Продолжить;
		КонецЕсли;
		ОтборСтрок.Идентификатор = СтрокаНабора.Идентификатор;
		НайденныеСтроки = Комментарии.НайтиСтроки(ОтборСтрок);
		Если НайденныеСтроки.Количество() = 0 Тогда
			УдаляемыеСтроки.Добавить(СтрокаНабора);
		КонецЕсли;
	КонецЦикла;
	Для Каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
		НаборЗаписей.Удалить(УдаляемаяСтрока);
	КонецЦикла;
	
	// Добавляем новые комментарии, изменяем существующие.
	ОтборСтрок = Новый Структура("МестоПубликации");
	ОтборСтрок.МестоПубликации = МестоПубликации;
	Для Каждого Строка Из Комментарии Цикл
		Если Не ЗначениеЗаполнено(Строка.Пользователь) Тогда
			Продолжить;
		КонецЕсли;
		СтрокаНабора = СтрокаКомментарияИзНабораПоИдентификатору(НаборЗаписей, Строка.Идентификатор, ОтборСтрок);
		Если СтрокаНабора = Неопределено Тогда
			СтрокаНабора = НаборЗаписей.Добавить();
			СтрокаНабора.Кандидат = Кандидат;
			СтрокаНабора.МестоПубликации = МестоПубликации;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(СтрокаНабора, Строка);
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Истина);
	НаборЗаписей.Записать();
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция КомментарииКандидатаHeadHunter(АдресКомментариев, ИдентификаторКомпании, Кандидат = Неопределено)
	
	ЧастиАдреса = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(АдресКомментариев);
	ИдентификаторРезюме = ЧастиАдреса[ЧастиАдреса.ВГраница()];
	
	Комментарии = ТаблицаКомментарииКандидатов();
	
	МестоПубликации = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter();
	
	АдресЗапроса = АдресЗапросаHeadHunter();
	МаркерДоступа = МаркерДоступа(МестоПубликации);
	ТекстЗапроса = "/applicant_comments/" + ИдентификаторРезюме;
	КодировкаОтвета = "UTF-8";
	
	Соединение = HTTPСоединениеHeadHunter();
	Запрос = HTTPЗапросHeadHunter(ТекстЗапроса, МаркерДоступа);
	Попытка
		Ответ = ВыполнитьЗапросКРекрутинговомуСайту(Соединение, Запрос);
	Исключение
		ЗарегистрироватьОшибкуОбращенияКРекрутинговомуСайту(ИнформацияОбОшибке(), Ложь);
		Возврат Комментарии;
	КонецПопытки;
	
	ТелоОтветаСтрокой = Ответ.ПолучитьТелоКакСтроку(КодировкаОтвета);
	КоллекцияКомментариев = ПарсерJSON(ТелоОтветаСтрокой);
	
	Для Каждого ОписаниеКомментария Из КоллекцияКомментариев["items"] Цикл
		НоваяСтрока = Комментарии.Добавить();
		НоваяСтрока.Период = ДатаПоСтрокеHeadHunter(ОписаниеКомментария["created_at"]);
		Если ОписаниеКомментария["access_type"]["id"] = "owner" Тогда
			НоваяСтрока.Скрытый = Истина;
		КонецЕсли;
		НоваяСтрока.Идентификатор = ОписаниеКомментария["id"];
		НоваяСтрока.Комментарий = ОписаниеКомментария["text"];
		НоваяСтрока.Автор = ОписаниеКомментария["author"]["full_name"];
		НоваяСтрока.ПринадлежитАвторизованномуПользователю = ОписаниеКомментария["is_mine"];
		НоваяСтрока.МестоПубликации = МестоПубликации;
	КонецЦикла;
	
	Если Не ЕстьИзмененияКомментариев(Кандидат, МестоПубликации, Комментарии) Тогда
		Возврат Комментарии;
	КонецЕсли;
	
	ПодобратьПользователейПоАвторамКомментариевHeadHunter(Комментарии, Соединение, МаркерДоступа, ИдентификаторКомпании);
	
	Возврат Комментарии;
	
КонецФункции

Функция МассивКомментариевКандидатаHeadHunter(АдресКомментариев, ИдентификаторКомпании, Кандидат = Неопределено)
	
	КомментарииТаблица = КомментарииКандидатаHeadHunter(АдресКомментариев, ИдентификаторКомпании, Кандидат);
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(КомментарииТаблица);
	
КонецФункции

Функция КомментарииКандидатаRabota(ИдентификаторРезюме, Кандидат = Неопределено)
	
	Комментарии = ТаблицаКомментарииКандидатов();
	МестоПубликации = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota();
	МаркерДоступа = МаркерДоступа(МестоПубликации);
	
	Соединение = HTTPСоединениеRabota();
	Запрос = HTTPЗапросRabota(СтрШаблон("search-resumes/%1/comments", ИдентификаторРезюме), МаркерДоступа);
	Попытка
		Ответ = ВыполнитьЗапросКРекрутинговомуСайту(Соединение, Запрос);
	Исключение
		ЗарегистрироватьОшибкуОбращенияКРекрутинговомуСайту(ИнформацияОбОшибке(), Ложь);
		Возврат Комментарии;
	КонецПопытки;
	
	ТелоОтветаСтрокой = Ответ.ПолучитьТелоКакСтроку();
	КоллекцияКомментариев = ПарсерJSON(ТелоОтветаСтрокой);
	ПочтаАвторов = Новый Соответствие;
	
	Для Каждого ОписаниеКомментария Из КоллекцияКомментариев Цикл
		
		ОписаниеАвтора = ОписаниеКомментария.person;
		
		НоваяСтрока = Комментарии.Добавить();
		НоваяСтрока.Период = ПрочитатьДатуJSON(ОписаниеКомментария.created, ФорматДатыJSON.ISO);
		НоваяСтрока.Идентификатор = Формат(ОписаниеКомментария.id, "ЧГ=");
		
		НоваяСтрока.Комментарий = ОписаниеКомментария.text;
		НоваяСтрока.Автор = СокрП(СтрШаблон("%1 %2 %3", ОписаниеАвтора.surname, ОписаниеАвтора.name, ОписаниеАвтора.secondName));
		НоваяСтрока.Скрытый = ОписаниеКомментария.forMe;
		
		Если ПочтаАвторов.Получить(НоваяСтрока.Автор) = Неопределено Тогда
			ПочтаАвторов.Вставить(НоваяСтрока.Автор, ОписаниеАвтора.email[0]);
		КонецЕсли;
		
		НоваяСтрока.МестоПубликации = МестоПубликации;
		
	КонецЦикла;
	
	Если Не ЕстьИзмененияКомментариев(Кандидат, МестоПубликации, Комментарии) Тогда
		Возврат Комментарии;
	КонецЕсли;
	
	ПодобратьПользователейПоАвторамКомментариевRabota(Комментарии, ПочтаАвторов);
	
	Возврат Комментарии;
	
КонецФункции

Функция МассивКомментариевКандидатаRabota(ИдентификаторРезюме, Кандидат = Неопределено)
	
	КомментарииТаблица = КомментарииКандидатаRabota(ИдентификаторРезюме, Кандидат);
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(КомментарииТаблица);
	
КонецФункции

Функция КомментарииКандидатаSuperJob(ИдентификаторРезюме, Кандидат = Неопределено)
	
	Комментарии = ТаблицаКомментарииКандидатов();
	
	МестоПубликации = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob();
	
	АдресЗапроса = АдресЗапросаSuperJob();
	МаркерДоступа = МаркерДоступа(МестоПубликации);
	ТекстЗапроса = "/2.0/hr/resumes/" + ИдентификаторРезюме + "/comments/";
	КодировкаОтвета = "UTF-8";
	
	Соединение = HTTPСоединениеSuperJob();
	Запрос = HTTPЗапросSuperJob(ТекстЗапроса, МаркерДоступа);
	Попытка
		Ответ = ВыполнитьЗапросКРекрутинговомуСайту(Соединение, Запрос);
	Исключение
		ЗарегистрироватьОшибкуОбращенияКРекрутинговомуСайту(ИнформацияОбОшибке(), Ложь);
		Возврат Комментарии;
	КонецПопытки;
	
	ТелоОтветаСтрокой = Ответ.ПолучитьТелоКакСтроку(КодировкаОтвета);
	КоллекцияКомментариев = ПарсерJSON(ТелоОтветаСтрокой);
	
	Если КоллекцияКомментариев["objects"] = Неопределено Тогда
		Возврат Комментарии;
	КонецЕсли;
	
	Для Каждого ОписаниеКомментария Из КоллекцияКомментариев["objects"] Цикл
		НоваяСтрока = Комментарии.Добавить();
		НоваяСтрока.Период = МестноеВремя(ДатаПоСтрокеSuperJob(ОписаниеКомментария["date"]), ЧасовойПояс());
		НоваяСтрока.Идентификатор = Формат(ОписаниеКомментария["id"], "ЧГ=");
		НоваяСтрока.Комментарий = ОписаниеКомментария["text"];
		НоваяСтрока.Автор = ОписаниеКомментария["author"];
		Если Не ЗначениеЗаполнено(ОписаниеКомментария["author"]) Тогда
			НоваяСтрока.ПринадлежитАвторизованномуПользователю = Истина;
		КонецЕсли;
		НоваяСтрока.МестоПубликации = МестоПубликации;
	КонецЦикла;
	
	Если Не ЕстьИзмененияКомментариев(Кандидат, МестоПубликации, Комментарии) Тогда
		Возврат Комментарии;
	КонецЕсли;
	
	ПодобратьПользователейПоАвторамКомментариевSuperJob(Комментарии, Соединение, МаркерДоступа);
	
	Возврат Комментарии;
	
КонецФункции

Функция МассивКомментариевКандидатаSuperJob(ИдентификаторРезюме, Кандидат = Неопределено)
	
	КомментарииТаблица = КомментарииКандидатаSuperJob(ИдентификаторРезюме, Кандидат);
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(КомментарииТаблица);
	
КонецФункции

Функция ЕстьИзмененияКомментариев(Кандидат, МестоПубликации, Комментарии)
	
	Если Комментарии.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Кандидат) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Кандидат", Кандидат);
	Запрос.УстановитьПараметр("МестоПубликации", МестоПубликации);
	Запрос.УстановитьПараметр("Идентификаторы", Комментарии.ВыгрузитьКолонку("Идентификатор"));
	Запрос.Текст = 
		"ВЫБРАТЬ 
		|	КомментарииКандидатов.Период,
		|	КомментарииКандидатов.Кандидат,
		|	КомментарииКандидатов.Пользователь,
		|	КомментарииКандидатов.Комментарий,
		|	КомментарииКандидатов.Скрытый,
		|	КомментарииКандидатов.МестоПубликации,
		|	КомментарииКандидатов.Идентификатор
		|ИЗ
		|	РегистрСведений.КомментарииКандидатов КАК КомментарииКандидатов
		|ГДЕ
		|	КомментарииКандидатов.Кандидат = &Кандидат
		|	И КомментарииКандидатов.МестоПубликации = &МестоПубликации
		|	И КомментарииКандидатов.Идентификатор В(&Идентификаторы)";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		// Есть новые комментарии.
		Возврат Истина;
	КонецЕсли;
	
	// Проверяем содержимое.
	ОтборСтрок = Новый Структура("Идентификатор");
	ЗагруженныеКомментарии = РезультатЗапроса.Выгрузить();
	Для Каждого Строка Из Комментарии Цикл
		ОтборСтрок.Идентификатор = Строка.Идентификатор;
		НайденныеСтроки = ЗагруженныеКомментарии.НайтиСтроки(ОтборСтрок);
		Если НайденныеСтроки.Количество() = 0 Тогда
			// Есть отсутствующие комментарии.
			Возврат Истина;
		КонецЕсли;
		// Есть различающиеся комментарии.
		НайденнаяСтрока = НайденныеСтроки[0];
		Если Строка.Комментарий <> НайденнаяСтрока.Комментарий Тогда
			Возврат Истина;
		КонецЕсли;
		Если Строка.Скрытый <> НайденнаяСтрока.Скрытый Тогда
			Возврат Истина;
		КонецЕсли;
		Если Строка.Период <> НайденнаяСтрока.Период Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Процедура ПодобратьПользователейПоАвторамКомментариевHeadHunter(Комментарии, Соединение, МаркерДоступа, ИдентификаторКомпании)
	
	ОтборСтрок = Новый Структура("ПринадлежитАвторизованномуПользователю");
	ОтборСтрок.ПринадлежитАвторизованномуПользователю = Ложь;
	
	НайденныеСтроки = Комментарии.НайтиСтроки(ОтборСтрок);
	Если НайденныеСтроки.Количество() = 0 Тогда
		// Все комментарии принадлежат текущему пользователю.
		Комментарии.ЗаполнитьЗначения(Пользователи.АвторизованныйПользователь(), "Пользователь");
		Возврат;
	КонецЕсли;
	
	АвторыКомментариев = ОбщегоНазначения.ВыгрузитьКолонку(НайденныеСтроки, "Автор", Истина);
	
	КодировкаОтвета	= "UTF-8";
	ТекстЗапроса = "/employers/" + ИдентификаторКомпании + "/managers";
	
	Запрос = HTTPЗапросHeadHunter(ТекстЗапроса, МаркерДоступа);
	Попытка
		Ответ = ВыполнитьЗапросКРекрутинговомуСайту(Соединение, Запрос);
	Исключение
		ЗарегистрироватьОшибкуОбращенияКРекрутинговомуСайту(ИнформацияОбОшибке(), Ложь);
		Возврат;
	КонецПопытки;
	
	ТелоОтветаСтрокой = Ответ.ПолучитьТелоКакСтроку(КодировкаОтвета);
	ОтветЗапроса = ПарсерJSON(ТелоОтветаСтрокой);
	
	АдресаАвторов = ТаблицаАдресаАвторов();
	Для Каждого ОписаниеМенеджера Из ОтветЗапроса["items"] Цикл
		Если АвторыКомментариев.Найти(ОписаниеМенеджера["name"]) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = АдресаАвторов.Добавить();
		НоваяСтрока.ИмяАвтора = ОписаниеМенеджера["name"];
		НоваяСтрока.АдресПочты = ОписаниеМенеджера["email"];
	КонецЦикла;
	
	ПользователиПоАвторам = ПользователиПоАвторам(АдресаАвторов);
	
	Для Каждого Строка Из Комментарии Цикл
		Если Строка.ПринадлежитАвторизованномуПользователю = Истина Тогда
			Строка.Пользователь = Пользователи.АвторизованныйПользователь();
			Продолжить;
		КонецЕсли;
		Пользователь = ПользователиПоАвторам[Строка.Автор];
		Если Пользователь <> Неопределено Тогда
			Строка.Пользователь = Пользователь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПодобратьПользователейПоАвторамКомментариевRabota(Комментарии, ПочтаАвторов)
	
	ОтборСтрок = Новый Структура("ПринадлежитАвторизованномуПользователю");
	ОтборСтрок.ПринадлежитАвторизованномуПользователю = Ложь;
	
	НайденныеСтроки = Комментарии.НайтиСтроки(ОтборСтрок);
	Если НайденныеСтроки.Количество() = 0 Тогда
		// Все комментарии принадлежат текущему пользователю.
		Комментарии.ЗаполнитьЗначения(Пользователи.АвторизованныйПользователь(), "Пользователь");
		Возврат;
	КонецЕсли;
	
	АдресаАвторов = ТаблицаАдресаАвторов();
	Для Каждого АвторИПочта Из ПочтаАвторов Цикл
		НоваяСтрока = АдресаАвторов.Добавить();
		НоваяСтрока.ИмяАвтора = АвторИПочта.Ключ;
		НоваяСтрока.АдресПочты = АвторИПочта.Значение;
	КонецЦикла;
	
	ПользователиПоАвторам = ПользователиПоАвторам(АдресаАвторов);
	
	Для Каждого Строка Из Комментарии Цикл
		
		Если Строка.ПринадлежитАвторизованномуПользователю Тогда
			Строка.Пользователь = Пользователи.АвторизованныйПользователь();
			Продолжить;
		КонецЕсли;
		
		Пользователь = ПользователиПоАвторам[Строка.Автор];
		Если Пользователь <> Неопределено Тогда
			Строка.Пользователь = Пользователь;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПодобратьПользователейПоАвторамКомментариевSuperJob(Комментарии, Соединение, МаркерДоступа)
	
	ОтборСтрок = Новый Структура("ПринадлежитАвторизованномуПользователю");
	ОтборСтрок.ПринадлежитАвторизованномуПользователю = Ложь;
	
	НайденныеСтроки = Комментарии.НайтиСтроки(ОтборСтрок);
	Если НайденныеСтроки.Количество() = 0 Тогда
		// Все комментарии принадлежат текущему пользователю.
		Комментарии.ЗаполнитьЗначения(Пользователи.АвторизованныйПользователь(), "Пользователь");
		Возврат;
	КонецЕсли;
	
	АвторыКомментариев = ОбщегоНазначения.ВыгрузитьКолонку(НайденныеСтроки, "Автор", Истина);
	
	КодировкаОтвета	= "UTF-8";
	ТекстЗапроса = "/2.0/user/list/";
	
	Соединение = HTTPСоединениеSuperJob();
	Запрос = HTTPЗапросSuperJob(ТекстЗапроса, МаркерДоступа);
	Попытка
		Ответ = ВыполнитьЗапросКРекрутинговомуСайту(Соединение, Запрос);
	Исключение
		ЗарегистрироватьОшибкуОбращенияКРекрутинговомуСайту(ИнформацияОбОшибке(), Ложь);
		Возврат;
	КонецПопытки;
	
	ТелоОтветаСтрокой = Ответ.ПолучитьТелоКакСтроку(КодировкаОтвета);
	ОтветЗапроса = ПарсерJSON(ТелоОтветаСтрокой, Истина);
	
	АдресаАвторов = ТаблицаАдресаАвторов();
	Для Каждого КлючИЗначение Из ОтветЗапроса["objects"] Цикл
		ОписаниеМенеджера = КлючИЗначение.Значение;
		ИмяАвтора = ОписаниеМенеджера["firstname"] + " " + ОписаниеМенеджера["lastname"];
		Если АвторыКомментариев.Найти(ИмяАвтора) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = АдресаАвторов.Добавить();
		НоваяСтрока.ИмяАвтора = ИмяАвтора;
		НоваяСтрока.АдресПочты = ОписаниеМенеджера["email"];
	КонецЦикла;
	
	ПользователиПоАвторам = ПользователиПоАвторам(АдресаАвторов);
	
	Для Каждого Строка Из Комментарии Цикл
		Если Строка.ПринадлежитАвторизованномуПользователю = Истина Тогда
			Строка.Пользователь = Пользователи.АвторизованныйПользователь();
			Продолжить;
		КонецЕсли;
		Пользователь = ПользователиПоАвторам[Строка.Автор];
		Если Пользователь <> Неопределено Тогда
			Строка.Пользователь = Пользователь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПользователиПоАвторам(АдресаАвторов)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("АдресаАвторов", АдресаАвторов);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АдресаАвторов.ИмяАвтора,
		|	АдресаАвторов.АдресПочты
		|ПОМЕСТИТЬ ВТАдресаАвторов
		|ИЗ
		|	&АдресаАвторов КАК АдресаАвторов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АдресаАвторов.ИмяАвтора,
		|	АдресаАвторов.АдресПочты,
		|	Пользователи.Ссылка КАК Пользователь
		|ПОМЕСТИТЬ ВТПользователиПоАвторам
		|ИЗ
		|	ВТАдресаАвторов КАК АдресаАвторов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица.КонтактнаяИнформация КАК КонтактыФизическихЛиц
		|		ПО (КонтактыФизическихЛиц.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты))
		|			И АдресаАвторов.АдресПочты = КонтактыФизическихЛиц.Представление
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО (Пользователи.ФизическоеЛицо = КонтактыФизическихЛиц.Ссылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	АдресаАвторов.ИмяАвтора,
		|	АдресаАвторов.АдресПочты,
		|	КонтактыПользователей.Ссылка
		|ИЗ
		|	ВТАдресаАвторов КАК АдресаАвторов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи.КонтактнаяИнформация КАК КонтактыПользователей
		|		ПО (КонтактыПользователей.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты))
		|			И АдресаАвторов.АдресПочты = КонтактыПользователей.Представление
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПользователиПоАвторам.ИмяАвтора,
		|	ПользователиПоАвторам.АдресПочты,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПользователиПоАвторам.Пользователь) КАК Количество
		|ПОМЕСТИТЬ ВТКоличествоПользователей
		|ИЗ
		|	ВТПользователиПоАвторам КАК ПользователиПоАвторам
		|
		|СГРУППИРОВАТЬ ПО
		|	ПользователиПоАвторам.ИмяАвтора,
		|	ПользователиПоАвторам.АдресПочты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПользователиПоАвторам.ИмяАвтора,
		|	ПользователиПоАвторам.АдресПочты,
		|	ПользователиПоАвторам.Пользователь
		|ИЗ
		|	ВТПользователиПоАвторам КАК ПользователиПоАвторам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКоличествоПользователей КАК КоличествоПользователей
		|		ПО (КоличествоПользователей.ИмяАвтора = ПользователиПоАвторам.ИмяАвтора)
		|			И (КоличествоПользователей.АдресПочты = ПользователиПоАвторам.АдресПочты)
		|			И (КоличествоПользователей.Количество = 1)";
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ПользователиПоАвторам = Новый Соответствие;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ПользователиПоАвторам.Вставить(Выборка.ИмяАвтора, Выборка.Пользователь);
	КонецЦикла;
	
	Возврат ПользователиПоАвторам;
	
КонецФункции

Функция ТаблицаАдресаАвторов()
	
	АдресаАвторов = Новый ТаблицаЗначений;
	АдресаАвторов.Колонки.Добавить("ИмяАвтора", Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(500)));
	АдресаАвторов.Колонки.Добавить("АдресПочты", Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(500)));
	
	Возврат АдресаАвторов;
	
КонецФункции

Функция ТаблицаКомментарииКандидатов()
	
	Комментарии = Новый ТаблицаЗначений;
	Комментарии.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	Комментарии.Колонки.Добавить("Скрытый", Новый ОписаниеТипов("Булево"));
	Комментарии.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	Комментарии.Колонки.Добавить("Комментарий", Новый ОписаниеТипов("Строка"));
	Комментарии.Колонки.Добавить("Автор", Новый ОписаниеТипов("Строка"));
	Комментарии.Колонки.Добавить("ПринадлежитАвторизованномуПользователю", Новый ОписаниеТипов("Булево"));
	Комментарии.Колонки.Добавить("Пользователь", Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	Комментарии.Колонки.Добавить("МестоПубликации", Новый ОписаниеТипов("СправочникСсылка.ИсточникиИнформацииОКандидатах"));
	
	Возврат Комментарии;
	
КонецФункции

Функция СтрокаКомментарияИзНабораПоИдентификатору(НаборЗаписей, Идентификатор, Отбор = Неопределено)
	
	Для Каждого СтрокаНабора Из НаборЗаписей Цикл
		Если СтрокаНабора.Идентификатор = Идентификатор Тогда
			Если Отбор = Неопределено Тогда
				Возврат СтрокаНабора;
			КонецЕсли;
			Для Каждого КлючИЗначение Из Отбор Цикл
				Если СтрокаНабора[КлючИЗначение.Ключ] <> КлючИЗначение.Значение Тогда
					Возврат Неопределено;
				КонецЕсли;
			КонецЦикла;
			Возврат СтрокаНабора;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

Функция СправочникиWebAPI(ПсевдонимWebAPI)
	
	СправочникиWebAPI = Новый Соответствие;
	
	Если ЗначениеЗаполнено(ПсевдонимWebAPI) Тогда
		
		ТабличныйДокумент = Обработки.ПубликацияВакансийНаРекрутинговыхСайтах.ПолучитьМакет("Классификаторы");
		
		Чтение = Новый ЧтениеXML;
		Чтение.УстановитьСтроку(ТабличныйДокумент.ПолучитьТекст());
		
		Чтение.Прочитать();
		Псевдоним = "";
		
		Пока Псевдоним <> ПсевдонимWebAPI Цикл
			
			Если ПустаяСтрока(Псевдоним) Тогда
				Чтение.Прочитать();
			Иначе
				Чтение.Пропустить();
			КонецЕсли;
			Псевдоним = Чтение.ПолучитьАтрибут("Alias");
			
		КонецЦикла;
		
		Пока Чтение.Прочитать() Цикл
			
			Если Чтение.ТипУзла = ТипУзлаXML.КонецЭлемента 
				И Чтение.Имя = "Dictionaries" Тогда
				Прервать;
			КонецЕсли;
			
			Если Чтение.Имя = "Dictionary" Тогда
				
				Ключ = Чтение.ПолучитьАтрибут("ID");
				Представление = Чтение.ПолучитьАтрибут("Name");
				
				СправочникиWebAPI.Вставить(Ключ, Представление);
				
			КонецЕсли;
			
		КонецЦикла;
		
		Чтение.Закрыть();
		
	КонецЕсли;
	
	Возврат СправочникиWebAPI;
	
КонецФункции

Функция УдалитьУточнениеВСкобках(Знач ОбрабатываемаяСтрока)
	
	НомерСимвола = СтрНайти(ОбрабатываемаяСтрока, "(");
	Если НомерСимвола <> 0 Тогда
		ОбрабатываемаяСтрока = Сред(ОбрабатываемаяСтрока, 0, НомерСимвола - 2);
	КонецЕсли;
	
	Возврат ОбрабатываемаяСтрока;
	
КонецФункции

Функция ПредставлениеДанныхПоИдентификатору(МассивДанных, Идентификатор, ПолеИдентификатор = "id", ПолеПредставление = "name")
	
	Представление = Неопределено;
	
	Для Каждого СтруктураДанных Из МассивДанных Цикл
		Если СтруктураДанных[ПолеИдентификатор] = Идентификатор Тогда
			Представление = СтруктураДанных[ПолеПредставление];
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Представление;
	
КонецФункции

Функция ИдентификаторыЗначенияКлассификаторов(КлассификаторыСайтов, СтруктураПутей, Значение)
	
	Идентификаторы = Новый Структура;
	
	Для Каждого КлючИЗначение Из КлассификаторыСайтов Цикл
		
		Если КлючИЗначение.Ключ = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
			Элемент = ЭлементКлассификатораHeadHunter(КлючИЗначение.Значение[СтруктураПутей.ПутьHeadHunter], Значение);
		ИначеЕсли КлючИЗначение.Ключ = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
			Элемент = ЭлементКлассификатораRabota(КлючИЗначение.Значение[СтруктураПутей.ПутьRabota], Значение);
		ИначеЕсли КлючИЗначение.Ключ = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
			Элемент = ЭлементКлассификатораSuperJob(КлючИЗначение.Значение[СтруктураПутей.ПутьSuperJob], Значение);
		КонецЕсли;
		
		Если Элемент = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Если КлючИЗначение.Ключ = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
			Идентификаторы.Вставить("IDHeadHunter", Элемент.id);
		ИначеЕсли КлючИЗначение.Ключ = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
			Идентификаторы.Вставить("IDRabota", Элемент.id);
		ИначеЕсли КлючИЗначение.Ключ = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
			Идентификаторы.Вставить("IDSuperJob", Элемент.Ключ);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Идентификаторы;
	
КонецФункции

Функция ИдентификаторыЗначенияИзДеревьевПоКлассификаторам(ДеревьяПоКлассификаторам, Значение, ЗначениеРодителя)
	
	Идентификаторы = Новый Структура;
	
	Для Каждого КлючИЗначение Из ДеревьяПоКлассификаторам Цикл
		
		Дерево = ДеревьяПоКлассификаторам.Получить(КлючИЗначение.Ключ);
		
		Если КлючИЗначение.Ключ = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
			СтрокаДерева = СтрокаДереваHeadHunter(Дерево.Строки, Значение, ЗначениеРодителя);
			ИмяКлючаИдентификатора = "IDHeadHunter";
		ИначеЕсли КлючИЗначение.Ключ = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
			СтрокаДерева = СтрокаДереваRabota(Дерево.Строки, Значение, ЗначениеРодителя);
			ИмяКлючаИдентификатора = "IDRabota";
		ИначеЕсли КлючИЗначение.Ключ = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
			СтрокаДерева = СтрокаДереваSuperJob(Дерево.Строки, Значение, ЗначениеРодителя);
			ИмяКлючаИдентификатора = "IDSuperJob";
		КонецЕсли;
		
		Если СтрокаДерева = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Идентификаторы.Вставить(ИмяКлючаИдентификатора, СтрокаДерева.Идентификатор);
		
	КонецЦикла;
	
	Возврат Идентификаторы;
	
КонецФункции

Функция КлассификаторыСайтов(ИспользуемыеСайты)
	
	КлассификаторыСайтов = Новый Соответствие;
	
	Для Каждого Сайт Из ИспользуемыеСайты Цикл
		
		Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
			ИмяНабора = "КлассификаторыHeadHunter";
		ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
			ИмяНабора = "КлассификаторыRabota";
		ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
			ИмяНабора = "КлассификаторыSuperJob";
		КонецЕсли;
		
		НаборКлассификаторовСайта = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить(ИмяНабора);
		
		Если НаборКлассификаторовСайта = Неопределено Тогда
			Возврат Неопределено;
		Иначе
			КлассификаторыСайтов.Вставить(Сайт, НаборКлассификаторовСайта);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат КлассификаторыСайтов;
	
КонецФункции

Функция ДеревьяРегионовПоКлассификаторамСайтов(ИспользуемыеСайты, ШаблонСтруктурыДерева)
	
	Деревья = Новый Соответствие;
	
	Для Каждого Сайт Из ИспользуемыеСайты Цикл
		
		ДеревоПоКлассификатору = ШаблонСтруктурыДерева.Скопировать();
		
		Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
			ЗаполнитьДеревоРегионовHeadHunter(ДеревоПоКлассификатору);
		ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
			ЗаполнитьДеревоРегионовRabota(ДеревоПоКлассификатору);
		ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
			ЗаполнитьДеревоРегионовSuperJob(ДеревоПоКлассификатору);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДеревоПоКлассификатору) Тогда
			Деревья.Вставить(Сайт, ДеревоПоКлассификатору);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Деревья;
	
КонецФункции

Функция ПодставитьСпецСимволы(Знач Текст)
	
	Текст = СтрЗаменить(Текст, "&amp;", "&");
	Текст = СтрЗаменить(Текст, "&quot;", """");
	Текст = СтрЗаменить(Текст, "&#8470;", "№");
	Текст = СтрЗаменить(Текст, "&sbquo;", ",");
	Текст = СтрЗаменить(Текст, "&raquo;", "»");
	Текст = СтрЗаменить(Текст, "&laquo;", "«");
	Текст = СтрЗаменить(Текст, "&ndash;", Символ(45));
	Текст = СтрЗаменить(Текст, "&mdash;", Символ(150));
	Текст = СтрЗаменить(Текст, "&bull;", "•");
	Текст = СтрЗаменить(Текст, "&nbsp;", " ");
	
	Возврат Текст;
	
КонецФункции

Функция ЗагружаемыеРеквизитыВакансии(Сайт) Экспорт
	
	Возврат ИнтеграцияРекрутинговыхСайтовВнутренний.ЗагружаемыеРеквизитыВакансии(Сайт);
	
КонецФункции

Функция ВидыПолей(Сайт, ПервичнаяПубликация = Истина) Экспорт
	
	ВидыПолей = Новый Структура("СМножественнымВыборомИзСписка, СОдиночнымВыборомИзСписка, СоСпискомБезКлассификатора, СоСпециальнойФормойРедактированияСписка,
		|СМножественнымВыборомИзДерева, СОдиночнымВыборомИзДерева, НедоступныеДляРедактирования,СТекстомВHTML, СМногострочнымВводом");
	
	Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter()
		Или Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.RabotaMail() Тогда
		
		ВидыПолей.СМножественнымВыборомИзДерева = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ПоляВакансииСМножественнымВыборомИзДереваHeadHunter();
		ВидыПолей.СОдиночнымВыборомИзДерева = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ПоляВакансииСОдиночнымВыборомИзДереваHeadHunter();
		ВидыПолей.СОдиночнымВыборомИзСписка = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ПоляВакансииСОдиночнымВыборомИзСпискаHeadHunter();
		ВидыПолей.НедоступныеДляРедактирования = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ПоляВакансииНедоступныеДляРедактированияHeadHunter();
		ВидыПолей.СоСпискомБезКлассификатора = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ПоляВакансииСоСпискомБезКлассификатораHeadHunter();
		ВидыПолей.СоСпециальнойФормойРедактированияСписка = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ПоляВакансииСоСпециальнойФормойРедактированияСпискаHeadHunter();
		ВидыПолей.СТекстомВHTML = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ПоляВакансииСТекстомВHTMLHeadHunter();
		
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
		
		ВидыПолей.СОдиночнымВыборомИзСписка = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ПоляВакансииСОдиночнымВыборомИзСпискаRabota();
		ВидыПолей.СМножественнымВыборомИзСписка = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ПоляВакансииСМножественнымВыборомИзСпискаRabota();
		ВидыПолей.СМножественнымВыборомИзДерева = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ПоляВакансииСМножественнымВыборомИзДереваRabota();
		ВидыПолей.НедоступныеДляРедактирования = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ПоляВакансииНедоступныеДляРедактированияRabota(ПервичнаяПубликация);
		ВидыПолей.СоСпециальнойФормойРедактированияСписка = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ПоляВакансииСоСпециальнойФормойРедактированияСпискаRabota();
		ВидыПолей.СТекстомВHTML = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ПоляВакансииСТекстомВHTMLRabota();
		
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
		
		ВидыПолей.СОдиночнымВыборомИзСписка = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ПоляВакансииСОдиночнымВыборомИзСпискаSuperJob();
		ВидыПолей.СМножественнымВыборомИзСписка = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ПоляВакансииСМножественнымВыборомИзСпискаSuperJob();
		ВидыПолей.СМножественнымВыборомИзДерева = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ПоляВакансииСМножественнымВыборомИзДереваSuperJob();
		ВидыПолей.СОдиночнымВыборомИзДерева = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ПоляВакансииСОдиночнымВыборомИзДереваSuperJob();
		ВидыПолей.СоСпециальнойФормойРедактированияСписка = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ПоляВакансииСоСпециальнойФормойРедактированияСпискаSuperJob();
		ВидыПолей.СМногострочнымВводом = ИнтеграцияРекрутинговыхСайтовКлиентСервер.ПоляВакансииСМногострочнымВводомSuperJob();
		
	КонецЕсли;
	
	Возврат ВидыПолей;
	
КонецФункции

Функция ПоляСТекстом(Сайт)
	
	ПоляСТекстом = Новый Массив;
	ВидыПолей = ВидыПолей(Сайт);
	
	ПоляСМногострочнымВводом = ВидыПолей.СМногострочнымВводом;
	ПоляСТекстомВHTML = ВидыПолей.СТекстомВHTML;
	
	Если ЗначениеЗаполнено(ПоляСМногострочнымВводом) Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПоляСТекстом, ПоляСМногострочнымВводом);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПоляСТекстомВHTML) Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПоляСТекстом, ПоляСТекстомВHTML);
	КонецЕсли;
	
	Возврат ПоляСТекстом;
	
КонецФункции

Функция УдалитьЛишнееИзСтроки(Знач ИсходнаяСтрока)
	
	СтрокаРезультата = СтрЗаменить(ИсходнаяСтрока, "\r", "");
	СтрокаРезультата = СтрЗаменить(СтрокаРезультата, "\t", Символы.Таб);
	СтрокаРезультата = СтрЗаменить(СтрокаРезультата, "\v", "");
	
	Возврат СтрокаРезультата;
	
КонецФункции

Функция ОткрытоеРезюмеSuperJob(ИдентификаторРезюме, Соединение, МаркерДоступа)
	
	Резюме = Неопределено;
	
	Запрос = HTTPЗапросSuperJob(СтрШаблон("/2.0/hr/resumes/%1/open/", ИдентификаторРезюме), МаркерДоступа);
	Ответ = Соединение.Получить(Запрос);
	ТекущаяСтрока = Ответ.ПолучитьТелоКакСтроку("UTF-8");
	
	Если Ответ.КодСостояния = 200 Тогда
		
		РезультатЗапроса = ПарсерJSON(ТекущаяСтрока);
		Если РезультатЗапроса.status Тогда
			Резюме = РезультатЗапроса.resume;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Резюме;
	
КонецФункции

Функция ЕстьДоступКРекрутинговымСайтам()
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюРекрутинговыхСайтов") 
		И ПравоДоступа("Просмотр", Метаданные.ОбщиеФормы.ПараметрыДоступаКРекрутинговымСайтам);
	
КонецФункции

Функция ПарсерJSON(ТекущаяСтрока, ПрочитатьВСоответствие = Ложь) Экспорт
	
	Возврат ИнтеграцияРекрутинговыхСайтовВнутренний.ПарсерJSON(ТекущаяСтрока, ПрочитатьВСоответствие);
	
КонецФункции

Функция ЗаписьJSON(Данные) Экспорт
	
	Возврат ИнтеграцияРекрутинговыхСайтовВнутренний.ЗаписьJSON(Данные);
	
КонецФункции

Функция ТипЗначенияJSONСтрока()
	
	Возврат ИнтеграцияРекрутинговыхСайтовВнутренний.ТипЗначенияJSONСтрока();
	
КонецФункции

Функция ТипЗначенияJSONЧисло()
	
	Возврат ИнтеграцияРекрутинговыхСайтовВнутренний.ТипЗначенияJSONЧисло();
	
КонецФункции

Функция ТипЗначенияJSONНачалоМассива() Экспорт
	
	Возврат ИнтеграцияРекрутинговыхСайтовВнутренний.ТипЗначенияJSONНачалоМассива();
	
КонецФункции

Функция ТипЗначенияJSONНачалоОбъекта() Экспорт
	
	Возврат ИнтеграцияРекрутинговыхСайтовВнутренний.ТипЗначенияJSONНачалоОбъекта();
	
КонецФункции

Функция ТипЗначенияJSONБулево()
	
	Возврат ИнтеграцияРекрутинговыхСайтовВнутренний.ТипЗначенияJSONБулево();
	
КонецФункции

Процедура ЗаполнитьМассивКонтактнойИнформацииHeadHunter(СтруктураРезюме, МассивКонтактнаяИнформация)
	
	ИнтеграцияРекрутинговыхСайтовВнутренний.ЗаполнитьМассивКонтактнойИнформацииHeadHunter(СтруктураРезюме, МассивКонтактнаяИнформация);
	
КонецПроцедуры

Процедура ЗаполнитьМассивКонтактнойИнформацииRabota(СтруктураРезюме, МассивКонтактнаяИнформация)
	
	ИнтеграцияРекрутинговыхСайтовВнутренний.ЗаполнитьМассивКонтактнойИнформацииRabota(СтруктураРезюме, МассивКонтактнаяИнформация);
	
КонецПроцедуры

Процедура ЗаполнитьМассивКонтактнойИнформацииSuperJob(СтруктураРезюме, МассивКонтактнаяИнформация)
	
	ИнтеграцияРекрутинговыхСайтовВнутренний.ЗаполнитьМассивКонтактнойИнформацииSuperJob(СтруктураРезюме, МассивКонтактнаяИнформация);
	
КонецПроцедуры

Процедура ЗаполнитьМассивОбразованиеHeadHunter(СтруктураРезюме, МассивОбразование)
	
	ИнтеграцияРекрутинговыхСайтовВнутренний.ЗаполнитьМассивОбразованиеHeadHunter(СтруктураРезюме, МассивОбразование);
	
КонецПроцедуры

Процедура ЗаполнитьМассивОбразованиеRabota(СтруктураРезюме, МассивОбразование)
	
	ИнтеграцияРекрутинговыхСайтовВнутренний.ЗаполнитьМассивОбразованиеRabota(СтруктураРезюме, МассивОбразование)
	
КонецПроцедуры

Процедура ЗаполнитьМассивОбразованиеSuperJob(СтруктураРезюме, МассивОбразование)
	
	ИнтеграцияРекрутинговыхСайтовВнутренний.ЗаполнитьМассивОбразованиеSuperJob(СтруктураРезюме, МассивОбразование);
	
КонецПроцедуры

Функция НайтиКандидатов(ПараметрыПроцедуры, УникальныйИдентификатор, АдресХранилища) Экспорт
	
	Возврат ИнтеграцияРекрутинговыхСайтовВнутренний.НайтиКандидатов(ПараметрыПроцедуры, УникальныйИдентификатор, АдресХранилища);
	
КонецФункции

Функция РеквизитВакансииСоответствующийПолюПубликации(Путь, Вакансия) Экспорт
	
	Возврат ИнтеграцияРекрутинговыхСайтовВнутренний.РеквизитВакансииСоответствующийПолюПубликации(Путь, Вакансия);
	
КонецФункции

Функция ОпубликованныеВакансииСИдентификаторами(Сайт) Экспорт
	
	Возврат ИнтеграцияРекрутинговыхСайтовВнутренний.ОпубликованныеВакансииСИдентификаторами(Сайт);
	
КонецФункции

Функция РеквизитПубликацииСоответствующийРеквизитуВакансии(Сайт, Вакансия, Реквизит) Экспорт
	
	Возврат ИнтеграцияРекрутинговыхСайтовВнутренний.РеквизитПубликацииСоответствующийРеквизитуВакансии(Сайт, Вакансия, Реквизит);
	
КонецФункции

Функция НоваяСтепеньЗнанияЯзыка(Код)
	
	Возврат ИнтеграцияРекрутинговыхСайтовВнутренний.НоваяСтепеньЗнанияЯзыка(Код);
	
КонецФункции

Функция КонстантаИспользоватьИнтеграциюРекрутинговыхСайтов()
	
	Возврат ИнтеграцияРекрутинговыхСайтовВнутренний.КонстантаИспользоватьИнтеграциюРекрутинговыхСайтов();
	
КонецФункции

Функция МесяцСтрокойВДату(Знач Значение)
	
	Результат = СтрЗаменить(Значение, "-", "");
	Результат = СтрЗаменить(Результат, " ", "");
	Результат = СтрЗаменить(Результат, Символы.НПП, "");
	
	Возврат ?(ЗначениеЗаполнено(Результат), Дата(Результат), Дата(1, 1, 1));
	
КонецФункции

Функция СообщитьОбОшибкеПолученияВакансии(КодСостояния, ОписаниеОшибки)
	
	Возврат СообщитьПользователю(СтрШаблон(НСтр("ru = 'Ошибка получения вакансии %1: %2'"), КодСостояния, ОписаниеОшибки));
	
КонецФункции

Функция СообщитьОбОшибкеПолученияВакансий(КодСостояния, ОписаниеОшибки)
	
	Возврат СообщитьПользователю(СтрШаблон(НСтр("ru = 'Ошибка получения вакансий %1: %2'"), КодСостояния, ОписаниеОшибки));
	
КонецФункции

Функция СообщитьОбОшибкеПолученияДанныхПользователя(КодСостояния, ОписаниеОшибки)
	
	Возврат СообщитьПользователю(СтрШаблон(НСтр("ru = 'Ошибка подтверждения пользователя %1: %2"), КодСостояния, ОписаниеОшибки));
	
КонецФункции

Функция СообщитьПользователю(ТекстСообщения)
	
	Возврат ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	
КонецФункции

#Область Rabota

Функция IdТипаРегионаКрай()
	Возврат 3;
КонецФункции

Функция IdТипаРегионаОбласть()
	Возврат 4;
КонецФункции

Функция IdТипаРегионаРеспублика()
	Возврат 5;
КонецФункции

Функция IdТипаРегионаАвтономныйОкруг()
	Возврат 6;
КонецФункции

Функция IdТипаРегионаРеспубликаОсобая()
	Возврат 8;
КонецФункции

Функция IdТипаРегионаАвтономнаяОбласть()
	Возврат 9;
КонецФункции

Функция ПолноеНазваниеРегиона(Знач НазваниеРегиона, IdТипаРегиона)
	
	ПолноеНазвание = УдалитьУточнениеВСкобках(НазваниеРегиона);
	
	Если IdТипаРегиона = IdТипаРегионаКрай() Тогда
		Возврат СтрШаблон(НСтр("ru = '%1 край'"), ПолноеНазвание);
	ИначеЕсли IdТипаРегиона = IdТипаРегионаОбласть() Тогда
		Возврат СтрШаблон(НСтр("ru = '%1 область'"), ПолноеНазвание);
	ИначеЕсли IdТипаРегиона = IdТипаРегионаРеспублика() Тогда
		Возврат СтрШаблон(НСтр("ru = 'Республика %1'"), ПолноеНазвание);
	ИначеЕсли IdТипаРегиона = IdТипаРегионаАвтономныйОкруг() Тогда
		Возврат СтрШаблон(НСтр("ru = '%1 АО'"), ПолноеНазвание);
	ИначеЕсли IdТипаРегиона = IdТипаРегионаРеспубликаОсобая() Тогда
		Возврат СтрШаблон(НСтр("ru = '%1 республика'"), ПолноеНазвание);
	ИначеЕсли IdТипаРегиона = IdТипаРегионаАвтономнаяОбласть() Тогда
		Возврат СтрШаблон(НСтр("ru = '%1 автономная область'"), ПолноеНазвание);
	КонецЕсли;
	
	Возврат ПолноеНазвание;
	
КонецФункции

#КонецОбласти

#КонецОбласти
