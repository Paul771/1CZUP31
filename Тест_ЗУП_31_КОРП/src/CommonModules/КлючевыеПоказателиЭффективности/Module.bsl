
#Область ПрограммныйИнтерфейс

// Заполняет значение показателя расчета заработной платы "ПоказательЭффективностиСотрудника".
//
// Параметры:
// 	- МенеджерВременныхТаблиц - МенеджерВременныхТаблиц, с временными таблицами менеджера расчета
// 	- ДополнительныеПоказатели - структура, содержащая показатели, участвующие в расчете.
//
Процедура ЗаполнитьЗначенияПоказателейЭффективностиСотрудников(МенеджерВременныхТаблиц, ДополнительныеПоказатели) Экспорт 
	
	ПоказательЭффективностиСотрудника = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.ПоказательЭффективностиСотрудника");
	Если ПоказательЭффективностиСотрудника = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	УдалитьВТ = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДополнительныеПоказатели.Сотрудник,
		|	НАЧАЛОПЕРИОДА(ДополнительныеПоказатели.ДатаНачала, МЕСЯЦ) КАК Месяц,
		|	НАЧАЛОПЕРИОДА(ДополнительныеПоказатели.ДатаНачала, ДЕНЬ) КАК Период
		|ПОМЕСТИТЬ ВТСотрудникиДаты
		|ИЗ
		|	ВТДополнительныеПоказатели КАК ДополнительныеПоказатели
		|ГДЕ
		|	ДополнительныеПоказатели.Показатель = &ПоказательЭффективностиСотрудника";
	Запрос.УстановитьПараметр("ПоказательЭффективностиСотрудника", ПоказательЭффективностиСотрудника);
	
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТСотрудникиДаты");
		
	Если Не ЗарплатаКадры.ВТСодержитСтроки(МенеджерВременныхТаблиц, "ВТСотрудникиДаты") Тогда
		ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
		Возврат;
	КонецЕсли;
	
	СоздатьВТОценкиПоказателейСотрудников(Запрос.МенеджерВременныхТаблиц, "ВТСотрудникиДаты");
	УдалитьВТ.Добавить("ВТОценкиПоказателейСотрудников");
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВТОценкиПоказателейСотрудников.Период КАК Период,
		|	ВТОценкиПоказателейСотрудников.Сотрудник КАК Сотрудник,
		|	СУММА(ВТОценкиПоказателейСотрудников.УдельнаяОценкаПоказателя) КАК ОценкаПоказателя
		|ПОМЕСТИТЬ ВТЗначенияПоказателейЭффективностиСотрудников
		|ИЗ
		|	ВТОценкиПоказателейСотрудников КАК ВТОценкиПоказателейСотрудников
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТОценкиПоказателейСотрудников.Период,
		|	ВТОценкиПоказателейСотрудников.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДополнительныеПоказатели.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ДополнительныеПоказатели.Показатель КАК Показатель,
		|	ВЫРАЗИТЬ(ЗначенияПоказателейЭффективностиСотрудников.ОценкаПоказателя КАК ЧИСЛО(15, 2)) КАК Значение
		|ИЗ
		|	ВТДополнительныеПоказатели КАК ДополнительныеПоказатели
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЗначенияПоказателейЭффективностиСотрудников КАК ЗначенияПоказателейЭффективностиСотрудников
		|		ПО ДополнительныеПоказатели.Сотрудник = ЗначенияПоказателейЭффективностиСотрудников.Сотрудник
		|			И ДополнительныеПоказатели.ДатаНачала = ЗначенияПоказателейЭффективностиСотрудников.Период
		|			И (ДополнительныеПоказатели.Показатель = &ПоказательЭффективностиСотрудника)";
	
	УдалитьВТ.Добавить("ВТЗначенияПоказателейЭффективностиСотрудников");
	Запрос.УстановитьПараметр("ПоказательЭффективностиСотрудника", ПоказательЭффективностиСотрудника);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ДополнительныеПоказатели.Добавить(), Выборка);
	КонецЦикла;
		
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура СоздатьВТОценкиПоказателейСотрудников(МенеджерВременныхТаблиц, ИмяФильтра) Экспорт

	УдалитьВТ = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	// Получаем кадровые данные.
	ОписательВТ = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц, ИмяФильтра, "Сотрудник,Период");
		
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВТ, Ложь, "ДолжностьПоШтатномуРасписанию, МестоВСтруктуреПредприятия");
	УдалитьВТ.Добавить("ВТКадровыеДанныеСотрудников");
	
	// Получаем действующие на сотрудников показатели.
	СоздатьВТПоказателиСотрудников(Запрос.МенеджерВременныхТаблиц, ИмяФильтра, Ложь);
	УдалитьВТ.Добавить("ВТПоказателиСотрудников");
	
	// Получаем плановые значения показателей.
	СоздатьВТПлановыеЗначенияПоказателейСотрудников(Запрос.МенеджерВременныхТаблиц, "ВТПоказателиСотрудников");
	УдалитьВТ.Добавить("ВТПлановыеЗначенияПоказателейСотрудников");
	
	// Получаем фактические значения показателей.
	СоздатьВТФактическиеЗначенияПоказателейСотрудников(Запрос.МенеджерВременныхТаблиц, "ВТПоказателиСотрудников");
	УдалитьВТ.Добавить("ВТФактическиеЗначенияПоказателейСотрудников");
	
	// Получаем сводим все воедино, получаем шкалы, рассчитываем оценки.
	//
	// Высчитываем удельные веса, получаем набор показателей, нуждающиеся в расчете по шкале.
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПоказателиСотрудников.Период КАК Период,
		|	ПоказателиСотрудников.Сотрудник КАК Сотрудник,
		|	СУММА(ПоказателиСотрудников.Вес) КАК СуммарныйВес
		|ПОМЕСТИТЬ ВТСуммыВесовПоказателейСотрудников
		|ИЗ
		|	ВТПоказателиСотрудников КАК ПоказателиСотрудников
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоказателиСотрудников.Период,
		|	ПоказателиСотрудников.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоказателиСотрудников.Период КАК Период,
		|	ПоказателиСотрудников.Сотрудник КАК Сотрудник,
		|	ПоказателиСотрудников.Позиция КАК Позиция,
		|	ПоказателиСотрудников.Регистратор КАК РегистраторНазначенияПоказателей,
		|	ПоказателиСотрудников.Подразделение КАК Подразделение,
		|	ПоказателиСотрудников.Показатель КАК Показатель,
		|	ПоказателиСотрудников.ТребуетсяВводПлана КАК ТребуетсяВводПлана,
		|	ПоказателиСотрудников.ВладелецПоказателя КАК ВладелецПоказателя,
		|	ПоказателиСотрудников.Вес КАК Вес,
		|	ПоказателиСотрудников.Вес / ВТСуммыВесовПоказателейСотрудников.СуммарныйВес КАК УдельныйВес
		|ПОМЕСТИТЬ ВТПоказателиСотрудниковСУдельнымиВесами
		|ИЗ
		|	ВТПоказателиСотрудников КАК ПоказателиСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСуммыВесовПоказателейСотрудников КАК ВТСуммыВесовПоказателейСотрудников
		|		ПО ПоказателиСотрудников.Период = ВТСуммыВесовПоказателейСотрудников.Период
		|			И ПоказателиСотрудников.Сотрудник = ВТСуммыВесовПоказателейСотрудников.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоказателиСотрудников.Период КАК Период,
		|	ПоказателиСотрудников.Сотрудник КАК Сотрудник,
		|	ПоказателиСотрудников.Позиция КАК Позиция,
		|	ПоказателиСотрудников.Подразделение КАК Подразделение,
		|	ПоказателиСотрудников.Показатель КАК Показатель,
		|	ПоказателиСотрудников.РегистраторНазначенияПоказателей КАК РегистраторНазначенияПоказателей,
		|	ПоказателиСотрудников.ВладелецПоказателя КАК ВладелецПоказателя,
		|	ПоказателиСотрудников.Вес КАК Вес,
		|	ПоказателиСотрудников.УдельныйВес КАК УдельныйВес,
		|	ПоказателиСотрудников.ТребуетсяВводПлана КАК ТребуетсяВводПлана,
		|	ПлановыеЗначенияПоказателейСотрудников.ПлановоеЗначение КАК ПлановоеЗначение,
		|	ПлановыеЗначенияПоказателейСотрудников.РегистраторПлановогоЗначения КАК РегистраторПлановогоЗначения
		|ПОМЕСТИТЬ ВТПоказателиСотрудниковСУдельнымиВесамиИПлановымиЗначениями
		|ИЗ
		|	ВТПоказателиСотрудниковСУдельнымиВесами КАК ПоказателиСотрудников
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПлановыеЗначенияПоказателейСотрудников КАК ПлановыеЗначенияПоказателейСотрудников
		|		ПО ПоказателиСотрудников.Сотрудник = ПлановыеЗначенияПоказателейСотрудников.Сотрудник
		|			И ПоказателиСотрудников.Показатель = ПлановыеЗначенияПоказателейСотрудников.Показатель
		|			И ПоказателиСотрудников.Период = ПлановыеЗначенияПоказателейСотрудников.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоказателиСотрудников.Период КАК Период,
		|	ПоказателиСотрудников.Сотрудник КАК Сотрудник,
		|	ПоказателиСотрудников.Показатель КАК Показатель,
		|	ПоказателиСотрудников.РегистраторНазначенияПоказателей КАК РегистраторНазначенияПоказателей,
		|	ПоказателиСотрудников.ВладелецПоказателя КАК ВладелецПоказателя,
		|	ПоказателиСотрудников.ПлановоеЗначение КАК ПлановоеЗначение,
		|	ПоказателиСотрудников.РегистраторПлановогоЗначения КАК РегистраторПлановогоЗначения,
		|	ФактическиеЗначенияПоказателейСотрудников.ФактическоеЗначение КАК ФактическоеЗначение,
		|	ФактическиеЗначенияПоказателейСотрудников.РегистраторФактическогоЗначения КАК РегистраторФактическогоЗначения,
		|	ВЫБОР
		|		КОГДА ПоказателиСотрудников.ТребуетсяВводПлана
		|			ТОГДА ВЫБОР
		|					КОГДА ПоказателиСотрудников.ПлановоеЗначение ЕСТЬ NULL
		|							ИЛИ ФактическиеЗначенияПоказателейСотрудников.ФактическоеЗначение ЕСТЬ NULL
		|						ТОГДА NULL
		|					ИНАЧЕ ВЫБОР
		|							КОГДА ПоказателиСотрудников.ПлановоеЗначение = 0
		|								ТОГДА 0
		|							ИНАЧЕ ФактическиеЗначенияПоказателейСотрудников.ФактическоеЗначение / ПоказателиСотрудников.ПлановоеЗначение * 100
		|						КОНЕЦ
		|				КОНЕЦ
		|		ИНАЧЕ ФактическиеЗначенияПоказателейСотрудников.ФактическоеЗначение
		|	КОНЕЦ КАК ЗначениеПоказателя,
		|	ПоказателиСотрудников.Вес КАК Вес
		|ПОМЕСТИТЬ ВТРассчитанныеПоказателиСотрудников
		|ИЗ
		|	ВТПоказателиСотрудниковСУдельнымиВесамиИПлановымиЗначениями КАК ПоказателиСотрудников
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФактическиеЗначенияПоказателейСотрудников КАК ФактическиеЗначенияПоказателейСотрудников
		|		ПО ПоказателиСотрудников.Сотрудник = ФактическиеЗначенияПоказателейСотрудников.Сотрудник
		|			И ПоказателиСотрудников.Показатель = ФактическиеЗначенияПоказателейСотрудников.Показатель
		|			И ПоказателиСотрудников.Период = ФактическиеЗначенияПоказателейСотрудников.Период
		|ГДЕ
		|	НЕ ВЫБОР
		|				КОГДА ПоказателиСотрудников.ТребуетсяВводПлана
		|					ТОГДА ВЫБОР
		|							КОГДА ПоказателиСотрудников.ПлановоеЗначение ЕСТЬ NULL
		|									ИЛИ ФактическиеЗначенияПоказателейСотрудников.ФактическоеЗначение ЕСТЬ NULL
		|								ТОГДА NULL
		|							ИНАЧЕ ВЫБОР
		|									КОГДА ПоказателиСотрудников.ПлановоеЗначение = 0
		|										ТОГДА 0
		|									ИНАЧЕ ФактическиеЗначенияПоказателейСотрудников.ФактическоеЗначение / ПоказателиСотрудников.ПлановоеЗначение * 100
		|								КОНЕЦ
		|						КОНЕЦ
		|				ИНАЧЕ ФактическиеЗначенияПоказателейСотрудников.ФактическоеЗначение
		|			КОНЕЦ ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТСуммыВесовПоказателейСотрудников
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПоказателиСотрудниковСУдельнымиВесами";
	
	УдалитьВТ.Добавить("ВТПоказателиСотрудниковСУдельнымиВесамиИПлановымиЗначениями");
	УдалитьВТ.Добавить("ВТРассчитанныеПоказателиСотрудников");
		
	// Расчет показателей по шкалам.
	ЗапросРасчета = ЗапросВТРассчитанныеОценкиПоказателейСотрудников("ВТРассчитанныеПоказателиСотрудников");
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьЗапросы(Запрос, ЗапросРасчета);
	УдалитьВТ.Добавить("ВТРассчитанныеОценкиПоказателейСотрудников");
	
	// Собираем итоговый результат.
	ФинальныйТекст =
		"ВЫБРАТЬ
		|	ПоказателиСотрудников.Период КАК Период,
		|	ПоказателиСотрудников.Сотрудник КАК Сотрудник,
		|	ПоказателиСотрудников.Позиция КАК Позиция,
		|	ПоказателиСотрудников.Подразделение КАК Подразделение,
		|	ПоказателиСотрудников.Показатель КАК Показатель,
		|	ПоказателиСотрудников.РегистраторНазначенияПоказателей КАК РегистраторНазначенияПоказателей,
		|	ПоказателиСотрудников.ТребуетсяВводПлана КАК ТребуетсяВводПлана,
		|	ПоказателиСотрудников.ВладелецПоказателя КАК ВладелецПоказателя,
		|	ПоказателиСотрудников.Вес КАК Вес,
		|	ПоказателиСотрудников.УдельныйВес КАК УдельныйВес,
		|	ПоказателиСотрудников.ПлановоеЗначение КАК ПлановоеЗначение,
		|	ПоказателиСотрудников.РегистраторПлановогоЗначения КАК РегистраторПлановогоЗначения,
		|	ВТРассчитанныеОценкиПоказателейСотрудников.ФактическоеЗначение КАК ФактическоеЗначение,
		|	ВТРассчитанныеОценкиПоказателейСотрудников.РегистраторФактическогоЗначения КАК РегистраторФактическогоЗначения,
		|	ВТРассчитанныеОценкиПоказателейСотрудников.ЗначениеПоказателя КАК ЗначениеПоказателя,
		|	ВТРассчитанныеОценкиПоказателейСотрудников.ОценкаПоказателя КАК ОценкаПоказателя,
		|	ВТРассчитанныеОценкиПоказателейСотрудников.ОценкаПоказателя * ПоказателиСотрудников.УдельныйВес КАК УдельнаяОценкаПоказателя,
		|	ВТРассчитанныеОценкиПоказателейСотрудников.ЗначениеОт КАК ЗначениеОт,
		|	ВТРассчитанныеОценкиПоказателейСотрудников.ЗначениеДо КАК ЗначениеДо
		|ПОМЕСТИТЬ ВТОценкиПоказателейСотрудников
		|ИЗ
		|	ВТПоказателиСотрудниковСУдельнымиВесамиИПлановымиЗначениями КАК ПоказателиСотрудников
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРассчитанныеОценкиПоказателейСотрудников КАК ВТРассчитанныеОценкиПоказателейСотрудников
		|		ПО ПоказателиСотрудников.Период = ВТРассчитанныеОценкиПоказателейСотрудников.Период
		|			И ПоказателиСотрудников.Сотрудник = ВТРассчитанныеОценкиПоказателейСотрудников.Сотрудник
		|			И ПоказателиСотрудников.Показатель = ВТРассчитанныеОценкиПоказателейСотрудников.Показатель";
	
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьТекстыЗапросов(Запрос.Текст, ФинальныйТекст);
	
	УстановитьПривилегированныйРежим(Истина);	
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);	
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);

КонецПроцедуры

#Область ОбработчикиОбновления

// Добавляет в список Обработчики процедуры-обработчики обновления,
// необходимые данной подсистеме.
//
// Параметры:
//   Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//                                   общего модуля ОбновлениеИнформационнойБазы.
// 
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.19.44";
	Обработчик.Процедура = "КлючевыеПоказателиЭффективности.СоздатьПоказательИНачислениеДоплатаПоРезультатамОценкиЭффективности";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.4.20";
	Обработчик.Процедура = "КлючевыеПоказателиЭффективности.УстановитьПараметрыНабораСвойствСправочников";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("a5dc6ea5-d514-4758-9eb9-01e2e7a932c0");
	Обработчик.Комментарий = НСтр("ru = 'Установка параметров для набора свойств справочников ключевых показателей эффективности. Дополнительные реквизиты временно недоступны.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.5.16";
	Обработчик.Процедура = "КлючевыеПоказателиЭффективности.УстановитьИспользоватьКлючевыеПоказателиЭффективности";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("e051c446-f853-455a-8a58-f08ded8ecd63");
	Обработчик.Комментарий = НСтр("ru = 'Установка константы ИспользоватьКлючевыеПоказателиЭффективности.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.5.16";
	Обработчик.Процедура = "КлючевыеПоказателиЭффективности.ПеренестиФактическиеЗначенияПоказателей";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("a4f6b294-8fa8-4fe6-aab4-0807f17c18b1");
	Обработчик.Комментарий = НСтр("ru = 'Установка константы ИспользоватьКлючевыеПоказателиЭффективности.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.5.19";
	Обработчик.Процедура = "Справочники.СтруктураЦелей.ЗаполнитьСпособОценки";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("e8790497-bfb5-4993-b43f-05a09733f88c");
	Обработчик.Комментарий = НСтр("ru = 'Установка константы ИспользоватьКлючевыеПоказателиЭффективности.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.5.42";
	Обработчик.Процедура = "КлючевыеПоказателиЭффективности.ЗаполнитьПорогиШкал";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("2d1543cd-5233-4925-a6cf-a2069704c4bb");
	Обработчик.Комментарий = НСтр("ru = 'Установка константы ИспользоватьКлючевыеПоказателиЭффективности.'");
	
КонецПроцедуры

#КонецОбласти

#Область ПоказателиОбъектов

Функция СписокПоказателейПодразделенийПоОтветственным(Период, КоллекцияОтборов = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	&Период КАК Период
		|ПОМЕСТИТЬ ВТФильтр";
	Запрос.УстановитьПараметр("Период", Период);
	
	ЗапросОтветственных = ЗапросВТПодразделенияВводаЗначенийПоказателей("ВТФильтр", "", КоллекцияОтборов);
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьЗапросы(Запрос, ЗапросОтветственных);
	
	ТекстИтоговогоЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТПодразделенияВводаЗначенийПоказателей.Показатель КАК Показатель
		|ИЗ
		|	ВТПодразделенияВводаЗначенийПоказателей КАК ВТПодразделенияВводаЗначенийПоказателей 
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТФильтр
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПодразделенияВводаЗначенийПоказателей";
	
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьТекстыЗапросов(Запрос.Текст, ТекстИтоговогоЗапроса);
	
	РезультатЗапроса = Запрос.Выполнить();	
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Показатель");
	
КонецФункции

Функция СписокПоказателейПозицийПоОтветственным(Период, КоллекцияОтборов = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	&Период КАК Период
		|ПОМЕСТИТЬ ВТФильтр";
	Запрос.УстановитьПараметр("Период", Период);
	
	ЗапросОтветственных = ЗапросВТПозицииВводаЗначенийПоказателей("ВТФильтр", "", КоллекцияОтборов);
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьЗапросы(Запрос, ЗапросОтветственных);
	
	ТекстИтоговогоЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТПозицииВводаЗначенийПоказателей.Показатель КАК Показатель
		|ИЗ
		|	ВТПозицииВводаЗначенийПоказателей КАК ВТПозицииВводаЗначенийПоказателей 
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТФильтр
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПозицииВводаЗначенийПоказателей";
	
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьТекстыЗапросов(Запрос.Текст, ТекстИтоговогоЗапроса);
	
	РезультатЗапроса = Запрос.Выполнить();	
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Показатель");
	
КонецФункции

#КонецОбласти

#Область ОбъектыПоПоказателю

Функция ТаблицаПодразделенийПоПоказателю(Период, Показатель, КоллекцияОтборов = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	&Период КАК Период, 
		|	&Показатель КАК Показатель
		|ПОМЕСТИТЬ ВТФильтр";
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Показатель", Показатель);
	
	ЗапросОтветственных = ЗапросВТПодразделенияВводаЗначенийПоказателей("ВТФильтр", "Показатель", КоллекцияОтборов);
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьЗапросы(Запрос, ЗапросОтветственных);
	
	ТекстИтоговогоЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТПодразделенияВводаЗначенийПоказателей.Подразделение КАК Подразделение
		|ИЗ
		|	ВТПодразделенияВводаЗначенийПоказателей КАК ВТПодразделенияВводаЗначенийПоказателей
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТФильтр
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПодразделенияВводаЗначенийПоказателей";
			
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьТекстыЗапросов(Запрос.Текст, ТекстИтоговогоЗапроса);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

Функция ТаблицаПозицийПоПоказателю(Период, Показатель, КоллекцияОтборов = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	&Период КАК Период, 
		|	&Показатель КАК Показатель
		|ПОМЕСТИТЬ ВТФильтр";
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Показатель", Показатель);
	
	ЗапросОтветственных = ЗапросВТПозицииВводаЗначенийПоказателей("ВТФильтр", "Показатель", КоллекцияОтборов);
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьЗапросы(Запрос, ЗапросОтветственных);
	
	ТекстИтоговогоЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТПозицииВводаЗначенийПоказателей.Позиция КАК Позиция
		|ИЗ
		|	ВТПозицииВводаЗначенийПоказателей КАК ВТПозицииВводаЗначенийПоказателей
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТФильтр
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПозицииВводаЗначенийПоказателей";
			
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьТекстыЗапросов(Запрос.Текст, ТекстИтоговогоЗапроса);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

#КонецОбласти

#Область ПоказателиОбъектов

//Подразделения

Функция ПоказателиПодразделений(Подразделения, Период, ПлановоеЗначение = Ложь, ФактическоеЗначение = Ложь) Экспорт

	Если Не ЗначениеЗаполнено(Подразделения) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(Подразделения) <> Тип("Массив") Тогда
		МассивПодразделений = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Подразделения);
	Иначе
		МассивПодразделений = Подразделения;
	КонецЕсли;
	
	// Получаем назначенные показатели.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтруктураПредприятия.Ссылка КАК Подразделение,
		|	&Период КАК Период
		|ПОМЕСТИТЬ ВТПодразделенияПериоды
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
		|ГДЕ
		|	СтруктураПредприятия.Ссылка В(&МассивПодразделений)";
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("МассивПодразделений", МассивПодразделений);
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	СоздатьВТПоказателиПодразделений(Запрос.МенеджерВременныхТаблиц, "ВТПодразделенияПериоды");
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоказателиПодразделений.Период КАК Период,
		|	ПоказателиПодразделений.Подразделение КАК Подразделение,
		|	ПоказателиПодразделений.Показатель КАК Показатель,
		|	ПоказателиПодразделений.ВладелецПоказателя КАК ВладелецПоказателя,
		|	ПоказателиПодразделений.Вес КАК Вес,
		|	ПоказателиПодразделений.ОтветственныйЗаВводПлана КАК ОтветственныйЗаВводПлана,
		|	ПоказателиПодразделений.ОтветственныйЗаВводФакта КАК ОтветственныйЗаВводФакта,
		|	ПоказателиПодразделений.МестоВводаЗначений КАК МестоВводаЗначений,
		|	ПоказателиПодразделений.ТребуетсяВводПлана КАК ТребуетсяВводПлана
		|ИЗ
		|	ВТПоказателиПодразделений КАК ПоказателиПодразделений";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);

	Возврат РезультатЗапроса.Выгрузить();

КонецФункции

Процедура СоздатьВТПоказателиПодразделений(МенеджерВременныхТаблиц, ИмяФильтра) Экспорт

	Запрос = ЗапросВТПоказателиПодразделений(ИмяФильтра);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Выполнить();

КонецПроцедуры

Функция ЗапросВТПоказателиПодразделений(ИмяФильтра, ИмяВыходнойВТ = "ВТПоказателиПодразделений") Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВТПодразделения.Период КАК Период,
		|	ВТПодразделения.Подразделение КАК ИсходноеПодразделение,
		|	ПодчиненностьСтруктурныхЕдиниц.ВышестоящаяСтруктурнаяЕдиница КАК Подразделение,
		|	ПодчиненностьСтруктурныхЕдиниц.Уровень КАК Уровень
		|ПОМЕСТИТЬ ВТПодразделениеИРодители
		|ИЗ
		|	РегистрСведений.ПодчиненностьСтруктурныхЕдиниц КАК ПодчиненностьСтруктурныхЕдиниц
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ИмяФильтра КАК ВТПодразделения
		|		ПО ПодчиненностьСтруктурныхЕдиниц.СтруктурнаяЕдиница = ВТПодразделения.Подразделение";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИмяФильтра", ИмяФильтра);
	
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра("ВТПодразделениеИРодители", "Подразделение");
	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	
	ЗапросПодразделения = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"ПоказателиЭффективностиПодразделений",
		Ложь,
		ОписаниеФильтра,
		ПараметрыПостроения,,
		"ВТПоказателиЭффективностиПодразделенийСрезПоследних");
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьЗапросы(Запрос, ЗапросПодразделения);
	
	ОсновнойТекстЗапроса =
		"ВЫБРАТЬ
		|	ВТПодразделениеИРодители.Период КАК Период,
		|	ВТПодразделениеИРодители.ИсходноеПодразделение КАК ИсходноеПодразделение,
		|	ВТПодразделениеИРодители.Подразделение КАК Подразделение,
		|	ВТПодразделениеИРодители.Уровень КАК Уровень,
		|	ПоказателиЭффективностиПодразделенийСрезПоследних.Показатель КАК Показатель,
		|	ПоказателиЭффективностиПодразделенийСрезПоследних.Регистратор КАК Регистратор,
		|	ПоказателиЭффективностиПодразделенийСрезПоследних.Вес КАК Вес,
		|	ПоказателиЭффективностиПодразделенийСрезПоследних.МестоВводаЗначений КАК МестоВводаЗначений
		|ПОМЕСТИТЬ ВТНазначенныеПоказателиПодразделенийИРодителей
		|ИЗ
		|	ВТПоказателиЭффективностиПодразделенийСрезПоследних КАК ПоказателиЭффективностиПодразделенийСрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПодразделениеИРодители КАК ВТПодразделениеИРодители
		|		ПО ПоказателиЭффективностиПодразделенийСрезПоследних.Подразделение = ВТПодразделениеИРодители.Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(ВТНазначенныеПоказателиПодразделенийИРодителей.Уровень) КАК Уровень,
		|	ВТНазначенныеПоказателиПодразделенийИРодителей.ИсходноеПодразделение КАК ИсходноеПодразделение
		|ПОМЕСТИТЬ ВТУказателиПодразделений
		|ИЗ
		|	ВТНазначенныеПоказателиПодразделенийИРодителей КАК ВТНазначенныеПоказателиПодразделенийИРодителей
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТНазначенныеПоказателиПодразделенийИРодителей.ИсходноеПодразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТНазначенныеПоказателиПодразделенийИРодителей.Период КАК Период,
		|	ВТНазначенныеПоказателиПодразделенийИРодителей.ИсходноеПодразделение КАК Подразделение,
		|	ВТНазначенныеПоказателиПодразделенийИРодителей.Подразделение КАК ВладелецПоказателя,
		|	ВТНазначенныеПоказателиПодразделенийИРодителей.Показатель КАК Показатель,
		|	ВТНазначенныеПоказателиПодразделенийИРодителей.Регистратор КАК Регистратор,
		|	ВТНазначенныеПоказателиПодразделенийИРодителей.Вес КАК Вес,
		|	ВТНазначенныеПоказателиПодразделенийИРодителей.МестоВводаЗначений КАК МестоВводаЗначений,
		|	ВЫРАЗИТЬ(ВТНазначенныеПоказателиПодразделенийИРодителей.Показатель КАК Справочник.СтруктураЦелей).ТребуетсяВводПлана КАК ТребуетсяВводПлана
		|ПОМЕСТИТЬ ВТПоказателиПодразделенийБезОтветственных
		|ИЗ
		|	ВТНазначенныеПоказателиПодразделенийИРодителей КАК ВТНазначенныеПоказателиПодразделенийИРодителей
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТУказателиПодразделений КАК ВТУказателиПодразделений
		|		ПО ВТНазначенныеПоказателиПодразделенийИРодителей.ИсходноеПодразделение = ВТУказателиПодразделений.ИсходноеПодразделение
		|			И ВТНазначенныеПоказателиПодразделенийИРодителей.Уровень = ВТУказателиПодразделений.Уровень
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПоказателиЭффективностиПодразделенийСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПодразделениеИРодители
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТНазначенныеПоказателиПодразделенийИРодителей
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТУказателиПодразделений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТПоказателиПодразделенийБезОтветственных.Период КАК Период,
		|	ВТПоказателиПодразделенийБезОтветственных.Показатель КАК Показатель,
		|	ВТПоказателиПодразделенийБезОтветственных.МестоВводаЗначений КАК Подразделение
		|ПОМЕСТИТЬ ВТФильтрДляОтветственных
		|ИЗ
		|	ВТПоказателиПодразделенийБезОтветственных КАК ВТПоказателиПодразделенийБезОтветственных";
	
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьТекстыЗапросов(Запрос.Текст, ОсновнойТекстЗапроса);
	
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра("ВТФильтрДляОтветственных", "Показатель,Подразделение");
	ЗапросОтветственных = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"ПодразделенияВводаЗначенийПоказателейЭффективности",
		Ложь,
		ОписаниеФильтра,
		ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез(),,
		"ВТОтветственныеЗаВводЗначений");
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьЗапросы(Запрос, ЗапросОтветственных);
	
	ФинальныйТекстЗапроса =
		"УНИЧТОЖИТЬ ВТФильтрДляОтветственных
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПоказателиПодразделенийБезОтветственных.Период КАК Период,
		|	ВТПоказателиПодразделенийБезОтветственных.Подразделение КАК Подразделение,
		|	ВТПоказателиПодразделенийБезОтветственных.ВладелецПоказателя КАК ВладелецПоказателя,
		|	ВТПоказателиПодразделенийБезОтветственных.Показатель КАК Показатель,
		|	ВТПоказателиПодразделенийБезОтветственных.Регистратор КАК Регистратор,
		|	ВТПоказателиПодразделенийБезОтветственных.Вес КАК Вес,
		|	ВТПоказателиПодразделенийБезОтветственных.МестоВводаЗначений КАК МестоВводаЗначений,
		|	ВТОтветственныеЗаВводЗначений.ОтветственныйЗаВводПлана КАК ОтветственныйЗаВводПлана,
		|	ВТОтветственныеЗаВводЗначений.ОтветственныйЗаВводФакта КАК ОтветственныйЗаВводФакта,
		|	ВТПоказателиПодразделенийБезОтветственных.ТребуетсяВводПлана КАК ТребуетсяВводПлана
		|ПОМЕСТИТЬ #ВТПоказателиПодразделений
		|ИЗ
		|	ВТПоказателиПодразделенийБезОтветственных КАК ВТПоказателиПодразделенийБезОтветственных
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОтветственныеЗаВводЗначений КАК ВТОтветственныеЗаВводЗначений
		|		ПО ВТПоказателиПодразделенийБезОтветственных.Период = ВТОтветственныеЗаВводЗначений.Период
		|			И ВТПоказателиПодразделенийБезОтветственных.Показатель = ВТОтветственныеЗаВводЗначений.Показатель
		|			И ВТПоказателиПодразделенийБезОтветственных.МестоВводаЗначений = ВТОтветственныеЗаВводЗначений.Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПоказателиПодразделенийБезОтветственных
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТОтветственныеЗаВводЗначений";
	
	ФинальныйТекстЗапроса = СтрЗаменить(ФинальныйТекстЗапроса, "#ВТПоказателиПодразделений", ИмяВыходнойВТ);
	
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьТекстыЗапросов(Запрос.Текст, ФинальныйТекстЗапроса);
	
	Возврат Запрос;
	
КонецФункции

//Позиции

Функция ПоказателиПозиции(Позиции, Период, ПлановоеЗначение = Ложь, ФактическоеЗначение = Ложь) Экспорт

	Если Не ЗначениеЗаполнено(Позиции) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(Позиции) <> Тип("Массив") Тогда
		МассивПозиций = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Позиции);
	Иначе
		МассивПозиций = Позиции;
	КонецЕсли;
	
	// Создание ВТНазначенныеПоказателиПозиций.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ШтатноеРасписание.Ссылка КАК Позиция,
		|	&Период КАК Период
		|ПОМЕСТИТЬ ВТПозицииПериоды
		|ИЗ
		|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
		|ГДЕ
		|	ШтатноеРасписание.Ссылка В(&МассивПозиций)";
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("МассивПозиций", МассивПозиций);
	
	Запрос.Выполнить();
	
	СоздатьВТПоказателиПозиций(Запрос.МенеджерВременныхТаблиц, "ВТПозицииПериоды");
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоказателиПозиций.Период КАК Период,
		|	ПоказателиПозиций.Позиция КАК Позиция,
		|	ПоказателиПозиций.Показатель КАК Показатель,
		|	ПоказателиПозиций.ВладелецПоказателя КАК ВладелецПоказателя,
		|	ПоказателиПозиций.Вес КАК Вес,
		|	ПоказателиПозиций.ОтветственныйЗаВводПлана КАК ОтветственныйЗаВводПлана,
		|	ПоказателиПозиций.ОтветственныйЗаВводФакта КАК ОтветственныйЗаВводФакта,
		|	ПоказателиПозиций.МестоВводаЗначений КАК МестоВводаЗначений,
		|	ПоказателиПозиций.ТребуетсяВводПлана КАК ТребуетсяВводПлана
		|ИЗ
		|	ВТПоказателиПозиций КАК ПоказателиПозиций";
	
	РезультатЗапроса = Запрос.Выполнить();

	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

Процедура СоздатьВТПоказателиПозиций(МенеджерВременныхТаблиц, ИмяФильтра, ИмяВыходнойВТ = "ВТПоказателиПозиций") Экспорт

	УдалитьВТ = Новый Массив;
	
	Запрос = СоздатьЗапросВТНазначенныеПоказателиПозиций(ИмяФильтра);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	УдалитьВТ.Добавить("ВТНазначенныеПоказателиПозиций");
	
	ТекстНенайденныхПодразделений =
		"ВЫБРАТЬ
		|	ВТПозиции.Позиция КАК Позиция,
		|	ВТПозиции.Период КАК Период
		|ПОМЕСТИТЬ ВТПозицииНаследующиеИзПодразделений
		|ИЗ
		|	#ВТПозиции КАК ВТПозиции
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНазначенныеПоказателиПозиций КАК НазначенныеПоказателиПозиций
		|		ПО ВТПозиции.Позиция = НазначенныеПоказателиПозиций.Позиция
		|			И ВТПозиции.Период = НазначенныеПоказателиПозиций.Период
		|ГДЕ
		|	НазначенныеПоказателиПозиций.Позиция ЕСТЬ NULL";
	ТекстНенайденныхПодразделений = СтрЗаменить(ТекстНенайденныхПодразделений, "#ВТПозиции", ИмяФильтра);
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьТекстыЗапросов(Запрос.Текст, ТекстНенайденныхПодразделений);
	
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТПозицииНаследующиеИзПодразделений");
	
	ОрганизационнаяСтруктура.СоздатьВТМестаПозицийВСтруктуреПредприятияПоВременнойТаблице(Запрос.МенеджерВременныхТаблиц, "ВТПозицииНаследующиеИзПодразделений");
	УдалитьВТ.Добавить("ВТМестаПозицийВСтруктуреПредприятия");
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТМестаПозицийВСтруктуреПредприятия.Подразделение КАК Подразделение,
		|	ВТПозицииНаследующиеИзПодразделений.Период КАК Период
		|ПОМЕСТИТЬ ВТПодразделения
		|ИЗ
		|	ВТПозицииНаследующиеИзПодразделений КАК ВТПозицииНаследующиеИзПодразделений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМестаПозицийВСтруктуреПредприятия КАК ВТМестаПозицийВСтруктуреПредприятия
		|		ПО ВТПозицииНаследующиеИзПодразделений.Позиция = ВТМестаПозицийВСтруктуреПредприятия.Позиция";
	
	ЗапросВТПоказателиПодразделений = ЗапросВТПоказателиПодразделений("ВТПодразделения", "ВТПоказателиПодразделенийВнутренний");
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьЗапросы(Запрос, ЗапросВТПоказателиПодразделений);
	
	ТекстПоказателейПодразделений =
		"ВЫБРАТЬ
		|	ВТПоказателиПодразделенийВнутренний.Период КАК Период,
		|	ВТПоказателиПодразделенийВнутренний.Показатель КАК Показатель,
		|	ВТПоказателиПодразделенийВнутренний.Регистратор КАК Регистратор,
		|	ВТМестаПозицийВСтруктуреПредприятия.Позиция КАК Позиция,
		|	ВТПоказателиПодразделенийВнутренний.ВладелецПоказателя КАК ВладелецПоказателя,
		|	ВТПоказателиПодразделенийВнутренний.Вес КАК Вес,
		|	ВТПоказателиПодразделенийВнутренний.МестоВводаЗначений КАК МестоВводаЗначений,
		|	ВТПоказателиПодразделенийВнутренний.ОтветственныйЗаВводПлана КАК ОтветственныйЗаВводПлана,
		|	ВТПоказателиПодразделенийВнутренний.ОтветственныйЗаВводФакта КАК ОтветственныйЗаВводФакта,
		|	ВЫРАЗИТЬ(ВТПоказателиПодразделенийВнутренний.Показатель КАК Справочник.СтруктураЦелей).ТребуетсяВводПлана КАК ТребуетсяВводПлана
		|ПОМЕСТИТЬ ВТПоказателиИзПодразделений
		|ИЗ
		|	ВТПоказателиПодразделенийВнутренний КАК ВТПоказателиПодразделенийВнутренний
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПодразделения КАК ВТПодразделения
		|		ПО ВТПоказателиПодразделенийВнутренний.Подразделение = ВТПодразделения.Подразделение
		|			И ВТПоказателиПодразделенийВнутренний.Период = ВТПодразделения.Период
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМестаПозицийВСтруктуреПредприятия КАК ВТМестаПозицийВСтруктуреПредприятия
		|		ПО ВТПоказателиПодразделенийВнутренний.Подразделение = ВТМестаПозицийВСтруктуреПредприятия.Подразделение";
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьТекстыЗапросов(Запрос.Текст, ТекстПоказателейПодразделений);
	
	ФинальныйТекст =
		"ВЫБРАТЬ
		|	ПоказателиПозиций.Период КАК Период,
		|	ПоказателиПозиций.Позиция КАК Позиция,
		|	ПоказателиПозиций.Регистратор КАК Регистратор,
		|	ПоказателиПозиций.Позиция КАК ВладелецПоказателя,
		|	ПоказателиПозиций.Показатель КАК Показатель,
		|	ПоказателиПозиций.Вес КАК Вес,
		|	ПоказателиПозиций.МестоВводаЗначений КАК МестоВводаЗначений,
		|	ПоказателиПозиций.ОтветственныйЗаВводПлана КАК ОтветственныйЗаВводПлана,
		|	ПоказателиПозиций.ОтветственныйЗаВводФакта КАК ОтветственныйЗаВводФакта,
		|	ВЫРАЗИТЬ(ПоказателиПозиций.Показатель КАК Справочник.СтруктураЦелей).ТребуетсяВводПлана КАК ТребуетсяВводПлана
		|ПОМЕСТИТЬ #ВТПоказателиПозиций
		|ИЗ
		|	ВТНазначенныеПоказателиПозиций КАК ПоказателиПозиций
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ПоказателиПодразделений.Период,
		|	ПоказателиПодразделений.Позиция,
		|	ПоказателиПодразделений.Регистратор,
		|	ПоказателиПодразделений.ВладелецПоказателя,
		|	ПоказателиПодразделений.Показатель,
		|	ПоказателиПодразделений.Вес,
		|	ПоказателиПодразделений.МестоВводаЗначений,
		|	ПоказателиПодразделений.ОтветственныйЗаВводПлана,
		|	ПоказателиПодразделений.ОтветственныйЗаВводФакта,
		|	ПоказателиПодразделений.ТребуетсяВводПлана
		|ИЗ
		|	ВТПоказателиИзПодразделений КАК ПоказателиПодразделений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПодразделения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПоказателиИзПодразделений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПоказателиПодразделенийВнутренний";
	
	ФинальныйТекст = СтрЗаменить(ФинальныйТекст, "#ВТПоказателиПозиций", ИмяВыходнойВТ);
	
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьТекстыЗапросов(Запрос.Текст, ФинальныйТекст);
	
	Запрос.Выполнить();
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
КонецПроцедуры

//Сотрудники

Функция ПоказателиСотрудников(Сотрудники, Период) Экспорт

	Если Не ЗначениеЗаполнено(Сотрудники) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(Сотрудники) <> Тип("Массив") Тогда
		МассивСотрудников = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудники);
	Иначе
		МассивСотрудников = Сотрудники;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	&Период КАК Период
		|ПОМЕСТИТЬ ВТСотрудникиПериоды
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Ссылка В(&МассивСотрудников)";
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("МассивСотрудников", МассивСотрудников);
	
	Запрос.Выполнить();
	
	СоздатьВТПоказателиСотрудников(Запрос.МенеджерВременныхТаблиц, "ВТСотрудникиПериоды");
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоказателиСотрудников.Период КАК Период,
		|	ПоказателиСотрудников.Сотрудник КАК Сотрудник,
		|	ПоказателиСотрудников.Позиция КАК Позиция,
		|	ПоказателиСотрудников.Показатель КАК Показатель,
		|	ПоказателиСотрудников.ВладелецПоказателя КАК ВладелецПоказателя,
		|	ПоказателиСотрудников.Вес КАК Вес,
		|	ПоказателиСотрудников.ОтветственныйЗаВводПлана КАК ОтветственныйЗаВводПлана,
		|	ПоказателиСотрудников.ОтветственныйЗаВводФакта КАК ОтветственныйЗаВводФакта,
		|	ПоказателиСотрудников.МестоВводаЗначений КАК МестоВводаЗначений,
		|	ПоказателиСотрудников.ТребуетсяВводПлана КАК ТребуетсяВводПлана
		|ИЗ
		|	ВТПоказателиСотрудников КАК ПоказателиСотрудников";
	
	РезультатЗапроса = Запрос.Выполнить();

	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

Процедура СоздатьВТПоказателиСотрудников(МенеджерВременныхТаблиц, ИмяФильтра, ПолучатьКадровыеДанныеСотрудников = Истина) Экспорт
	
	УдалитьВТ = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если ПолучатьКадровыеДанныеСотрудников Тогда
		
		ОписательВТ = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
			Запрос.МенеджерВременныхТаблиц, ИмяФильтра, "Сотрудник,Период");
		КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВТ, Ложь, "ДолжностьПоШтатномуРасписанию, МестоВСтруктуреПредприятия");
		
		УдалитьВТ.Добавить("ВТКадровыеДанныеСотрудников");
	КонецЕсли;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Сотрудники.Период КАК Период,
		|	Сотрудники.ДолжностьПоШтатномуРасписанию КАК Позиция
		|ПОМЕСТИТЬ ВТПозиции
		|ИЗ
		|	ВТКадровыеДанныеСотрудников КАК Сотрудники";
	
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТПозиции");
		
	// Получить набор, действующий не сотрудника.
	СоздатьВТПоказателиПозиций(Запрос.МенеджерВременныхТаблиц, "ВТПозиции", "ВТПоказателиПозицийВнутренний");
	УдалитьВТ.Добавить("ВТПоказателиПозицийВнутренний");
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Сотрудники.Период КАК Период,
		|	Сотрудники.Сотрудник КАК Сотрудник,
		|	Сотрудники.ДолжностьПоШтатномуРасписанию КАК Позиция,
		|	Сотрудники.МестоВСтруктуреПредприятия КАК Подразделение,
		|	ВТПоказателиПозицийВнутренний.Показатель КАК Показатель,
		|	ВТПоказателиПозицийВнутренний.Регистратор КАК Регистратор,
		|	ВТПоказателиПозицийВнутренний.ВладелецПоказателя КАК ВладелецПоказателя,
		|	ВТПоказателиПозицийВнутренний.Вес КАК Вес,
		|	ВТПоказателиПозицийВнутренний.ОтветственныйЗаВводПлана КАК ОтветственныйЗаВводПлана,
		|	ВТПоказателиПозицийВнутренний.ОтветственныйЗаВводФакта КАК ОтветственныйЗаВводФакта,
		|	ВТПоказателиПозицийВнутренний.МестоВводаЗначений КАК МестоВводаЗначений,
		|	ВТПоказателиПозицийВнутренний.ТребуетсяВводПлана КАК ТребуетсяВводПлана
		|ПОМЕСТИТЬ ВТПоказателиСотрудников
		|ИЗ
		|	ВТКадровыеДанныеСотрудников КАК Сотрудники
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПоказателиПозицийВнутренний КАК ВТПоказателиПозицийВнутренний
		|		ПО Сотрудники.Период = ВТПоказателиПозицийВнутренний.Период
		|			И Сотрудники.ДолжностьПоШтатномуРасписанию = ВТПоказателиПозицийВнутренний.Позиция";
	
	Запрос.Выполнить();
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
КонецПроцедуры

#КонецОбласти

#Область ПлановыеПоказатели

Функция ВведенныеПлановыеЗначенияПоказателейПодразделений(ТаблицаФильтра, ИсключаемыйРегистратор = Неопределено) Экспорт 

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	УдалитьВТ = Новый Массив;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаФильтра.ПериодДействия КАК ПериодДействия,
		|	ТаблицаФильтра.Подразделение КАК Подразделение,
		|	ТаблицаФильтра.Показатель КАК Показатель
		|ПОМЕСТИТЬ ВТТаблицаФильтра
		|ИЗ
		|	&ТаблицаФильтра КАК ТаблицаФильтра";
	Запрос.УстановитьПараметр("ТаблицаФильтра", ТаблицаФильтра);
	УдалитьВТ.Добавить("ВТТаблицаФильтра");
	
	ЗапросВведенныеПлановыеЗначения = ЗапросВТВведенныеПлановыеЗначенияПоказателейПодразделений("ВТТаблицаФильтра", ИсключаемыйРегистратор);
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьЗапросы(Запрос, ЗапросВведенныеПлановыеЗначения);
	
	ВозвращаемаяТаблица = Запрос.Выполнить().Выгрузить();
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
	Возврат ВозвращаемаяТаблица;

КонецФункции

Функция ВведенныеПлановыеЗначенияПоказателейПозиций(ТаблицаФильтра, ИсключаемыйРегистратор = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	УдалитьВТ = Новый Массив;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаФильтра.ПериодДействия КАК ПериодДействия,
		|	ТаблицаФильтра.Позиция КАК Позиция,
		|	ТаблицаФильтра.Показатель КАК Показатель
		|ПОМЕСТИТЬ ВТТаблицаФильтра
		|ИЗ
		|	&ТаблицаФильтра КАК ТаблицаФильтра";
	Запрос.УстановитьПараметр("ТаблицаФильтра", ТаблицаФильтра);
	УдалитьВТ.Добавить("ВТТаблицаФильтра");
	
	ЗапросВведенныеПлановыеЗначения = ЗапросВТВведенныеПлановыеЗначенияПоказателейПозиций("ВТТаблицаФильтра", ИсключаемыйРегистратор);
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьЗапросы(Запрос, ЗапросВведенныеПлановыеЗначения);
	
	ВозвращаемаяТаблица = Запрос.Выполнить().Выгрузить();
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
	Возврат ВозвращаемаяТаблица;

КонецФункции

Функция ВведенныеПлановыеЗначенияПоказателейСотрудников(ТаблицаФильтра, ИсключаемыйРегистратор = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	УдалитьВТ = Новый Массив;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаФильтра.ПериодДействия КАК ПериодДействия,
		|	ТаблицаФильтра.Сотрудник КАК Сотрудник,
		|	ТаблицаФильтра.Показатель КАК Показатель
		|ПОМЕСТИТЬ ВТТаблицаФильтра
		|ИЗ
		|	&ТаблицаФильтра КАК ТаблицаФильтра";
	Запрос.УстановитьПараметр("ТаблицаФильтра", ТаблицаФильтра);
	УдалитьВТ.Добавить("ВТТаблицаФильтра");
	
	ЗапросВведенныеПлановыеЗначения = ЗапросВТВведенныеПлановыеЗначенияПоказателейСотрудников("ВТТаблицаФильтра", ИсключаемыйРегистратор);
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьЗапросы(Запрос, ЗапросВведенныеПлановыеЗначения);
	
	ВозвращаемаяТаблица = Запрос.Выполнить().Выгрузить();
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
	Возврат ВозвращаемаяТаблица;

КонецФункции

#КонецОбласти

#Область ФактическиеПоказатели

Функция ВведенныеФактическиеЗначенияПоказателейПодразделений(ТаблицаФильтра, ИсключаемыйРегистратор = Неопределено) Экспорт 

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	УдалитьВТ = Новый Массив;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаФильтра.ДатаНачала КАК ДатаНачала,
		|	ТаблицаФильтра.ДатаОкончания КАК ДатаОкончания,
		|	ТаблицаФильтра.Подразделение КАК Подразделение,
		|	ТаблицаФильтра.Показатель КАК Показатель
		|ПОМЕСТИТЬ ВТТаблицаФильтра
		|ИЗ
		|	&ТаблицаФильтра КАК ТаблицаФильтра";
	Запрос.УстановитьПараметр("ТаблицаФильтра", ТаблицаФильтра);
	УдалитьВТ.Добавить("ВТТаблицаФильтра");
	
	ЗапросВведенныеПлановыеЗначения = ЗапросВТВведенныеФактическиеЗначенияПоказателейПодразделений("ВТТаблицаФильтра", ИсключаемыйРегистратор);
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьЗапросы(Запрос, ЗапросВведенныеПлановыеЗначения);
	
	ВозвращаемаяТаблица = Запрос.Выполнить().Выгрузить();
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
	Возврат ВозвращаемаяТаблица;

КонецФункции

Функция ВведенныеФактическиеЗначенияПоказателейСотрудников(ТаблицаФильтра, ИсключаемыйРегистратор = Неопределено) Экспорт 

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	УдалитьВТ = Новый Массив;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаФильтра.ДатаНачала КАК ДатаНачала,
		|	ТаблицаФильтра.ДатаОкончания КАК ДатаОкончания,
		|	ТаблицаФильтра.Сотрудник КАК Сотрудник,
		|	ТаблицаФильтра.Показатель КАК Показатель
		|ПОМЕСТИТЬ ВТТаблицаФильтра
		|ИЗ
		|	&ТаблицаФильтра КАК ТаблицаФильтра";
	Запрос.УстановитьПараметр("ТаблицаФильтра", ТаблицаФильтра);
	УдалитьВТ.Добавить("ВТТаблицаФильтра");
	
	ЗапросВведенныеПлановыеЗначения = ЗапросВТВведенныеФактическиеЗначенияПоказателеСотрудников("ВТТаблицаФильтра", ИсключаемыйРегистратор);
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьЗапросы(Запрос, ЗапросВведенныеПлановыеЗначения);
	
	ВозвращаемаяТаблица = Запрос.Выполнить().Выгрузить();
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
	Возврат ВозвращаемаяТаблица;

КонецФункции

#КонецОбласти

#Область ДвиженияДокументов

// Заполняет движения ПоказателиЭффективностиПодразделений и ШкалыПоказателиЭффективностиПодразделений.
//
// Параметры:
//	Движения - коллекция движений, в которой необходимо заполнить движения.
//
Процедура СформироватьДвиженияПоказателейПодразделений(Движения, ПоказателиПодразделений, ШкалыПодразделений) Экспорт

	СформироватьКоллекциюДвижений(Движения.ПоказателиЭффективностиПодразделений, ПоказателиПодразделений);
	СформироватьКоллекциюДвижений(Движения.ШкалыПоказателиЭффективностиПодразделений, ШкалыПодразделений);
	
КонецПроцедуры

// Заполняет движения ПоказателиЭффективностиПозиций и ШкалыПоказателиЭффективностиПозиций.
//
// Параметры:
//	Движения - коллекция движений, в которой необходимо заполнить движения.
//
Процедура СформироватьДвиженияПоказателейПозиций(Движения, ПоказателиПозиций, ШкалыПозиций) Экспорт

	СформироватьКоллекциюДвижений(Движения.ПоказателиЭффективностиПозиций, ПоказателиПозиций);
	СформироватьКоллекциюДвижений(Движения.ШкалыПоказателиЭффективностиПозиций, ШкалыПозиций);
	
КонецПроцедуры

// Заполняет движения ПодразделенияВводаЗначенийПоказателейЭффективности И ПозицииВводаЗначенийПоказателейЭффективности.
//
// Параметры:
//	Движения - коллекция движений, в которой необходимо заполнить движения.
//
Процедура СформироватьДвиженияМестВводаЗначенийПоказателейЭффективности(Движения, ПодразделенияВводаЗначений, ПозицииВводаЗначений) Экспорт
	
	СформироватьКоллекциюДвижений(Движения.ПодразделенияВводаЗначенийПоказателейЭффективности, ПодразделенияВводаЗначений);
	СформироватьКоллекциюДвижений(Движения.ПозицииВводаЗначенийПоказателейЭффективности, ПозицииВводаЗначений);
	
КонецПроцедуры

Процедура СформироватьДвиженияПлановыеЗначенияПоказателейЭффективностиПодразделений(Движения, ПланПодразделений) Экспорт
	СформироватьКоллекциюДвижений(Движения.ПлановыеЗначенияПоказателейЭффективностиПодразделений, ПланПодразделений);
КонецПроцедуры

Процедура СформироватьДвиженияПлановыеЗначенияПоказателейЭффективностиПозиций(Движения, ПланПозиций) Экспорт
	СформироватьКоллекциюДвижений(Движения.ПлановыеЗначенияПоказателейЭффективностиПозиций, ПланПозиций);
КонецПроцедуры

Процедура СформироватьДвиженияПлановыеЗначенияПоказателейЭффективностиСотрудников(Движения, ПланСотрудников) Экспорт
	СформироватьКоллекциюДвижений(Движения.ПлановыеЗначенияПоказателейЭффективностиСотрудников, ПланСотрудников);
КонецПроцедуры

Процедура СформироватьДвиженияФактическиеЗначенияПоказателейЭффективностиПодразделений(Движения, ФактПодразделений) Экспорт
	СформироватьКоллекциюДвижений(Движения.ФактическиеЗначенияПоказателейЭффективностиПодразделений, ФактПодразделений);
КонецПроцедуры

Процедура СформироватьДвиженияФактическиеЗначенияПоказателейЭффективностиСотрудников(Движения, ФактСотрудников) Экспорт
	СформироватьКоллекциюДвижений(Движения.ФактическиеЗначенияПоказателейЭффективностиСотрудников, ФактСотрудников);
КонецПроцедуры

#КонецОбласти

#Область ПанельНастроекУправлениеПерсоналом

Процедура ПанельУправлениеПерсоналомЗаписатьНастройкиКлючевыхПоказателейЭффективности(Значение) Экспорт
	
	Константы.ИспользоватьКлючевыеПоказателиЭффективности.Установить(Значение);
	
	СоздатьПоказательИНачислениеДоплатаПоРезультатамОценкиЭффективности();
	
КонецПроцедуры

Процедура ПанельУправлениеПерсоналомДополнитьСтрокуКонстант(СтрокаКонстант) Экспорт 
	СтрокаКонстант = СтрокаКонстант + ",ИспользоватьКлючевыеПоказателиЭффективности";
КонецПроцедуры

#КонецОбласти

Функция ПоказателиСтрокой(Показатели) Экспорт

	МассивПоказателей = Показатели.Выгрузить(, "Показатель").ВыгрузитьКолонку("Показатель");
	СоответствиеРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивПоказателей, "Наименование, КраткоеНаименование");
	
	ПоказателиСтрокой = "";
	
	Для каждого СтрокаПоказателя Из СоответствиеРеквизитов Цикл
		ПоказателиСтрокой = ПоказателиСтрокой + ?(ПустаяСтрока(ПоказателиСтрокой), "", ", ")
			+ ?(ЗначениеЗаполнено(СтрокаПоказателя.Значение.КраткоеНаименование), СтрокаПоказателя.Значение.КраткоеНаименование, Лев(СтрокаПоказателя.Значение.Наименование, 30));
	КонецЦикла; 

	Возврат ПоказателиСтрокой;
	
КонецФункции

Процедура ЗаполнитьИсточникиДанныхДляПроверкиЗапретаИзменения(ИсточникиДанных) Экспорт

	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.НазначениеПоказателейЭффективности", "Период", "Зарплата", "Подразделение.Источник");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПланПоказателейЭффективностиПодразделения", "Период", "Зарплата", "Подразделение.Источник");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПланПоказателейЭффективностиПозиции", "Период", "Зарплата", "Подразделение.Источник");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПланПоказателейЭффективностиСотрудника", "Период", "Зарплата", "Подразделение.Источник");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПланПоказателяЭффективностиПодразделений", "Период", "Зарплата", "Подразделения.Подразделение.Источник");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПланПоказателяЭффективностиПозиций", "Период", "Зарплата", "Подразделение.Источник");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ФактПоказателейЭффективностиПодразделения", "Период", "Зарплата", "Подразделение.Источник");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ФактПоказателейЭффективностиСотрудника", "Период", "Зарплата", "Подразделение.Источник");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ФактПоказателяЭффективностиПодразделений", "Период", "Зарплата", "Подразделения.Подразделение.Источник");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ФактПоказателяЭффективностиСотрудников", "Период", "Зарплата", "Подразделение.Источник");

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗапросВТРассчитанныеОценкиПоказателейСотрудников(ИмяФильтра = "ВТРассчитанныеПоказателиСотрудников")

	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВТРассчитанныеПоказателиСотрудников.Период КАК Период,
		|	ВТРассчитанныеПоказателиСотрудников.ВладелецПоказателя КАК Подразделение,
		|	ВТРассчитанныеПоказателиСотрудников.Показатель КАК Показатель
		|ПОМЕСТИТЬ ВТРассчитанныеПоказателиВладелецПодразделения
		|ИЗ
		|	#ВТРассчитанныеПоказателиСотрудников КАК ВТРассчитанныеПоказателиСотрудников
		|ГДЕ
		|	ВТРассчитанныеПоказателиСотрудников.ВладелецПоказателя ССЫЛКА Справочник.СтруктураПредприятия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТРассчитанныеПоказателиСотрудников.Период КАК Период,
		|	ВТРассчитанныеПоказателиСотрудников.ВладелецПоказателя КАК Позиция,
		|	ВТРассчитанныеПоказателиСотрудников.Показатель КАК Показатель
		|ПОМЕСТИТЬ ВТРассчитанныеПоказателиВладелецПозиции
		|ИЗ
		|	#ВТРассчитанныеПоказателиСотрудников КАК ВТРассчитанныеПоказателиСотрудников
		|ГДЕ
		|	ВТРассчитанныеПоказателиСотрудников.ВладелецПоказателя ССЫЛКА Справочник.ШтатноеРасписание";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ВТРассчитанныеПоказателиСотрудников", ИмяФильтра);
	
	// Получаем шкалы.
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра("ВТРассчитанныеПоказателиВладелецПодразделения", "Подразделение,Показатель");
	ЗапросШкалПодразделений = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"ШкалыПоказателиЭффективностиПодразделений",
		Ложь,
		ОписаниеФильтра,
		ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез(),,
		"ВТШкалыПоказателиЭффективностиПодразделений");
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьЗапросы(Запрос, ЗапросШкалПодразделений);
		
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра("ВТРассчитанныеПоказателиВладелецПозиции", "Позиция,Показатель");
	ЗапросШкалПозиций = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"ШкалыПоказателиЭффективностиПозиций",
		Ложь,
		ОписаниеФильтра,
		ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез(),,
		"ВТШкалыПоказателиЭффективностиПозиций");
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьЗапросы(Запрос, ЗапросШкалПозиций);
	
	ТекстРасчета =
		"ВЫБРАТЬ
		|	РассчитанныеПоказателиСотрудников.Период КАК Период,
		|	РассчитанныеПоказателиСотрудников.Сотрудник КАК Сотрудник,
		|	РассчитанныеПоказателиСотрудников.Показатель КАК Показатель,
		|	ШкалыПоказателиЭффективностиПодразделений.ЗначениеДо КАК ЗначениеДо,
		|	ШкалыПоказателиЭффективностиПодразделений.ЗначениеОт КАК ЗначениеОт,
		|	ШкалыПоказателиЭффективностиПодразделений.Оценка КАК Оценка,
		|	ШкалыПоказателиЭффективностиПодразделений.ОценкаЗадаетсяИнтервалом КАК ОценкаЗадаетсяИнтервалом,
		|	ШкалыПоказателиЭффективностиПодразделений.ИнтервалОценкиОт КАК ИнтервалОценкиОт,
		|	ШкалыПоказателиЭффективностиПодразделений.ИнтервалОценкиДо КАК ИнтервалОценкиДо,
		|	ШкалыПоказателиЭффективностиПодразделений.Показатель.ОбратнаяШкала КАК ПоказательОбратнаяШкала
		|ПОМЕСТИТЬ ВТПоказателиСотрудниковСоШкаламиЗначений
		|ИЗ
		|	ВТРассчитанныеПоказателиСотрудников КАК РассчитанныеПоказателиСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТШкалыПоказателиЭффективностиПодразделений КАК ШкалыПоказателиЭффективностиПодразделений
		|		ПО РассчитанныеПоказателиСотрудников.ВладелецПоказателя = ШкалыПоказателиЭффективностиПодразделений.Подразделение
		|			И РассчитанныеПоказателиСотрудников.Показатель = ШкалыПоказателиЭффективностиПодразделений.Показатель
		|			И РассчитанныеПоказателиСотрудников.Период = ШкалыПоказателиЭффективностиПодразделений.Период
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РассчитанныеПоказателиСотрудников.Период,
		|	РассчитанныеПоказателиСотрудников.Сотрудник,
		|	РассчитанныеПоказателиСотрудников.Показатель,
		|	ШкалыПоказателиЭффективностиПозиций.ЗначениеДо,
		|	ШкалыПоказателиЭффективностиПозиций.ЗначениеОт,
		|	ШкалыПоказателиЭффективностиПозиций.Оценка,
		|	ШкалыПоказателиЭффективностиПозиций.ОценкаЗадаетсяИнтервалом,
		|	ШкалыПоказателиЭффективностиПозиций.ИнтервалОценкиОт,
		|	ШкалыПоказателиЭффективностиПозиций.ИнтервалОценкиДо,
		|	ШкалыПоказателиЭффективностиПозиций.Показатель.ОбратнаяШкала
		|ИЗ
		|	ВТРассчитанныеПоказателиСотрудников КАК РассчитанныеПоказателиСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТШкалыПоказателиЭффективностиПозиций КАК ШкалыПоказателиЭффективностиПозиций
		|		ПО РассчитанныеПоказателиСотрудников.ВладелецПоказателя = ШкалыПоказателиЭффективностиПозиций.Позиция
		|			И РассчитанныеПоказателиСотрудников.Показатель = ШкалыПоказателиЭффективностиПозиций.Показатель
		|			И РассчитанныеПоказателиСотрудников.Период = ШкалыПоказателиЭффективностиПозиций.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПоказателиСотрудниковСоШкаламиЗначений.Период КАК Период,
		|	ВТПоказателиСотрудниковСоШкаламиЗначений.Сотрудник КАК Сотрудник,
		|	ВТПоказателиСотрудниковСоШкаламиЗначений.Показатель КАК Показатель,
		|	ВТПоказателиСотрудниковСоШкаламиЗначений.ЗначениеДо КАК ЗначениеДо,
		|	ВТПоказателиСотрудниковСоШкаламиЗначений.ЗначениеОт КАК ЗначениеОт,
		|	ВТПоказателиСотрудниковСоШкаламиЗначений.Оценка КАК Оценка,
		|	ВТПоказателиСотрудниковСоШкаламиЗначений.ОценкаЗадаетсяИнтервалом КАК ОценкаЗадаетсяИнтервалом,
		|	ВТПоказателиСотрудниковСоШкаламиЗначений.ИнтервалОценкиОт КАК ИнтервалОценкиОт,
		|	ВТПоказателиСотрудниковСоШкаламиЗначений.ИнтервалОценкиДо КАК ИнтервалОценкиДо,
		|	ВТПоказателиСотрудниковСоШкаламиЗначений.ПоказательОбратнаяШкала КАК ПоказательОбратнаяШкала
		|ПОМЕСТИТЬ ВТПоказателиСотрудниковПрямаяШкала
		|ИЗ
		|	ВТПоказателиСотрудниковСоШкаламиЗначений КАК ВТПоказателиСотрудниковСоШкаламиЗначений
		|ГДЕ
		|	НЕ ВТПоказателиСотрудниковСоШкаламиЗначений.ПоказательОбратнаяШкала
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПоказателиСотрудниковПрямаяШкала.Период КАК Период,
		|	ВТПоказателиСотрудниковПрямаяШкала.Сотрудник КАК Сотрудник,
		|	ВТПоказателиСотрудниковПрямаяШкала.Показатель КАК Показатель,
		|	МИНИМУМ(ВТПоказателиСотрудниковПрямаяШкала.ЗначениеДо) КАК ЗначениеДо
		|ПОМЕСТИТЬ ВТПрямаяШкалаУказатели
		|ИЗ
		|	ВТРассчитанныеПоказателиСотрудников КАК ВТРассчитанныеПоказателиСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПоказателиСотрудниковПрямаяШкала КАК ВТПоказателиСотрудниковПрямаяШкала
		|		ПО ВТРассчитанныеПоказателиСотрудников.Период = ВТПоказателиСотрудниковПрямаяШкала.Период
		|			И ВТРассчитанныеПоказателиСотрудников.Сотрудник = ВТПоказателиСотрудниковПрямаяШкала.Сотрудник
		|			И ВТРассчитанныеПоказателиСотрудников.Показатель = ВТПоказателиСотрудниковПрямаяШкала.Показатель
		|			И ВТРассчитанныеПоказателиСотрудников.ЗначениеПоказателя <= ВТПоказателиСотрудниковПрямаяШкала.ЗначениеДо
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТПоказателиСотрудниковПрямаяШкала.Период,
		|	ВТПоказателиСотрудниковПрямаяШкала.Сотрудник,
		|	ВТПоказателиСотрудниковПрямаяШкала.Показатель
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПоказателиСотрудниковПрямаяШкала.Период КАК Период,
		|	ВТПоказателиСотрудниковПрямаяШкала.Сотрудник КАК Сотрудник,
		|	ВТПоказателиСотрудниковПрямаяШкала.Показатель КАК Показатель,
		|	ВТРассчитанныеПоказателиСотрудников.ПлановоеЗначение КАК ПлановоеЗначение,
		|	ВТРассчитанныеПоказателиСотрудников.РегистраторПлановогоЗначения КАК РегистраторПлановогоЗначения,
		|	ВТРассчитанныеПоказателиСотрудников.ФактическоеЗначение КАК ФактическоеЗначение,
		|	ВТРассчитанныеПоказателиСотрудников.РегистраторФактическогоЗначения КАК РегистраторФактическогоЗначения,
		|	ВТРассчитанныеПоказателиСотрудников.ЗначениеПоказателя КАК ЗначениеПоказателя,
		|	ВЫБОР
		|		КОГДА ВТПоказателиСотрудниковПрямаяШкала.ОценкаЗадаетсяИнтервалом
		|			ТОГДА (ВТПоказателиСотрудниковПрямаяШкала.ИнтервалОценкиДо - ВТПоказателиСотрудниковПрямаяШкала.ИнтервалОценкиОт) * (ВТРассчитанныеПоказателиСотрудников.ЗначениеПоказателя - ВТПоказателиСотрудниковПрямаяШкала.ЗначениеОт) / (ВТПоказателиСотрудниковПрямаяШкала.ЗначениеДо - ВТПоказателиСотрудниковПрямаяШкала.ЗначениеОт) + ВТПоказателиСотрудниковПрямаяШкала.ИнтервалОценкиОт
		|		ИНАЧЕ ВТПоказателиСотрудниковПрямаяШкала.Оценка
		|	КОНЕЦ КАК ОценкаПоказателя,
		|	ВТПоказателиСотрудниковПрямаяШкала.ЗначениеОт КАК ЗначениеОт,
		|	ВТПоказателиСотрудниковПрямаяШкала.ЗначениеДо КАК ЗначениеДо
		|ПОМЕСТИТЬ ВТОценкаПрямошкальныхПоказателей
		|ИЗ
		|	ВТПоказателиСотрудниковПрямаяШкала КАК ВТПоказателиСотрудниковПрямаяШкала
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПрямаяШкалаУказатели КАК ВТПрямаяШкалаУказатели
		|		ПО ВТПоказателиСотрудниковПрямаяШкала.Период = ВТПрямаяШкалаУказатели.Период
		|			И ВТПоказателиСотрудниковПрямаяШкала.Сотрудник = ВТПрямаяШкалаУказатели.Сотрудник
		|			И ВТПоказателиСотрудниковПрямаяШкала.Показатель = ВТПрямаяШкалаУказатели.Показатель
		|			И ВТПоказателиСотрудниковПрямаяШкала.ЗначениеДо = ВТПрямаяШкалаУказатели.ЗначениеДо
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРассчитанныеПоказателиСотрудников КАК ВТРассчитанныеПоказателиСотрудников
		|		ПО ВТПоказателиСотрудниковПрямаяШкала.Период = ВТРассчитанныеПоказателиСотрудников.Период
		|			И ВТПоказателиСотрудниковПрямаяШкала.Сотрудник = ВТРассчитанныеПоказателиСотрудников.Сотрудник
		|			И ВТПоказателиСотрудниковПрямаяШкала.Показатель = ВТРассчитанныеПоказателиСотрудников.Показатель
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПоказателиСотрудниковСоШкаламиЗначений.Период КАК Период,
		|	ВТПоказателиСотрудниковСоШкаламиЗначений.Сотрудник КАК Сотрудник,
		|	ВТПоказателиСотрудниковСоШкаламиЗначений.Показатель КАК Показатель,
		|	ВТПоказателиСотрудниковСоШкаламиЗначений.ЗначениеДо КАК ЗначениеДо,
		|	ВТПоказателиСотрудниковСоШкаламиЗначений.ЗначениеОт КАК ЗначениеОт,
		|	ВТПоказателиСотрудниковСоШкаламиЗначений.Оценка КАК Оценка,
		|	ВТПоказателиСотрудниковСоШкаламиЗначений.ОценкаЗадаетсяИнтервалом КАК ОценкаЗадаетсяИнтервалом,
		|	ВТПоказателиСотрудниковСоШкаламиЗначений.ИнтервалОценкиОт КАК ИнтервалОценкиОт,
		|	ВТПоказателиСотрудниковСоШкаламиЗначений.ИнтервалОценкиДо КАК ИнтервалОценкиДо,
		|	ВТПоказателиСотрудниковСоШкаламиЗначений.ПоказательОбратнаяШкала КАК ПоказательОбратнаяШкала
		|ПОМЕСТИТЬ ВТПоказателиСотрудниковОбратнаяШкала
		|ИЗ
		|	ВТПоказателиСотрудниковСоШкаламиЗначений КАК ВТПоказателиСотрудниковСоШкаламиЗначений
		|ГДЕ
		|	ВТПоказателиСотрудниковСоШкаламиЗначений.ПоказательОбратнаяШкала
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПоказателиСотрудниковОбратнаяШкала.Период КАК Период,
		|	ВТПоказателиСотрудниковОбратнаяШкала.Сотрудник КАК Сотрудник,
		|	ВТПоказателиСотрудниковОбратнаяШкала.Показатель КАК Показатель,
		|	МАКСИМУМ(ВТПоказателиСотрудниковОбратнаяШкала.ЗначениеДо) КАК ЗначениеДо
		|ПОМЕСТИТЬ ВТОбратнаяШкалаУказатели
		|ИЗ
		|	ВТРассчитанныеПоказателиСотрудников КАК ВТРассчитанныеПоказателиСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПоказателиСотрудниковОбратнаяШкала КАК ВТПоказателиСотрудниковОбратнаяШкала
		|		ПО ВТРассчитанныеПоказателиСотрудников.Период = ВТПоказателиСотрудниковОбратнаяШкала.Период
		|			И ВТРассчитанныеПоказателиСотрудников.Сотрудник = ВТПоказателиСотрудниковОбратнаяШкала.Сотрудник
		|			И ВТРассчитанныеПоказателиСотрудников.Показатель = ВТПоказателиСотрудниковОбратнаяШкала.Показатель
		|			И ВТРассчитанныеПоказателиСотрудников.ЗначениеПоказателя >= ВТПоказателиСотрудниковОбратнаяШкала.ЗначениеДо
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТПоказателиСотрудниковОбратнаяШкала.Период,
		|	ВТПоказателиСотрудниковОбратнаяШкала.Сотрудник,
		|	ВТПоказателиСотрудниковОбратнаяШкала.Показатель
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПоказателиСотрудниковОбратнаяШкала.Период КАК Период,
		|	ВТПоказателиСотрудниковОбратнаяШкала.Сотрудник КАК Сотрудник,
		|	ВТПоказателиСотрудниковОбратнаяШкала.Показатель КАК Показатель,
		|	ВТРассчитанныеПоказателиСотрудников.ПлановоеЗначение КАК ПлановоеЗначение,
		|	ВТРассчитанныеПоказателиСотрудников.РегистраторПлановогоЗначения КАК РегистраторПлановогоЗначения,
		|	ВТРассчитанныеПоказателиСотрудников.ФактическоеЗначение КАК ФактическоеЗначение,
		|	ВТРассчитанныеПоказателиСотрудников.РегистраторФактическогоЗначения КАК РегистраторФактическогоЗначения,
		|	ВТРассчитанныеПоказателиСотрудников.ЗначениеПоказателя КАК ЗначениеПоказателя,
		|	ВЫБОР
		|		КОГДА ВТПоказателиСотрудниковОбратнаяШкала.ОценкаЗадаетсяИнтервалом
		|			ТОГДА (ВТПоказателиСотрудниковОбратнаяШкала.ИнтервалОценкиДо - ВТПоказателиСотрудниковОбратнаяШкала.ИнтервалОценкиОт) * (ВТРассчитанныеПоказателиСотрудников.ЗначениеПоказателя - ВТПоказателиСотрудниковОбратнаяШкала.ЗначениеОт) / (ВТПоказателиСотрудниковОбратнаяШкала.ЗначениеДо - ВТПоказателиСотрудниковОбратнаяШкала.ЗначениеОт) + ВТПоказателиСотрудниковОбратнаяШкала.ИнтервалОценкиОт
		|		ИНАЧЕ ВТПоказателиСотрудниковОбратнаяШкала.Оценка
		|	КОНЕЦ КАК ОценкаПоказателя,
		|	ВТПоказателиСотрудниковОбратнаяШкала.ЗначениеОт КАК ЗначениеОт,
		|	ВТПоказателиСотрудниковОбратнаяШкала.ЗначениеДо КАК ЗначениеДо
		|ПОМЕСТИТЬ ВТОценкаОбратношкальныхПоказателей
		|ИЗ
		|	ВТПоказателиСотрудниковОбратнаяШкала КАК ВТПоказателиСотрудниковОбратнаяШкала
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОбратнаяШкалаУказатели КАК ВТОбратнаяШкалаУказатели
		|		ПО ВТПоказателиСотрудниковОбратнаяШкала.Период = ВТОбратнаяШкалаУказатели.Период
		|			И ВТПоказателиСотрудниковОбратнаяШкала.Сотрудник = ВТОбратнаяШкалаУказатели.Сотрудник
		|			И ВТПоказателиСотрудниковОбратнаяШкала.Показатель = ВТОбратнаяШкалаУказатели.Показатель
		|			И ВТПоказателиСотрудниковОбратнаяШкала.ЗначениеДо = ВТОбратнаяШкалаУказатели.ЗначениеДо
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРассчитанныеПоказателиСотрудников КАК ВТРассчитанныеПоказателиСотрудников
		|		ПО ВТПоказателиСотрудниковОбратнаяШкала.Период = ВТРассчитанныеПоказателиСотрудников.Период
		|			И ВТПоказателиСотрудниковОбратнаяШкала.Сотрудник = ВТРассчитанныеПоказателиСотрудников.Сотрудник
		|			И ВТПоказателиСотрудниковОбратнаяШкала.Показатель = ВТРассчитанныеПоказателиСотрудников.Показатель
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТОценкаПрямошкальныхПоказателей.Период КАК Период,
		|	ВТОценкаПрямошкальныхПоказателей.Сотрудник КАК Сотрудник,
		|	ВТОценкаПрямошкальныхПоказателей.Показатель КАК Показатель,
		|	ВТОценкаПрямошкальныхПоказателей.ПлановоеЗначение КАК ПлановоеЗначение,
		|	ВТОценкаПрямошкальныхПоказателей.РегистраторПлановогоЗначения КАК РегистраторПлановогоЗначения,
		|	ВТОценкаПрямошкальныхПоказателей.ФактическоеЗначение КАК ФактическоеЗначение,
		|	ВТОценкаПрямошкальныхПоказателей.РегистраторФактическогоЗначения КАК РегистраторФактическогоЗначения,
		|	ВТОценкаПрямошкальныхПоказателей.ЗначениеПоказателя КАК ЗначениеПоказателя,
		|	ВТОценкаПрямошкальныхПоказателей.ОценкаПоказателя КАК ОценкаПоказателя,
		|	ВТОценкаПрямошкальныхПоказателей.ЗначениеОт КАК ЗначениеОт,
		|	ВТОценкаПрямошкальныхПоказателей.ЗначениеДо КАК ЗначениеДо
		|ПОМЕСТИТЬ ВТРассчитанныеОценкиПоказателейСотрудников
		|ИЗ
		|	ВТОценкаПрямошкальныхПоказателей КАК ВТОценкаПрямошкальныхПоказателей
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТОценкаОбратношкальныхПоказателей.Период,
		|	ВТОценкаОбратношкальныхПоказателей.Сотрудник,
		|	ВТОценкаОбратношкальныхПоказателей.Показатель,
		|	ВТОценкаОбратношкальныхПоказателей.ПлановоеЗначение,
		|	ВТОценкаОбратношкальныхПоказателей.РегистраторПлановогоЗначения,
		|	ВТОценкаОбратношкальныхПоказателей.ФактическоеЗначение,
		|	ВТОценкаОбратношкальныхПоказателей.РегистраторФактическогоЗначения,
		|	ВТОценкаОбратношкальныхПоказателей.ЗначениеПоказателя,
		|	ВТОценкаОбратношкальныхПоказателей.ОценкаПоказателя,
		|	ВТОценкаОбратношкальныхПоказателей.ЗначениеОт,
		|	ВТОценкаОбратношкальныхПоказателей.ЗначениеДо
		|ИЗ
		|	ВТОценкаОбратношкальныхПоказателей КАК ВТОценкаОбратношкальныхПоказателей
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТРассчитанныеПоказателиВладелецПодразделения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТРассчитанныеПоказателиВладелецПозиции
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТШкалыПоказателиЭффективностиПодразделений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТШкалыПоказателиЭффективностиПозиций
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПоказателиСотрудниковСоШкаламиЗначений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПоказателиСотрудниковПрямаяШкала
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПрямаяШкалаУказатели
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТОценкаПрямошкальныхПоказателей
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПоказателиСотрудниковОбратнаяШкала
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТОбратнаяШкалаУказатели
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТОценкаОбратношкальныхПоказателей";	
	
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьТекстыЗапросов(Запрос.Текст, ТекстРасчета);
	
	Возврат Запрос;

КонецФункции

#Область ОбработчикиОбновления

Процедура СоздатьПоказательИНачислениеДоплатаПоРезультатамОценкиЭффективности() Экспорт 
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьКлючевыеПоказателиЭффективности") Тогда
		Возврат;
	КонецЕсли;
	
	ПоказательЭффективностиСотрудника = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.ПоказательЭффективностиСотрудника");
	Если ПоказательЭффективностиСотрудника <> Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	СоздатьПоказательЭффективностиСотрудника();
	ОбновитьПовторноИспользуемыеЗначения();
	
	КоллекторНачислений = Новый Соответствие;
	СвойстваНачислений = ПланыВидовРасчета.Начисления.СвойстваНачисленийПоКатегориям();
	ПараметрыПланаВидовРасчета = РасчетЗарплатыРасширенный.ОписаниеПараметровПланаВидовРасчета();
	
	СоздатьНачислениеДоплатаПоРезультатамОценкиЭффективности(ПараметрыПланаВидовРасчета, СвойстваНачислений, КоллекторНачислений);
	
	Для Каждого КлючИЗначение Из КоллекторНачислений Цикл
		КлючИЗначение.Значение.ДополнительныеСвойства.Вставить("ИзменениеПланаВидовРасчетаПоНастройкам", Истина);
	КонецЦикла;
	
	ПланыВидовРасчета.Начисления.ЗаписатьВидыРасчетаКоллектора(КоллекторНачислений, СвойстваНачислений);
	
КонецПроцедуры

Процедура УстановитьПараметрыНабораСвойствСправочников(ПараметрыОбновления = Неопределено) Экспорт
	
	ПараметрыНабора = УправлениеСвойствами.СтруктураПараметровНабораСвойств();
	ПараметрыНабора.Используется = Константы.ИспользоватьКлючевыеПоказателиЭффективности.Получить();
	
	УправлениеСвойствами.УстановитьПараметрыНабораСвойств("Справочник_СтруктураЦелей", ПараметрыНабора);
	
	Если ПараметрыОбновления <> Неопределено Тогда
		ПараметрыОбновления.ОбработкаЗавершена = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьИспользоватьКлючевыеПоказателиЭффективности(ПараметрыОбновления = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиКлючевыхПоказателейЭффективности.УдалитьИспользоватьКлючевыеПоказателиЭффективности КАК УдалитьИспользоватьКлючевыеПоказателиЭффективности
		|ИЗ
		|	РегистрСведений.НастройкиКлючевыхПоказателейЭффективности КАК НастройкиКлючевыхПоказателейЭффективности";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Константы.ИспользоватьКлючевыеПоказателиЭффективности.Установить(Выборка.УдалитьИспользоватьКлючевыеПоказателиЭффективности);
	
	Если ПараметрыОбновления <> Неопределено Тогда
		ПараметрыОбновления.ОбработкаЗавершена = Истина;
	КонецЕсли;

КонецПроцедуры

Процедура ПеренестиФактическиеЗначенияПоказателей(ПараметрыОбновления = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УдалитьЗначенияЦелевыхПоказателей.Месяц КАК Период,
		|	УдалитьЗначенияЦелевыхПоказателей.Сотрудник КАК Сотрудник,
		|	УдалитьЗначенияЦелевыхПоказателей.ЦелевойПоказатель КАК Показатель,
		|	УдалитьЗначенияЦелевыхПоказателей.Значение КАК Значение
		|ПОМЕСТИТЬ ВТФактСотрудников
		|ИЗ
		|	РегистрСведений.УдалитьЗначенияЦелевыхПоказателей КАК УдалитьЗначенияЦелевыхПоказателей
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ФактическиеЗначенияПоказателейЭффективностиСотрудников.Обороты(, , Месяц, ) КАК ФактическиеЗначенияПоказателейЭффективностиСотрудниковОбороты
		|		ПО УдалитьЗначенияЦелевыхПоказателей.Месяц = ФактическиеЗначенияПоказателейЭффективностиСотрудниковОбороты.Период
		|			И УдалитьЗначенияЦелевыхПоказателей.Сотрудник = ФактическиеЗначенияПоказателейЭффективностиСотрудниковОбороты.Сотрудник
		|			И УдалитьЗначенияЦелевыхПоказателей.ЦелевойПоказатель = ФактическиеЗначенияПоказателейЭффективностиСотрудниковОбороты.Показатель
		|ГДЕ
		|	ФактическиеЗначенияПоказателейЭффективностиСотрудниковОбороты.Сотрудник ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УдалитьЗначенияЦелевыхПоказателейПодразделений.Месяц КАК Период,
		|	УдалитьЗначенияЦелевыхПоказателейПодразделений.Подразделение КАК Подразделение,
		|	УдалитьЗначенияЦелевыхПоказателейПодразделений.ЦелевойПоказатель КАК Показатель,
		|	УдалитьЗначенияЦелевыхПоказателейПодразделений.Значение КАК Значение
		|ПОМЕСТИТЬ ВТФактПодразделений
		|ИЗ
		|	РегистрСведений.УдалитьЗначенияЦелевыхПоказателейПодразделений КАК УдалитьЗначенияЦелевыхПоказателейПодразделений
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ФактическиеЗначенияПоказателейЭффективностиПодразделений.Обороты(, , Месяц, ) КАК ФактическиеЗначенияПоказателейЭффективностиПодразделенийОбороты
		|		ПО УдалитьЗначенияЦелевыхПоказателейПодразделений.Месяц = ФактическиеЗначенияПоказателейЭффективностиПодразделенийОбороты.Период
		|			И УдалитьЗначенияЦелевыхПоказателейПодразделений.Подразделение = ФактическиеЗначенияПоказателейЭффективностиПодразделенийОбороты.Подразделение
		|			И УдалитьЗначенияЦелевыхПоказателейПодразделений.ЦелевойПоказатель = ФактическиеЗначенияПоказателейЭффективностиПодразделенийОбороты.Показатель
		|ГДЕ
		|	ФактическиеЗначенияПоказателейЭффективностиПодразделенийОбороты.Подразделение ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТФактСотрудников.Период КАК Период
		|ИЗ
		|	ВТФактСотрудников КАК ВТФактСотрудников
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТФактПодразделений.Период
		|ИЗ
		|	ВТФактПодразделений КАК ВТФактПодразделений";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
	
	ВыборкаСотрудники = РезультатыЗапроса[РезультатыЗапроса.Количество() - 3].Выбрать();
	ВыборкаПодразделения = РезультатыЗапроса[РезультатыЗапроса.Количество() - 2].Выбрать();
	Выборка = РезультатыЗапроса[РезультатыЗапроса.Количество() - 1].Выбрать();

	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
		
			ДокументОбъект = Документы.ПереносДанных.СоздатьДокумент();
			ДокументОбъект.ОбменДанными.Загрузка = Истина;
			ДокументОбъект.ПериодРегистрации = Выборка.Период;
			ДокументОбъект.Дата = Выборка.Период;
			
			// Сотрудники.
			НаборЗаписейСотрудники = РегистрыНакопления.ФактическиеЗначенияПоказателейЭффективностиСотрудников.СоздатьНаборЗаписей();
			НаборЗаписейСотрудники.ОбменДанными.Загрузка = Истина;
			ВыборкаСотрудники.Сбросить();
			Пока ВыборкаСотрудники.НайтиСледующий(Новый Структура("Период", Выборка.Период)) Цикл
				ЗаполнитьЗначенияСвойств(НаборЗаписейСотрудники.Добавить(), ВыборкаСотрудники);
			КонецЦикла; 
			
			// Подразделения.
			НаборЗаписейПодразделения = РегистрыНакопления.ФактическиеЗначенияПоказателейЭффективностиПодразделений.СоздатьНаборЗаписей();
			НаборЗаписейПодразделения.ОбменДанными.Загрузка = Истина;
			ВыборкаПодразделения.Сбросить();
			Пока ВыборкаПодразделения.НайтиСледующий(Новый Структура("Период", Выборка.Период)) Цикл
				ЗаполнитьЗначенияСвойств(НаборЗаписейПодразделения.Добавить(), ВыборкаПодразделения);
			КонецЦикла; 
			
			Если НаборЗаписейСотрудники.Количество() > 0 Тогда
				ДокументОбъект.ТаблицаРегистров.Добавить().Имя = "ФактическиеЗначенияПоказателейЭффективностиСотрудников";
			КонецЕсли;
			Если НаборЗаписейПодразделения.Количество() > 0 Тогда
				ДокументОбъект.ТаблицаРегистров.Добавить().Имя = "ФактическиеЗначенияПоказателейЭффективностиПодразделений";
			КонецЕсли;

			// Запись результата.
			ДокументОбъект.Записать();
			
			Если НаборЗаписейСотрудники.Количество() > 0 Тогда
				НаборЗаписейСотрудники.Отбор.Регистратор.Установить(ДокументОбъект.Ссылка);
				НаборЗаписейСотрудники.Записать(Истина);
			КонецЕсли;
			Если НаборЗаписейПодразделения.Количество() > 0 Тогда
				НаборЗаписейПодразделения.Отбор.Регистратор.Установить(ДокументОбъект.Ссылка);
				НаборЗаписейПодразделения.Записать(Истина);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка переноса значений показателей эффективности.'"), УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЦикла;
	
	Если ПараметрыОбновления <> Неопределено Тогда
		ПараметрыОбновления.ОбработкаЗавершена = Истина;
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьПорогиШкал(ПараметрыОбновления = Неопределено) Экспорт
	
	УдалитьВТ = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СтруктураЦелейШкалаЗначений.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТСправочники
		|ИЗ
		|	Справочник.СтруктураЦелей.ШкалаЗначений КАК СтруктураЦелейШкалаЗначений
		|ГДЕ
		|	СтруктураЦелейШкалаЗначений.ЗначениеДо = 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТДокументы
		|ИЗ
		|	Документ.НазначениеПоказателейЭффективности.ШкалыЗначенийПодразделений КАК НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений
		|ГДЕ
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.ЗначениеДо = 0
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.Ссылка
		|ИЗ
		|	Документ.НазначениеПоказателейЭффективности.ШкалыЗначенийПозиций КАК НазначениеПоказателейЭффективностиШкалыЗначенийПозиций
		|ГДЕ
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.ЗначениеДо = 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ШкалыПоказателиЭффективностиПодразделений.Регистратор КАК Ссылка
		|ПОМЕСТИТЬ ВТРегистраторыПодразделений
		|ИЗ
		|	РегистрСведений.ШкалыПоказателиЭффективностиПодразделений КАК ШкалыПоказателиЭффективностиПодразделений
		|ГДЕ
		|	ШкалыПоказателиЭффективностиПодразделений.ЗначениеДо = 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ШкалыПоказателиЭффективностиПозиций.Регистратор КАК Ссылка
		|ПОМЕСТИТЬ ВТРегистраторыПозиций
		|ИЗ
		|	РегистрСведений.ШкалыПоказателиЭффективностиПозиций КАК ШкалыПоказателиЭффективностиПозиций
		|ГДЕ
		|	ШкалыПоказателиЭффективностиПозиций.ЗначениеДо = 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СтруктураЦелейШкалаЗначений.Ссылка КАК Ссылка,
		|	СтруктураЦелейШкалаЗначений.НомерСтроки КАК НомерСтроки,
		|	ВЫБОР
		|		КОГДА СтруктураЦелейШкалаЗначений.ЗначениеДо = 0
		|			ТОГДА ВЫБОР
		|					КОГДА СтруктураЦелейШкалаЗначений.Ссылка.ОбратнаяШкала
		|						ТОГДА &НижнийПорогШкалы
		|					ИНАЧЕ &ВерхнийПорогШкалы
		|				КОНЕЦ
		|		ИНАЧЕ СтруктураЦелейШкалаЗначений.ЗначениеДо
		|	КОНЕЦ КАК ЗначениеДо,
		|	СтруктураЦелейШкалаЗначений.Оценка КАК Оценка,
		|	СтруктураЦелейШкалаЗначений.ОценкаЗадаетсяИнтервалом КАК ОценкаЗадаетсяИнтервалом,
		|	СтруктураЦелейШкалаЗначений.ИнтервалОценкиОт КАК ИнтервалОценкиОт,
		|	СтруктураЦелейШкалаЗначений.ИнтервалОценкиДо КАК ИнтервалОценкиДо
		|ИЗ
		|	Справочник.СтруктураЦелей.ШкалаЗначений КАК СтруктураЦелейШкалаЗначений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСправочники КАК ВТСправочники
		|		ПО СтруктураЦелейШкалаЗначений.Ссылка = ВТСправочники.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТДокументы.Ссылка КАК Ссылка
		|ИЗ
		|	ВТДокументы КАК ВТДокументы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.Ссылка КАК Ссылка,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.НомерСтроки КАК НомерСтроки,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.ИдентификаторСтрокиПоказателя КАК ИдентификаторСтрокиПоказателя,
		|	ВЫБОР
		|		КОГДА НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.ЗначениеДо = 0
		|			ТОГДА ВЫБОР
		|					КОГДА НазначениеПоказателейЭффективностиПоказателиПодразделений.Показатель.ОбратнаяШкала
		|						ТОГДА &НижнийПорогШкалы
		|					ИНАЧЕ &ВерхнийПорогШкалы
		|				КОНЕЦ
		|		ИНАЧЕ НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.ЗначениеДо
		|	КОНЕЦ КАК ЗначениеДо,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.ОценкаЗадаетсяИнтервалом КАК ОценкаЗадаетсяИнтервалом,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.ИнтервалОценкиОт КАК ИнтервалОценкиОт,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.ИнтервалОценкиДо КАК ИнтервалОценкиДо,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.Оценка КАК Оценка
		|ИЗ
		|	Документ.НазначениеПоказателейЭффективности.ШкалыЗначенийПодразделений КАК НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументы КАК ВТДокументы
		|		ПО НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.Ссылка = ВТДокументы.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НазначениеПоказателейЭффективности.ПоказателиПодразделений КАК НазначениеПоказателейЭффективностиПоказателиПодразделений
		|		ПО НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.Ссылка = НазначениеПоказателейЭффективностиПоказателиПодразделений.Ссылка
		|			И НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.ИдентификаторСтрокиПоказателя = НазначениеПоказателейЭффективностиПоказателиПодразделений.ИдентификаторСтрокиПоказателя
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.Ссылка КАК Ссылка,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.НомерСтроки КАК НомерСтроки,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.ИдентификаторСтрокиПоказателя КАК ИдентификаторСтрокиПоказателя,
		|	ВЫБОР
		|		КОГДА НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.ЗначениеДо = 0
		|			ТОГДА ВЫБОР
		|					КОГДА НазначениеПоказателейЭффективностиПоказателиПозиций.Показатель.ОбратнаяШкала
		|						ТОГДА &НижнийПорогШкалы
		|					ИНАЧЕ &ВерхнийПорогШкалы
		|				КОНЕЦ
		|		ИНАЧЕ НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.ЗначениеДо
		|	КОНЕЦ КАК ЗначениеДо,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.ОценкаЗадаетсяИнтервалом КАК ОценкаЗадаетсяИнтервалом,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.ИнтервалОценкиОт КАК ИнтервалОценкиОт,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.ИнтервалОценкиДо КАК ИнтервалОценкиДо,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.Оценка КАК Оценка
		|ИЗ
		|	Документ.НазначениеПоказателейЭффективности.ШкалыЗначенийПозиций КАК НазначениеПоказателейЭффективностиШкалыЗначенийПозиций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументы КАК ВТДокументы
		|		ПО НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.Ссылка = ВТДокументы.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НазначениеПоказателейЭффективности.ПоказателиПозиций КАК НазначениеПоказателейЭффективностиПоказателиПозиций
		|		ПО НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.Ссылка = НазначениеПоказателейЭффективностиПоказателиПозиций.Ссылка
		|			И НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.ИдентификаторСтрокиПоказателя = НазначениеПоказателейЭффективностиПоказателиПозиций.ИдентификаторСтрокиПоказателя
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ШкалыПоказателиЭффективностиПодразделений.Период КАК Период,
		|	ШкалыПоказателиЭффективностиПодразделений.Регистратор КАК Регистратор,
		|	ШкалыПоказателиЭффективностиПодразделений.НомерСтроки КАК НомерСтроки,
		|	ШкалыПоказателиЭффективностиПодразделений.Подразделение КАК Подразделение,
		|	ШкалыПоказателиЭффективностиПодразделений.Показатель КАК Показатель,
		|	ВЫБОР
		|		КОГДА ШкалыПоказателиЭффективностиПодразделений.ЗначениеДо = 0
		|			ТОГДА ВЫБОР
		|					КОГДА ШкалыПоказателиЭффективностиПодразделений.Показатель.ОбратнаяШкала
		|						ТОГДА &НижнийПорогШкалы
		|					ИНАЧЕ &ВерхнийПорогШкалы
		|				КОНЕЦ
		|		ИНАЧЕ ШкалыПоказателиЭффективностиПодразделений.ЗначениеДо
		|	КОНЕЦ КАК ЗначениеДо,
		|	ШкалыПоказателиЭффективностиПодразделений.ОценкаЗадаетсяИнтервалом КАК ОценкаЗадаетсяИнтервалом,
		|	ШкалыПоказателиЭффективностиПодразделений.ИнтервалОценкиОт КАК ИнтервалОценкиОт,
		|	ШкалыПоказателиЭффективностиПодразделений.ИнтервалОценкиДо КАК ИнтервалОценкиДо,
		|	ШкалыПоказателиЭффективностиПодразделений.Оценка КАК Оценка,
		|	ШкалыПоказателиЭффективностиПодразделений.ЗначениеОт КАК ЗначениеОт
		|ИЗ
		|	РегистрСведений.ШкалыПоказателиЭффективностиПодразделений КАК ШкалыПоказателиЭффективностиПодразделений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистраторыПодразделений КАК ВТРегистраторыПодразделений
		|		ПО ШкалыПоказателиЭффективностиПодразделений.Регистратор = ВТРегистраторыПодразделений.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ШкалыПоказателиЭффективностиПозиций.Период КАК Период,
		|	ШкалыПоказателиЭффективностиПозиций.Регистратор КАК Регистратор,
		|	ШкалыПоказателиЭффективностиПозиций.НомерСтроки КАК НомерСтроки,
		|	ШкалыПоказателиЭффективностиПозиций.Позиция КАК Позиция,
		|	ШкалыПоказателиЭффективностиПозиций.Показатель КАК Показатель,
		|	ВЫБОР
		|		КОГДА ШкалыПоказателиЭффективностиПозиций.ЗначениеДо = 0
		|			ТОГДА ВЫБОР
		|					КОГДА ШкалыПоказателиЭффективностиПозиций.Показатель.ОбратнаяШкала
		|						ТОГДА &НижнийПорогШкалы
		|					ИНАЧЕ &ВерхнийПорогШкалы
		|				КОНЕЦ
		|		ИНАЧЕ ШкалыПоказателиЭффективностиПозиций.ЗначениеДо
		|	КОНЕЦ КАК ЗначениеДо,
		|	ШкалыПоказателиЭффективностиПозиций.ОценкаЗадаетсяИнтервалом КАК ОценкаЗадаетсяИнтервалом,
		|	ШкалыПоказателиЭффективностиПозиций.ИнтервалОценкиОт КАК ИнтервалОценкиОт,
		|	ШкалыПоказателиЭффективностиПозиций.ИнтервалОценкиДо КАК ИнтервалОценкиДо,
		|	ШкалыПоказателиЭффективностиПозиций.Оценка КАК Оценка,
		|	ШкалыПоказателиЭффективностиПозиций.ЗначениеОт КАК ЗначениеОт
		|ИЗ
		|	РегистрСведений.ШкалыПоказателиЭффективностиПозиций КАК ШкалыПоказателиЭффективностиПозиций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистраторыПозиций КАК ВТРегистраторыПозиций
		|		ПО ШкалыПоказателиЭффективностиПозиций.Регистратор = ВТРегистраторыПозиций.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Регистратор";
	
	Запрос.УстановитьПараметр("НижнийПорогШкалы", КлючевыеПоказателиЭффективностиКлиентСервер.НижнийПорогШкалы());
	Запрос.УстановитьПараметр("ВерхнийПорогШкалы", КлючевыеПоказателиЭффективностиКлиентСервер.ВерхнийПорогШкалы());
	
	УдалитьВТ.Добавить("ВТСправочники");
	УдалитьВТ.Добавить("ВТДокументы");
	УдалитьВТ.Добавить("ВТРегистраторыПодразделений");
	УдалитьВТ.Добавить("ВТРегистраторыПозиций");
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаСправочник = РезультатыЗапроса[РезультатыЗапроса.Количество() - 6].Выбрать();
	
	ВыборкаДокументов = РезультатыЗапроса[РезультатыЗапроса.Количество() - 5].Выбрать();
	ВыборкаПодразделений = РезультатыЗапроса[РезультатыЗапроса.Количество() - 4].Выбрать();
	ВыборкаПозиций = РезультатыЗапроса[РезультатыЗапроса.Количество() - 3].Выбрать();
	
	ВыборкаШкалПодразделений = РезультатыЗапроса[РезультатыЗапроса.Количество() - 2].Выбрать();
	ВыборкаШкалПозиций = РезультатыЗапроса[РезультатыЗапроса.Количество() - 1].Выбрать();
	
	// Справочник.
	Пока ВыборкаСправочник.СледующийПоЗначениюПоля("Ссылка") Цикл
		СправочникОбъект = ВыборкаСправочник.Ссылка.ПолучитьОбъект();
		СправочникОбъект.ШкалаЗначений.Очистить();
		Пока ВыборкаСправочник.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(СправочникОбъект.ШкалаЗначений.Добавить(), ВыборкаСправочник);
		КонецЦикла; 
		СправочникОбъект.ОбменДанными.Загрузка = Истина;
		СправочникОбъект.Записать();
	КонецЦикла;
	
	// Документ.
	
	Пока ВыборкаДокументов.Следующий() Цикл
		ДокументОбъект = ВыборкаДокументов.Ссылка.ПолучитьОбъект();
		
		ДокументОбъект.ШкалыЗначенийПодразделений.Очистить();
		ВыборкаПодразделений.Сбросить();
		Пока ВыборкаПодразделений.НайтиСледующий(Новый Структура("Ссылка", ВыборкаДокументов.Ссылка)) Цикл
			ЗаполнитьЗначенияСвойств(ДокументОбъект.ШкалыЗначенийПодразделений.Добавить(), ВыборкаПодразделений);
		КонецЦикла;
		
		ДокументОбъект.ШкалыЗначенийПозиций.Очистить();
		ВыборкаПозиций.Сбросить();
		Пока ВыборкаПозиций.НайтиСледующий(Новый Структура("Ссылка", ВыборкаДокументов.Ссылка)) Цикл
			ЗаполнитьЗначенияСвойств(ДокументОбъект.ШкалыЗначенийПозиций.Добавить(), ВыборкаПозиций);
		КонецЦикла;
		
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		ДокументОбъект.Записать();
	КонецЦикла;
	
	// Регистры.
	
	Пока ВыборкаШкалПодразделений.СледующийПоЗначениюПоля("Регистратор") Цикл
		НаборЗаписей = РегистрыСведений.ШкалыПоказателиЭффективностиПодразделений.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаШкалПодразделений.Регистратор);
		
		Пока ВыборкаШкалПодразделений.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), ВыборкаШкалПодразделений);
		КонецЦикла;
		
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Записать();
	КонецЦикла;
	
	Пока ВыборкаШкалПозиций.СледующийПоЗначениюПоля("Регистратор") Цикл
		НаборЗаписей = РегистрыСведений.ШкалыПоказателиЭффективностиПозиций.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаШкалПозиций.Регистратор);
		
		Пока ВыборкаШкалПозиций.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), ВыборкаШкалПозиций);
		КонецЦикла;
		
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Записать();
	КонецЦикла;
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
	Если ПараметрыОбновления <> Неопределено Тогда
		ПараметрыОбновления.ОбработкаЗавершена = Истина;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область НастройкиВариантовОтчетов

// Определяет разделы, в которых доступна панель отчетов.
//
// Параметры:
//   Разделы (Массив) из (ОбъектМетаданных).
//
// Описание:
//   В Разделы необходимо добавить метаданные тех разделов,
//   в которых размещены команды вызова панелей отчетов.
//
// Например:
//	Разделы.Добавить(Метаданные.Подсистемы.ИмяПодсистемы);
//
Процедура ОпределитьРазделыСВариантамиОтчетов(Разделы) Экспорт
	
	Если Разделы.НайтиПоЗначению(Метаданные.Подсистемы.Мотивация) = Неопределено Тогда
		Разделы.Добавить(Метаданные.Подсистемы.Мотивация, НСтр("ru = 'Отчеты по мотивации'"));
	КонецЕсли;
	
КонецПроцедуры

// Содержит настройки размещения вариантов отчетов в панели отчетов.
// Описание см. ЗарплатаКадрыВариантыОтчетов.НастроитьВариантыОтчетов.
//
Процедура НастроитьВариантыОтчетов(Настройки) Экспорт
	
	ВариантыОтчетов.НастроитьОтчетВМодулеМенеджера(Настройки, Метаданные.Отчеты.АнализЦелевыхПоказателейСотрудников);
	
	Вариант = ВариантыОтчетов.ОписаниеВарианта(Настройки, Метаданные.Отчеты.АнализЦелевыхПоказателейСотрудников, "СравнительныйАнализЦелевыхПоказателейСотрудников");
	Вариант.ФункциональныеОпции.Добавить("ИспользоватьКлючевыеПоказателиЭффективности");
	Вариант.Включен = Ложь;
	
	Вариант = ВариантыОтчетов.ОписаниеВарианта(Настройки, Метаданные.Отчеты.АнализЦелевыхПоказателейСотрудников, "ПлановыеИФактическиеЗначенияЦелевыхПоказателейСотрудников");
	Вариант.ФункциональныеОпции.Добавить("ИспользоватьКлючевыеПоказателиЭффективности");
	Вариант.Включен = Ложь;
	
	ВариантыОтчетов.НастроитьОтчетВМодулеМенеджера(Настройки, Метаданные.Отчеты.НазначенныеПоказатели);
	Вариант = ВариантыОтчетов.ОписаниеВарианта(Настройки, Метаданные.Отчеты.НазначенныеПоказатели, "ПоказателиПозиций");
	Вариант.ФункциональныеОпции.Добавить("ИспользоватьКлючевыеПоказателиЭффективности");
	
	ВариантыОтчетов.НастроитьОтчетВМодулеМенеджера(Настройки, Метаданные.Отчеты.ВводЗначенийПоказателейЭффективности);
	Вариант = ВариантыОтчетов.ОписаниеВарианта(Настройки, Метаданные.Отчеты.ВводЗначенийПоказателейЭффективности, "ВводЗначенийПоказателей");
	Вариант.ФункциональныеОпции.Добавить("ИспользоватьКлючевыеПоказателиЭффективности");
	
КонецПроцедуры

#КонецОбласти

#Область НачальнаяНастройкаПрограммы

Процедура ЗначенияСохраняемыхРеквизитовФормыНачальнаяНастройкаПрограммы(Форма, СохраняемыеРеквизиты) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьЗарплатаКадрыКорпоративнаяПодсистемы") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Форма.РаботаВКОРП Тогда
		Возврат;
	КонецЕсли;
	
	СохраняемыеРеквизиты.Вставить("НастройкиКлючевыхПоказателейЭффективности", ОбщегоНазначения.СтруктураПоМенеджеруЗаписи(
			Форма.НастройкиКлючевыхПоказателейЭффективности, Метаданные.РегистрыСведений.НастройкиКлючевыхПоказателейЭффективности));
	
КонецПроцедуры

Процедура СохраненныеНастройкиВРеквизитыФормыНачальнаяНастройкаПрограммы(Форма, СохраненныеНастройки) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьЗарплатаКадрыКорпоративнаяПодсистемы") Тогда
		Возврат;
	КонецЕсли;
	
	Если СохраненныеНастройки.Свойство("НастройкиКлючевыхПоказателейЭффективности") Тогда
		ЗаполнитьЗначенияСвойств(Форма["НастройкиКлючевыхПоказателейЭффективности"], СохраненныеНастройки["НастройкиКлючевыхПоказателейЭффективности"]);
	КонецЕсли;
	
КонецПроцедуры

Процедура НастройкиПрограммыВРеквизитыФормы(Форма) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьЗарплатаКадрыКорпоративнаяПодсистемы") Тогда
		Возврат;
	КонецЕсли;
	
	Настройки = РегистрыСведений.НастройкиКлючевыхПоказателейЭффективности.СоздатьМенеджерЗаписи();
	Настройки.Прочитать();
	НастройкиСтруктура = ОбщегоНазначения.СтруктураПоМенеджеруЗаписи(Настройки, Метаданные.РегистрыСведений.НастройкиКлючевыхПоказателейЭффективности);
	ЗаполнитьЗначенияСвойств(Форма.НастройкиКлючевыхПоказателейЭффективности, НастройкиСтруктура);
	
КонецПроцедуры

Процедура ПолучитьНастройкиПрограммы(НастройкиПрограммы) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьЗарплатаКадрыКорпоративнаяПодсистемы") Тогда
		Возврат;
	КонецЕсли;
	
	Настройки = РегистрыСведений.НастройкиКлючевыхПоказателейЭффективности.СоздатьМенеджерЗаписи();
	Настройки.Прочитать();
	НастройкиСтруктура = ОбщегоНазначения.СтруктураПоМенеджеруЗаписи(Настройки, Метаданные.РегистрыСведений.НастройкиКлючевыхПоказателейЭффективности);
	НастройкиПрограммы.Вставить("НастройкиКлючевыхПоказателейЭффективности", НастройкиСтруктура);
	
КонецПроцедуры

Процедура ЗаписатьНастройкиНачальнаяНастройкаПрограммы(Параметры) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьЗарплатаКадрыКорпоративнаяПодсистемы") Тогда
		Возврат;
	КонецЕсли;
	
	Настройки = РегистрыСведений.НастройкиКлючевыхПоказателейЭффективности.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Настройки, Параметры.НастройкиКлючевыхПоказателейЭффективности);
	Настройки.Записать();
	
КонецПроцедуры

Процедура СоздатьПоказательЭффективностиСотрудника() Экспорт 
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКлючевыеПоказателиЭффективности") Тогда 
		ОписаниеПоказателя = Справочники.ПоказателиРасчетаЗарплаты.ОписаниеПоказателя();
		ОписаниеПоказателя.Идентификатор = "ПоказательЭффективностиСотрудника";
		ОписаниеПоказателя.Наименование = НСтр("ru = 'Показатель эффективности сотрудника'");
		ОписаниеПоказателя.КраткоеНаименование = НСтр("ru = 'Оценка эффективн.'");
		ОписаниеПоказателя.Точность = 2;
		Справочники.ПоказателиРасчетаЗарплаты.ЗаписатьПоказатель(ОписаниеПоказателя);
	Иначе 
		Справочники.ПоказателиРасчетаЗарплаты.ОтключитьИспользованиеПредопределенногоЭлемента("ПоказательЭффективностиСотрудника");
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьНачислениеДоплатаПоРезультатамОценкиЭффективности(ПараметрыПланаВидовРасчета, СвойстваНачислений, КоллекторНачислений) Экспорт 
	
	ПоказательЭффективностиСотрудника = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.ПоказательЭффективностиСотрудника");
	
	Если ПоказательЭффективностиСотрудника <> Неопределено Тогда 
		
		Запрос = Новый Запрос;
		
		Запрос.УстановитьПараметр("Показатель", ПоказательЭффективностиСотрудника);
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	НачисленияПоказатели.Ссылка
		               |ИЗ
		               |	ПланВидовРасчета.Начисления.Показатели КАК НачисленияПоказатели
		               |ГДЕ
		               |	НачисленияПоказатели.Показатель = &Показатель";
					   
		РезультатЗапроса = Запрос.Выполнить();
		
		Если Не РезультатЗапроса.Пустой() Тогда 
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	СвойстваПоКатегории = СвойстваНачислений[Перечисления.КатегорииНачисленийИНеоплаченногоВремени.Премия];
	СвойстваПоКатегории.СпособВыполненияНачисления = Перечисления.СпособыВыполненияНачислений.ЕжемесячноПриОкончательномРасчете;
	СвойстваПоКатегории.ВключатьВФОТ = Истина;
	
	Описание = ПланыВидовРасчета.Начисления.ОписаниеНачисления();
	Описание.КатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.Премия;
	Описание.СвойстваПоКатегории 	= СвойстваПоКатегории;
	Описание.Код					= НСтр("ru = 'ДПРОЭ'");
	Описание.Наименование			= НСтр("ru = 'Доплата по результатам оценки эффективности'");
	Описание.КраткоеНаименование 	= НСтр("ru = 'Допл. за эффективн.'");
	Описание.ФормулаРасчета 		= "РасчетнаяБаза * ПоказательЭффективностиСотрудника / 100";
	Описание.ВидВремени 			= Перечисления.ВидыРабочегоВремениСотрудников.ДополнительноОплачиваемоеВПределахНормы;
	Описание.НачисляетсяПриРасчетеПервойПоловиныМесяца = Ложь;
	Описание.ОтборБазовых 			= ПланыВидовРасчета.Начисления.ОтборБазовыхПоУмолчанию();
	Описание.СсылкаНаОбъект 		= ПараметрыПланаВидовРасчета.СсылкиНачисленийУдержаний.ДоплатаПоРезультатамОценкиЭффективности;
	Описание.КлючевыеСвойства 		= "Ссылка";
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКлючевыеПоказателиЭффективности") Тогда
		ПланыВидовРасчета.Начисления.СоздатьИзменитьНачисленияПоОписанию(КоллекторНачислений, Описание);
		ПараметрыПланаВидовРасчета.СсылкиНачисленийУдержаний.ДоплатаПоРезультатамОценкиЭффективности = Описание.СсылкаНаОбъект;
	ИначеЕсли ПараметрыПланаВидовРасчета.НачальнаяНастройкаПрограммы Тогда
		Описание.Вставить("Ссылка", Описание.СсылкаНаОбъект);
		ПланыВидовРасчета.Начисления.ОтключитьИспользованиеНачисленийПоОписанию(КоллекторНачислений, Описание);
	КонецЕсли;	
	
КонецПроцедуры	

#КонецОбласти

#Область НазначенныеПоказатели

Функция СоздатьЗапросВТНазначенныеПоказателиПозиций(ИмяФильтра)
	
	// Назначенные показатели.
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ИмяФильтра, "Позиция");
	Запрос = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"ПоказателиЭффективностиПозиций",
		Ложь,
		ОписаниеФильтра,
		ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез(),,
		"ВТПоказателиЭффективностиПозицийСрезПоследних");
		
	// Ответственные.
	ОсновнойТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.Период КАК Период,
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.Показатель КАК Показатель,
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.МестоВводаЗначений КАК Подразделение
		|ПОМЕСТИТЬ ВТФильтрДляОтветственныхПодразделения
		|ИЗ
		|	ВТПоказателиЭффективностиПозицийСрезПоследних КАК ВТПоказателиЭффективностиПозицийСрезПоследних
		|ГДЕ
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.МестоВводаЗначений ССЫЛКА Справочник.СтруктураПредприятия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.Период КАК Период,
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.Показатель КАК Показатель,
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.МестоВводаЗначений КАК Позиция
		|ПОМЕСТИТЬ ВТФильтрДляОтветственныхПозиции
		|ИЗ
		|	ВТПоказателиЭффективностиПозицийСрезПоследних КАК ВТПоказателиЭффективностиПозицийСрезПоследних
		|ГДЕ
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.МестоВводаЗначений ССЫЛКА Справочник.ШтатноеРасписание";
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьТекстыЗапросов(Запрос.Текст, ОсновнойТекстЗапроса);	
		
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра("ВТФильтрДляОтветственныхПодразделения", "Показатель,Подразделение");
	ЗапросОтветственныхПодразделений = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"ПодразделенияВводаЗначенийПоказателейЭффективности",
		Ложь,
		ОписаниеФильтра,
		ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез(),,
		"ВТПодразделенияВводаЗначенийПоказателей");
		
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьЗапросы(Запрос, ЗапросОтветственныхПодразделений);
	
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра("ВТФильтрДляОтветственныхПозиции", "Показатель,Позиция");
	ЗапросОтветственныхПозиций = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"ПозицииВводаЗначенийПоказателейЭффективности",
		Ложь,
		ОписаниеФильтра,
		ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез(),,
		"ВТПозицииВводаЗначенийПоказателей");
		
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьЗапросы(Запрос, ЗапросОтветственныхПозиций);
		
	// Финальное.
	ФинальныйТекстЗапроса =
		"УНИЧТОЖИТЬ ВТФильтрДляОтветственныхПодразделения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТФильтрДляОтветственныхПозиции
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.Период КАК Период,
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.Позиция КАК Позиция,
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.Регистратор КАК Регистратор,
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.Позиция КАК ВладелецПоказателя,
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.Показатель КАК Показатель,
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.Вес КАК Вес,
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.МестоВводаЗначений КАК МестоВводаЗначений,
		|	ВТПодразделенияВводаЗначенийПоказателей.ОтветственныйЗаВводПлана КАК ОтветственныйЗаВводПлана,
		|	ВТПодразделенияВводаЗначенийПоказателей.ОтветственныйЗаВводФакта КАК ОтветственныйЗаВводФакта,
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.Показатель.ТребуетсяВводПлана КАК ТребуетсяВводПлана
		|ПОМЕСТИТЬ ВТНазначенныеПоказателиПозиций
		|ИЗ
		|	ВТПоказателиЭффективностиПозицийСрезПоследних КАК ВТПоказателиЭффективностиПозицийСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПодразделенияВводаЗначенийПоказателей КАК ВТПодразделенияВводаЗначенийПоказателей
		|		ПО ВТПоказателиЭффективностиПозицийСрезПоследних.Период = ВТПодразделенияВводаЗначенийПоказателей.Период
		|			И ВТПоказателиЭффективностиПозицийСрезПоследних.Показатель = ВТПодразделенияВводаЗначенийПоказателей.Показатель
		|			И ВТПоказателиЭффективностиПозицийСрезПоследних.МестоВводаЗначений = ВТПодразделенияВводаЗначенийПоказателей.Подразделение
		|ГДЕ
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.МестоВводаЗначений ССЫЛКА Справочник.СтруктураПредприятия
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.Период,
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.Позиция,
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.Регистратор КАК Регистратор,
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.Позиция,
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.Показатель,
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.Вес,
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.МестоВводаЗначений,
		|	ВТПозицииВводаЗначенийПоказателей.ОтветственныйЗаВводПлана,
		|	ВТПозицииВводаЗначенийПоказателей.ОтветственныйЗаВводФакта,
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.Показатель.ТребуетсяВводПлана
		|ИЗ
		|	ВТПоказателиЭффективностиПозицийСрезПоследних КАК ВТПоказателиЭффективностиПозицийСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПозицииВводаЗначенийПоказателей КАК ВТПозицииВводаЗначенийПоказателей
		|		ПО ВТПоказателиЭффективностиПозицийСрезПоследних.Период = ВТПозицииВводаЗначенийПоказателей.Период
		|			И ВТПоказателиЭффективностиПозицийСрезПоследних.Показатель = ВТПозицииВводаЗначенийПоказателей.Показатель
		|			И ВТПоказателиЭффективностиПозицийСрезПоследних.МестоВводаЗначений = ВТПозицииВводаЗначенийПоказателей.Позиция
		|ГДЕ
		|	ВТПоказателиЭффективностиПозицийСрезПоследних.МестоВводаЗначений ССЫЛКА Справочник.ШтатноеРасписание
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПоказателиЭффективностиПозицийСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПодразделенияВводаЗначенийПоказателей
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПозицииВводаЗначенийПоказателей";
	
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьТекстыЗапросов(Запрос.Текст, ФинальныйТекстЗапроса);
		
	Возврат Запрос;
	
КонецФункции

#КонецОбласти

#Область ОбъектыПоПоказателю

Функция ЗапросВТПодразделенияВводаЗначенийПоказателей(ИмяФильтра, СтрокаИзмерений = "", КоллекцияОтборов = Неопределено)

	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ИмяФильтра, СтрокаИзмерений);
	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	
	Если КоллекцияОтборов <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПараметрыПостроения.Отборы, КоллекцияОтборов);
	КонецЕсли;
	
	ЗапросВТИмяРегистраСрез = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"ПодразделенияВводаЗначенийПоказателейЭффективности",
		Ложь,
		ОписаниеФильтра,
		ПараметрыПостроения,,
		"ВТПодразделенияВводаЗначенийПоказателей");
		
	Возврат ЗапросВТИмяРегистраСрез;
	
КонецФункции

Функция ЗапросВТПозицииВводаЗначенийПоказателей(ИмяФильтра, СтрокаИзмерений = "", КоллекцияОтборов = Неопределено)

	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ИмяФильтра, СтрокаИзмерений);
	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	
	Если КоллекцияОтборов <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПараметрыПостроения.Отборы, КоллекцияОтборов);
	КонецЕсли;
	
	ЗапросВТИмяРегистраСрез = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"ПозицииВводаЗначенийПоказателейЭффективности",
		Ложь,
		ОписаниеФильтра,
		ПараметрыПостроения,,
		"ВТПозицииВводаЗначенийПоказателей");
		
	Возврат ЗапросВТИмяРегистраСрез;
	
КонецФункции

#КонецОбласти

#Область ПлановыеПоказатели

Функция ЗапросВТВведенныеПлановыеЗначенияПоказателейПодразделений(ИмяФильтра, ИсключаемыйРегистратор = Неопределено)

	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПлановыеЗначенияПоказателейЭффективностиПодразделений.Подразделение КАК Подразделение,
		|	ПлановыеЗначенияПоказателейЭффективностиПодразделений.Показатель КАК Показатель
		|ИЗ
		|	РегистрСведений.ПлановыеЗначенияПоказателейЭффективностиПодразделений КАК ПлановыеЗначенияПоказателейЭффективностиПодразделений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ВТТаблицаДвижений КАК ТаблицаДвижений
		|		ПО ПлановыеЗначенияПоказателейЭффективностиПодразделений.ПериодДействия = ТаблицаДвижений.ПериодДействия
		|			И ПлановыеЗначенияПоказателейЭффективностиПодразделений.Подразделение = ТаблицаДвижений.Подразделение
		|			И ПлановыеЗначенияПоказателейЭффективностиПодразделений.Показатель = ТаблицаДвижений.Показатель
		|ГДЕ
		|	ПлановыеЗначенияПоказателейЭффективностиПодразделений.Регистратор <> &ИсключаемыйРегистратор";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ВТТаблицаДвижений", ИмяФильтра);
	
	Запрос.УстановитьПараметр("ИсключаемыйРегистратор", ?(ИсключаемыйРегистратор = Неопределено, Null, ИсключаемыйРегистратор));
	
	Возврат Запрос;

КонецФункции

Функция ЗапросВТВведенныеПлановыеЗначенияПоказателейПозиций(ИмяФильтра, ИсключаемыйРегистратор = Неопределено)

	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПлановыеЗначенияПоказателейЭффективностиПозиций.Позиция КАК Позиция,
		|	ПлановыеЗначенияПоказателейЭффективностиПозиций.Показатель КАК Показатель
		|ИЗ
		|	РегистрСведений.ПлановыеЗначенияПоказателейЭффективностиПозиций КАК ПлановыеЗначенияПоказателейЭффективностиПозиций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ВТТаблицаДвижений КАК ТаблицаДвижений
		|		ПО ПлановыеЗначенияПоказателейЭффективностиПозиций.ПериодДействия = ТаблицаДвижений.ПериодДействия
		|			И ПлановыеЗначенияПоказателейЭффективностиПозиций.Позиция = ТаблицаДвижений.Позиция
		|			И ПлановыеЗначенияПоказателейЭффективностиПозиций.Показатель = ТаблицаДвижений.Показатель
		|ГДЕ
		|	ПлановыеЗначенияПоказателейЭффективностиПозиций.Регистратор <> &ИсключаемыйРегистратор";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ВТТаблицаДвижений", ИмяФильтра);
	
	Запрос.УстановитьПараметр("ИсключаемыйРегистратор", ?(ИсключаемыйРегистратор = Неопределено, Null, ИсключаемыйРегистратор));
	
	Возврат Запрос;

КонецФункции

Функция ЗапросВТВведенныеПлановыеЗначенияПоказателейСотрудников(ИмяФильтра, ИсключаемыйРегистратор = Неопределено) Экспорт

	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПлановыеЗначенияПоказателейЭффективностиСотрудников.Сотрудник КАК Сотрудник,
		|	ПлановыеЗначенияПоказателейЭффективностиСотрудников.Показатель КАК Показатель
		|ИЗ
		|	РегистрСведений.ПлановыеЗначенияПоказателейЭффективностиСотрудников КАК ПлановыеЗначенияПоказателейЭффективностиСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ВТТаблицаДвижений КАК ТаблицаДвижений
		|		ПО ПлановыеЗначенияПоказателейЭффективностиСотрудников.ПериодДействия = ТаблицаДвижений.ПериодДействия
		|			И ПлановыеЗначенияПоказателейЭффективностиСотрудников.Сотрудник = ТаблицаДвижений.Сотрудник
		|			И ПлановыеЗначенияПоказателейЭффективностиСотрудников.Показатель = ТаблицаДвижений.Показатель
		|ГДЕ
		|	ПлановыеЗначенияПоказателейЭффективностиСотрудников.Регистратор <> &ИсключаемыйРегистратор";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ВТТаблицаДвижений", ИмяФильтра);
	
	Запрос.УстановитьПараметр("ИсключаемыйРегистратор", ?(ИсключаемыйРегистратор = Неопределено, Null, ИсключаемыйРегистратор));
	
	Возврат Запрос;

КонецФункции

Процедура СоздатьВТПлановыеЗначенияПоказателейПодразделений(МенеджерВременныхТаблиц, ИмяФильтра)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВТПодразделения.Период КАК Период,
		|	ВТПодразделения.Подразделение КАК ИсходноеПодразделение,
		|	ВТПодразделения.Показатель КАК Показатель,
		|	ПодчиненностьСтруктурныхЕдиниц.ВышестоящаяСтруктурнаяЕдиница КАК Подразделение,
		|	ПодчиненностьСтруктурныхЕдиниц.Уровень КАК Уровень
		|ПОМЕСТИТЬ ВТПодразделениеИРодители
		|ИЗ
		|	РегистрСведений.ПодчиненностьСтруктурныхЕдиниц КАК ПодчиненностьСтруктурныхЕдиниц
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ВТПодразделения КАК ВТПодразделения
		|		ПО ПодчиненностьСтруктурныхЕдиниц.СтруктурнаяЕдиница = ВТПодразделения.Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПлановыеЗначенияПоказателейЭффективностиПодразделений.ПериодДействия КАК Период,
		|	ПлановыеЗначенияПоказателейЭффективностиПодразделений.Подразделение КАК Подразделение,
		|	ПлановыеЗначенияПоказателейЭффективностиПодразделений.Показатель КАК Показатель,
		|	ПлановыеЗначенияПоказателейЭффективностиПодразделений.Регистратор КАК РегистраторПлановогоЗначения,
		|	ПлановыеЗначенияПоказателейЭффективностиПодразделений.Значение КАК ПлановоеЗначение
		|ПОМЕСТИТЬ ВТВведенныеПлановыеЗначенияПоказателейПодразделений
		|ИЗ
		|	РегистрСведений.ПлановыеЗначенияПоказателейЭффективностиПодразделений КАК ПлановыеЗначенияПоказателейЭффективностиПодразделений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПодразделениеИРодители КАК ПодразделениеИРодители
		|		ПО ПлановыеЗначенияПоказателейЭффективностиПодразделений.Подразделение = ПодразделениеИРодители.Подразделение
		|			И ПлановыеЗначенияПоказателейЭффективностиПодразделений.Показатель = ПодразделениеИРодители.Показатель
		|			И (НАЧАЛОПЕРИОДА(ПлановыеЗначенияПоказателейЭффективностиПодразделений.ПериодДействия, ДЕНЬ) = НАЧАЛОПЕРИОДА(ПодразделениеИРодители.Период, ДЕНЬ))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТПодразделениеИРодители.Период КАК Период,
		|	ВТПодразделениеИРодители.ИсходноеПодразделение КАК ИсходноеПодразделение,
		|	ВТПодразделениеИРодители.Подразделение КАК Подразделение,
		|	ВТПодразделениеИРодители.Уровень КАК Уровень,
		|	ВТПодразделениеИРодители.Показатель КАК Показатель,
		|	ВведенныеПлановыеЗначенияПоказателейПодразделений.РегистраторПлановогоЗначения КАК РегистраторПлановогоЗначения,
		|	ВведенныеПлановыеЗначенияПоказателейПодразделений.ПлановоеЗначение КАК ПлановоеЗначение
		|ПОМЕСТИТЬ ВТПлановыеПоказателиПодразделенийИРодителей
		|ИЗ
		|	ВТВведенныеПлановыеЗначенияПоказателейПодразделений КАК ВведенныеПлановыеЗначенияПоказателейПодразделений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПодразделениеИРодители КАК ВТПодразделениеИРодители
		|		ПО ВведенныеПлановыеЗначенияПоказателейПодразделений.Период = ВТПодразделениеИРодители.Период
		|			И ВведенныеПлановыеЗначенияПоказателейПодразделений.Показатель = ВТПодразделениеИРодители.Показатель
		|			И ВведенныеПлановыеЗначенияПоказателейПодразделений.Подразделение = ВТПодразделениеИРодители.Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(ВТПлановыеПоказателиПодразделенийИРодителей.Уровень) КАК Уровень,
		|	ВТПлановыеПоказателиПодразделенийИРодителей.ИсходноеПодразделение КАК ИсходноеПодразделение,
		|	ВТПлановыеПоказателиПодразделенийИРодителей.Показатель КАК Показатель,
		|	ВТПлановыеПоказателиПодразделенийИРодителей.Период КАК Период
		|ПОМЕСТИТЬ ВТУказателиПодразделений
		|ИЗ
		|	ВТПлановыеПоказателиПодразделенийИРодителей КАК ВТПлановыеПоказателиПодразделенийИРодителей
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТПлановыеПоказателиПодразделенийИРодителей.ИсходноеПодразделение,
		|	ВТПлановыеПоказателиПодразделенийИРодителей.Показатель,
		|	ВТПлановыеПоказателиПодразделенийИРодителей.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПлановыеПоказателиПодразделенийИРодителей.Период КАК Период,
		|	ВТПлановыеПоказателиПодразделенийИРодителей.ИсходноеПодразделение КАК Подразделение,
		|	ВТПлановыеПоказателиПодразделенийИРодителей.Показатель КАК Показатель,
		|	ВТПлановыеПоказателиПодразделенийИРодителей.Подразделение КАК МестоВводаЗначения,
		|	ВТПлановыеПоказателиПодразделенийИРодителей.РегистраторПлановогоЗначения КАК РегистраторПлановогоЗначения,
		|	ВТПлановыеПоказателиПодразделенийИРодителей.ПлановоеЗначение КАК ПлановоеЗначение
		|ПОМЕСТИТЬ ВТПлановыеЗначенияПоказателейПодразделений
		|ИЗ
		|	ВТПлановыеПоказателиПодразделенийИРодителей КАК ВТПлановыеПоказателиПодразделенийИРодителей
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТУказателиПодразделений КАК ВТУказателиПодразделений
		|		ПО ВТПлановыеПоказателиПодразделенийИРодителей.ИсходноеПодразделение = ВТУказателиПодразделений.ИсходноеПодразделение
		|			И ВТПлановыеПоказателиПодразделенийИРодителей.Показатель = ВТУказателиПодразделений.Показатель
		|			И ВТПлановыеПоказателиПодразделенийИРодителей.Период = ВТУказателиПодразделений.Период
		|			И ВТПлановыеПоказателиПодразделенийИРодителей.Уровень = ВТУказателиПодразделений.Уровень
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПодразделениеИРодители
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТВведенныеПлановыеЗначенияПоказателейПодразделений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПлановыеПоказателиПодразделенийИРодителей
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТУказателиПодразделений";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ВТПодразделения", ИмяФильтра);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьВТПлановыеЗначенияПоказателейПозиций(МенеджерВременныхТаблиц, ИмяФильтра)

	УдалитьВТ = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПоказателиПозиций.Период КАК Период,
		|	ПоказателиПозиций.Позиция КАК Позиция,
		|	ПоказателиПозиций.Показатель КАК Показатель,
		|	ПлановыеЗначенияПоказателейЭффективностиПозиций.Регистратор КАК РегистраторПлановогоЗначения,
		|	ПлановыеЗначенияПоказателейЭффективностиПозиций.Значение КАК ПлановоеЗначение
		|ПОМЕСТИТЬ ВТВведенныеПлановыеЗначенияПозиций
		|ИЗ
		|	#ВТПоказателиПозиций КАК ПоказателиПозиций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеЗначенияПоказателейЭффективностиПозиций КАК ПлановыеЗначенияПоказателейЭффективностиПозиций
		|		ПО ПоказателиПозиций.Позиция = ПлановыеЗначенияПоказателейЭффективностиПозиций.Позиция
		|			И ПоказателиПозиций.Показатель = ПлановыеЗначенияПоказателейЭффективностиПозиций.Показатель
		|			И (НАЧАЛОПЕРИОДА(ПоказателиПозиций.Период, ДЕНЬ) = НАЧАЛОПЕРИОДА(ПлановыеЗначенияПоказателейЭффективностиПозиций.ПериодДействия, ДЕНЬ))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоказателиПозиций.Период КАК Период,
		|	ПоказателиПозиций.Позиция КАК Позиция,
		|	ПоказателиПозиций.Показатель КАК Показатель,
		|	ПоказателиПозиций.Подразделение КАК Подразделение
		|ПОМЕСТИТЬ ВТПозицииНаследующиеПлановыеЗначенияИзПодразделений
		|ИЗ
		|	#ВТПоказателиПозиций КАК ПоказателиПозиций
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВведенныеПлановыеЗначенияПозиций КАК ВведенныеПлановыеЗначенияПозиций
		|		ПО ПоказателиПозиций.Позиция = ВведенныеПлановыеЗначенияПозиций.Позиция
		|			И ПоказателиПозиций.Показатель = ВведенныеПлановыеЗначенияПозиций.Показатель
		|			И ПоказателиПозиций.Период = ВведенныеПлановыеЗначенияПозиций.Период
		|ГДЕ
		|	ВведенныеПлановыеЗначенияПозиций.Позиция ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПозицииНаследующиеПлановыеЗначенияИзПодразделений.Период КАК Период,
		|	ПозицииНаследующиеПлановыеЗначенияИзПодразделений.Подразделение КАК Подразделение,
		|	ПозицииНаследующиеПлановыеЗначенияИзПодразделений.Позиция КАК Позиция,
		|	ПозицииНаследующиеПлановыеЗначенияИзПодразделений.Показатель КАК Показатель
		|ПОМЕСТИТЬ ВТПоказателиПодразделений
		|ИЗ
		|	ВТПозицииНаследующиеПлановыеЗначенияИзПодразделений КАК ПозицииНаследующиеПлановыеЗначенияИзПодразделений";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ВТПоказателиПозиций", ИмяФильтра);
	УдалитьВТ.Добавить("ВТВведенныеПлановыеЗначенияПозиций");
	УдалитьВТ.Добавить("ВТПозицииНаследующиеПлановыеЗначенияИзПодразделений");
	УдалитьВТ.Добавить("ВТПоказателиПодразделений");
	
	Запрос.Выполнить();
	
	СоздатьВТПлановыеЗначенияПоказателейПодразделений(МенеджерВременныхТаблиц, "ВТПоказателиПодразделений");
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПозицииНаследующиеПлановыеЗначенияИзПодразделений.Период КАК Период,
		|	ПозицииНаследующиеПлановыеЗначенияИзПодразделений.Позиция КАК Позиция,
		|	ПозицииНаследующиеПлановыеЗначенияИзПодразделений.Показатель КАК Показатель,
		|	ПлановыеЗначенияПоказателейПодразделений.РегистраторПлановогоЗначения КАК РегистраторПлановогоЗначения,
		|	ПлановыеЗначенияПоказателейПодразделений.ПлановоеЗначение КАК ПлановоеЗначение
		|ПОМЕСТИТЬ ВТПлановыеЗначенияПоказателейПозиций
		|ИЗ
		|	ВТПозицииНаследующиеПлановыеЗначенияИзПодразделений КАК ПозицииНаследующиеПлановыеЗначенияИзПодразделений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПлановыеЗначенияПоказателейПодразделений КАК ПлановыеЗначенияПоказателейПодразделений
		|		ПО ПозицииНаследующиеПлановыеЗначенияИзПодразделений.Период = ПлановыеЗначенияПоказателейПодразделений.Период
		|			И ПозицииНаследующиеПлановыеЗначенияИзПодразделений.Показатель = ПлановыеЗначенияПоказателейПодразделений.Показатель
		|			И ПозицииНаследующиеПлановыеЗначенияИзПодразделений.Подразделение = ПлановыеЗначенияПоказателейПодразделений.Подразделение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВведенныеПлановыеЗначенияПозиций.Период,
		|	ВведенныеПлановыеЗначенияПозиций.Позиция,
		|	ВведенныеПлановыеЗначенияПозиций.Показатель,
		|	ВведенныеПлановыеЗначенияПозиций.РегистраторПлановогоЗначения,
		|	ВведенныеПлановыеЗначенияПозиций.ПлановоеЗначение
		|ИЗ
		|	ВТВведенныеПлановыеЗначенияПозиций КАК ВведенныеПлановыеЗначенияПозиций";
	
	Запрос.Выполнить();
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
КонецПроцедуры

Процедура СоздатьВТПлановыеЗначенияПоказателейСотрудников(МенеджерВременныхТаблиц, ИмяФильтра)

	УдалитьВТ = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПоказателиСотрудников.Период КАК Период,
		|	ПоказателиСотрудников.Сотрудник КАК Сотрудник,
		|	ПоказателиСотрудников.Показатель КАК Показатель,
		|	ПлановыеЗначенияПоказателейЭффективностиСотрудников.Регистратор КАК РегистраторПлановогоЗначения,
		|	ПлановыеЗначенияПоказателейЭффективностиСотрудников.Значение КАК ПлановоеЗначение
		|ПОМЕСТИТЬ ВТВведенныеПлановыеЗначенияСотрудников
		|ИЗ
		|	#ВТПоказателиСотрудников КАК ПоказателиСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеЗначенияПоказателейЭффективностиСотрудников КАК ПлановыеЗначенияПоказателейЭффективностиСотрудников
		|		ПО ПоказателиСотрудников.Сотрудник = ПлановыеЗначенияПоказателейЭффективностиСотрудников.Сотрудник
		|			И ПоказателиСотрудников.Показатель = ПлановыеЗначенияПоказателейЭффективностиСотрудников.Показатель
		|			И (НАЧАЛОПЕРИОДА(ПоказателиСотрудников.Период, ДЕНЬ) = НАЧАЛОПЕРИОДА(ПлановыеЗначенияПоказателейЭффективностиСотрудников.ПериодДействия, ДЕНЬ))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоказателиСотрудников.Период КАК Период,
		|	ПоказателиСотрудников.Сотрудник КАК Сотрудник,
		|	ПоказателиСотрудников.Показатель КАК Показатель,
		|	ПоказателиСотрудников.Подразделение КАК Подразделение,
		|	ПоказателиСотрудников.Позиция КАК Позиция
		|ПОМЕСТИТЬ ВТСотрудникиНаследующиеПлановыеЗначенияИзПозиций
		|ИЗ
		|	#ВТПоказателиСотрудников КАК ПоказателиСотрудников
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВведенныеПлановыеЗначенияСотрудников КАК ВведенныеПлановыеЗначенияСотрудников
		|		ПО ПоказателиСотрудников.Сотрудник = ВведенныеПлановыеЗначенияСотрудников.Сотрудник
		|			И ПоказателиСотрудников.Показатель = ВведенныеПлановыеЗначенияСотрудников.Показатель
		|			И ПоказателиСотрудников.Период = ВведенныеПлановыеЗначенияСотрудников.Период
		|ГДЕ
		|	ВведенныеПлановыеЗначенияСотрудников.Сотрудник ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СотрудникиНаследующиеПлановыеЗначенияИзПозиций.Период КАК Период,
		|	СотрудникиНаследующиеПлановыеЗначенияИзПозиций.Подразделение КАК Подразделение,
		|	СотрудникиНаследующиеПлановыеЗначенияИзПозиций.Позиция КАК Позиция,
		|	СотрудникиНаследующиеПлановыеЗначенияИзПозиций.Показатель КАК Показатель
		|ПОМЕСТИТЬ ВТПоказателиПозиций
		|ИЗ
		|	ВТСотрудникиНаследующиеПлановыеЗначенияИзПозиций КАК СотрудникиНаследующиеПлановыеЗначенияИзПозиций";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ВТПоказателиСотрудников", ИмяФильтра);
	УдалитьВТ.Добавить("ВТВведенныеПлановыеЗначенияСотрудников");
	УдалитьВТ.Добавить("ВТСотрудникиНаследующиеПлановыеЗначенияИзПозиций");
	УдалитьВТ.Добавить("ВТПоказателиПозиций");
	
	Запрос.Выполнить();
	
	СоздатьВТПлановыеЗначенияПоказателейПозиций(МенеджерВременныхТаблиц, "ВТПоказателиПозиций");
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СотрудникиНаследующиеПлановыеЗначенияИзПозиций.Период КАК Период,
		|	СотрудникиНаследующиеПлановыеЗначенияИзПозиций.Сотрудник КАК Сотрудник,
		|	СотрудникиНаследующиеПлановыеЗначенияИзПозиций.Показатель КАК Показатель,
		|	ПлановыеЗначенияПоказателейПозиций.РегистраторПлановогоЗначения КАК РегистраторПлановогоЗначения,
		|	ПлановыеЗначенияПоказателейПозиций.ПлановоеЗначение КАК ПлановоеЗначение
		|ПОМЕСТИТЬ ВТПлановыеЗначенияПоказателейСотрудников
		|ИЗ
		|	ВТСотрудникиНаследующиеПлановыеЗначенияИзПозиций КАК СотрудникиНаследующиеПлановыеЗначенияИзПозиций
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПлановыеЗначенияПоказателейПозиций КАК ПлановыеЗначенияПоказателейПозиций
		|		ПО СотрудникиНаследующиеПлановыеЗначенияИзПозиций.Период = ПлановыеЗначенияПоказателейПозиций.Период
		|			И СотрудникиНаследующиеПлановыеЗначенияИзПозиций.Показатель = ПлановыеЗначенияПоказателейПозиций.Показатель
		|			И СотрудникиНаследующиеПлановыеЗначенияИзПозиций.Позиция = ПлановыеЗначенияПоказателейПозиций.Позиция
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВведенныеПлановыеЗначенияСотрудников.Период,
		|	ВведенныеПлановыеЗначенияСотрудников.Сотрудник,
		|	ВведенныеПлановыеЗначенияСотрудников.Показатель,
		|	ВведенныеПлановыеЗначенияСотрудников.РегистраторПлановогоЗначения,
		|	ВведенныеПлановыеЗначенияСотрудников.ПлановоеЗначение
		|ИЗ
		|	ВТВведенныеПлановыеЗначенияСотрудников КАК ВведенныеПлановыеЗначенияСотрудников";
	
	Запрос.Выполнить();
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
КонецПроцедуры

#КонецОбласти

#Область ФактическиеПоказатели

Функция ЗапросВТВведенныеФактическиеЗначенияПоказателейПодразделений(ИмяФильтра, ИсключаемыйРегистратор = Неопределено)

	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФактическиеЗначенияПоказателейЭффективностиПодразделений.Подразделение КАК Подразделение,
		|	ФактическиеЗначенияПоказателейЭффективностиПодразделений.Показатель КАК Показатель
		|ИЗ
		|	РегистрНакопления.ФактическиеЗначенияПоказателейЭффективностиПодразделений КАК ФактическиеЗначенияПоказателейЭффективностиПодразделений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ВТТаблицаДвижений КАК ТаблицаДвижений
		|		ПО ФактическиеЗначенияПоказателейЭффективностиПодразделений.Подразделение = ТаблицаДвижений.Подразделение
		|			И ФактическиеЗначенияПоказателейЭффективностиПодразделений.Показатель = ТаблицаДвижений.Показатель
		|			И (НАЧАЛОПЕРИОДА(ФактическиеЗначенияПоказателейЭффективностиПодразделений.Период, ДЕНЬ) >= ТаблицаДвижений.ДатаНачала)
		|			И (НАЧАЛОПЕРИОДА(ФактическиеЗначенияПоказателейЭффективностиПодразделений.Период, ДЕНЬ) <= ТаблицаДвижений.ДатаОкончания)
		|ГДЕ
		|	ФактическиеЗначенияПоказателейЭффективностиПодразделений.Регистратор <> &ИсключаемыйРегистратор";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ВТТаблицаДвижений", ИмяФильтра);
	
	Запрос.УстановитьПараметр("ИсключаемыйРегистратор", ?(ИсключаемыйРегистратор = Неопределено, Null, ИсключаемыйРегистратор));
	
	Возврат Запрос;

КонецФункции

Функция ЗапросВТВведенныеФактическиеЗначенияПоказателеСотрудников(ИмяФильтра, ИсключаемыйРегистратор = Неопределено)

	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФактическиеЗначенияПоказателейЭффективностиСотрудников.Сотрудник КАК Сотрудник,
		|	ФактическиеЗначенияПоказателейЭффективностиСотрудников.Показатель КАК Показатель
		|ИЗ
		|	РегистрНакопления.ФактическиеЗначенияПоказателейЭффективностиСотрудников КАК ФактическиеЗначенияПоказателейЭффективностиСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ВТТаблицаДвижений КАК ТаблицаДвижений
		|		ПО ФактическиеЗначенияПоказателейЭффективностиСотрудников.Сотрудник = ТаблицаДвижений.Сотрудник
		|			И ФактическиеЗначенияПоказателейЭффективностиСотрудников.Показатель = ТаблицаДвижений.Показатель
		|			И (НАЧАЛОПЕРИОДА(ФактическиеЗначенияПоказателейЭффективностиСотрудников.Период, ДЕНЬ) >= ТаблицаДвижений.ДатаНачала)
		|			И (НАЧАЛОПЕРИОДА(ФактическиеЗначенияПоказателейЭффективностиСотрудников.Период, ДЕНЬ) <= ТаблицаДвижений.ДатаОкончания)
		|ГДЕ
		|	ФактическиеЗначенияПоказателейЭффективностиСотрудников.Регистратор <> &ИсключаемыйРегистратор";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ВТТаблицаДвижений", ИмяФильтра);
	
	Запрос.УстановитьПараметр("ИсключаемыйРегистратор", ?(ИсключаемыйРегистратор = Неопределено, Null, ИсключаемыйРегистратор));
	
	Возврат Запрос;

КонецФункции




Процедура СоздатьВТФактическиеЗначенияПоказателейПодразделений(МенеджерВременныхТаблиц, ИмяФильтра)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВТПодразделения.Период КАК Период,
		|	ВТПодразделения.Подразделение КАК ИсходноеПодразделение,
		|	ВТПодразделения.Показатель КАК Показатель,
		|	ПодчиненностьСтруктурныхЕдиниц.ВышестоящаяСтруктурнаяЕдиница КАК Подразделение,
		|	ПодчиненностьСтруктурныхЕдиниц.Уровень КАК Уровень
		|ПОМЕСТИТЬ ВТПодразделениеИРодители
		|ИЗ
		|	РегистрСведений.ПодчиненностьСтруктурныхЕдиниц КАК ПодчиненностьСтруктурныхЕдиниц
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ВТПодразделения КАК ВТПодразделения
		|		ПО ПодчиненностьСтруктурныхЕдиниц.СтруктурнаяЕдиница = ВТПодразделения.Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФактическиеЗначенияПоказателейЭффективностиПодразделений.Период КАК Период,
		|	ФактическиеЗначенияПоказателейЭффективностиПодразделений.Подразделение КАК Подразделение,
		|	ФактическиеЗначенияПоказателейЭффективностиПодразделений.Показатель КАК Показатель,
		|	ФактическиеЗначенияПоказателейЭффективностиПодразделений.Регистратор КАК РегистраторФактическогоЗначения,
		|	ФактическиеЗначенияПоказателейЭффективностиПодразделений.Значение КАК ФактическоеЗначение
		|ПОМЕСТИТЬ ВТВведенныеФактическиеЗначенияПодразделенийСРегистраторами
		|ИЗ
		|	РегистрНакопления.ФактическиеЗначенияПоказателейЭффективностиПодразделений КАК ФактическиеЗначенияПоказателейЭффективностиПодразделений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПодразделениеИРодители КАК ПодразделениеИРодители
		|		ПО ФактическиеЗначенияПоказателейЭффективностиПодразделений.Подразделение = ПодразделениеИРодители.Подразделение
		|			И ФактическиеЗначенияПоказателейЭффективностиПодразделений.Показатель = ПодразделениеИРодители.Показатель
		|			И (НАЧАЛОПЕРИОДА(ФактическиеЗначенияПоказателейЭффективностиПодразделений.Период, ДЕНЬ) >= НАЧАЛОПЕРИОДА(ПодразделениеИРодители.Период, МЕСЯЦ))
		|			И (НАЧАЛОПЕРИОДА(ФактическиеЗначенияПоказателейЭффективностиПодразделений.Период, ДЕНЬ) <= НАЧАЛОПЕРИОДА(ПодразделениеИРодители.Период, МЕСЯЦ))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФактическиеЗначенияПоказателейЭффективностиПодразделений.Период КАК Период,
		|	ФактическиеЗначенияПоказателейЭффективностиПодразделений.Подразделение КАК Подразделение,
		|	ФактическиеЗначенияПоказателейЭффективностиПодразделений.Показатель КАК Показатель,
		|	МАКСИМУМ(ФактическиеЗначенияПоказателейЭффективностиПодразделений.РегистраторФактическогоЗначения) КАК РегистраторФактическогоЗначения,
		|	СУММА(ФактическиеЗначенияПоказателейЭффективностиПодразделений.ФактическоеЗначение) КАК ФактическоеЗначение
		|ПОМЕСТИТЬ ВТВведенныеФактическиеЗначенияПодразделений
		|ИЗ
		|	ВТВведенныеФактическиеЗначенияПодразделенийСРегистраторами КАК ФактическиеЗначенияПоказателейЭффективностиПодразделений
		|
		|СГРУППИРОВАТЬ ПО
		|	ФактическиеЗначенияПоказателейЭффективностиПодразделений.Период,
		|	ФактическиеЗначенияПоказателейЭффективностиПодразделений.Показатель,
		|	ФактическиеЗначенияПоказателейЭффективностиПодразделений.Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТПодразделениеИРодители.Период КАК Период,
		|	ВТПодразделениеИРодители.ИсходноеПодразделение КАК ИсходноеПодразделение,
		|	ВТПодразделениеИРодители.Подразделение КАК Подразделение,
		|	ВТПодразделениеИРодители.Уровень КАК Уровень,
		|	ВТПодразделениеИРодители.Показатель КАК Показатель,
		|	ВведенныеФактическиеЗначенияПодразделений.РегистраторФактическогоЗначения КАК РегистраторФактическогоЗначения,
		|	ВведенныеФактическиеЗначенияПодразделений.ФактическоеЗначение КАК ФактическоеЗначение
		|ПОМЕСТИТЬ ВТФактическиеПоказателиПодразделенийИРодителей
		|ИЗ
		|	ВТВведенныеФактическиеЗначенияПодразделений КАК ВведенныеФактическиеЗначенияПодразделений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПодразделениеИРодители КАК ВТПодразделениеИРодители
		|		ПО ВведенныеФактическиеЗначенияПодразделений.Период = ВТПодразделениеИРодители.Период
		|			И ВведенныеФактическиеЗначенияПодразделений.Показатель = ВТПодразделениеИРодители.Показатель
		|			И ВведенныеФактическиеЗначенияПодразделений.Подразделение = ВТПодразделениеИРодители.Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(ВТФактическиеПоказателиПодразделенийИРодителей.Уровень) КАК Уровень,
		|	ВТФактическиеПоказателиПодразделенийИРодителей.ИсходноеПодразделение КАК ИсходноеПодразделение,
		|	ВТФактическиеПоказателиПодразделенийИРодителей.Показатель КАК Показатель,
		|	ВТФактическиеПоказателиПодразделенийИРодителей.Период КАК Период
		|ПОМЕСТИТЬ ВТУказателиПодразделений
		|ИЗ
		|	ВТФактическиеПоказателиПодразделенийИРодителей КАК ВТФактическиеПоказателиПодразделенийИРодителей
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТФактическиеПоказателиПодразделенийИРодителей.ИсходноеПодразделение,
		|	ВТФактическиеПоказателиПодразделенийИРодителей.Показатель,
		|	ВТФактическиеПоказателиПодразделенийИРодителей.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФактическиеПоказателиПодразделенийИРодителей.Период КАК Период,
		|	ФактическиеПоказателиПодразделенийИРодителей.ИсходноеПодразделение КАК Подразделение,
		|	ФактическиеПоказателиПодразделенийИРодителей.Показатель КАК Показатель,
		|	ФактическиеПоказателиПодразделенийИРодителей.Подразделение КАК МестоВводаЗначения,
		|	ФактическиеПоказателиПодразделенийИРодителей.РегистраторФактическогоЗначения КАК РегистраторФактическогоЗначения,
		|	ФактическиеПоказателиПодразделенийИРодителей.ФактическоеЗначение КАК ФактическоеЗначение
		|ПОМЕСТИТЬ ВТФактическиеЗначенияПоказателейПодразделений
		|ИЗ
		|	ВТФактическиеПоказателиПодразделенийИРодителей КАК ФактическиеПоказателиПодразделенийИРодителей
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТУказателиПодразделений КАК УказателиПодразделений
		|		ПО ФактическиеПоказателиПодразделенийИРодителей.ИсходноеПодразделение = УказателиПодразделений.ИсходноеПодразделение
		|			И ФактическиеПоказателиПодразделенийИРодителей.Показатель = УказателиПодразделений.Показатель
		|			И ФактическиеПоказателиПодразделенийИРодителей.Период = УказателиПодразделений.Период
		|			И ФактическиеПоказателиПодразделенийИРодителей.Уровень = УказателиПодразделений.Уровень
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПодразделениеИРодители
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТВведенныеФактическиеЗначенияПодразделенийСРегистраторами
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТВведенныеФактическиеЗначенияПодразделений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТФактическиеПоказателиПодразделенийИРодителей
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТУказателиПодразделений";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ВТПодразделения", ИмяФильтра);
	
	Запрос.Выполнить();


КонецПроцедуры

Процедура СоздатьВТФактическиеЗначенияПоказателейСотрудников(МенеджерВременныхТаблиц, ИмяФильтра)

	УдалитьВТ = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПоказателиСотрудников.Период КАК Период,
		|	ПоказателиСотрудников.Сотрудник КАК Сотрудник,
		|	ПоказателиСотрудников.Показатель КАК Показатель,
		|	ФактическиеЗначенияПоказателейЭффективностиСотрудников.Регистратор КАК РегистраторФактическогоЗначения,
		|	ФактическиеЗначенияПоказателейЭффективностиСотрудников.Значение КАК ФактическоеЗначение
		|ПОМЕСТИТЬ ВТВведенныеФактическиеЗначенияСотрудниковСРегистраторами
		|ИЗ
		|	#ВТПоказателиСотрудников КАК ПоказателиСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ФактическиеЗначенияПоказателейЭффективностиСотрудников КАК ФактическиеЗначенияПоказателейЭффективностиСотрудников
		|		ПО ПоказателиСотрудников.Сотрудник = ФактическиеЗначенияПоказателейЭффективностиСотрудников.Сотрудник
		|			И ПоказателиСотрудников.Показатель = ФактическиеЗначенияПоказателейЭффективностиСотрудников.Показатель
		|			И (НАЧАЛОПЕРИОДА(ПоказателиСотрудников.Период, ДЕНЬ) >= НАЧАЛОПЕРИОДА(ФактическиеЗначенияПоказателейЭффективностиСотрудников.Период, МЕСЯЦ))
		|			И (НАЧАЛОПЕРИОДА(ПоказателиСотрудников.Период, ДЕНЬ) <= НАЧАЛОПЕРИОДА(ФактическиеЗначенияПоказателейЭффективностиСотрудников.Период, МЕСЯЦ))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТВведенныеФактическиеЗначенияСотрудниковСРегистраторами.Период КАК Период,
		|	ВТВведенныеФактическиеЗначенияСотрудниковСРегистраторами.Сотрудник КАК Сотрудник,
		|	ВТВведенныеФактическиеЗначенияСотрудниковСРегистраторами.Показатель КАК Показатель,
		|	МАКСИМУМ(ВТВведенныеФактическиеЗначенияСотрудниковСРегистраторами.РегистраторФактическогоЗначения) КАК РегистраторФактическогоЗначения,
		|	СУММА(ВТВведенныеФактическиеЗначенияСотрудниковСРегистраторами.ФактическоеЗначение) КАК ФактическоеЗначение
		|ПОМЕСТИТЬ ВТВведенныеФактическиеЗначенияСотрудников
		|ИЗ
		|	ВТВведенныеФактическиеЗначенияСотрудниковСРегистраторами КАК ВТВведенныеФактическиеЗначенияСотрудниковСРегистраторами
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТВведенныеФактическиеЗначенияСотрудниковСРегистраторами.Сотрудник,
		|	ВТВведенныеФактическиеЗначенияСотрудниковСРегистраторами.Показатель,
		|	ВТВведенныеФактическиеЗначенияСотрудниковСРегистраторами.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоказателиСотрудников.Период КАК Период,
		|	ПоказателиСотрудников.Сотрудник КАК Сотрудник,
		|	ПоказателиСотрудников.Показатель КАК Показатель,
		|	ПоказателиСотрудников.Подразделение КАК Подразделение
		|ПОМЕСТИТЬ ВТСотрудникиНаследующиеФактическиеЗначенияИзПодразделений
		|ИЗ
		|	#ВТПоказателиСотрудников КАК ПоказателиСотрудников
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВведенныеФактическиеЗначенияСотрудников КАК ВведенныеФактическиеЗначенияСотрудников
		|		ПО ПоказателиСотрудников.Сотрудник = ВведенныеФактическиеЗначенияСотрудников.Сотрудник
		|			И ПоказателиСотрудников.Показатель = ВведенныеФактическиеЗначенияСотрудников.Показатель
		|			И ПоказателиСотрудников.Период = ВведенныеФактическиеЗначенияСотрудников.Период
		|ГДЕ
		|	ВведенныеФактическиеЗначенияСотрудников.Сотрудник ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СотрудникиНаследующиеФактическиеЗначенияИзПодразделений.Период КАК Период,
		|	СотрудникиНаследующиеФактическиеЗначенияИзПодразделений.Подразделение КАК Подразделение,
		|	СотрудникиНаследующиеФактическиеЗначенияИзПодразделений.Показатель КАК Показатель
		|ПОМЕСТИТЬ ВТПоказателиПодразделений
		|ИЗ
		|	ВТСотрудникиНаследующиеФактическиеЗначенияИзПодразделений КАК СотрудникиНаследующиеФактическиеЗначенияИзПодразделений";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ВТПоказателиСотрудников", ИмяФильтра);
	УдалитьВТ.Добавить("ВТВведенныеФактическиеЗначенияСотрудниковСРегистраторами");
	УдалитьВТ.Добавить("ВТВведенныеФактическиеЗначенияСотрудников");
	УдалитьВТ.Добавить("ВТСотрудникиНаследующиеФактическиеЗначенияИзПодразделений");
	УдалитьВТ.Добавить("ВТПоказателиПодразделений");
	
	Запрос.Выполнить();
	
	СоздатьВТФактическиеЗначенияПоказателейПодразделений(МенеджерВременныхТаблиц, "ВТПоказателиПодразделений");
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СотрудникиНаследующиеФактическиеЗначенияИзПодразделений.Период КАК Период,
		|	СотрудникиНаследующиеФактическиеЗначенияИзПодразделений.Сотрудник КАК Сотрудник,
		|	СотрудникиНаследующиеФактическиеЗначенияИзПодразделений.Показатель КАК Показатель,
		|	ФактическиеЗначенияПоказателейПодразделений.РегистраторФактическогоЗначения КАК РегистраторФактическогоЗначения,
		|	ФактическиеЗначенияПоказателейПодразделений.ФактическоеЗначение КАК ФактическоеЗначение
		|ПОМЕСТИТЬ ВТФактическиеЗначенияПоказателейСотрудников
		|ИЗ
		|	ВТСотрудникиНаследующиеФактическиеЗначенияИзПодразделений КАК СотрудникиНаследующиеФактическиеЗначенияИзПодразделений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФактическиеЗначенияПоказателейПодразделений КАК ФактическиеЗначенияПоказателейПодразделений
		|		ПО СотрудникиНаследующиеФактическиеЗначенияИзПодразделений.Период = ФактическиеЗначенияПоказателейПодразделений.Период
		|			И СотрудникиНаследующиеФактическиеЗначенияИзПодразделений.Показатель = ФактическиеЗначенияПоказателейПодразделений.Показатель
		|			И СотрудникиНаследующиеФактическиеЗначенияИзПодразделений.Подразделение = ФактическиеЗначенияПоказателейПодразделений.Подразделение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВведенныеФактическиеЗначенияСотрудников.Период,
		|	ВведенныеФактическиеЗначенияСотрудников.Сотрудник,
		|	ВведенныеФактическиеЗначенияСотрудников.Показатель,
		|	ВведенныеФактическиеЗначенияСотрудников.РегистраторФактическогоЗначения,
		|	ВведенныеФактическиеЗначенияСотрудников.ФактическоеЗначение
		|ИЗ
		|	ВТВведенныеФактическиеЗначенияСотрудников КАК ВведенныеФактическиеЗначенияСотрудников";
	
	Запрос.Выполнить();
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
КонецПроцедуры

#КонецОбласти

#Область ДвиженияДокументов

Процедура СформироватьКоллекциюДвижений(КоллекцияДвижений, ТаблицаДанных)

	Для каждого СтрокаДанных Из ТаблицаДанных Цикл
		Движение = КоллекцияДвижений.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, СтрокаДанных);
	КонецЦикла;
	
	КоллекцияДвижений.Записать();
	КоллекцияДвижений.Записывать = Ложь;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
