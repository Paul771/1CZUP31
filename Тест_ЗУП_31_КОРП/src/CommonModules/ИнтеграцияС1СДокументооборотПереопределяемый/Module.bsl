////////////////////////////////////////////////////////////////////////////////
// Подсистема "Интеграция с 1С:Документооборотом"
// Модуль ИнтеграцияС1СДокументооборотПереопределяемый: сервер, внешнее соединение
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

//Вызывается при создании форм документов Документооборота. Позволяет выполнить дополнительные действия.
//Параметры:
//	Форма - форма документа Документооборота
Процедура ДополнительнаяОбработкаФормыДокумента(Форма) Экспорт

КонецПроцедуры

//Вызывается при создании форм бизнес-процессов 1С:Документооборота. Позволяет выполнить дополнительные действия.
//Параметры:
//	Форма - форма бизнес-процесса Документооборота
Процедура ДополнительнаяОбработкаФормыБизнесПроцесса(Форма) Экспорт
	
КонецПроцедуры

//Вызывается при создании формы задачи Документооборота. Позволяет выполнить дополнительные действия.
//Параметры:
//	Форма - форма задачи Документооборота
Процедура ДополнительнаяОбработкаФормыЗадачи(Форма) Экспорт
	
КонецПроцедуры

// Заполняет свойства объекта XDTO из реквизитов объекта конфигурации-потребителя. По умолчанию заполняется
// только наименование и идентификатор. При необходимости обеспечить заполнение других реквизитов объектов
// ДО, создаваемых по ссылкам, следует заполнить соответствующие свойства объекта XDTO. Эти же значения будут
// использованы при поиске соответствий для справочников, имеющих внешние ключи (ИНН, КПП).
// 
// Параметры:
//   Прокси - WSПрокси - прокси веб-сервиса Документооборота.
//   ОбъектXDTO - ОбъектXDTO - заполняемый объект.
//   СсылкаНаПотребитель - ЛюбаяСсылка - ссылка на исходный объект конфигурации-потребителя.
//
// Пример реализации:
//
//	Если ИнтеграцияС1СДокументооборот.ПроверитьТип(Прокси, ОбъектXDTO, "DMCorrespondent") 
//		и ТипЗнч(СсылкаНаПотребитель) = Тип("СправочникСсылка._ДемоКонтрагенты") Тогда
//		ОбъектXDTO.inn = СсылкаНаПотребитель.ИНН;
//		ОбъектXDTO.kpp = СсылкаНаПотребитель.КПП;
//	ИначеЕсли ИнтеграцияС1СДокументооборот.ПроверитьТип(Прокси, ОбъектXDTO, "DMOrganization") 
//		и ТипЗнч(СсылкаНаПотребитель) = Тип("СправочникСсылка.Организации") Тогда
//		ОбъектXDTO.inn = СсылкаНаПотребитель.ИНН;
//		ОбъектXDTO.kpp = СсылкаНаПотребитель.КПП;
//	Иначе //...
//	КонецЕсли;
//
Процедура ЗаполнитьРеквизитыИзПотребителя(Прокси, ОбъектXDTO, СсылкаНаПотребитель) Экспорт 
	
КонецПроцедуры

// Вызывается при заполнении реквизита объекта ИС по значению реквизита объекта ДО. При установке
// в параметр Результат значения, отличного от Неопределено, стандартное заполнение не выполняется.
// 
// Параметры:
//   Результат - Произвольный - заполняемое значение реквизита объекта ИС.
//   ОбъектXDTO - ОбъектXDTO - исходный объект, соответствие которому нужно найти.
//   ТипРеквизита - Тип - тип заполняемого реквизита объекта ИС.
//   ИмяРеквизита - Строка - имя заполняемого реквизита ИС.
//   ТипОбъекта - Тип - тип заполняемого объекта ИС.
//   Объект - Произвольный - заполняемый объект ИС.
//   ЭтоДополнительноеСвойство - Булево - истина, если переданный объект является доп. свойством.
//   Свойство - ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения - свойство-владелец.
//
// Пример реализации:
//
//	Если ОбъектXDTO.Установлено("objectId")
//		И ОбъектXDTO.objectId.type = "DMDocumentStatus" Тогда
//		И ТипРеквизита = Тип("Булево")
//		И ИмяРеквизита = "Согласован" Тогда
//		Результат = (ОбъектXDTO.objectId.id = "Согласован");
//	КонецЕсли;
//
Процедура ПриОпределенииЗначенияРеквизитаПоОбъектуXDTO(Результат, ОбъектXDTO, ТипРеквизита,
	ИмяРеквизита, ТипОбъекта, Объект, ЭтоДополнительноеСвойство = Ложь, Свойство = Неопределено) Экспорт
	
	
КонецПроцедуры

// Вызывается при создании объекта ИС по ссылке в реквизите объекта ДО. Может использоваться для
// дозаполнения реквизитов нового объекта ИС, например, если стандартный обработчик заполнения не
// заполняет обязательные реквизиты по умолчанию.
// 
// Параметры:
//   ОбъектИС - Произвольный - созданный, но еще не записанный объект ИС.
//   ОбъектXDTO - ОбъектXDTO - исходный объект, соответствие которому не найдено в текущей ИБ.
//   ЗаполняемыйОбъектИС - Произвольный - объект ИС, в ходе заполнения которого вызывается создание
//     ОбъектИС по ссылке. Обеспечивает контекст, необходимый для заполнения ОбъектИС.
//
// Пример:
//	Если ТипЗнч(ОбъектИС) = Тип("СправочникОбъект.Организации") Тогда
//		ОбъектИС.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо;
//		Если ОбъектXDTO.objectId.type = "DMOrganization"
//			И ОбъектXDTO.objectId.id = "ИндивидуальныйПредприниматель" Тогда
//			ОбъектИС.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель;
//		КонецЕсли;
//	КонецЕсли;
//
Процедура ПриСозданииПоСсылке(ОбъектИС, ОбъектXDTO, ЗаполняемыйОбъектИС = Неопределено) Экспорт
	
	
КонецПроцедуры

// Получает таблицу менеджеров печати форм объектов.
//
Процедура ЗаполнитьТаблицуМенеджеровПечатиОбъекта(МенеджерОбъекта, ПечатныеФормы) Экспорт
	
	КомандыПечати = ИнтеграцияС1СДокументооборот.КомандыПечатиФормы(МенеджерОбъекта + ".Форма.ФормаСписка");
	
	Для Каждого Команда Из КомандыПечати Цикл
		Строка = ПечатныеФормы.Добавить();
		Строка.МенеджерПечати = Команда.МенеджерПечати;
		Строка.ИмяКоманды = Команда.Идентификатор;
		Строка.ПредставлениеКоманды = Команда.Представление;
	КонецЦикла;
		
КонецПроцедуры

// Заполняет коллекцию печатных форм объекта по имени менеджера печати и команде менеджера печати.
//
Процедура ЗаполнитьПечатныеФормы(ВнешнийОбъект, ИмяКоманды, КоллекцияПечатныхФорм) Экспорт
	
	НомерКомандыСтрокой = Сред(ИмяКоманды, СтрДлина("КомандаПечати") + 1);
	Если НомерКомандыСтрокой = "0" Тогда
		НомерКоманды = 0;
	Иначе
		ОписаниеТипов = Новый ОписаниеТипов("Число");
		НомерКоманды = ОписаниеТипов.ПривестиЗначение(НомерКомандыСтрокой);
		НомерКоманды = ?(НомерКоманды = 0, -1, НомерКоманды);
	КонецЕсли;
	
	МенеджерОбъекта = ВнешнийОбъект.Метаданные().ПолноеИмя();

	КомандыПечати = ИнтеграцияС1СДокументооборот.КомандыПечатиФормы(МенеджерОбъекта + ".Форма.ФормаСписка");
	Если КомандыПечати.Количество() = 0 Тогда
		КоллекцияПечатныхФорм = Новый ТаблицаЗначений;
		Возврат;
	КонецЕсли;
		
	Если НомерКоманды = -1 Тогда
		КомандаПечати = КомандыПечати.Найти(ИмяКоманды, "Идентификатор");
	Иначе
		КомандаПечати = КомандыПечати[НомерКоманды];
	КонецЕсли;
	ОписаниеКоманды = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(КомандаПечати);

	ОбъектыПечатиСписок = новый СписокЗначений;
	ОбъектыПечатиСписок.Добавить(ВнешнийОбъект);
	
	ОбъектыПечати = новый Массив;
	ОбъектыПечати.Добавить(ВнешнийОбъект);

	ОписаниеКоманды.Вставить("ОбъектыПечати", ОбъектыПечати);
	
	ПараметрыПечати = ОписаниеКоманды.ДополнительныеПараметры;
	ПараметрыВывода = Неопределено;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	Если ОписаниеКоманды.ДополнительныеПараметры.Свойство("Ссылка") 
		И ЗначениеЗаполнено(ОписаниеКоманды.ДополнительныеПараметры.Ссылка) Тогда
		Попытка
			ПараметрыПечати = Новый Структура;
			ПараметрыПечати.Вставить("ИдентификаторКоманды", ОписаниеКоманды.Идентификатор);
			ПараметрыПечати.Вставить("ОбъектыНазначения", ОбъектыПечати);
			
			ДополнительныеОтчетыИОбработки.ПечатьПоВнешнемуИсточнику(ОписаниеКоманды.ДополнительныеПараметры.Ссылка,
					ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
		Исключение
			КоллекцияПечатныхФорм = Новый ТаблицаЗначений;
		КонецПопытки;
	Иначе
		ПечатныеФормы = УправлениеПечатью.СформироватьПечатныеФормы(
			ОписаниеКоманды.МенеджерПечати,
			ОписаниеКоманды.Идентификатор,
			ОбъектыПечати,
			ПараметрыПечати);
		КоллекцияПечатныхФорм = ПечатныеФормы.КоллекцияПечатныхФорм;
	КонецЕсли;
		
КонецПроцедуры

//Устанавливает кнопки печати на форме объекта по доступным командам печати.
//
Процедура ЗаполнитьКомандыПечати(Форма, ВнешнийОбъект, МестоРазмещенияКоманд) Экспорт
	
	ИмяФормы = ВнешнийОбъект.Метаданные().ПолноеИмя() + ".Форма.ФормаСписка";
	
	КомандыПечати = ИнтеграцияС1СДокументооборот.КомандыПечатиФормы(ИмяФормы);

	Если МестоРазмещенияКоманд = Неопределено Тогда
		МестоРазмещенияКоманд = Форма.КоманднаяПанель;
	КонецЕсли;
	
	Для НомерКоманды = 0 По КомандыПечати.Количество() - 1 Цикл
		
		ОписаниеКомандыПечати = КомандыПечати[НомерКоманды];
		
		ИмяКоманды = "КомандаПечати" + НомерКоманды;
		
		КомандаФормы = Форма.Команды.Добавить(ИмяКоманды);
		КомандаФормы.Действие = "Подключаемый_ВыполнитьКомандуПечати";
		КомандаФормы.Заголовок = ОписаниеКомандыПечати.Представление;
		КомандаФормы.ИзменяетСохраняемыеДанные = Ложь;
		КомандаФормы.Отображение = ОтображениеКнопки.КартинкаИТекст;
		
		Если ЗначениеЗаполнено(ОписаниеКомандыПечати.Картинка) Тогда
			КомандаФормы.Картинка = ОписаниеКомандыПечати.Картинка;
		КонецЕсли;
		
		НовыйЭлемент = Форма.Элементы.Добавить("ПодменюПечать" + ИмяКоманды, Тип("КнопкаФормы"), МестоРазмещенияКоманд);
		НовыйЭлемент.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
		НовыйЭлемент.ИмяКоманды = ИмяКоманды;
		
	КонецЦикла;
	
КонецПроцедуры

//Возвращает индекс картинки элемента справочника.
//
Функция ИндексКартинкиЭлемента() Экспорт
	
	Возврат 3;
	
КонецФункции

//Заполняет правила заполнения при выгрузке по умолчанию.
Процедура УстановитьПравилаЗаполненияПриВыгрузкеПоУмолчанию(ПравилаЗаполнения, Приемник, Источник) Экспорт
	
	МетаданныеИсточника = Метаданные.НайтиПоПолномуИмени(Источник);
	Если МетаданныеИсточника = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ВариантыЗаполнения = Новый Соответствие;
	ВариантыЗаполнения.Вставить("Организация", "organization");
	ВариантыЗаполнения.Вставить("Контрагент", "correspondent");
	ВариантыЗаполнения.Вставить("Комментарий", "comment");
	ВариантыЗаполнения.Вставить("ДатаНачала", "beginDate");
	ВариантыЗаполнения.Вставить("ДатаОкончания", "endDate");
	ВариантыЗаполнения.Вставить("СрокИсполнения", "performanceDate");
	ВариантыЗаполнения.Вставить("Сумма", "sum");
	ВариантыЗаполнения.Вставить("СуммаДокумента", "sum");
	ВариантыЗаполнения.Вставить("Валюта", "currency");
	ВариантыЗаполнения.Вставить("ВалютаВзаиморасчетов", "currency");
	ВариантыЗаполнения.Вставить("ВалютаРасчетов", "currency");
	ВариантыЗаполнения.Вставить("ВалютаДокумента", "currency");
	ВариантыЗаполнения.Вставить("Содержание", "summary");
	
	ВариантыЗаполнения.Вставить("ДатаРегистрации", "regDate");
	ВариантыЗаполнения.Вставить("ПодразделениеОрганизации", "subdivision");
	ВариантыЗаполнения.Вставить("Подразделение", "subdivision");
	ВариантыЗаполнения.Вставить("Утвердил", "signer");
	ВариантыЗаполнения.Вставить("Подготовил", "author");
	ВариантыЗаполнения.Вставить("Автор", "author");
	ВариантыЗаполнения.Вставить("КонтактноеЛицо", "contactPerson");
	ВариантыЗаполнения.Вставить("Ответственный", "responsible");
	ВариантыЗаполнения.Вставить("Наименование", "name");
	ВариантыЗаполнения.Вставить("ЮрФизЛицо", "legalPrivatePerson");
	ВариантыЗаполнения.Вставить("ИНН", "inn");
	ВариантыЗаполнения.Вставить("КПП", "kpp");
	ВариантыЗаполнения.Вставить("КодПоОКПО", "okpo");
	ВариантыЗаполнения.Вставить("ПолноеНаименование", "fullName");
	ВариантыЗаполнения.Вставить("НаименованиеПолное", "fullName");
	ВариантыЗаполнения.Вставить("ФизическоеЛицо", "privatePerson");
	ВариантыЗаполнения.Вставить("ФизЛицо", "privatePerson");
	ВариантыЗаполнения.Вставить("Ответственный", "author");
	ВариантыЗаполнения.Вставить("Проект", "project");
	
	ВариантыЗаполнения.Вставить("ДатаВходящегоДокумента", "externalDate");
	ВариантыЗаполнения.Вставить("НомерВходящегоДокумента", "externalNumber");
	
	Для Каждого Реквизит Из МетаданныеИсточника.Реквизиты Цикл
		Значение =  ВариантыЗаполнения.Получить(Реквизит.Имя);
		Если Значение <> Неопределено Тогда
			ПравилаЗаполнения.Вставить(Значение, Реквизит.Имя);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

//Заполняет правила заполнения при загрузке по умолчанию.
Процедура УстановитьПравилаЗаполненияПриЗагрузкеПоУмолчанию(ПравилаЗаполнения, Приемник, Источник) Экспорт
	
	ВариантыЗаполнения = новый Соответствие;
	
	МетаданныеПриемника = Метаданные.НайтиПоПолномуИмени(Приемник);
	Если МетаданныеПриемника = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Найти(Источник, "Document") <> 0 Тогда
		ВариантыЗаполнения.Вставить("Организация", "organization");
		ВариантыЗаполнения.Вставить("Контрагент", "correspondent");
		ВариантыЗаполнения.Вставить("Комментарий", "comment");
		ВариантыЗаполнения.Вставить("ДатаНачала", "beginDate");
		ВариантыЗаполнения.Вставить("ДатаОкончания", "endDate");
		ВариантыЗаполнения.Вставить("СрокИсполнения", "performanceDate");
		ВариантыЗаполнения.Вставить("Сумма", "sum");
		ВариантыЗаполнения.Вставить("СуммаДокумента", "sum");
		ВариантыЗаполнения.Вставить("Валюта", "currency");
		ВариантыЗаполнения.Вставить("ВалютаВзаиморасчетов", "currency");
		ВариантыЗаполнения.Вставить("ВалютаРасчетов", "currency");
		ВариантыЗаполнения.Вставить("ВалютаДокумента", "currency");
		ВариантыЗаполнения.Вставить("Содержание", "summary");
		
		ВариантыЗаполнения.Вставить("ДатаРегистрации", "regDate");
		ВариантыЗаполнения.Вставить("ПодразделениеОрганизации", "subdivision");
		ВариантыЗаполнения.Вставить("Подразделение", "subdivision");
		ВариантыЗаполнения.Вставить("Утвердил", "signer");
		ВариантыЗаполнения.Вставить("Подготовил", "author");
		ВариантыЗаполнения.Вставить("Автор", "author");
		ВариантыЗаполнения.Вставить("КонтактноеЛицо", "contactPerson");
		ВариантыЗаполнения.Вставить("Ответственный", "responsible");
		ВариантыЗаполнения.Вставить("Наименование", "title");
		ВариантыЗаполнения.Вставить("Статус", "statusApproval");
		ВариантыЗаполнения.Вставить("Согласован", "statusApproval");
		
		ВариантыЗаполнения.Вставить("ДатаВходящегоДокумента", "externalDate");
		ВариантыЗаполнения.Вставить("НомерВходящегоДокумента", "externalNumber");
		
	ИначеЕсли Источник = "DMCorrespondent" Тогда
		ВариантыЗаполнения.Вставить("Наименование", "name");
		ВариантыЗаполнения.Вставить("Ответственный", "responsible");
		ВариантыЗаполнения.Вставить("ЮрФизЛицо", "legalPrivatePerson");
		ВариантыЗаполнения.Вставить("ИНН", "inn");
		ВариантыЗаполнения.Вставить("КПП", "kpp");
		ВариантыЗаполнения.Вставить("КодПоОКПО", "okpo");
		ВариантыЗаполнения.Вставить("ПолноеНаименование", "fullName");
		ВариантыЗаполнения.Вставить("НаименованиеПолное", "fullName");
		ВариантыЗаполнения.Вставить("ФизическоеЛицо", "privatePerson");
		ВариантыЗаполнения.Вставить("ФизЛицо", "privatePerson");
	КонецЕсли;
	
	Для Каждого Реквизит Из МетаданныеПриемника.Реквизиты Цикл
		Значение =  ВариантыЗаполнения.Получить(Реквизит.Имя);
		Если Значение <> Неопределено Тогда
			ПравилаЗаполнения.Вставить(Реквизит.Имя, Значение);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

//Вызывается при записи констант подсистемы Интеграция с 1С:Документооборотом.
Процедура ПриЗаписиКонстанты(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИмяКонстанты 	  = Источник.ЭтотОбъект.Метаданные().Имя;
	ЗначениеКонстанты = Источник.Значение;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ТипКонстанты	= ТипЗнч(ЗначениеКонстанты);
	ПримитивныеТипы = Новый ОписаниеТипов("Число,Строка,Дата,Булево,Неопределено");
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Синхронизировать "простые" зависимые константы
	Если ПримитивныеТипы.СодержитТип(ТипКонстанты)
	 ИЛИ ОбщегоНазначения.ЗначениеСсылочногоТипа(ЗначениеКонстанты) Тогда
		
		ПодчиненныеКонстанты = ПолучитьДопустимыеЗначенияПодчиненныхКонстант(ИмяКонстанты, ЗначениеКонстанты);
		
		Если ЗначениеЗаполнено(ПодчиненныеКонстанты) Тогда
			
			Для Каждого КлючИЗначение Из ПодчиненныеКонстанты Цикл
				Если Константы[КлючИЗначение.Ключ].Получить() <> КлючИЗначение.Значение Тогда
					Константы[КлючИЗначение.Ключ].Установить(КлючИЗначение.Значение);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

//Заполняет таблицу реквизитов объекта по типу объекта.
//
// Параметры:
//  ИмяОбъектаПотребителя  - Строка - имя типа объекта потребителя
//  Реквизиты  - ТаблицаЗначений - Таблица описания реквизитов
//
Процедура ПолучитьРеквизитыОбъектаПотребителя(ИмяОбъектаПотребителя, Реквизиты) Экспорт

	

КонецПроцедуры // ИнтеграцияС1СДокументооборотПереопределяемый.ПолучитьРеквизитыОбъектаПотребителя()

// Получает подходящий тип потребителя для объекта XDTO.
//
// Параметры: 
//   ТипОбъектаXDTO - Строка - тип объекта XDTO, соответствие которому следует выбрать
//   ТипыКандидаты - Массив - массив типов, из которых требуется выбрать подходящий
//
// Возвращаемое значение:
//   Тип - один из элементов массива ТипыКандидаты, подходящий для типа объекта XDTO
//
Функция ПодходящийТип(ТипОбъектаXDTO, ТипыКандидаты) Экспорт
	
	// Пример реализации: выбор типа Пользователи для реквизита составного типа (Пользователи, ВнешниеПользователи)
	//Если ТипОбъектаXDTO = "DMUser" и ТипыКандидаты.Найти(Тип("СправочникСсылка.Пользователи")) <> Неопределено Тогда
	//	Возврат Тип("СправочникСсылка.Пользователи");
	//Иначе // другие варианты
	//КонецЕсли;
	
	Возврат ТипыКандидаты[0];
	
КонецФункции

// Возвращает штрихкод документа для присвоения связанному документу ДО.
//
// Параметры
//   Документ - ДокументСсылка, на основании которого создается документ ДО
//
// Возвращаемое значение:
//   Строка - штрихкод для присвоения связанному документу ДО
//
Функция ШтрихкодДокумента(Документ) Экспорт
	
	// Пример кода:
	//Если ПолучитьФункциональнуюОпцию("ИспользоватьШтрихкодированиеПечатныхФормОбъектов") Тогда
	//	ШестнадцатиричноеЧисло = НРег(СтрЗаменить(Документ.УникальныйИдентификатор(), "-", ""));
	//	ДлинаСтроки = СтрДлина(ШестнадцатиричноеЧисло);
	//	Результат = 0;
	//	Для НомерСимвола = 1 По ДлинаСтроки Цикл
	//		Результат = Результат * 16 + Найти("0123456789abcdef", 
	//			Сред(ШестнадцатиричноеЧисло, НомерСимвола, 1)) - 1;
	//	КонецЦикла;
	//	Возврат Формат(Результат, "ЧГ=0");
	//Иначе
	//	Возврат "";
	//КонецЕсли;
	
	Возврат "";
	
КонецФункции

// Возвращает предельное количество объектов, выбираемых сервисом при получении списков.
//
// Параметры:
//   Тип - Строка - тип объекта XDTO
//
// Возвращаемое значение:
//   Число - предельное количество элементов, выбираемых за раз
//
Функция ПредельноеКоличествоВыбираемыхОбъектов(Тип) Экспорт
	
	Возврат 20; // Если Тип = "DMCorrespondent" Тогда Возврат 50 ИначеЕсли <...>
	
КонецФункции

// Уточняет сокращенное наименование интегрируемой конфигурации для вывода в заголовках и декорациях.
//
// Параметры:
//   СокращенноеНаименование - Строка - неявно возвращаемое значение, краткое название конфигурации.
//
// Пример реализации:
//   СокращенноеНаименование = НСтр("ru = '1С:ERP УП'");
//
Процедура ПриОпределенииСокращенногоНаименованияКонфигурации(СокращенноеНаименование) Экспорт
	
	
КонецПроцедуры

// Определяет доступность интеграции для текущего пользователя. Может переопределяться
// при отказе от использования поставляемой роли ИспользованиеИнтеграцииС1СДокументооборотом.
//
// Возвращаемое значение:
//   Булево - Истина, если использование интеграции разрешено.
//
Функция ПользователюРазрешеноИспользованиеИнтеграции() Экспорт
	
	Возврат ПравоДоступа("Использование", Метаданные.Обработки.ИнтеграцияС1СДокументооборот);
	
КонецФункции

// Проверяет возможность запуска согласования в ДО.
//
// Параметры:
//   ПредметСогласования - ЛюбаяСсылка - согласуемый объект ИС.
//   ТекстПредупреждения - Строка - неявно возвращаемое значение, текст предупреждения.
//
// Возвращаемое значение:
//   Булево - Истина, если запуск согласования разрешен, Ложь - запрещен, Неопределено - согласно ролям.
//
// Пример реализации:
//   Если ТипЗнч(ПредметСогласования) = Тип("ДокументСсылка._ДемоЗаказПокупателя") Тогда
//   	Если ПредметСогласования.Состояние <> Перечисления._ДемоСостоянияЗаказаПокупателя.НаСогласовании Тогда
//   		ТекстПредупреждения = НСтр("ru = 'Создание процесса согласования разрешено только на основании
//   		|документов в состоянии ""На согласовании"".'");
//   		Возврат Ложь;
//   	КонецЕсли;
//   КонецЕсли;
//
//
Функция ПользователюРазрешенЗапускСогласования(ПредметСогласования, ТекстПредупреждения) Экспорт

	

КонецФункции

// Проверяет возможность прерывания согласования в ДО.
//
// Параметры:
//   ПредметСогласования - ЛюбаяСсылка - согласуемый объект ИС.
//   ПредметДО - Структура - описание связанного объекта ДО:
//      name - Строка - представление связанного объекта.
//      id - Строка - идентификатор связанного объекта.
//      type - Строка - имя типа XDTO.
//   ТекстПредупреждения - Строка - неявно возвращаемое значение, текст предупреждения.
//
// Возвращаемое значение:
//   Булево - Истина, если прерывание согласования разрешено, Ложь - запрещено, Неопределено - согласно ролям.
//
Функция ПользователюРазрешеноПрерываниеСогласования(ПредметСогласования, ПредметДО, ТекстПредупреждения) Экспорт

	

КонецФункции

// Вызывается при изменении состояния согласования в ДО. Предназначена для изменения согласуемого объекта.
//
// Параметры:
//   ПредметСогласования - ЛюбаяСсылка - согласуемый объект.
//   Состояние - ПеречислениеСсылка.СостоянияСогласованияВДокументообороте.
//   ВызовИзФормыОбъекта - Булево - Истина, если изменение состояния вызвано пользователем из формы объекта.
//
// Пример реализации:
//
//	Если ВызовИзФормыОбъекта Тогда
//		Возврат;
//	КонецЕсли;
//
//	Если ТипЗнч(ПредметСогласования) = Тип("ДокументСсылка._ДемоЗаказПокупателя") Тогда
//	
//		Объект = ПредметСогласования.ПолучитьОбъект();
//		Если Состояние = Перечисления.СостоянияСогласованияВДокументообороте.НаСогласовании Тогда
//			Объект.Состояние = Перечисления._ДемоСостоянияЗаказаПокупателя.НаСогласовании;
//		ИначеЕсли Состояние = Перечисления.СостоянияСогласованияВДокументообороте.НеСогласован Тогда
//			Объект.Состояние = Перечисления._ДемоСостоянияЗаказаПокупателя.НеСогласован;
//		ИначеЕсли Состояние = Перечисления.СостоянияСогласованияВДокументообороте.Согласован Тогда
//			Объект.Состояние = Перечисления._ДемоСостоянияЗаказаПокупателя.Согласован;
//		Иначе
//			Объект.Состояние = Перечисления._ДемоСостоянияЗаказаПокупателя.Подготовлен;
//		КонецЕсли;
//		Объект.Записать(РежимЗаписиДокумента.Запись);
//	
//	КонецЕсли;
//
//
Процедура ПриИзмененииСостоянияСогласования(ПредметСогласования, Состояние, ВызовИзФормыОбъекта) Экспорт

	

КонецПроцедуры

// Получает пояснение к состоянию согласования, предназначенное для чтения пользователем.
//
// Параметры:
//   Установил - Строка - представление пользователя, установившего новое состояние.
//   ДатаУстановки - Дата - дата и время установки нового состояния.
//
// Возвращаемое значение:
//   Строка - пояснение к состоянию согласования.
//
Функция ПояснениеСостоянияСогласования(Установил, ДатаУстановки) Экспорт
	
	Если ЗначениеЗаполнено(ДатаУстановки) Тогда
		Результат = Формат(ДатаУстановки, "ДЛФ=DT");
		Если ЗначениеЗаполнено(Установил) Тогда
			Результат = Результат + " (" + СокрЛП(Установил) + ")";
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(Установил) Тогда
			Результат = Установил;
		Иначе
			Результат = "";
		КонецЕсли;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

// Переопределяет список команд печати. Вызывается из УправлениеПечатьюПереопределяемый и предназначена
// для дополнения команд печати листом согласования.
//
// Параметры:
//   ИмяФормы - Строка - имя вызывающей формы в виде ""Документ._ДемоЗаказПокупателя.Форма.ФормаДокумента"
//   КомандыПечати - ТаблицаЗначений - таблица команд к дополнению. Колонки см. в УправлениеПечатью.
//   СтандартнаяОбработка - Булево - Истина, если необходимо стандартное наполнение таблицы команд.
//
// Пример реализации:
//	Если ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.4.8.1") Тогда
//		Если ИмяФормы = "Документ._ДемоЗаказПокупателя.Форма.ФормаДокумента" Тогда
//			КомандаПечати = КомандыПечати.Добавить();
//			КомандаПечати.МенеджерПечати = "Обработка.ИнтеграцияС1СДокументооборот";
//			КомандаПечати.Идентификатор = "ЛистСогласования";
//			КомандаПечати.Представление = НСтр("ru = 'Документооборот: лист согласования'");
//			КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
//			КомандаПечати.Порядок = 2;
//		КонецЕсли;
//	КонецЕсли;
//
Процедура ПередДобавлениемКомандПечати(ИмяФормы, КомандыПечати, СтандартнаяОбработка) Экспорт

	

КонецПроцедуры

// Вызывается при определении таймаута веб-сервиса. При необходимости таймаут может быть вынесен в
// настройки администратора интеграции и переопределен в этой процедуре.
//
// Параметры:
//   Таймаут - Число - неявно возвращаемое значение, время в секундах, по истечении которого вызов
//     веб-сервиса Документооборота считается неудачным.
//
Процедура ПриОпределенииТаймаутаСервиса(Таймаут) Экспорт
	
	Таймаут = 20;
	
КонецПроцедуры

// Заполняет реквизиты формы по данным объекта интегрированной системы.
//
// Параметры:
//   ВнешнийОбъект - ЛюбаяСсылка - ссылка на объект-основание.
//   Форма - УправляемаяФорма - форма обработки ИнтеграцияС1СДокументооборот, соответствующая заполняемому
//    объекту Документооборота.
//
Процедура ЗаполнитьФормуИзПотребителя(ВнешнийОбъект, Форма) Экспорт
	
	
	
КонецПроцедуры

// Вызывается при построении списка интегрируемых объектов. В переданную таблицу следует добавить
// типы объектов, поддерживающих автоматическую настройку бесшовной интеграции с созданием правил
// интеграции и заполнением связанных данных.
//
// Параметры:
//   Объекты - ТаблицаЗначений - заполняемая таблица с колонками:
//     ИмяТипаОбъекта - Строка - полное имя типа, как задано в метаданных.
//     ОписаниеВыполняемыхДействий - Строка - необязательна, краткое описание действий, выполняемых
//        при автонастройке.
//
// Пример реализации:
//	Строка = Объекты.Добавить();
//	Строка.ИмяТипаОбъекта = "Справочник._ДемоДоговорыКонтрагентов";
//	Строка.ОписаниеВыполняемыхДействий =
//		НСтр("ru = 'Будут созданы правила интеграции и виды внутренних документов для всех видов договоров.'");
//
//
Процедура ПриОпределенииОбъектовПоддерживающихАвтонастройку(Объекты) Экспорт
	
	
	
КонецПроцедуры

// Вызывается при добавлении подсистемы в дерево интегрируемых объектов, с помощью которого настраивается
// интеграция. По умолчанию выводятся подсистемы первого уровня, включаемые в командный интерфейс, и
// подчиненные им.
//
// Параметры:
//   Подсистема - ОбъектМетаданных.Подсистема - добавляемая подсистема, содержащая интегрируемые объекты.
//   Отказ - Булево - при установке в Ложь подсистема будет пропущена вместе со всеми подчиненными.
//
Процедура ПриДобавленииПодсистемыВДеревоИнтегрируемыхОбъектов(Подсистема, Отказ) Экспорт
	
	
	
КонецПроцедуры

// Переопределяет типы объектов ИС, поддерживающие бесшовную интеграцию. По умолчанию заполняется всеми
// типами, входящими в состав типов параметров общих команд подсистемы ИнтеграцияС1СДокументооборотом.
//
// Параметры:
//   Типы - Массив - массив объектов типа Тип.
//
Процедура ПриОпределенииТиповОбъектовПоддерживающихИнтеграцию(Типы) Экспорт
	
	
	
КонецПроцедуры

// Переопределяет представление объекта интегрируемой конфигурации. Используется, если стандартное
// представление из синонима и ключевых реквизитов через запятую оказывается недостаточно читабельным.
//
// Параметры:
//   Правило - СправочникОбъект.ПравилаИнтеграцииС1СДокументооборотом - правило, для которого
//             определяется представление.
//   ПредставлениеОбъектаПотребителя - Строка - неявно возвращаемое значение, переопределяемое представление
//             объекта. По умолчанию собирается из синонима объекта и значений ключевых реквизитов правила.
//
// Пример реализации:
//
//	Если Правило.ТипОбъектаПотребителя = "Документ.ПоступлениеТоваровУслуг" Тогда 
//		СтруктураПоиска = Новый Структура;
//		СтруктураПоиска.Вставить("Ключевой", Истина);
//		СтруктураПоиска.Вставить("ИмяРеквизитаОбъектаПотребителя", "ВидОперации");
//		КлючевыеРеквизиты = Правило.ПравилаЗаполненияРеквизитовПриЗагрузке.НайтиСтроки(СтруктураПоиска);
//		Если КлючевыеРеквизиты.Количество() > 0 Тогда
//			ВидОперации = КлючевыеРеквизиты[0].ЗначениеРеквизитаПотребителя;
//			Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары Тогда
//				ПредставлениеОбъектаПотребителя = НСтр("ru = 'Поступление товаров'");
//			ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги Тогда
//				ПредставлениеОбъектаПотребителя = НСтр("ru = 'Поступление услуг'");
//			КонецЕсли;
//		КонецЕсли;
//	КонецЕсли;
//
Процедура ПриОпределенииПредставленияОбъектаПотребителя(Правило, ПредставлениеОбъектаПотребителя) Экспорт
	
	
	
КонецПроцедуры

// Переопределяет представление вида объекта интегрируемой конфигурации.
//
// Параметры:
//   Правило - СправочникОбъект.ПравилаИнтеграцииС1СДокументооборотом - правило, для которого
//             определяется представление.
//   ПредставлениеВидаОбъектаПотребителя - Строка - неявно возвращаемое значение, переопределяемое представление
//             вида объекта. По умолчанию определяется как представление объекта с ключевыми реквизитами.
//
// Пример реализации:
//	Если Правило.ТипОбъектаПотребителя = "Документ.ПоступлениеТоваровУслуг" Тогда
//		СтруктураПоиска = Новый Структура;
//		СтруктураПоиска.Вставить("Ключевой", Истина);
//		СтруктураПоиска.Вставить("ИмяРеквизитаОбъектаПотребителя", "ВидОперации");
//		КлючевыеРеквизиты = Правило.ПравилаЗаполненияРеквизитовПриЗагрузке.НайтиСтроки(СтруктураПоиска);
//		Если КлючевыеРеквизиты.Количество() > 0 Тогда
//			ВидОперации = КлючевыеРеквизиты[0].ЗначениеРеквизитаПотребителя;
//			Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары Тогда
//				ПредставлениеОбъектаПотребителя = НСтр("ru = 'Товары (накладная)'");
//			КонецЕсли;
//		КонецЕсли;
//	КонецЕсли;
//
Процедура ПриОпределенииПредставленияВидаОбъектаПотребителя(Правило, ПредставлениеОбъектаПотребителя) Экспорт
	
	
КонецПроцедуры

// Переопределяет представление формы выбора правила интеграции. Используется при неоднозначности правил и
// позволяет предложить пользователю менее техногенную форму выбора.
//
// Параметры:
//   Форма - УправляемаяФорма - форма ВыборПравила обработки ИнтеграцияС1СДокументооборот.
//
// Пример реализации:
//	Форма.Заголовок = НСтр("ru = 'Форма документа'");
//	Форма.Автозаголовок = Ложь;
//	Форма.Пояснение = НСтр("ru = 'Выберите вид документа:'");
//	Форма.Элементы.Пояснение.Видимость = Истина;
//
Процедура ПриСозданииНаСервереФормыВыбораПравила(Форма) Экспорт
	
	
КонецПроцедуры

// Проверяет соответствие правила интеграции функциональным опциям. Вызвается при выборе подходящего правила
// и позволяет отказаться от правил, связанных с объектами, выключенными по ФО.
//
// Параметры:
//   Правило - СправочникСсылка.ПравилаИнтеграцииС1СДокументооборотом - проверяемое правило.
//   ТипОбъектаДО - Строка - Тип объекта ДО.
//   ТипОбъектаИС - Строка - Тип объекта ИС (полное имя в метаданных).
//   Отказ - Булево - при установке в Истина правило не будет использоваться.
//   ИмяКлючевогоРеквизита - Строка - имя ключевого реквизита правила.
//   ЗначениеКлючевогоРеквизита - Произвольный - значение ключевого реквизита правила.
//
// Пример реализации:
//	Если ТипОбъектаИС = "Документ.ПоступлениеНМА"
//		И Не ПолучитьФункциональнуюОпцию("ВедетсяУчетНМА") Тогда
//		Отказ = Истина;
//		Возврат;
//	КонецЕсли;
//
Процедура ПриПроверкеСоответствияПравилаФункциональнымОпциям(Правило, ТипОбъектаДО, ТипОбъектаИС, Отказ,
	ИмяКлючевогоРеквизита = "", ЗначениеКлючевогоРеквизита = Неопределено) Экспорт
	
	
КонецПроцедуры

// Устанавливает признак наличия присоединенных файлов ДО для объекта ИС. Вызывается при добавлении
// связи с объектом ДО, имеющим файлы, при добавлении файла из интерфейса ИС и при фоновом обновлении.
//
Процедура ПриПоявленииПрисоединенныхФайловДокументооборота(ОбъектИС) Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПрисоединенныеФайлы")
		Или Не ПолучитьФункциональнуюОпцию("ИспользоватьПрисоединенныеФайлы1СДокументооборота") Тогда
		Возврат;
	КонецЕсли;
	
	ВладелецПрисоединенныхФайлов =  Метаданные.ОпределяемыеТипы.Найти("ВладелецПрисоединенныхФайлов");
	Если ВладелецПрисоединенныхФайлов = Неопределено
		Или Не ВладелецПрисоединенныхФайлов.Тип.СодержитТип(ТипЗнч(ОбъектИС)) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаписьИзменилась = Ложь;
	
	МенеджерЗаписи = РегистрыСведений["НаличиеПрисоединенныхФайлов"].СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ОбъектСФайлами = ОбъектИС;
	МенеджерЗаписи.Прочитать();
	
	Если НЕ ЗначениеЗаполнено(МенеджерЗаписи.ОбъектСФайлами) Тогда
		МенеджерЗаписи.ОбъектСФайлами = ОбъектИС;
		ЗаписьИзменилась = Истина;
	КонецЕсли;
	
	Если НЕ МенеджерЗаписи.ЕстьФайлы Тогда
		МенеджерЗаписи.ЕстьФайлы = Истина;
		ЗаписьИзменилась = Истина;
	КонецЕсли;
	
	Если ПустаяСтрока(МенеджерЗаписи.ИдентификаторОбъекта) Тогда
		МенеджерЗаписи.ИдентификаторОбъекта = "0000000000";
		ЗаписьИзменилась = Истина;
	КонецЕсли;
	
	Если ЗаписьИзменилась Тогда
		МенеджерЗаписи.Записать();
	КонецЕсли;
	
КонецПроцедуры

// Снимает признак наличия присоединенных файлов для объекта ИС, если их нет в самой ИС. Вызывается при
// удалении связи с объектом ДО и при фоновом обновлении.
//
Процедура ПриУдаленииПрисоединенныхФайловДокументооборота(ОбъектИС) Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПрисоединенныеФайлы")
		Или Не ПолучитьФункциональнуюОпцию("ИспользоватьПрисоединенныеФайлы1СДокументооборота") Тогда
		Возврат;
	КонецЕсли;
	
	ВладелецПрисоединенныхФайлов =  Метаданные.ОпределяемыеТипы.Найти("ВладелецПрисоединенныхФайлов");
	Если Не ВладелецПрисоединенныхФайлов.Тип.СодержитТип(ТипЗнч(ОбъектИС)) Тогда
		Возврат;
	КонецЕсли;
	
	МассивФайлов = Новый Массив;
	МодульПрисоединенныеФайлы = ОбщегоНазначения.ОбщийМодуль("ПрисоединенныеФайлы");
	МодульПрисоединенныеФайлы.ПолучитьПрикрепленныеФайлыКОбъекту(ОбъектИС, МассивФайлов);
	
	Если МассивФайлов.Количество() <> 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Удалим запись.
	МенеджерЗаписи = РегистрыСведений["НаличиеПрисоединенныхФайлов"].СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ОбъектСФайлами = ОбъектИС;
	МенеджерЗаписи.Прочитать();
	Если МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.ЕстьФайлы = Ложь;
		МенеджерЗаписи.Записать();
	КонецЕсли;
	
КонецПроцедуры

// Определяет реквизиты проведенного документа, подлежащие обновлению при их изменении в ДО.
//
// Параметры:
//   ПолноеИмя - Строка - полное имя объекта, как оно задано в метаданных, напр., Документ.ЗаказКлиента.
//   ОбновляемыеРеквизиты - Массив - неявно возвращаемое значение, имена реквизитов, обновление которых возможно.
//
// Пример реализации:
//	ОбновляемыеРеквизиты.Добавить("Комментарий");
//	Если ПолноеИмя = "Документ.ЗаказКлиента" Тогда
//   	ОбновляемыеРеквизиты.Добавить("Состояние");
//	КонецЕсли;
// 
Процедура ПриОпределенииОбновляемыхРеквизитовПроведенногоДокумента(ПолноеИмя, ОбновляемыеРеквизиты) Экспорт
	
	
	
КонецПроцедуры

// Вызывается при заполнении формы объекта ДО при его создании на основании объекта ИС. Позволяет дозаполнить объект
// ДО способом, не поддерживаемым правилами интеграции.
//
// Параметры:
//   ОбъектИС - Произвольный - ссылка на объект ИС.
//   Форма - УправляемаяФорма - форма объекта 1С:Документооборота.
//   Правило - СправочникСсылка.ПравилаИнтеграцииС1СДокументооборотом - применямое правило.
//
Процедура ПриЗаполненииФормыОбъектаДОПоОбъектуИС(ОбъектИС, Форма, Правило) Экспорт
	
	
	
КонецПроцедуры

// Вызывается при добавлении связи к объекту ИС, связанному с объектом ДО. Позволяет дополнить список подходящих объектов,
// предъявлемый пользователю, теми объектами, которые не могут быть определены автоматически по ссылкам из шапки или из
// табличных частей объекта ИС.
//
// Параметры:
//   ОбъектИС - Произвольный - ссылка на объект ИС.
//   ПодходящиеОбъекты - Массив - дополняемый массив объектов ИС, подходящих для добавления связи.
//
Процедура ПриОпределенииПодходящихОбъектовДляДобавленияСвязи(ОбъектИС, ПодходящиеОбъекты) Экспорт
	
	
	
КонецПроцедуры

// Вызвается при получении WSПрокси. Переопределяется, если в конфигурации есть возможность настройки интернет-прокси.
//
// Параметры:
//   ИнтернетПрокси - может быть проинициализировано значением типа ИнтернетПрокси.
//
Процедура ПриПолученииWSПрокси(ИнтернетПрокси) Экспорт
	
	
	
КонецПроцедуры

// Вызывается при определении наличия файлов, хранимых в интегрируемой конфигурации.
//
// Параметры:
//   ВладелецФайлов - Произвольный - объект-владелец.
//   ЕстьФайлы - Булево, Неопределено - неявно возвращаемое значение, признак наличия присоединенных файлов.
//
Процедура ПриОпределенииНаличияПрисоединенныхФайлов(ВладелецФайлов, ЕстьФайлы) Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("РаботаСФайлами") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	НаличиеФайлов.ЕстьФайлы
		|ИЗ
		|	РегистрСведений.НаличиеФайлов КАК НаличиеФайлов
		|ГДЕ
		|	НаличиеФайлов.ОбъектСФайлами = &ВладелецФайлов
		|	И НаличиеФайлов.ЕстьФайлы
		|");
	Запрос.УстановитьПараметр("ВладелецФайлов", ВладелецФайлов);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЕстьФайлы = Не Запрос.Выполнить().Пустой();
	
КонецПроцедуры

// Вызвается при получении присоединенных файлов.
//
// Параметры:
//   ВладелецФайлов - Произвольный - объект-владелец.
//   ДанныеФайлов - Массив - неявно возвращаемое значение, массив структур, содержащих данные файлов,
//     свойства см. РаботаСФайлами.ДанныеФайла().
//
Процедура ПриПолученииПрисоединенныхФайлов(ВладелецФайлов, ДанныеФайлов) Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("РаботаСФайлами") Тогда
		Возврат;
	КонецЕсли;
	
	Файлы = Новый Массив;
	РаботаСФайлами.ЗаполнитьПрисоединенныеФайлыКОбъекту(ВладелецФайлов, Файлы);
	
	Для Каждого Файл Из Файлы Цикл
		ДанныеФайла = РаботаСФайлами.ДанныеФайла(Файл, Неопределено, Истина, Ложь);
		ДанныеФайлов.Добавить(ДанныеФайла);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//Возвращает структуру, описывающую "подчиненные" константы для указанной "родительской" константы.
//
//	Параметры:
//		ИмяРодительскойКонстанты 	  - Строка - имя константы как оно задано в конфигураторе
//		ЗначениеРодительскойКонстанты - Произвольный - значение константы
//
//	Возвращаемое значение:
//		Структура
//			Ключ 	 - имя подчиненной константы
//			Значение - значение подчиненной константы, соответствующее значению родительской константы
//
Функция ПолучитьДопустимыеЗначенияПодчиненныхКонстант(ИмяРодительскойКонстанты, ЗначениеРодительскойКонстанты) Экспорт
	
	Результат 		= Новый Структура;
	ТаблицаКонстант = ПолучитьТаблицуЗависимостиКонстант();
	
	ПодчиненныеКонстанты = ТаблицаКонстант.НайтиСтроки(
		Новый Структура(
			"ИмяРодительскойКонстанты, ЗначениеРодительскойКонстанты",
			ИмяРодительскойКонстанты, ЗначениеРодительскойКонстанты));
	
	Для Каждого СтрокаТаблицы Из ПодчиненныеКонстанты Цикл
		Результат.Вставить(СтрокаТаблицы.ИмяПодчиненнойКонстанты, СтрокаТаблицы.ЗначениеПодчиненнойКонстанты);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьТаблицуЗависимостиКонстант() Экспорт
	
	Результат = Новый ТаблицаЗначений;
	
	Результат.Колонки.Добавить("ИмяРодительскойКонстанты", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ИмяПодчиненнойКонстанты",  Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ЗначениеРодительскойКонстанты");
	Результат.Колонки.Добавить("ЗначениеПодчиненнойКонстанты");
	
	Результат.Индексы.Добавить("ИмяРодительскойКонстанты");
	Результат.Индексы.Добавить("ИмяПодчиненнойКонстанты");
	
	// Интеграция с 1С:Документооборот.
	ДобавитьСтрокуТаблицыЗависимостиКонстант(Результат,
		"ИспользоватьИнтеграциюС1СДокументооборот", 	  Ложь,   "ИспользоватьЕжедневныеОтчеты1СДокументооборота", 					Ложь);
	ДобавитьСтрокуТаблицыЗависимостиКонстант(Результат,
		"ИспользоватьИнтеграциюС1СДокументооборот", 	  Ложь,   "ИспользоватьЭлектроннуюПочту1СДокументооборота", 					Ложь);
	ДобавитьСтрокуТаблицыЗависимостиКонстант(Результат,
		"ИспользоватьИнтеграциюС1СДокументооборот", 	  Ложь,   "ИспользоватьФайловоеХранилище1СДокументооборота", 					Ложь);
	ДобавитьСтрокуТаблицыЗависимостиКонстант(Результат,
		"ИспользоватьИнтеграциюС1СДокументооборот", 	  Ложь,   "ИспользоватьПроцессыИЗадачи1СДокументооборота", 						Ложь);
	ДобавитьСтрокуТаблицыЗависимостиКонстант(Результат,
		"ИспользоватьИнтеграциюС1СДокументооборот", 	  Ложь,   "ИспользоватьСвязанныеДокументы1СДокументооборота", 					Ложь);
	ДобавитьСтрокуТаблицыЗависимостиКонстант(Результат,
		"ИспользоватьИнтеграциюС1СДокументооборот", 	  Ложь,   "ИспользоватьПрисоединенныеФайлы1СДокументооборота", 					Ложь);
	
	ДобавитьСтрокуИнвертируемыхКонстант(Результат, 
		"ИспользоватьПрисоединенныеФайлы1СДокументооборота", "ИспользоватьПрисоединенныеФайлыИнтегрированнойСистемы");
	ДобавитьСтрокуИнвертируемыхКонстант(Результат, 
		"ИспользоватьФайловоеХранилище1СДокументооборота", "ИспользоватьПрисоединенныеФайлыИнтегрированнойСистемы");
	
	Возврат Результат;
	
КонецФункции

Процедура ДобавитьСтрокуТаблицыЗависимостиКонстант(ТаблицаКонстант,
			ИмяРодительскойКонстанты, ЗначениеРодительскойКонстанты, ИмяПодчиненнойКонстанты, ЗначениеПодчиненнойКонстанты)
	
	НоваяСтрока = ТаблицаКонстант.Добавить();
	НоваяСтрока.ИмяРодительскойКонстанты 	  = ИмяРодительскойКонстанты;
	НоваяСтрока.ЗначениеРодительскойКонстанты = ЗначениеРодительскойКонстанты;
	НоваяСтрока.ИмяПодчиненнойКонстанты 	  = ИмяПодчиненнойКонстанты;
	НоваяСтрока.ЗначениеПодчиненнойКонстанты  = ЗначениеПодчиненнойКонстанты;
	
КонецПроцедуры

Процедура ДобавитьСтрокуИнвертируемыхКонстант(ТаблицаКонстант,
			ИмяРодительскойКонстанты, ИмяПодчиненнойКонстанты = "")
	
	Если ИмяПодчиненнойКонстанты = "" Тогда
		ИмяПодчиненнойКонстанты = "Не" + ИмяРодительскойКонстанты;
	КонецЕсли;
	
	ДобавитьСтрокуТаблицыЗависимостиКонстант(ТаблицаКонстант,
		ИмяРодительскойКонстанты, Ложь,   ИмяПодчиненнойКонстанты, Истина);
	ДобавитьСтрокуТаблицыЗависимостиКонстант(ТаблицаКонстант,
		ИмяРодительскойКонстанты, Истина, ИмяПодчиненнойКонстанты, Ложь);
	
КонецПроцедуры

#КонецОбласти
