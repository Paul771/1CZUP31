////////////////////////////////////////////////////////////////////////////////
// Обмен данными через универсальный формат Enterprise Data.
// Серверные процедуры и функции.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает признак существования настроенной синхронизации с конфигурацией "1С:ERP Управление предприятием 2".
// Параметры:
//   УдаляемыйУзел - ПланОбменаСсылка - Ссылка на удаляемый узел обмена.
// Возвращаемое значение:
//   Булево - Истина, если есть хотя бы одна настроенная синхронизация с "1С:ERP Управление предприятием 2".
//
Функция ИспользуетсяОбменУправлениеПредприятием2(УдаляемыйУзел = Неопределено) Экспорт
	Запрос = ОбменДаннымиУниверсальныйФормат.ЗапросУзлыОбменаСУправлениеПредприятием2();

	ЕстьОбменСУправлениеПредприятием = Ложь;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Не УдаляемыйУзел = Неопределено Тогда
		Пока Выборка.Следующий() Цикл
			Если Выборка.Ссылка = УдаляемыйУзел Тогда
				Продолжить;
			КонецЕсли;
			ЕстьОбменСУправлениеПредприятием = Истина;
			Прервать;
		КонецЦикла;
	Иначе
		Если Выборка.Следующий() Тогда
			ЕстьОбменСУправлениеПредприятием = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЕстьОбменСУправлениеПредприятием;
КонецФункции

// Определяет перечень доступных версий формата.
// Параметры:
//   ВерсииФормата - Соответствие - в данный параметр помещаются доступные версии формата.
//
Процедура ДоступныеВерсииФормата(ВерсииФормата) Экспорт
	
	ВерсииФормата.Вставить("1.0", МенеджерОбменаЧерезУниверсальныйФормат);
	ВерсииФормата.Вставить("1.1", МенеджерОбменаЧерезУниверсальныйФормат);
	ВерсииФормата.Вставить("1.2", МенеджерОбменаЧерезУниверсальныйФормат);
	ВерсииФормата.Вставить("1.3", МенеджерОбменаЧерезУниверсальныйФормат);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Вызывается при записи настройки синхронизации данных через универсальный формат.
// Используется для обновления настроек использования обмена с определенным корреспондентом.
//
// Параметры:
//    ПланОбменаОбъект - ПланОбменаОбъект.СинхронизацияДанныхЧерезУниверсальныйФормат - записываемый узел плана обмена.
//    ЭтоУдаление - Булево - признак того, что выполняется пометка на удаление или непосредственное удаление узла.
//
Процедура ОбновитьНастройкиИспользованияОбмена(ПланОбменаОбъект, ЭтоУдаление) Экспорт
	
	Если ПланОбменаОбъект.ЭтотУзел Тогда
		Возврат;
	КонецЕсли;

	// Обмен с 1С:ERP Управление предприятием 2.
	ОбменНастроенУП2 = Ложь;
	Если ЭтоУдаление Тогда
		ОбменНастроенУП2 = ОбменДаннымиУниверсальныйФормат.ИспользуетсяОбменУправлениеПредприятием2(ПланОбменаОбъект.Ссылка);
	Иначе
		ОбменНастроенУП2 = ОбменДаннымиУниверсальныйФормат.ИспользуетсяОбменУправлениеПредприятием2();
	КонецЕсли;
	ОбменДаннымиУниверсальныйФорматПереопределяемый.ОбновитьНастройкиИспользованияОбменаУправлениеПредприятием2(ОбменНастроенУП2);
	
КонецПроцедуры

// Возвращает запрос, содержащий все узлы обмена с конфигурацией "1С:ERP Управление предприятием 2".
//
Функция ЗапросУзлыОбменаСУправлениеПредприятием2() Экспорт
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СинхронизацияДанныхЧерезУниверсальныйФормат.Ссылка
	|ИЗ
	|	ПланОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат КАК СинхронизацияДанныхЧерезУниверсальныйФормат
	|ГДЕ
	|	НЕ СинхронизацияДанныхЧерезУниверсальныйФормат.ПометкаУдаления
	|	И НЕ СинхронизацияДанныхЧерезУниверсальныйФормат.ЭтотУзел
	|	И СинхронизацияДанныхЧерезУниверсальныйФормат.ВариантНастройки В(&МассивВариантов)");

	МассивВариантов = Новый Массив;
	МассивВариантов.Добавить("ОбменУП2ЗУП3");
	Запрос.УстановитьПараметр("МассивВариантов", МассивВариантов);
	
	Возврат Запрос;
КонецФункции

// Обновляет кэш значений настроек узлов планов обмена.
Процедура ОбновитьКэшМеханизмовРегистрации() Экспорт
	
	Если Не ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
		ОбменДаннымиВызовСервера.СброситьКэшМеханизмаРегистрацииОбъектов();
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// Добавляет в список Обработчики процедуры-обработчики обновления,
// необходимые данной подсистеме.
//
// Параметры:
//   Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//                                   общего модуля ОбновлениеИнформационнойБазы.
// 
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	ОбменДаннымиУниверсальныйФорматПереопределяемый.ЗарегистрироватьОбработчикиОбновления(Обработчики);
	
КонецПроцедуры

#КонецОбласти