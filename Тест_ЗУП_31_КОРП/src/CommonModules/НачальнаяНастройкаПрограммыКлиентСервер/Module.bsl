
#Область СлужебныеПроцедурыИФункции

// Формирование заголовка текущей страницы помощника начальной настройки и начального ввода данных.
//
// Параметры:
//		Форма - Управляемая форма
//
Функция ЗаголовокСтраницы(Форма) Экспорт

	ТекущаяСтраницаИмя = Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница.Имя;
	Если Форма.ЗаголовкиСтраниц.Свойство(ТекущаяСтраницаИмя) Тогда
		Возврат Форма.ЗаголовкиСтраниц[ТекущаяСтраницаИмя];
	КонецЕсли;
	
	Возврат "";

КонецФункции

// Обновление индикатора текущей страницы помощника начальной настройки, начального ввода данных.
//
// Параметры:
//		Форма - Управляемая форма
//
Процедура ОбновитьИндикатор(Форма) Экспорт

	ТекущаяСтраницаИмя = Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница.Имя;
	ИндексСтраницы = Форма.ПорядокСтраниц.Найти(ТекущаяСтраницаИмя);
	Если ИндексСтраницы <> Неопределено Тогда
		Если Форма.ПорядокСтраниц.Количество() <= 1 Тогда
			Форма.ИндикаторТекущаяСтраница = 100;
		Иначе
			Форма.ИндикаторТекущаяСтраница = 100 * ИндексСтраницы / (Форма.ПорядокСтраниц.Количество() - 1);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Обработка команды Вперед помощника начальной настройки, начального ввода данных.
//
// Параметры:
//		Форма - Управляемая форма
//
Процедура ОбработатьКомандуВперед(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	ПорядокСтраниц = Форма.ПорядокСтраниц;
	
	ТекущаяСтраницаИмя = Элементы.ГруппаСтраницы.ТекущаяСтраница.Имя;
	ИндексСтраницы = ПорядокСтраниц.Найти(ТекущаяСтраницаИмя);
	Если ИндексСтраницы <> Неопределено Тогда
		ИндексНовойСтраницы = ИндексСтраницы + 1;
	КонецЕсли;
	
	Если ИндексНовойСтраницы > ПорядокСтраниц.ВГраница() Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтраницаИмя = ПорядокСтраниц.Получить(ИндексНовойСтраницы);
	Если НоваяСтраницаИмя <> Неопределено Тогда
		НоваяСтраница = Элементы.Найти(НоваяСтраницаИмя);
		Если НоваяСтраница <> Неопределено Тогда
			Элементы.ГруппаСтраницы.ТекущаяСтраница = НоваяСтраница;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Обработка команды Назад помощника начальной настройки, начального ввода данных.
//
// Параметры:
//		Форма - Управляемая форма
//
Процедура ОбработатьКомандуНазад(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	ПорядокСтраниц = Форма.ПорядокСтраниц;
	
	ТекущаяСтраницаИмя = Элементы.ГруппаСтраницы.ТекущаяСтраница.Имя;
	ИндексСтраницы = ПорядокСтраниц.Найти(ТекущаяСтраницаИмя);
	Если ИндексСтраницы <> Неопределено Тогда
		ИндексНовойСтраницы = ИндексСтраницы - 1;
	КонецЕсли;
	
	Если ИндексНовойСтраницы < 0 Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтраницаИмя = ПорядокСтраниц.Получить(ИндексНовойСтраницы);
	Если НоваяСтраницаИмя <> Неопределено Тогда
		НоваяСтраница = Элементы.Найти(НоваяСтраницаИмя);
		Если НоваяСтраница <> Неопределено Тогда
			Элементы.ГруппаСтраницы.ТекущаяСтраница = НоваяСтраница;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Установка начальной страницы при открытии помощника начальной настройки, начального ввода данных.
//
// Параметры:
//		Форма - Управляемая форма
//		НачальнаяСтраницаИмя - Имя страницы или Неопределено.
//
Процедура УстановитьНачальнуюСтраницу(Форма, НачальнаяСтраницаИмя) Экспорт

	Элементы = Форма.Элементы;
	ПорядокСтраниц = Форма.ПорядокСтраниц;
	
	Если НачальнаяСтраницаИмя = Неопределено Тогда
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.Найти(ПорядокСтраниц[0]);
		
	Иначе
		
		ИндексСтраницы = ПорядокСтраниц.Найти(НачальнаяСтраницаИмя);
		Если ИндексСтраницы <> Неопределено Тогда
			НачальнаяСтраниц = Элементы.Найти(НачальнаяСтраницаИмя);
			Если НачальнаяСтраниц <> Неопределено Тогда
				Элементы.ГруппаСтраницы.ТекущаяСтраница = НачальнаяСтраниц;
			КонецЕсли;
		Иначе
			
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.Найти(ПорядокСтраниц[0]);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Функция ИмяФормыДляКомандыПереносДанныхИзПредыдущихПрограмм() Экспорт

	Возврат "Обработка.НачальнаяНастройкаПрограммы.Форма";

КонецФункции 


#КонецОбласти
