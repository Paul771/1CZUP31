
#Область СлужебныйПрограммныйИнтерфейс

#Область ГенерацияРеквизитовИЭлементовФорм

#Область ФормаШтатногоРасписания

// Дополняет форму элемента Штатного расписания информацией о кадровом резерве
//
Процедура ДополнитьФормуПозиции(Форма, ИмяГруппыКадровыйРезерв = "КадровыйРезервСтраница") Экспорт 
	
	Если НЕ КадровыйРезерв.КадровыйРезервДоступенПользователю() Тогда 
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, ИмяГруппыКадровыйРезерв, "Видимость", НЕ Форма.ВнешниеДанные);
	
	Если Форма.ВнешниеДанные Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьРеквизитыКадровогоРезерваВФормеШтатногоРасписания(Форма);
	
	УстановитьЗначениеСвязаннойПозицииРезерва(Форма);
	СформироватьИнфоСтрокуГруппРезерва(Форма);
	УстановитьПараметрыРеквизитаДинамическогоСпискаКадровыйРезервШР(Форма);
	
	СоздатьКомандуФормыСоздатьОткрытьВФормеШтатногоРасписания(Форма);
	СоздатьКомандыФормыКадровогоРезерва(Форма);
	СоздатьКомандыРейтинга(Форма);
	
	СоздатьЭлементыФормыКадровогоРезерваВФормеШтатногоРасписания(Форма);
	УстановитьСвойстваЭлементовФормыШтатногоРасписания(Форма);
	
КонецПроцедуры

// Устанавливает значение реквизита КадровыйРезервСвязаннаяПозицияРезерва формы элемента штатного расписания.
//
Процедура УстановитьЗначениеСвязаннойПозицииРезерва(Форма) Экспорт

	Если НЕ КадровыйРезерв.КадровыйРезервДоступенПользователю() Тогда 
		Возврат;
	КонецЕсли;
	
	СписокПозицийШР = КадровыйРезерв.ПодобратьПозицииРезерваПоПозицииШтатногоРасписания(Форма.Объект.Ссылка, Ложь);
	Если СписокПозицийШР.Количество() = 0 Тогда
		Форма.КадровыйРезервСвязаннаяПозицияРезерва = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.КадровыйРезерв.ПустаяСсылка");
	Иначе
		Форма.КадровыйРезервСвязаннаяПозицияРезерва = СписокПозицийШР[0];
	КонецЕсли;
	
КонецПроцедуры

// Формирует инфо-строку в форме элемента штатного расписания.
//
Процедура СформироватьИнфоСтрокуГруппРезерва(Форма) Экспорт

	Если НЕ КадровыйРезерв.КадровыйРезервДоступенПользователю() Тогда 
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	КадровыйРезерв.СоздатьВТКадровыйРезерв(Запрос.МенеджерВременныхТаблиц);
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КадровыйРезервДолжности.Ссылка КАК ПозицияРезерва,
		|	КадровыйРезерв.Представление КАК ПозицияРезерваПредставление
		|ПОМЕСТИТЬ ВТПозицияРезерва
		|ИЗ
		|	Справочник.КадровыйРезерв.Должности КАК КадровыйРезервДолжности
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КадровыйРезерв КАК КадровыйРезерв
		|		ПО КадровыйРезервДолжности.Ссылка = КадровыйРезерв.Ссылка
		|ГДЕ
		|	КадровыйРезервДолжности.Должность = &Должность
		|	И НЕ КадровыйРезерв.ВАрхиве
		|	И НЕ КадровыйРезерв.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПозицияРезерва.ПозицияРезерва,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ КадровыйРезерв.ФизическоеЛицо) КАК Количество,
		|	ВТПозицияРезерва.ПозицияРезерваПредставление
		|ИЗ
		|	ВТКадровыйРезерв КАК КадровыйРезерв
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПозицияРезерва КАК ВТПозицияРезерва
		|		ПО КадровыйРезерв.ПозицияРезерва = ВТПозицияРезерва.ПозицияРезерва
		|ГДЕ
		|	КадровыйРезерв.Статус В(&Статусы)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТПозицияРезерва.ПозицияРезерва,
		|	ВТПозицияРезерва.ПозицияРезерваПредставление";
	
	Запрос.УстановитьПараметр("Должность", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Форма.Объект.Ссылка, "Должность"));
	Запрос.УстановитьПараметр("Статусы", ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Согласовано"));
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстНадписи = "";
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МассивСтрок = Новый Массив;
		Если ЗначениеЗаполнено(ТекстНадписи) Тогда
			МассивСтрок.Добавить(Новый ФорматированнаяСтрока(", "));
		КонецЕсли;
		СсылкаПозиции = Строка(ВыборкаДетальныеЗаписи.ПозицияРезерва.УникальныйИдентификатор());
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(ВыборкаДетальныеЗаписи.ПозицияРезерваПредставление,,,,СсылкаПозиции));
		ТекстКоличества = " " + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru ='(%1 чел.)'"), ВыборкаДетальныеЗаписи.Количество);
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(ТекстКоличества));
		ТекстНадписи = Новый ФорматированнаяСтрока(ТекстНадписи, Новый ФорматированнаяСтрока(МассивСтрок));
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ТекстНадписи) Тогда
		Форма.КадровыйРезервИнфоНадписьГруппРезерва = Новый ФорматированнаяСтрока(НСтр("ru = 'Данная позиция включена в следующие группы резерва:'") + " ", ТекстНадписи);
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает свойства элементов формы объекта справочника ШтатноеРасписание
//
Процедура УстановитьСвойстваЭлементовФормыШтатногоРасписания(Форма) Экспорт

	Если НЕ КадровыйРезерв.КадровыйРезервДоступенПользователю() Тогда 
		Возврат;
	КонецЕсли;
	
	КлючеваяПозицияШтатногоРасписания = ЗначениеЗаполнено(Форма.КадровыйРезервСвязаннаяПозицияРезерва);
	Если КлючеваяПозицияШтатногоРасписания Тогда
		Заголовок = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Форма.КадровыйРезервСвязаннаяПозицияРезерва, "Наименование");
	Иначе
		Заголовок = НСтр("ru = 'Создать кадровый резерв'");
	КонецЕсли;
	
	УстановитьЗаголовокКомандыСоздатьОткрытьВФормеШтатногоРасписания(Форма, КлючеваяПозицияШтатногоРасписания, Заголовок);
	УстановитьДоступностьКомандКадровогоРезерва(Форма, КлючеваяПозицияШтатногоРасписания);

КонецПроцедуры

#КонецОбласти

// Создает команду "Кадровый резерв" в формах элементов Сотрудники и ФизическиеЛица
//
Процедура СоздатьКомандуКадровыйРезервВФорме(Форма, ГруппаКнопки) Экспорт

	Если НЕ КадровыйРезерв.КадровыйРезервДоступенПользователю() Тогда 
		Возврат;
	КонецЕсли;
	
	Если Форма.Команды.Найти("КадровыйРезерв") = Неопределено Тогда
		КомандаКадровыйРезерв = Форма.Команды.Добавить("КадровыйРезерв");
		КомандаКадровыйРезерв.Действие = "Подключаемый_КадровыйРезерв";
		КомандаКадровыйРезерв.Заголовок = НСтр("ru = 'Кадровый резерв'");
	КонецЕсли;
	
	// Добавляем саму кнопку
	Если Форма.Элементы.Найти("КадровыйРезерв") = Неопределено Тогда
		Элемент = Форма.Элементы.Вставить("КадровыйРезерв", Тип("КнопкаФормы"), ГруппаКнопки);
		Элемент.Вид = ВидКнопкиФормы.Гиперссылка;
		Элемент.ИмяКоманды = "КадровыйРезерв"; 
	КонецЕсли;

КонецПроцедуры

// Создает команды рейтингования резервиста.
//
Процедура СоздатьКомандыРейтинга(Форма) Экспорт

	Если НЕ КадровыйРезерв.КадровыйРезервДоступенПользователю() Тогда 
		Возврат;
	КонецЕсли;
	
	Для Сч=1 По 5 Цикл
		РейтингНаименование = "Рейтинг" + Строка(Сч);
		Если Форма.Команды.Найти(РейтингНаименование) = Неопределено Тогда
			КомандаКадровыйРезерв = Форма.Команды.Добавить(РейтингНаименование);
			КомандаКадровыйРезерв.Действие = "Подключаемый_" + РейтингНаименование;
			КомандаКадровыйРезерв.Заголовок = Строка(Сч);
		КонецЕсли;
	КонецЦикла; 

	Если Форма.Команды.Найти("РейтингСбросить") = Неопределено Тогда
		КомандаКадровыйРезерв = Форма.Команды.Добавить("РейтингСбросить");
		КомандаКадровыйРезерв.Действие = "Подключаемый_РейтингСбросить";
		КомандаКадровыйРезерв.Заголовок = НСтр("ru = 'Сбросить'");
	КонецЕсли;
	
КонецПроцедуры

// Создает команды рейтингования резервиста.
//
Процедура СоздатьЭлементыФормыКомандыРейтинга(Форма, ГруппаКоманд) Экспорт

	Если НЕ КадровыйРезерв.КадровыйРезервДоступенПользователю() Тогда 
		Возврат;
	КонецЕсли;
	
	Для Сч=1 По 5 Цикл
		РейтингНаименование = "Рейтинг" + Строка(Сч);
		Если Форма.Элементы.Найти(РейтингНаименование) = Неопределено Тогда
			Элемент = Форма.Элементы.Вставить(РейтингНаименование, Тип("КнопкаФормы"), ГруппаКоманд);
			Элемент.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
			Элемент.ИмяКоманды = РейтингНаименование; 
		КонецЕсли;
	КонецЦикла; 

	Если Форма.Элементы.Найти("РейтингСбросить") = Неопределено Тогда
		Элемент = Форма.Элементы.Вставить("РейтингСбросить", Тип("КнопкаФормы"), ГруппаКоманд);
		Элемент.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
		Элемент.ИмяКоманды = "РейтингСбросить"; 
	КонецЕсли;

КонецПроцедуры

// Создает команды "Включить в резерв", "Создать заявку", "Исключить из резерва" в формах.
Процедура СоздатьКомандыФормыКадровогоРезерва(Форма) Экспорт

	Если НЕ КадровыйРезерв.КадровыйРезервДоступенПользователю() Тогда 
		Возврат;
	КонецЕсли;
	
	Если Форма.Команды.Найти("ВключениеВКадровыйРезерв") = Неопределено Тогда
		КомандаКадровыйРезерв = Форма.Команды.Добавить("ВключениеВКадровыйРезерв");
		КомандаКадровыйРезерв.Действие = "Подключаемый_ВключениеВКадровыйРезерв";
		КомандаКадровыйРезерв.Заголовок = НСтр("ru = 'Включить в резерв'");
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьЗаявкиНаВключениеВКадровыйРезерв")
		И Форма.Команды.Найти("ЗаявкаНаВключениеВКадровыйРезерв") = Неопределено Тогда
		КомандаКадровыйРезерв = Форма.Команды.Добавить("ЗаявкаНаВключениеВКадровыйРезерв");
		КомандаКадровыйРезерв.Действие = "Подключаемый_ЗаявкаНаВключениеВКадровыйРезерв";
		КомандаКадровыйРезерв.Заголовок = НСтр("ru = 'Создать заявку'");
	КонецЕсли;
	
	Если Форма.Команды.Найти("ИсключениеИзКадровогоРезерва") = Неопределено Тогда
		КомандаКадровыйРезерв = Форма.Команды.Добавить("ИсключениеИзКадровогоРезерва");
		КомандаКадровыйРезерв.Действие = "Подключаемый_ИсключениеИзКадровогоРезерва";
		КомандаКадровыйРезерв.Заголовок = НСтр("ru = 'Исключить из резерва'");
	КонецЕсли;
	
КонецПроцедуры

// Создает элементы формы команд "Включить в резерв", "Создать заявку", "Исключить из резерва" в формах.
Процедура СоздатьЭлементыФормыКомандыКадровогоРезерва(Форма, ГруппаПосадкаФормы) Экспорт

	Если НЕ КадровыйРезерв.КадровыйРезервДоступенПользователю() Тогда 
		Возврат;
	КонецЕсли;
	
	СозданиеВКоманднойПанели = ГруппаПосадкаФормы.Вид = ВидГруппыФормы.КоманднаяПанель ИЛИ ГруппаПосадкаФормы.Вид = ВидГруппыФормы.ГруппаКнопок;
	Если СозданиеВКоманднойПанели Тогда
		ВидКнопок = ВидКнопкиФормы.КнопкаКоманднойПанели;
	Иначе
		ВидКнопок = ВидКнопкиФормы.ОбычнаяКнопка;
	КонецЕсли;

	Если НЕ СозданиеВКоманднойПанели Тогда
		ГруппаФормы = Форма.Элементы.Найти("ГруппаКомандыКадровогоРезерва");
		Если ГруппаФормы = Неопределено Тогда
			// Создаем группу
			ГруппаФормы = Форма.Элементы.Вставить("ГруппаКомандыКадровогоРезерва", Тип("ГруппаФормы"), ГруппаПосадкаФормы);
			ГруппаФормы.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			ГруппаФормы.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
			ГруппаФормы.Отображение = ОтображениеОбычнойГруппы.Нет;
			ГруппаФормы.ОтображатьЗаголовок = Ложь;
		КонецЕсли;
	Иначе
		ГруппаФормы = ГруппаПосадкаФормы;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьЗаявкиНаВключениеВКадровыйРезерв")
		И Форма.Элементы.Найти("ФормаЗаявкаНаВключениеВКадровыйРезерв") = Неопределено Тогда
		// Добавляем элементы
		Элемент = Форма.Элементы.Вставить("ФормаЗаявкаНаВключениеВКадровыйРезерв", Тип("КнопкаФормы"), ГруппаФормы);
		Элемент.Вид = ВидКнопок;
		Элемент.ИмяКоманды = "ЗаявкаНаВключениеВКадровыйРезерв"; 
	КонецЕсли;
	
	Если Форма.Элементы.Найти("ФормаВключениеВКадровыйРезерв") = Неопределено Тогда
		Элемент = Форма.Элементы.Вставить("ФормаВключениеВКадровыйРезерв", Тип("КнопкаФормы"), ГруппаФормы);
		Элемент.Вид = ВидКнопок;
		Элемент.ИмяКоманды = "ВключениеВКадровыйРезерв"; 
	КонецЕсли;
	
	Если Форма.Элементы.Найти("ФормаИсключениеИзКадровогоРезерва") = Неопределено Тогда
		Элемент = Форма.Элементы.Вставить("ФормаИсключениеИзКадровогоРезерва", Тип("КнопкаФормы"), ГруппаФормы);
		Элемент.Вид = ВидКнопок;
		Элемент.ИмяКоманды = "ИсключениеИзКадровогоРезерва"; 
	КонецЕсли;
	
КонецПроцедуры

// Дополняет форму элемента документов информацией о кадровом резерве
// 
Процедура ДополнитьФормуДокументаКадровымРезервом(Форма, СоздаватьКолонкуСтатусаВТаблице = Истина, ИмяГруппыКадровыйРезерв = "КадровыйРезервСтраница", ДобавлятьЭлементыФормы = Истина, ДобавлятьРеквизитыФормы = Истина, ОтложенноеИзменение = Ложь) Экспорт 
	
	Если НЕ КадровыйРезерв.КадровыйРезервДоступенПользователю() Тогда 
		Возврат;
	КонецЕсли;
	
	СоздаватьКолонкуСтатусаВТаблице = ПолучитьФункциональнуюОпцию("ИспользоватьЗаявкиНаВключениеВКадровыйРезерв") И СоздаватьКолонкуСтатусаВТаблице;
	
	СоздатьТаблицуКадровыйРезервВФормеОбщегоДокумента(Форма, СоздаватьКолонкуСтатусаВТаблице, ДобавлятьЭлементыФормы, ДобавлятьРеквизитыФормы, ОтложенноеИзменение);
	
	Если ДобавлятьЭлементыФормы Тогда
		СоздатьКомандыКадровогоРезерваВФормеОбщегоДокумента(Форма);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, ИмяГруппыКадровыйРезерв, "Видимость", Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбслуживаниеФорм

// Для справочника ШтатноеРасписание обновляет динамический список
//
Процедура ОбновитьКадровыйРезервПозицииШР(Форма) Экспорт
	
	Если НЕ КадровыйРезерв.КадровыйРезервДоступенПользователю() Тогда 
		Возврат;
	КонецЕсли;
	
	Если НЕ Форма.Элементы.Найти("КадровыйРезервПозицииШР") = Неопределено Тогда
		Форма.Элементы.КадровыйРезервПозицииШР.Обновить();
	КонецЕсли;
	
КонецПроцедуры

// Заполняет табличную часть позициями резерва по физическому лицу
// Параметры
// 	ТабличнаяЧастьРезерва - табличная часть документа
// 	ФизическоеЛицо
// 	ДатаНачала
// 	ДатаНачала
// 	Действие - действие по умолчанию, устанавливаемое для новых строк табличной части.
//
Процедура ЗаполнитьПозицииРезерваПоСотруднику(ПараметрыЗаполнения) Экспорт
	
	Если ПараметрыЗаполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Проверка переданных параметров.
	Если ПараметрыЗаполнения.Свойство("ТабличнаяЧасть") Тогда
		ТабличнаяЧастьРезерва = ПараметрыЗаполнения.ТабличнаяЧасть;
	Иначе
		Возврат;
	КонецЕсли;
	
	Если НЕ КадровыйРезерв.КадровыйРезервДоступенПользователю() Тогда 
		Возврат;
	КонецЕсли;
	
	ТабличнаяЧастьРезерва.Очистить();
	
	Если ПараметрыЗаполнения.Свойство("ФизическоеЛицо") И ЗначениеЗаполнено(ПараметрыЗаполнения.ФизическоеЛицо) Тогда
		ФизическоеЛицо = ПараметрыЗаполнения.ФизическоеЛицо;
	Иначе
		Возврат;
	КонецЕсли;
	
	Если ПараметрыЗаполнения.Свойство("ДатаНачала") Тогда
		ДатаСреза = ПараметрыЗаполнения.ДатаНачала;
	Иначе
		ДатаСреза = ТекущаяДатаСеанса();
	КонецЕсли;
	Если ПараметрыЗаполнения.Свойство("Действие") Тогда
		ПредопределенноеДействие = ПараметрыЗаполнения.Действие;
	Иначе
		ПредопределенноеДействие = Неопределено;
	КонецЕсли;
	
	Отбор = Новый Структура("Статус, ФизическоеЛицо");
	Отбор.ФизическоеЛицо = ФизическоеЛицо;
	МассивСтатусов = Новый Массив;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьЗаявкиНаВключениеВКадровыйРезерв") Тогда
		МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Рассматривается"));
	КонецЕсли;
	МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Согласовано"));
	Отбор.Статус = МассивСтатусов;
	
	СписокПозицийРезерваСотрудника = КадровыйРезерв.КадровыйРезервНаДату(ДатаСреза, Отбор);
	
	Если СписокПозицийРезерваСотрудника = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого ПозицияРезерва Из СписокПозицийРезерваСотрудника Цикл
		НоваяПозиция = ТабличнаяЧастьРезерва.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПозиция, ПозицияРезерва);
		НоваяПозиция.Действие = ПредопределенноеДействие;
		КадровыйРезервКлиентСервер.ЗаполнитьОписаниеДействия(НоваяПозиция);
		КадровыйРезервКлиентСервер.ЗаполнитьОписаниеСтатуса(НоваяПозиция);
	КонецЦикла; 

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ФормаШтатногоРасписания

Процедура СоздатьКомандуФормыСоздатьОткрытьВФормеШтатногоРасписания(Форма)

	Если Форма.Команды.Найти("СоздатьОткрытьПозициюКадровогоРезерваШтатногоРасписания") = Неопределено Тогда
		// Добавляем команду формы
		КомандаКадровыйРезерв = Форма.Команды.Добавить("СоздатьОткрытьПозициюКадровогоРезерваШтатногоРасписания");
		КомандаКадровыйРезерв.Действие = "Подключаемый_СоздатьОткрытьПозициюКадровогоРезерваШтатногоРасписания";
		КомандаКадровыйРезерв.Заголовок = НСтр("ru = 'Создать кадровый резерв'");
	КонецЕсли;
		
КонецПроцедуры

Процедура СоздатьРеквизитыКадровогоРезерваВФормеШтатногоРасписания(Форма)

	// Создание реквизитов формы.
	МассивРеквизитов = Новый Массив;
	// Связанная позиция
	РеквизитКадровыйРезервСвязаннаяПозицияРезерва = Новый РеквизитФормы("КадровыйРезервСвязаннаяПозицияРезерва", Новый ОписаниеТипов("СправочникСсылка.КадровыйРезерв"));
	МассивРеквизитов.Добавить(РеквизитКадровыйРезервСвязаннаяПозицияРезерва);
	// ИнфоНадпись
	РеквизитКадровыйРезервИнфоНадписьГруппРезерва = Новый РеквизитФормы("КадровыйРезервИнфоНадписьГруппРезерва", Новый ОписаниеТипов("ФорматированнаяСтрока"));
	МассивРеквизитов.Добавить(РеквизитКадровыйРезервИнфоНадписьГруппРезерва);
	// ДинамическийСписок
	РеквизитКадровыйРезервПозицииШР = Новый РеквизитФормы("КадровыйРезервПозицииШР", Новый ОписаниеТипов("ДинамическийСписок"));
	МассивРеквизитов.Добавить(РеквизитКадровыйРезервПозицииШР);
	
	МассивИменРеквизитовФормы = Новый Массив;
	ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(Форма, МассивИменРеквизитовФормы);
	ЗарплатаКадры.ИзменитьРеквизитыФормы(Форма, МассивРеквизитов, МассивИменРеквизитовФормы);
	
КонецПроцедуры

Процедура УстановитьПараметрыРеквизитаДинамическогоСпискаКадровыйРезервШР(Форма)

	УстановитьЗапросДинамическогоСпискаКадровыйРезервШР(Форма);
	УстановитьСортировкуДинамическогоСпискаКадровыйРезервШР(Форма);
	УстановитьУсловноеОформлениеДинамическогоСпискаКадровыйРезервШР(Форма);
	УстановитьПараметрыДинамическогоСпискаКадровыйРезервШР(Форма);
	УстановитьОбязательноеИспользованиеПолейДинамическогоСпискаКадровыйРезервШР(Форма);
	
КонецПроцедуры

Процедура УстановитьЗапросДинамическогоСпискаКадровыйРезервШР(Форма)

	РеквизитКадровыйРезервПозицииШР = Форма.КадровыйРезервПозицииШР;
	
	РеквизитКадровыйРезервПозицииШР.ПроизвольныйЗапрос = Истина;
	РеквизитКадровыйРезервПозицииШР.ТекстЗапроса = ТекстЗапросаДинамическогоСпискаКадровыйРезервПозицииШР();

КонецПроцедуры

Функция ТекстЗапросаДинамическогоСпискаКадровыйРезервПозицииШР()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	КадровыйРезерв.ПозицияРезерва КАК ПозицияРезерва,
	|	КадровыйРезерв.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВЫБОР
	|		КОГДА КадровыйРезерв.Статус = ЗНАЧЕНИЕ(Перечисление.СостоянияСогласования.Рассматривается)
	|			ТОГДА &Кандидат
	|		КОГДА КадровыйРезерв.Статус = ЗНАЧЕНИЕ(Перечисление.СостоянияСогласования.Согласовано)
	|			ТОГДА &ЗачисленВРезерв
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Статус,
	|	КадровыйРезерв.ДатаВключения КАК ДатаВключения,
	|	КадровыйРезерв.ДатаИсключения КАК ДатаИсключения,
	|	КадровыйРезерв.ВидРезерва КАК ВидРезерва,
	|	КадровыйРезерв.Рейтинг КАК Рейтинг,
	|	ВЫБОР
	|		КОГДА КадровыйРезерв.ДатаИсключения = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА КадровыйРезерв.ДатаИсключения > &ТекущаяДата
	|					ТОГДА ЛОЖЬ
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ
	|	КОНЕЦ КАК Исключен
	|ИЗ
	|	Справочник.КадровыйРезерв КАК ШтатныйРезерв
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КадровыйРезерв КАК КадровыйРезерв
	|		ПО ШтатныйРезерв.Ссылка = КадровыйРезерв.ПозицияРезерва
	|			И (НЕ ШтатныйРезерв.ВАрхиве)
	|			И (ШтатныйРезерв.Назначение = ЗНАЧЕНИЕ(Перечисление.НазначенияКадровогоРезерва.ПозицияШтатногоРасписания))
	|			И (ШтатныйРезерв.ПозицияШтатногоРасписания = &ПозицияШтатногоРасписания)
	|ГДЕ
	|	КадровыйРезерв.Статус В(&Статусы)
	|	И НЕ ВЫБОР
	|				КОГДА КадровыйРезерв.ДатаИсключения = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА ЛОЖЬ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА КадровыйРезерв.ДатаИсключения > &ТекущаяДата
	|							ТОГДА ЛОЖЬ
	|						ИНАЧЕ ИСТИНА
	|					КОНЕЦ
	|			КОНЕЦ";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура УстановитьСортировкуДинамическогоСпискаКадровыйРезервШР(Форма)

	ПорядокДинамическогоСписка = Форма.КадровыйРезервПозицииШР.Порядок;
	ПорядокДинамическогоСписка.Элементы.Очистить();
	
	ЭлементПорядка = ПорядокДинамическогоСписка.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
	ЭлементПорядка.Поле = Новый ПолеКомпоновкиДанных("Статус");
	ЭлементПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
	ЭлементПорядка.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ЭлементПорядка.Использование = Истина;

	ЭлементПорядка = ПорядокДинамическогоСписка.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
	ЭлементПорядка.Поле = Новый ПолеКомпоновкиДанных("ВидРезерва");
	ЭлементПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
	ЭлементПорядка.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ЭлементПорядка.Использование = Истина;

КонецПроцедуры

Процедура УстановитьУсловноеОформлениеДинамическогоСпискаКадровыйРезервШР(Форма)

	УсловноеОформлениеРейтинга = Форма.КадровыйРезервПозицииШР.КомпоновщикНастроек.Настройки.УсловноеОформление;
	УсловноеОформлениеРейтинга.Элементы.Очистить();
	Для Сч = 1 По 5 Цикл
		
		ЭлементУсловногоОформления = УсловноеОформлениеРейтинга.Элементы.Добавить();
		ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		
		ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Рейтинг");
		ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбораДанных.ПравоеЗначение = Сч;
		ЭлементОтбораДанных.Использование = Истина;
		
		ЭлементТекстаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("Текст");
		ТекстПоля = "";
		Для СчСимволов =1 По Сч Цикл
			ТекстПоля = ТекстПоля + "";
		КонецЦикла;
		ЭлементТекстаОформления.Значение = ТекстПоля;
		ЭлементТекстаОформления.Использование = Истина;
		
		ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("Рейтинг");
		ОформляемоеПоле.Использование = Истина;
		
	КонецЦикла; 

КонецПроцедуры

Процедура УстановитьПараметрыДинамическогоСпискаКадровыйРезервШР(Форма)
	
	ДолжностьПозицииШтатногоРасписания = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Форма.Объект.Ссылка, "Должность");
	Статусы = Новый Массив;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьЗаявкиНаВключениеВКадровыйРезерв") Тогда
		Статусы.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Рассматривается"));
	КонецЕсли;
	Статусы.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Согласовано"));
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Форма.КадровыйРезервПозицииШР, "ПозицияШтатногоРасписания", Форма.Объект.Ссылка, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Форма.КадровыйРезервПозицииШР, "ТекущаяДата", ТекущаяДатаСеанса(), Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Форма.КадровыйРезервПозицииШР, "Статусы", Статусы, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Форма.КадровыйРезервПозицииШР, "Кандидат", НСтр("ru = 'Кандидат'"), Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Форма.КадровыйРезервПозицииШР, "ЗачисленВРезерв", НСтр("ru = 'Зачислен в резерв'"), Истина);
	
КонецПроцедуры

Процедура УстановитьОбязательноеИспользованиеПолейДинамическогоСпискаКадровыйРезервШР(Форма)

	РеквизитКадровыйРезервПозицииШР = Форма.КадровыйРезервПозицииШР;
	
	РеквизитКадровыйРезервПозицииШР.УстановитьОбязательноеИспользование("ФизическоеЛицо", Истина);
	РеквизитКадровыйРезервПозицииШР.УстановитьОбязательноеИспользование("ВидРезерва", Истина);

КонецПроцедуры

Процедура УстановитьЗаголовокКомандыСоздатьОткрытьВФормеШтатногоРасписания(Форма, КлючеваяПозицияШтатногоРасписания, Заголовок = "")
	
	Форма.Команды.СоздатьОткрытьПозициюКадровогоРезерваШтатногоРасписания.Заголовок = Заголовок;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ДекорацияЗаголовокКомандыПозицииРезерва",
		"Видимость",
		КлючеваяПозицияШтатногоРасписания);
	
КонецПроцедуры

Процедура УстановитьДоступностьКомандКадровогоРезерва(Форма, КомандыДоступны)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ФормаЗаявкаНаВключениеВКадровыйРезерв",
		"Доступность",
		КомандыДоступны);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ФормаВключениеВКадровыйРезерв",
		"Доступность",
		КомандыДоступны);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ФормаИсключениеИзКадровогоРезерва",
		"Доступность",
		КомандыДоступны);

КонецПроцедуры

Процедура СоздатьЭлементыФормыКадровогоРезерваВФормеШтатногоРасписания(Форма)

	СоздатьЭлементыФормыКомандаСоздатьОткрытьПозициюКадровогоРезерваШтатногоРасписания(Форма);
	СоздатьЭлементыФормыТаблицаКадровыйРезерв(Форма);
	СоздатьЭлементыФормыКомандыКадровогоРезерва(Форма, Форма.Элементы.КадровыйРезервПозицииШРКоманднаяПанель);
	
	СоздатьЭлементыФормыКомандыРейтинга(Форма, Форма.Элементы.Рейтинг);
	СоздатьЭлементыФормыИнфоНадписьГруппРезерва(Форма, Форма.Элементы.ГруппаКадровогоРезерва);
	СоздатьЭлементыФормыДекорацияПояснение(Форма, Форма.Элементы.ГруппаКадровогоРезерва);
	
КонецПроцедуры

Процедура СоздатьЭлементыФормыКомандаСоздатьОткрытьПозициюКадровогоРезерваШтатногоРасписания(Форма)

	ГруппаФормы = Форма.Элементы.Найти("ГруппаКомандыСоздатьОткрытьПозицию");
	Если ГруппаФормы = Неопределено Тогда
		// Создаем группу
		ГруппаФормы = Форма.Элементы.Вставить("ГруппаКомандыСоздатьОткрытьПозицию", Тип("ГруппаФормы"), Форма.Элементы.ГруппаКадровогоРезерва);
		ГруппаФормы.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаФормы.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		ГруппаФормы.Отображение = ОтображениеОбычнойГруппы.Нет;
		ГруппаФормы.ОтображатьЗаголовок = Ложь;
	КонецЕсли;
	
	Если Форма.Элементы.Найти("ДекорацияЗаголовокКомандыПозицииРезерва") = Неопределено Тогда
		// Добавляем декорацию
		Элемент = Форма.Элементы.Вставить("ДекорацияЗаголовокКомандыПозицииРезерва", Тип("ДекорацияФормы"), ГруппаФормы);
		Элемент.Вид = ВидДекорацииФормы.Надпись;
		Элемент.Заголовок = НСтр("ru = 'Позиция резерва:'") + " ";
	КонецЕсли;

	Если Форма.Элементы.Найти("СоздатьОткрытьПозициюКадровогоРезерваШтатногоРасписания") = Неопределено Тогда
		// Добавляем саму кнопку
		Элемент = Форма.Элементы.Вставить("СоздатьОткрытьПозициюКадровогоРезерваШтатногоРасписания", Тип("КнопкаФормы"), ГруппаФормы);
		Элемент.Вид = ВидКнопкиФормы.Гиперссылка;
		Элемент.ИмяКоманды = "СоздатьОткрытьПозициюКадровогоРезерваШтатногоРасписания"; 
	КонецЕсли;

КонецПроцедуры

Процедура СоздатьЭлементыФормыТаблицаКадровыйРезерв(Форма)

	ГруппаСписка = Форма.Элементы.Найти("ГруппаСпискаКадровогоРезерва");
	Если ГруппаСписка = Неопределено Тогда
		// Создаем группу.
		ГруппаСписка = Форма.Элементы.Вставить("ГруппаСпискаКадровогоРезерва", Тип("ГруппаФормы"), Форма.Элементы.ГруппаКадровогоРезерва);
		ГруппаСписка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаСписка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		ГруппаСписка.Отображение = ОтображениеОбычнойГруппы.Нет;
		ГруппаСписка.ОтображатьЗаголовок = Ложь;
	КонецЕсли;
	
	ТаблицаКадровыйРезерв = Форма.Элементы.Найти("КадровыйРезервПозицииШР");
	Если ТаблицаКадровыйРезерв = Неопределено Тогда
		// Создаем таблицу.
		ТаблицаКадровыйРезерв = Форма.Элементы.Вставить("КадровыйРезервПозицииШР", Тип("ТаблицаФормы"), ГруппаСписка);
		ТаблицаКадровыйРезерв.ПутьКДанным = "КадровыйРезервПозицииШР";
		ТаблицаКадровыйРезерв.ТолькоПросмотр = Истина;
		ТаблицаКадровыйРезерв.УстановитьДействие("Выбор", "Подключаемый_КадровыйРезервПозицииШРВыбор");
	КонецЕсли;
	
	Если Форма.Элементы.Найти("Рейтинг") = Неопределено Тогда
		// Создаем контекстное меню.
		ГруппаКомандРейтинга = Форма.Элементы.Вставить("Рейтинг", Тип("ГруппаФормы"), ТаблицаКадровыйРезерв.КонтекстноеМеню);
		ГруппаКомандРейтинга.Вид = ВидГруппыФормы.Подменю;
		ГруппаКомандРейтинга.Заголовок = НСтр("ru = 'Оценить как'");
	КонецЕсли;
	
	Если Форма.Элементы.Найти("КадровыйРезервПозицииШРВидРезерва") = Неопределено Тогда
		// Создаем элементы таблицы.
		ПолеКадровыйРезервПозицииШРВидРезерва = Форма.Элементы.Вставить("КадровыйРезервПозицииШРВидРезерва", Тип("ПолеФормы"), ТаблицаКадровыйРезерв);
		ПолеКадровыйРезервПозицииШРВидРезерва.ПутьКДанным = "КадровыйРезервПозицииШР.ВидРезерва";
	КонецЕсли;
	
	Если Форма.Элементы.Найти("КадровыйРезервПозицииШРФизическоеЛицо") = Неопределено Тогда
		ПолеКадровыйРезервПозицииШРФизическоеЛицо = Форма.Элементы.Вставить("КадровыйРезервПозицииШРФизическоеЛицо", Тип("ПолеФормы"), ТаблицаКадровыйРезерв);
		ПолеКадровыйРезервПозицииШРФизическоеЛицо.ПутьКДанным = "КадровыйРезервПозицииШР.ФизическоеЛицо";
		ПолеКадровыйРезервПозицииШРФизическоеЛицо.Заголовок = НСтр("ru = 'Резервист'");
	КонецЕсли;
	
	Если Форма.Элементы.Найти("КадровыйРезервПозицииШРСтатус") = Неопределено Тогда
		ПолеКадровыйРезервПозицииШРСтатус = Форма.Элементы.Вставить("КадровыйРезервПозицииШРСтатус", Тип("ПолеФормы"), ТаблицаКадровыйРезерв);
		ПолеКадровыйРезервПозицииШРСтатус.ПутьКДанным = "КадровыйРезервПозицииШР.Статус";
	КонецЕсли;
	
	Если Форма.Элементы.Найти("КадровыйРезервПозицииШРРейтинг") = Неопределено Тогда
		ПолеКадровыйРезервПозицииШРРейтинг = Форма.Элементы.Вставить("КадровыйРезервПозицииШРРейтинг", Тип("ПолеФормы"), ТаблицаКадровыйРезерв);
		ПолеКадровыйРезервПозицииШРРейтинг.ПутьКДанным = "КадровыйРезервПозицииШР.Рейтинг";
		ПолеКадровыйРезервПозицииШРРейтинг.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
		ПолеКадровыйРезервПозицииШРРейтинг.ЦветТекста = Новый Цвет(255, 211, 47);
		ШрифтЗвездный = Новый Шрифт("Wingdings", 16,,,,,100);
		ПолеКадровыйРезервПозицииШРРейтинг.Шрифт = ШрифтЗвездный;
	КонецЕсли;
	
	Если Форма.Элементы.Найти("КадровыйРезервПозицииШРДатаВключения") = Неопределено Тогда
		ПолеКадровыйРезервПозицииШРДатаВключения = Форма.Элементы.Вставить("КадровыйРезервПозицииШРДатаВключения", Тип("ПолеФормы"), ТаблицаКадровыйРезерв);
		ПолеКадровыйРезервПозицииШРДатаВключения.ПутьКДанным = "КадровыйРезервПозицииШР.ДатаВключения";
	КонецЕсли;
	
	Если Форма.Элементы.Найти("КадровыйРезервПозицииШРДатаИсключения") = Неопределено Тогда
		ПолеКадровыйРезервПозицииШРДатаИсключения = Форма.Элементы.Вставить("КадровыйРезервПозицииШРДатаИсключения", Тип("ПолеФормы"), ТаблицаКадровыйРезерв);
		ПолеКадровыйРезервПозицииШРДатаИсключения.ПутьКДанным = "КадровыйРезервПозицииШР.ДатаИсключения";
	КонецЕсли;

КонецПроцедуры

Процедура СоздатьЭлементыФормыИнфоНадписьГруппРезерва(Форма, ГруппаПосадкаФормы)

	Если Форма.Элементы.Найти("КадровыйРезервИнфоНадписьГруппРезерва") = Неопределено Тогда
		Элемент = Форма.Элементы.Вставить("КадровыйРезервИнфоНадписьГруппРезерва", Тип("ПолеФормы"), ГруппаПосадкаФормы);
		Элемент.Вид = ВидПоляФормы.ПолеНадписи;
		Элемент.ПутьКДанным = "КадровыйРезервИнфоНадписьГруппРезерва";
		Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элемент.УстановитьДействие("ОбработкаНавигационнойСсылки", "Подключаемый_КадровыйРезервИнфоНадписьГруппРезерваОбработкаНавигационнойСсылки");
	КонецЕсли;

КонецПроцедуры

Процедура СоздатьЭлементыФормыДекорацияПояснение(Форма, ГруппаПосадкаФормы)

	Если Форма.Элементы.Найти("ДекорацияПояснение") = Неопределено Тогда
		// Добавляем декорацию
		Элемент = Форма.Элементы.Вставить("ДекорацияПояснение", Тип("ДекорацияФормы"), ГруппаПосадкаФормы);
		Элемент.Вид = ВидДекорацииФормы.Надпись;
		Элемент.Заголовок = НСтр("ru = 'Сведения о кадровом резерве, в которые входит данная позиция штатного расписания.'");
	КонецЕсли;

КонецПроцедуры
            
#КонецОбласти 

#Область ФормаДокументов

Процедура СоздатьТаблицуКадровыйРезервВФормеОбщегоДокумента(Форма, СоздаватьКолонкуСтатусаВТаблице, ДобавлятьЭлементыФормы = Истина, ДобавлятьРеквизитыФормы = Истина, ОтложенноеИзменение = Ложь)

	Если ДобавлятьРеквизитыФормы Тогда
		ПутьКДанным = "Объект.КадровыйРезерв";
		// Создание реквизитов формы.
		МассивРеквизитов = Новый Массив;
		РеквизитОписаниеДействия = Новый РеквизитФормы("ОписаниеДействия", Новый ОписаниеТипов("Строка"), ПутьКДанным);
		МассивРеквизитов.Добавить(РеквизитОписаниеДействия);
		Если СоздаватьКолонкуСтатусаВТаблице Тогда
			РеквизитОписаниеСтатуса = Новый РеквизитФормы("ОписаниеСтатуса", Новый ОписаниеТипов("Строка"), ПутьКДанным);
			МассивРеквизитов.Добавить(РеквизитОписаниеСтатуса);
		КонецЕсли;
		
		МассивИменРеквизитовФормы = Новый Массив;
		ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(Форма, МассивИменРеквизитовФормы);
		ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(Форма, МассивИменРеквизитовФормы, ПутьКДанным);
		ЗарплатаКадры.ИзменитьРеквизитыФормы(Форма, МассивРеквизитов, МассивИменРеквизитовФормы,, ОтложенноеИзменение);
		
	КонецЕсли;
	
	Если ДобавлятьЭлементыФормы Тогда
		
		ТаблицаКадровыйРезерв = Форма.Элементы.Найти("КадровыйРезерв");
		Если ТаблицаКадровыйРезерв = Неопределено Тогда
			// Создание элементов формы
			ТаблицаКадровыйРезерв = Форма.Элементы.Вставить("КадровыйРезерв", Тип("ТаблицаФормы"), Форма.Элементы.ГруппаКадровогоРезерва);
			ТаблицаКадровыйРезерв.ПутьКДанным = "Объект.КадровыйРезерв";
			ТаблицаКадровыйРезерв.ТолькоПросмотр = Истина;
			ТаблицаКадровыйРезерв.ИзменятьСоставСтрок = Ложь;
			ТаблицаКадровыйРезерв.ИзменятьПорядокСтрок = Истина;
			ТаблицаКадровыйРезерв.УстановитьДействие("ПриАктивизацииСтроки", "Подключаемый_КадровыйРезервПриАктивизацииСтроки");
		КонецЕсли;
		
		Если Форма.Элементы.Найти("КадровыйРезервПозицияРезерва") = Неопределено Тогда
			ПолеКадровыйРезервПозицияРезерва = Форма.Элементы.Вставить("КадровыйРезервПозицияРезерва", Тип("ПолеФормы"), ТаблицаКадровыйРезерв);
			ПолеКадровыйРезервПозицияРезерва.ПутьКДанным = "Объект.КадровыйРезерв.ПозицияРезерва";
		КонецЕсли;
		
		Если СоздаватьКолонкуСтатусаВТаблице И Форма.Элементы.Найти("КадровыйРезервОписаниеСтатуса") = Неопределено Тогда
			ПолеКадровыйРезервОписаниеСтатуса = Форма.Элементы.Вставить("КадровыйРезервОписаниеСтатуса", Тип("ПолеФормы"), ТаблицаКадровыйРезерв);
			ПолеКадровыйРезервОписаниеСтатуса.ПутьКДанным = "Объект.КадровыйРезерв.ОписаниеСтатуса";
			ПолеКадровыйРезервОписаниеСтатуса.Заголовок = НСтр("ru = 'Статус'");
		КонецЕсли;
		
		Если Форма.Элементы.Найти("КадровыйРезервВидРезерва") = Неопределено Тогда
			ПолеКадровыйРезервПозицияРезерва = Форма.Элементы.Вставить("КадровыйРезервВидРезерва", Тип("ПолеФормы"), ТаблицаКадровыйРезерв);
			ПолеКадровыйРезервПозицияРезерва.ПутьКДанным = "Объект.КадровыйРезерв.ВидРезерва";
		КонецЕсли;
		
		Если Форма.Элементы.Найти("КадровыйРезервДействие") = Неопределено Тогда
			ПолеКадровыйРезервПозицияРезерва = Форма.Элементы.Вставить("КадровыйРезервДействие", Тип("ПолеФормы"), ТаблицаКадровыйРезерв);
			ПолеКадровыйРезервПозицияРезерва.ПутьКДанным = "Объект.КадровыйРезерв.ОписаниеДействия";
			ПолеКадровыйРезервПозицияРезерва.Заголовок = НСтр("ru = 'Действие'");
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьКомандыКадровогоРезерваВФормеОбщегоДокумента(Форма)

	Если Форма.Команды.Найти("КадровыйРезервИсключить") = Неопределено Тогда
		КомандаКадровыйРезервИсключить = Форма.Команды.Добавить("КадровыйРезервИсключить");
		КомандаКадровыйРезервИсключить.Действие = "Подключаемый_КадровыйРезервИсключить";
		КомандаКадровыйРезервИсключить.Заголовок = НСтр("ru = 'Исключить'");
		КомандаКадровыйРезервИсключить.ИзменяетСохраняемыеДанные = Истина;
		КомандаКадровыйРезервИсключить.Картинка = БиблиотекаКартинок.УдалитьСтраницу;
	КонецЕсли;
	
	Если Форма.Команды.Найти("КадровыйРезервОставить") = Неопределено Тогда
		КомандаКадровыйРезервОставить = Форма.Команды.Добавить("КадровыйРезервОставить");
		КомандаКадровыйРезервОставить.Действие = "Подключаемый_КадровыйРезервОставить";
		КомандаКадровыйРезервОставить.Заголовок = НСтр("ru = 'Оставить'");
		КомандаКадровыйРезервОставить.ИзменяетСохраняемыеДанные = Истина;
		КомандаКадровыйРезервОставить.Картинка =  БиблиотекаКартинок.ОперацияВыполненаУспешно;
	КонецЕсли;
	
	// Добавляем сами кнопки
	ГруппаПанель = Форма.Элементы.КадровыйРезерв.КоманднаяПанель;
	ГруппаКнопокКадровогоРезерва = Форма.Элементы.Найти("КадровыйРезервГруппаКоманд");
	Если ГруппаКнопокКадровогоРезерва = Неопределено Тогда
		ГруппаКнопокКадровогоРезерва = Форма.Элементы.Вставить("КадровыйРезервГруппаКоманд", Тип("ГруппаФормы"), ГруппаПанель);
		ГруппаКнопокКадровогоРезерва.Вид = ВидГруппыФормы.ГруппаКнопок;
	КонецЕсли;
	
	Если Форма.Элементы.Найти("КадровыйРезервКадровыйРезервОставить") = Неопределено Тогда
		Элемент = Форма.Элементы.Вставить("КадровыйРезервКадровыйРезервОставить", Тип("КнопкаФормы"), ГруппаКнопокКадровогоРезерва);
		Элемент.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
		Элемент.Отображение = ОтображениеКнопки.КартинкаИТекст;
		Элемент.ИмяКоманды = "КадровыйРезервОставить"; 
	КонецЕсли;
	
	Если Форма.Элементы.Найти("КадровыйРезервКадровыйРезервИсключить") = Неопределено Тогда
		Элемент = Форма.Элементы.Вставить("КадровыйРезервКадровыйРезервИсключить", Тип("КнопкаФормы"), ГруппаКнопокКадровогоРезерва);
		Элемент.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
		Элемент.Отображение = ОтображениеКнопки.КартинкаИТекст;
		Элемент.ИмяКоманды = "КадровыйРезервИсключить"; 
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьКадровыйРезервВДокументеКадровыйПеревод(Форма) Экспорт
	
	Если Не КадровыйРезерв.КадровыйРезервДоступенПользователю() Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	
	ПараметрыЗаполнения = ПараметрыЗаполненияПозицииРезерваПоСотрудникуДляОбщегоДокумента(Объект);
	ЗаполнитьПозицииРезерваПоСотруднику(ПараметрыЗаполнения);
	СписокПозицийРезерва = КадровыйРезерв.ПодобратьПозицииРезерваПоПозицииШтатногоРасписания(Объект.ДолжностьПоШтатномуРасписанию, Ложь);
	ИсключитьИзТехПозицийКудаПереводится(Объект.КадровыйРезерв, СписокПозицийРезерва);
	КадровыйРезервКлиентСервер.ЗаполнитьВторичныеДанныеКадровогоРезерва(Объект.КадровыйРезерв);
	
КонецПроцедуры

Процедура ЗаполнитьКадровыйРезервВДокументеУвольнение(Форма) Экспорт
	
	Если Не КадровыйРезерв.КадровыйРезервДоступенПользователю() Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	
	ПараметрыЗаполнения = ПараметрыЗаполненияПозицииРезерваПоСотрудникуДляОбщегоДокумента(Объект, Ложь);
	ЗаполнитьПозицииРезерваПоСотруднику(ПараметрыЗаполнения);
	КадровыйРезервКлиентСервер.ЗаполнитьВторичныеДанныеКадровогоРезерва(Объект.КадровыйРезерв);
	
КонецПроцедуры

#КонецОбласти 

// Для документа КадровыйПеревод возвращает параметры для передачи в процедуру ЗаполнитьПозицииРезерваПоСотруднику.
//
Функция ПараметрыЗаполненияПозицииРезерваПоСотрудникуДляОбщегоДокумента(Объект, КадровыйПеревод = Истина)

	Если НЕ КадровыйРезерв.КадровыйРезервДоступенПользователю() Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыЗаполнения = Новый Структура("ТабличнаяЧасть, ДатаНачала, ФизическоеЛицо, Действие");
	ПараметрыЗаполнения.ТабличнаяЧасть = Объект.КадровыйРезерв;
	
	ПараметрыЗаполнения.ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Сотрудник, "ФизическоеЛицо");
	Если КадровыйПеревод Тогда
		ПараметрыЗаполнения.ДатаНачала = Объект.ДатаНачала;
		ПараметрыЗаполнения.Действие = ПредопределенноеЗначение("Перечисление.ДействияСКадровымиРезервистами.Оставить");
	Иначе
		ПараметрыЗаполнения.ДатаНачала = Объект.ДатаУвольнения;
		ПараметрыЗаполнения.Действие = ПредопределенноеЗначение("Перечисление.ДействияСКадровымиРезервистами.Отклонить");
	КонецЕсли;
	
	Возврат ПараметрыЗаполнения;

КонецФункции

// Дополняет ТЧ Кадровый резерв значением по умолчанию
//
Процедура ИсключитьИзТехПозицийКудаПереводится(ТЧКадровыйРезерв, СписокПозицийРезерва)

	Если НЕ КадровыйРезерв.КадровыйРезервДоступенПользователю() Тогда 
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаКадровыйРезерв Из ТЧКадровыйРезерв Цикл
		Если НЕ СписокПозицийРезерва.Найти(СтрокаКадровыйРезерв.ПозицияРезерва) = Неопределено Тогда
			СтрокаКадровыйРезерв.Действие = ПредопределенноеЗначение("Перечисление.ДействияСКадровымиРезервистами.Отклонить");
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти 