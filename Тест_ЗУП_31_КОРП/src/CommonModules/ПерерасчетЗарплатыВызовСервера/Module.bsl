#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру, содержащую месяц открытого периода и список документов
// начисления зарплаты в режиме доначисления.
//
// Параметры:
//		Организация		- СправочникСсылка.Организации
//		МесяцНачисления	- Дата
//
// Возвращаемое значение:
//		Структура:
//			* МесяцНачисления	- Дата
//			* Документы			- Массив
//
Функция ДокументыПерерасчета(Организация, МесяцНачисления) Экспорт
	
	ОткрытыйПериод = ПерерасчетЗарплаты.ОткрытыйПериодНачисленияЗарплаты(Организация, МесяцНачисления);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("МесяцНачисления", ОткрытыйПериод);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	НачислениеЗарплаты.Ссылка
		|ИЗ
		|	Документ.НачислениеЗарплаты КАК НачислениеЗарплаты
		|ГДЕ
		|	НачислениеЗарплаты.МесяцНачисления = &МесяцНачисления
		|	И НачислениеЗарплаты.Организация = &Организация
		|	И НачислениеЗарплаты.РежимДоначисления
		|	И НЕ НачислениеЗарплаты.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	НачислениеЗарплаты.Проведен УБЫВ,
		|	НачислениеЗарплаты.Дата";
		
	ДоступныеДоначисления = Новый Структура;
	ДоступныеДоначисления.Вставить("МесяцНачисления", ОткрытыйПериод);
	ДоступныеДоначисления.Вставить("Документы", Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
	Возврат ДоступныеДоначисления;
	
КонецФункции

#КонецОбласти
