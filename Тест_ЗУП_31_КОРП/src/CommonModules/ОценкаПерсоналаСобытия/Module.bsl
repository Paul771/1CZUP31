#Область ПрограммныйИнтерфейс

// Процедура-обработчик события "ПриЗаписи" регистра сведений РегистрСведений.ОтветыНаВопросыАнкет.
//
// Параметры:
//		НаборЗаписейРегистра	- набор записей, источник события
//		Отказ		- Булево - флаг отказа от выполнения обработчика
//		Отказ		- Булево - флаг отказа от выполнения обработчика
//		Замещение	- Булево - флаг замещения записей при записи.
Процедура ПересчитатьОценкиПоМероприятию(НаборЗаписейРегистра, Отказ, Замещение) Экспорт

	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(НаборЗаписейРегистра) Тогда
		Возврат;
	КонецЕсли;
	
	Если НаборЗаписейРегистра.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Анкета = НаборЗаписейРегистра.Отбор.Регистратор.Значение;
	
	Если НЕ ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Анкета, "Опрос") Тогда
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Анкета.Опрос) Тогда
		Возврат;
	КонецЕсли;
	
	Мероприятия = СписокМероприятийПоНазначению(Анкета.Опрос);
	Для каждого Мероприятие Из Мероприятия Цикл
		Удаление = НаборЗаписейРегистра.Количество()=0;
		ОценкаПерсонала.РассчитатьОценкиПоОтветам(Мероприятие, Анкета, Удаление);
	КонецЦикла; 

КонецПроцедуры

// Обработчик подписки на событие УстановитьИспользованиеОценкиПерсонала
//
Процедура УстановитьИспользованиеОценкиПерсоналаПриЗаписи(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Источник.Значение Тогда
		НастройкаОценки = РегистрыСведений.НастройкиОценкиПерсонала.СоздатьМенеджерЗаписи();
		НастройкаОценки.Прочитать();
		Если НастройкаОценки.ИспользоватьОценкуПерсонала Тогда
			// Значит надо выключить оценку, так как ее использование без "Анкетирования" невозможно.
			НастройкаОценки.ИспользоватьОценкуПерсонала = Ложь;
			НастройкаОценки.Записать(Истина);
			
			Если ТипЗнч(Источник) = Тип("КонстантаМенеджерЗначения.ИспользоватьАнкетирование") Тогда
				НаименованиеИсточникаРодительныйПадеж = НСтр("ru = 'Анкетирования'");
			ИначеЕсли ТипЗнч(Источник) = Тип("КонстантаМенеджерЗначения.ИспользоватьШаблоныСообщений") Тогда
				НаименованиеИсточникаРодительныйПадеж = НСтр("ru = 'Шаблонов сообщений'");
			Иначе
				НаименованиеИсточникаРодительныйПадеж = "";
			КонецЕсли;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Использование Оценки персонала невозможно без использования %1. Оценка персонала так же было выключено.'"),
					НаименованиеИсточникаРодительныйПадеж);

			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция СписокМероприятийПоНазначению(НазначениеОпроса)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МероприятияОценкиПерсоналаУчастники.Ссылка КАК Мероприятие
		|ИЗ
		|	Справочник.МероприятияОценкиПерсонала.Участники КАК МероприятияОценкиПерсоналаУчастники
		|ГДЕ
		|	МероприятияОценкиПерсоналаУчастники.НазначениеОпроса = &НазначениеОпроса";
	Запрос.УстановитьПараметр("НазначениеОпроса", НазначениеОпроса);
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Мероприятие");
	
КонецФункции

#КонецОбласти 

