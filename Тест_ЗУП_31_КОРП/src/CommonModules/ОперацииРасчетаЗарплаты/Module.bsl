
#Область СлужебныеПроцедурыИФункции

Функция ВидыОперацийРасчетаЗарплаты() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВидыОперацийРасчетаЗарплаты.Ссылка
	               |ИЗ
	               |	Справочник.ВидыОперацийРасчетаЗарплаты КАК ВидыОперацийРасчетаЗарплаты
	               |ГДЕ
	               |	НЕ ВидыОперацийРасчетаЗарплаты.ПометкаУдаления
	               |	И НЕ ВидыОперацийРасчетаЗарплаты.ВАрхиве
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ВидыОперацийРасчетаЗарплаты.Наименование";
				   
	УстановитьПривилегированныйРежим(Истина);			   
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	ВидыОпераций = Новый Массив;
	Пока Выборка.Следующий() Цикл 
		ВидыОпераций.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат ВидыОпераций;
	
КонецФункции

Функция ДанныеВидаОперации(ВидОперации) Экспорт 
	
	ДанныеВидаОперации = Новый Структура;
	ДанныеВидаОперации.Вставить("ИспользоватьНачисление", Истина);
	ДанныеВидаОперации.Вставить("ИспользоватьДоговоры", Истина);
	ДанныеВидаОперации.Вставить("ИспользоватьПособия", Истина);
	ДанныеВидаОперации.Вставить("ИспользоватьЛьготы", Истина);
	ДанныеВидаОперации.Вставить("ИспользоватьУдержания", Истина);
	ДанныеВидаОперации.Вставить("ИспользоватьНДФЛ", Истина);
	ДанныеВидаОперации.Вставить("ИспользоватьЗаймы", Истина);
	ДанныеВидаОперации.Вставить("ИспользоватьВзносы", Истина);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ВидОперации", ВидОперации);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВидыОперацийРасчетаЗарплаты.Начисление КАК ИспользоватьНачисление,
	               |	ВидыОперацийРасчетаЗарплаты.Договоры КАК ИспользоватьДоговоры,
	               |	ВидыОперацийРасчетаЗарплаты.Пособия КАК ИспользоватьПособия,
	               |	ВидыОперацийРасчетаЗарплаты.Льготы КАК ИспользоватьЛьготы,
	               |	ВидыОперацийРасчетаЗарплаты.Удержания КАК ИспользоватьУдержания,
	               |	ВидыОперацийРасчетаЗарплаты.НДФЛ КАК ИспользоватьНДФЛ,
	               |	ВидыОперацийРасчетаЗарплаты.Займы КАК ИспользоватьЗаймы,
	               |	ВидыОперацийРасчетаЗарплаты.Взносы КАК ИспользоватьВзносы
	               |ИЗ
	               |	Справочник.ВидыОперацийРасчетаЗарплаты КАК ВидыОперацийРасчетаЗарплаты
	               |ГДЕ
	               |	ВидыОперацийРасчетаЗарплаты.Ссылка = &ВидОперации";
				   
	УстановитьПривилегированныйРежим(Истина);			   
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);			   
	
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ДанныеВидаОперации, Выборка);
	КонецЕсли;
	
	Возврат ДанныеВидаОперации;
	
КонецФункции

Функция ВидОперацииДокумента(Документ) Экспорт 
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Документ", Документ);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОперацииРасчетаЗарплаты.ВидОперации
	               |ИЗ
	               |	РегистрСведений.ОперацииРасчетаЗарплаты КАК ОперацииРасчетаЗарплаты
	               |ГДЕ
	               |	ОперацииРасчетаЗарплаты.Документ = &Документ";
				   
	УстановитьПривилегированныйРежим(Истина);			   
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);			   
	
	ВидОперации = Неопределено;
	Если Выборка.Следующий() Тогда
		ВидОперации = Выборка.ВидОперации;
	КонецЕсли;
	
	Возврат ВидОперации;
	
КонецФункции

Процедура УстановитьВидОперацииДокумента(Форма, Документ, ПутьКДанным = "ВидОперации") Экспорт 
	
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(Форма, ПутьКДанным, ВидОперацииДокумента(Документ));
	
КонецПроцедуры	

Процедура ЗаписатьВидОперацииДокумента(Документ, ВидОперации) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.ОперацииРасчетаЗарплаты.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(Документ);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Документ = Документ;
	НоваяЗапись.ВидОперации = ВидОперации;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура ДоработатьЗапросСпискаНачислений(Форма, ИмяРеквизитаСписок = "Список", ИмяОсновнойТаблицы = "ДокументНачислениеЗарплаты") Экспорт 
	
	Список = Форма[ИмяРеквизитаСписок];
	
	Список.ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, "&ПредставлениеТипаРасчетЗарплаты", 
		"ВЫБОР
		|	КОГДА ЕСТЬNULL(ОперацииРасчетаЗарплаты.ВидОперации, ЗНАЧЕНИЕ(Справочник.ВидыОперацийРасчетаЗарплаты.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.ВидыОперацийРасчетаЗарплаты.ПустаяСсылка)
		|		ТОГДА &ПредставлениеТипаРасчетЗарплаты
		|	ИНАЧЕ ОперацииРасчетаЗарплаты.ВидОперации.Наименование
		|КОНЕЦ");
	
	ТекстСоединения = "
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОперацииРасчетаЗарплаты КАК ОперацииРасчетаЗарплаты
		|	ПО &ИмяОсновнойТаблицы.Ссылка = ОперацииРасчетаЗарплаты.Документ";
		
	ТекстСоединения = СтрЗаменить(ТекстСоединения, "&ИмяОсновнойТаблицы", ИмяОсновнойТаблицы);	
	
	ПозицияСловаГДЕ = СтрНайти(Список.ТекстЗапроса, "ГДЕ" + Символы.ПС);
	ПозицияСловаГДЕКомпоновки = СтрНайти(Список.ТекстЗапроса, "{ГДЕ" + Символы.ПС);
	
	ПозицияВставки = ?(ПозицияСловаГДЕ <> 0 И ПозицияСловаГДЕКомпоновки <> 0, 
		Мин(ПозицияСловаГДЕ, ПозицияСловаГДЕКомпоновки), ?(ПозицияСловаГДЕ = 0, ПозицияСловаГДЕКомпоновки, ПозицияСловаГДЕ));
	
	Если ПозицияВставки <> 0 Тогда 
		ПерваяЧастьЗапроса = Лев(Список.ТекстЗапроса, ПозицияВставки - 1);
		ВтораяЧастьЗапроса = Сред(Список.ТекстЗапроса, ПозицияВставки);
		Список.ТекстЗапроса = ПерваяЧастьЗапроса + ТекстСоединения + Символы.ПС + ВтораяЧастьЗапроса;
	Иначе 
		Список.ТекстЗапроса = Список.ТекстЗапроса + ТекстСоединения;
	КонецЕсли;
	
КонецПроцедуры

#Область ПервоначальногоЗаполненияИОбновленияИБ

// Добавляет в список Обработчики процедуры-обработчики обновления,
// необходимые данной подсистеме.
//
// Параметры:
//   Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//                                   общего модуля ОбновлениеИнформационнойБазы.
// 
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.23.69";
	Обработчик.Процедура = "ОперацииРасчетаЗарплаты.НачальноеЗаполнениеВидовОперацийРасчетаЗарплаты";
	Обработчик.НачальноеЗаполнение = Истина;
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.4.20";
	Обработчик.Процедура = "ОперацииРасчетаЗарплаты.УстановитьПараметрыНабораСвойствВидыОперацийРасчетаЗарплаты";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("f523eed4-c1f0-466c-85ee-a19c79196ce8");
	Обработчик.Комментарий = НСтр("ru = 'Установка параметров для набора свойств справочников.'");
	
КонецПроцедуры

Процедура НачальноеЗаполнениеВидовОперацийРасчетаЗарплаты() Экспорт 
	
	Справочники.ВидыОперацийРасчетаЗарплаты.НачальноеЗаполнение();
	
КонецПроцедуры

Процедура УстановитьПараметрыНабораСвойствВидыОперацийРасчетаЗарплаты(ПараметрыОбновления = НеОпределено) Экспорт
	
	УправлениеСвойствами.УстановитьПараметрыНабораСвойств("Справочник_ВидыОперацийРасчетаЗарплаты", УправлениеСвойствами.СтруктураПараметровНабораСвойств());
	
	Если ПараметрыОбновления <> Неопределено Тогда
		ПараметрыОбновления.ОбработкаЗавершена = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

