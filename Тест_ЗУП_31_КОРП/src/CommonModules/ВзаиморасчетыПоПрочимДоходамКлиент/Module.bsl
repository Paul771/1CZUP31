
#Область ПрограммныйИнтерфейс

// Возвращает имя события оповещения об изменении состояния оплаты ведомости на выплату зарплаты.
// Уместно использовать в платежных документах в вызовах Оповестить:
// 	Оповестить(ВзаиморасчетыПоПрочимДоходамКлиент.ИмяСобытияИзмененияОплатыВедомости());
//
//	Возвращаемое значение
//		строка
//
Функция ИмяСобытияИзмененияОплатыВедомости() Экспорт
	Возврат "ИзменениеОплатыВедомостиПоПрочимДоходам"
КонецФункции

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиСобытийКомандФормы

Процедура ВедомостьЗаполнить(Форма) Экспорт
	
	ОчиститьСообщения();
	Форма.ЗаполнитьНаСервере()
	
КонецПроцедуры

Процедура ВедомостьОчистить(Форма) Экспорт
	
	ОчиститьСообщения();
	Форма.ОчиститьНаСервере()
	
КонецПроцедуры

Процедура ВедомостьРедактироватьВыплатуСтрокиСтроки(Форма, ДанныеСтроки) Экспорт
	
	Если ДанныеСтроки = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	
	ПараметрыОткрытия.Вставить("Организация",			ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	ПараметрыОткрытия.Вставить("ПериодРегистрации",		Дата(1,1,1));
	ПараметрыОткрытия.Вставить("СтатьяФинансирования",	ПредопределенноеЗначение("Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка"));
	ПараметрыОткрытия.Вставить("СтатьяРасходов",		ПредопределенноеЗначение("Справочник.СтатьиРасходовЗарплата.ПустаяСсылка"));
	ЗаполнитьЗначенияСвойств(ПараметрыОткрытия, Форма.Объект);
	
	ПараметрыОткрытия.Вставить("ИдентификаторСтроки",	ДанныеСтроки.ИдентификаторСтроки);
	ПараметрыОткрытия.Вставить("ФизическоеЛицо", 		ДанныеСтроки.ФизическоеЛицо);
	ПараметрыОткрытия.Вставить("АдресВХранилищеВыплатыПоСтроке", Форма.АдресВХранилищеВыплатыПоСтроке(ДанныеСтроки.ИдентификаторСтроки));
	
	ПараметрыОткрытия.Вставить("ТолькоПросмотр",		Форма.ТолькоПросмотр);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);
	Оповещение = Новый ОписаниеОповещения("ВедомостьРедактироватьВыплатуСтрокиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("ОбщаяФорма.РедактированиеВыплатыСтрокиВедомости", ПараметрыОткрытия, Форма, , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ВедомостьРедактироватьВыплатуСтрокиЗавершение(РезультатыРедактирования, ДополнительныеПараметры) Экспорт
	
	Если РезультатыРедактирования <> Неопределено И РезультатыРедактирования.Модифицированность Тогда
		ДополнительныеПараметры.Форма.РедактироватьВыплатуСтрокиЗавершениеНаСервере(РезультатыРедактирования)
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийФормы

Процедура ВедомостьОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник) Экспорт
	
	Если ИмяСобытия = ИмяСобытияИзмененияОплатыВедомости() Тогда
		
		Форма.УстановитьДоступностьЭлементов();
		Форма.УстановитьПредставлениеОплаты();
		
		Если Форма.Элементы.ВнешниеОперацииГруппа.Видимость Тогда
			ВзаиморасчетыПоПрочимДоходамКлиентСервер.УстановитьОтображениеВХО(Форма);
		КонецЕсли	
		
	ИначеЕсли ИмяСобытия = "ВыборДокументовОснованийВедомостейПрочихДоходов" Тогда
		
		Форма.Активизировать();
	
		Форма.Объект.Основания.Очистить();
		Для Каждого Основание Из Параметр Цикл
			Строка = Форма.Объект.Основания.Добавить();
			Строка.Документ = Основание.Значение
		КонецЦикла;	
		ВзаиморасчетыПоПрочимДоходамКлиентСервер.ВедомостьУстановитьПредставлениеОснований(Форма);
		
		Если Форма.Объект.Основания.Количество() > 0 Тогда
			ВедомостьЗаполнить(Форма);
		Иначе
			ВедомостьОчистить(Форма);
		КонецЕсли	
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

Процедура ВедомостьПредставлениеОснованийНажатие(Форма, Элемент, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ВыбранныеДокументы = Новый СписокЗначений;
	Для Каждого Основание Из Форма.Объект.Основания Цикл
		ВыбранныеДокументы.Добавить(Основание.Документ)
	КонецЦикла;	
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СпособВыплаты", Форма.Объект.СпособВыплаты);
	ПараметрыФормы.Вставить("ДатаОстатков", КонецМесяца(Форма.Объект.ПериодРегистрации));
	ПараметрыФормы.Вставить("Организация", Форма.Объект.Организация);
	ПараметрыФормы.Вставить("ВыбранныеДокументы", ВыбранныеДокументы);
	ПараметрыФормы.Вставить("ИсключаемыйРегистратор", Форма.Объект.Ссылка);
	
	ОткрытьФорму("ОбщаяФорма.ВыборДокументовОснованийВедомостейПрочихДоходов", ПараметрыФормы, Форма);
	
КонецПроцедуры

Процедура ВедомостьПеречислениеНДФЛВыполненоПриИзменении(Форма, Элемент) Экспорт
	ВзаиморасчетыПоПрочимДоходамКлиентСервер.УстановитьОтображениеВХО(Форма);
КонецПроцедуры

Процедура ВедомостьДатаПриИзменении(Форма) Экспорт
	Форма.ДатаВыплаты = ВзаиморасчетыПоПрочимДоходамКлиентСервер.ВедомостьДатаВыплаты(Форма.Объект);
КонецПроцедуры

Процедура ВедомостьДатаВыплатыПриИзменении(Форма) Экспорт
	Форма.Объект.ДатаВыплаты = Форма.ДатаВыплаты;
	Форма.ДатаВыплаты = ВзаиморасчетыПоПрочимДоходамКлиентСервер.ВедомостьДатаВыплаты(Форма.Объект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыСостав

Процедура ВедомостьСоставВыбор(Форма, Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка) Экспорт

	ТекущиеДанные = Форма.Элементы.Состав.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ИдентификаторСтроки) Тогда
		
		Если Поле.ГиперссылкаЯчейки И Поле.Имя = "СоставНДФЛРасшифровка" ИЛИ Поле.Имя = "СоставНДФЛ" Тогда
			Форма.РедактироватьНДФЛСтроки(ТекущиеДанные);
			СтандартнаяОбработка = Ложь;
			Возврат
		КонецЕсли;
		
		Если Поле.ГиперссылкаЯчейки ИЛИ Поле.ТолькоПросмотр Тогда
			Форма.РедактироватьВыплатуСтроки(ТекущиеДанные);
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти



#КонецОбласти

