
#Область СлужебныеПроцедурыИФункции

Процедура ОткрытьКадровыйПриказ(Форма, ИмяТаблицы) Экспорт
	
	ПараметрыФормы = Новый Структура("Ключ", Форма.КадровыйПриказ);
	ОткрытьФорму("Документ." + ИмяТаблицы + ".ФормаОбъекта", ПараметрыФормы, Форма);
	
КонецПроцедуры

Процедура ОформитьКадровыйПриказ(Форма, ИмяТаблицы, ДанныеЗаявкиСотрудника) Экспорт 
	
	Объект = Форма.Объект;
	ДополнительныеПараметры = Новый Структура("Форма, ИмяТаблицы, ДанныеЗаявкиСотрудника", Форма, ИмяТаблицы, ДанныеЗаявкиСотрудника);
	
	Если Объект.Ссылка.Пустая() Или Форма.Модифицированность Тогда
		
		Если Объект.Ссылка.Пустая() Тогда
			ТекстВопроса = НСтр("ru = 'Данные еще не записаны.
								|Оформление документа возможно только после записи данных.
								|Данные будут записаны.'");
				
			ОписаниеОповещения = Новый ОписаниеОповещения("ОформитьКадровыйПриказЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Иначе 
			ОформитьКадровыйПриказЗавершение(КодВозвратаДиалога.ОК, ДополнительныеПараметры);
		КонецЕсли;
		
	Иначе 
		ОформитьКадровыйПриказЗавершение(Неопределено, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОформитьКадровыйПриказЗавершение(Ответ, ДополнительныеПараметры) Экспорт 

	Форма = ДополнительныеПараметры.Форма;
	ИмяТаблицы = ДополнительныеПараметры.ИмяТаблицы;
	ДанныеЗаявкиСотрудника = ДополнительныеПараметры.ДанныеЗаявкиСотрудника;
	
	Объект = Форма.Объект;
	
	Если Ответ = КодВозвратаДиалога.ОК Тогда
		Форма.Записать();
		Если Объект.Ссылка.Пустая() Или Форма.Модифицированность Тогда
			Возврат; // Запись не удалась, сообщения о причинах выводит платформа.
		КонецЕсли;
	ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	КадровыйПриказИсправленногоДокумента = Неопределено;
	ДанныеЗаявкиСотрудника.Свойство("КадровыйПриказИсправленногоДокумента", КадровыйПриказИсправленногоДокумента);
	
	Если ЗначениеЗаполнено(КадровыйПриказИсправленногоДокумента) Тогда 
		
		Если Форма.ДоступноИсправлениеКадровогоПриказаИсправленногоДокумента Тогда 
			
			Основание = Новый Структура;
			Основание.Вставить("Ссылка", Форма.КадровыйПриказИсправленногоДокумента);
			Основание.Вставить("Действие", "Исправить");
			Основание.Вставить("ДанныеЗаявкиСотрудника", ДанныеЗаявкиСотрудника);
			
			ПараметрыФормы = Новый Структура("Основание", Основание);
			ОткрытьФорму("Документ." + ИмяТаблицы + ".ФормаОбъекта", ПараметрыФормы, Форма);
			
		Иначе
			
			Основание = Новый Структура;
			Основание.Вставить("Ссылка", Объект.Ссылка);
			Основание.Вставить("Действие", "ЗаполнитьПоЗаявке");
			Основание.Вставить("ДанныеЗаявкиСотрудника", ДанныеЗаявкиСотрудника);
			
			ПараметрыФормы = Новый Структура("Основание, Ключ", Основание, Форма.КадровыйПриказИсправленногоДокумента);
			ОткрытьФорму("Документ." + ИмяТаблицы + ".ФормаОбъекта", ПараметрыФормы, Форма);
			
		КонецЕсли;
		
	Иначе
		
		Основание = Новый Структура;
		Основание.Вставить("Ссылка", Объект.Ссылка);
		Основание.Вставить("Действие", "ЗаполнитьПоЗаявке");
		Основание.Вставить("ДанныеЗаявкиСотрудника", ДанныеЗаявкиСотрудника);
		
		ПараметрыФормы = Новый Структура("Основание", Основание);
		ОткрытьФорму("Документ." + ИмяТаблицы + ".ФормаОбъекта", ПараметрыФормы, Форма);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередНачаломРаботыСистемы(Параметры) Экспорт
	
	ПараметрыКлиента = СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиентаПриЗапуске();
	
	Если ПараметрыКлиента.ПревышеноМаксимальноеКоличествоСоединений Тогда 
		
		ТекстПредупреждения = НСтр("ru = 'Превышено максимальное количество подключений. Повторите попытку позднее.'");
		
		Параметры.Вставить("Отказ", Истина);
		Параметры.ИнтерактивнаяОбработка = Новый ОписаниеОповещения("ПоказатьПредупреждениеИПродолжить", ЭтотОбъект, ТекстПредупреждения);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказатьПредупреждениеИПродолжить(Параметры, ТекстПредупреждения) Экспорт 
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПоказатьПредупреждениеИПродолжитьЗавершение", ЭтотОбъект, Параметры);
	
	ПоказатьПредупреждение(ОповещениеОЗакрытии, ТекстПредупреждения, 30, НСтр("ru = 'Завершение работы'"));
	
КонецПроцедуры

Процедура ПоказатьПредупреждениеИПродолжитьЗавершение(Параметры) Экспорт 
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения);
	
КонецПроцедуры

#КонецОбласти
