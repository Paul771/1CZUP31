#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьСоставСотрудниковДокументаПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеДокумента = ЗарплатаКадрыСоставДокументов.ОписаниеДокументаПоИсточнику(Источник);
	Если ОписаниеДокумента = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СотрудникиФизическиеЛица = ЗарплатаКадрыСоставДокументов.СотрудникиФизическиеЛица(Источник, ОписаниеДокумента);
	ФизическиеЛица = ОбщегоНазначения.ВыгрузитьКолонку(СотрудникиФизическиеЛица, "ФизическоеЛицо", Истина);
	
	Если ОписаниеДокумента.ЗаполнятьФизическиеЛицаПоСотрудникам Тогда
		ЗарплатаКадрыСоставДокументов.ЗаполнитьФизическиеЛицаПоСотрудникамВДокументе(Источник, ОписаниеДокумента);
	КонецЕсли;

	Если ОписаниеДокумента.ЗаполнятьТабличнуюЧастьФизическиеЛицаДокумента Тогда
		ЗарплатаКадрыСоставДокументов.ЗаполнитьТабличнуюЧастьФизическиеЛицаДокумента(Источник, ОписаниеДокумента, ФизическиеЛица);
	КонецЕсли;
	
	Если ОписаниеДокумента.ИспользоватьКраткийСостав Тогда
		ЗарплатаКадрыСоставДокументов.ЗаполнитьКраткийСоставДокумента(Источник, ОписаниеДокумента, СотрудникиФизическиеЛица);
	КонецЕсли;
	
	ЗарплатаКадрыСоставДокументов.УстановитьКонтекстЗаполненияСоставаДокумента(Источник, ФизическиеЛица, СотрудникиФизическиеЛица);
	
КонецПроцедуры

Процедура ЗаполнитьСоставСотрудниковДокументаПриЗаписи(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеДокумента = ЗарплатаКадрыСоставДокументов.ОписаниеДокументаПоИсточнику(Источник);
	Если ОписаниеДокумента = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДокументСсылка = Источник.Ссылка;
	
	КонтекстЗаполнения = ЗарплатаКадрыСоставДокументов.КонтекстЗаполненияСоставаДокумента(Источник);
	Если КонтекстЗаполнения <> Неопределено Тогда
		СотрудникиФизическиеЛица = КонтекстЗаполнения.СотрудникиФизическиеЛица;
		ФизическиеЛица = КонтекстЗаполнения.ФизическиеЛица;
	Иначе
		СотрудникиФизическиеЛица = ЗарплатаКадрыСоставДокументов.СотрудникиФизическиеЛица(Источник, ОписаниеДокумента);
		ФизическиеЛица = ОбщегоНазначения.ВыгрузитьКолонку(СотрудникиФизическиеЛица, "ФизическоеЛицо", Истина);
	КонецЕсли;
	
	Если ОписаниеДокумента.ИспользоватьОграничениеДоступа Тогда
		ЗарплатаКадрыСоставДокументов.ЗаполнитьРегистрФизическиеЛицаДокументов(ДокументСсылка, ФизическиеЛица);
	КонецЕсли;
	
	Если ОписаниеДокумента.ЗаполнятьСоставДокументов Тогда
		ЗарплатаКадрыСоставДокументов.ЗаполнитьРегистрСоставДокументовЗарплатаКадры(ДокументСсылка, СотрудникиФизическиеЛица);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
