#Область ПрограммныйИнтерфейс

// Возвращает текущую версию компоненты.
//
// Возвращаемое значение:
//   Строка - версия компоненты.
//
Функция ВерсияКомпоненты() Экспорт

	КэшированныеКомпоненты = ПараметрыПриложения["СтандартныеПодсистемы.ВнешниеКомпоненты.Объекты"];
	
	ОбъектДляРаботыСКартинкой = Неопределено;
	
	Если ТипЗнч(КэшированныеКомпоненты) = Тип("ФиксированноеСоответствие") Тогда
		ОбъектДляРаботыСКартинкой = КэшированныеКомпоненты.Получить("ОбщийМакет.КомпонентаПолученияКартинкиИзБуфера");
	КонецЕсли;
	
	Если ОбъектДляРаботыСКартинкой = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ОбъектДляРаботыСКартинкой.Версия();
	
КонецФункции

// Запускает процесс подключения компоненты работы с картинкой из макета.
//  Параметры:
//   ОписаниеОповещения - ОписаниеОповещения - содержит процедуру и параметры для передачи результата подключения компоненты.
//
Процедура ПровестиИнициализациюВнешнихКомпонент(ОписаниеОповещения) Экспорт
	
	ПараметрыПодключения = ОбщегоНазначенияКлиент.ПараметрыПодключенияКомпоненты();
	ПараметрыПодключения.ТекстПояснения = НСтр("ru = 'Для получения картинки из буфера обмена необходимо  установить компоненту.'");
	
	ОбщегоНазначенияКлиент.ПодключитьКомпонентуИзМакета(ОписаниеОповещения, "AddInNativeExtension", "ОбщийМакет.КомпонентаПолученияКартинкиИзБуфера", ПараметрыПодключения);
	
КонецПроцедуры

#КонецОбласти 

