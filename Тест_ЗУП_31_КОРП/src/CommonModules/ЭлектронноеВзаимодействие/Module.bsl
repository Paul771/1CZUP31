
////////////////////////////////////////////////////////////////////////////////
// Подсистема "Электронное взаимодействие".
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Обрабатывает исключительные ситуации по электронным документам.
//
// Параметры:
//   ВидОперации - Строка - вид операции при которой возникло исключение.
//   ПодробныйТекстОшибки - Строка - описание ошибки.
//   ТекстСообщения - Строка - текст ошибки который выводится в виде сообщения.
//   КодСобытия - Строка - код события, используется для стандартизации иерархии событий.
//                Может принимать значения: "ЭлектронноеВзаимодействие" - Общая подсистема, 
//                                          "ОбменСБанками" - Обмен с банками, 
//                                          "ОбменСКонтрагентами" - Обмен с контрагентами,
//                                          "ОбменССайтами" - Обмен с сайтами, 
//                                          "РегламентныеЗадания" - Регламентные задания, 
//                                          "БизнесСеть" - Бизнес-сеть,
//                                          "ИнтеграцияСЯндексКассой" - Интеграция с Яндекс.Кассой.
//   СсылкаНаОбъект - ДокументСсылка, СправочникСсылка - объект с которым связано данное событие.ЗаписатьОшибкуВЖурналРегистрации().
//
Процедура ОбработатьОшибку(ВидОперации, ПодробныйТекстОшибки, ТекстСообщения = "", КодСобытия = "ОбменСКонтрагентами",
	СсылкаНаОбъект = Неопределено) Экспорт
	
	ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
		ВидОперации, ПодробныйТекстОшибки, ТекстСообщения, КодСобытия, СсылкаНаОбъект);
	
КонецПроцедуры

#Область ОбработчикиСобытийБСП

// См. процедуру РегламентныеЗаданияПереопределяемый.ПриОпределенииНастроекРегламентныхЗаданий.
//
Процедура ПриОпределенииНастроекРегламентныхЗаданий(Настройки) Экспорт
	
	// ОбменСКонтрагентами
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСКонтрагентами") Тогда
		
		ФООбменСКонтрагентами = "ИспользоватьОбменЭД";
		
		НоваяСтрока = Настройки.Добавить();
		НоваяСтрока.РегламентноеЗадание = Метаданные.РегламентныеЗадания["ОтправкаЭлектронныхДокументов"];
		НоваяСтрока.ФункциональнаяОпция = Метаданные.ФункциональныеОпции[ФООбменСКонтрагентами];
		НоваяСтрока.РаботаетСВнешнимиРесурсами = Истина;
		НоваяСтрока.ДоступноВМоделиСервиса = Ложь;
	
		НоваяСтрока = Настройки.Добавить();
		НоваяСтрока.РегламентноеЗадание = Метаданные.РегламентныеЗадания["ПолучениеЭлектронныхДокументов"];
		НоваяСтрока.ФункциональнаяОпция = Метаданные.ФункциональныеОпции[ФООбменСКонтрагентами];
		НоваяСтрока.РаботаетСВнешнимиРесурсами = Истина;
		НоваяСтрока.ДоступноВМоделиСервиса = Ложь;
		
		НоваяСтрока = Настройки.Добавить();
		НоваяСтрока.РегламентноеЗадание = Метаданные.РегламентныеЗадания["ПроверкаНовыхЭлектронныхДокументов"];
		НоваяСтрока.ФункциональнаяОпция = Метаданные.ФункциональныеОпции[ФООбменСКонтрагентами];
		
		НоваяСтрока = Настройки.Добавить();
		НоваяСтрока.РегламентноеЗадание = Метаданные.РегламентныеЗадания["ПроверкаКонтрагентовБЭД"];
		НоваяСтрока.ФункциональнаяОпция = Метаданные.ФункциональныеОпции[ФООбменСКонтрагентами];
		
	КонецЕсли;
	
	// ОбменСБанками
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСБанками") Тогда
		НоваяСтрока = Настройки.Добавить();
		НоваяСтрока.РегламентноеЗадание = Метаданные.РегламентныеЗадания["ЗагрузкаСпискаDirectBank"];
		Если НЕ ОбщегоНазначения.РазделениеВключено() Тогда
			НоваяСтрока.ФункциональнаяОпция = Метаданные.ФункциональныеОпции["ИспользоватьОбменСБанками"];
		КонецЕсли;
		НоваяСтрока.ДоступноВМоделиСервиса = Ложь;
	КонецЕсли;

	// ОбменССайтами
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменССайтами") Тогда
		НоваяСтрока = Настройки.Добавить();
		НоваяСтрока.РегламентноеЗадание = Метаданные.РегламентныеЗадания["ОбменССайтом"];
		НоваяСтрока.ФункциональнаяОпция = Метаданные.ФункциональныеОпции["ИспользоватьОбменССайтом"];
		НоваяСтрока.РаботаетСВнешнимиРесурсами = Истина;
	КонецЕсли;

	// ТорговыеПредложения
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ТорговыеПредложения") Тогда
		НоваяСтрока = Настройки.Добавить();
		НоваяСтрока.РегламентноеЗадание = Метаданные.РегламентныеЗадания["СинхронизацияТорговыхПредложений"];
		НоваяСтрока.ФункциональнаяОпция = Метаданные.ФункциональныеОпции["ИспользоватьОбменБизнесСеть"];
		НоваяСтрока.РаботаетСВнешнимиРесурсами = Истина;
	КонецЕсли;
	
	// ИнтеграцияСЯндексКассой
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ИнтеграцияСЯндексКассой") Тогда
		МодульИнтеграцияСЯндексКассой = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияСЯндексКассой");
		МодульИнтеграцияСЯндексКассой.ПриОпределенииНастроекРегламентныхЗаданий(Настройки);
	КонецЕсли;
	
	// РаботаСНоменклатурой
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.РаботаСНоменклатурой") Тогда
		НоваяСтрока = Настройки.Добавить();
		НоваяСтрока.РегламентноеЗадание = Метаданные.РегламентныеЗадания["ОбновлениеНоменклатурыРаботаСНоменклатурой"];
		НоваяСтрока.ФункциональнаяОпция = Метаданные.ФункциональныеОпции["ИспользоватьСервисРаботаСНоменклатурой"];
		НоваяСтрока.РаботаетСВнешнимиРесурсами = Истина;
	КонецЕсли;

	
КонецПроцедуры

// Регистрирует обработчики поставляемых данных.
//
// Параметры:
//  Обработчики - ТаблицаЗначений - исходная таблица обработчиков поставляемых данных.
//
Процедура ЗарегистрироватьОбработчикиПоставляемыхДанных(Знач Обработчики) Экспорт
	
	ЕстьОбменСБанками = ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСБанками");

	Если ЕстьОбменСБанками Тогда
		МодульОбменСБанками = ОбщегоНазначения.ОбщийМодуль("ОбменСБанками");
		Обработчик = Обработчики.Добавить();
		Обработчик.ВидДанных = "DirectBankList";
		Обработчик.КодОбработчика = "DirectBankList";
		Обработчик.Обработчик = МодульОбменСБанками;
		МодульДополнительныеВнешниеКомпоненты = ОбщегоНазначения.ОбщийМодуль("ДополнительныеВнешниеКомпоненты");
		МодульДополнительныеВнешниеКомпоненты.ЗарегистрироватьОбработчикиПоставляемыхДанных(Обработчики);
	КонецЕсли;
	
КонецПроцедуры

// См. процедуру РаботаСФайламиПереопределяемый.ПриОпределенииСправочниковХраненияФайлов.
//
Процедура ПриОпределенииСправочниковХраненияФайлов(ТипВладелецФайла, ИменаСправочников) Экспорт
	
	ЕстьОбменСКонтрагентами = ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСКонтрагентами");
	
	Если ЕстьОбменСКонтрагентами Тогда
		МодульОбменСКонтрагентами = ОбщегоНазначения.ОбщийМодуль("ОбменСКонтрагентами");
		МодульОбменСКонтрагентами.ПриОпределенииСправочниковХраненияФайлов(ТипВладелецФайла, ИменаСправочников);
	КонецЕсли;
	
КонецПроцедуры

// См. процедуру РаботаСФайламиПереопределяемый.ПриОпределенииНастроек.
//
Процедура ПриОпределенииНастроек(Настройки) Экспорт
	
	ЕстьОбменСКонтрагентами = ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСКонтрагентами");

	Если ЕстьОбменСКонтрагентами Тогда
		Настройки.НеОчищатьФайлы.Добавить(Метаданные.Справочники["ЭДПрисоединенныеФайлы"]);
		Настройки.НеСинхронизироватьФайлы.Добавить(Метаданные.Справочники["ЭДПрисоединенныеФайлы"]);
		Настройки.НеВыводитьВИнтерфейс.Добавить(Метаданные.Справочники["ЭДПрисоединенныеФайлы"]);
	КонецЕсли;
	
	ЕстьОбменСБанками = ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСБанками");

	Если ЕстьОбменСБанками Тогда
		Настройки.НеОчищатьФайлы.Добавить(Метаданные.Справочники["СообщениеОбменСБанкамиПрисоединенныеФайлы"]);
		Настройки.НеСинхронизироватьФайлы.Добавить(Метаданные.Справочники["СообщениеОбменСБанкамиПрисоединенныеФайлы"]);
		Настройки.НеВыводитьВИнтерфейс.Добавить(Метаданные.Справочники["СообщениеОбменСБанкамиПрисоединенныеФайлы"]);
	КонецЕсли;

	
КонецПроцедуры

// См. процедуру ЭлектроннаяПодписьПереопределяемый.ПриСозданииФормыПроверкаСертификата.
//
Процедура ПриСозданииФормыПроверкаСертификата(Сертификат, ДополнительныеПроверки, ПараметрыДополнительныхПроверок, СтандартныеПроверки, ВводитьПароль = Истина) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСБанками") Тогда
		МодульОбменаСБанками = ОбщегоНазначения.ОбщийМодуль("ОбменСБанками");
		МодульОбменаСБанками.ПриСозданииФормыПроверкаСертификата(
			Сертификат, ДополнительныеПроверки, ПараметрыДополнительныхПроверок, СтандартныеПроверки, ВводитьПароль);
	КонецЕсли;
	
КонецПроцедуры

// Формирование текстового представления рекламы.
//
// Параметры:
//  ДополнительнаяИнформация - Структура - с полями:
//   * Картинка - Картинка - картинка из библиотеки картинок;
//   * Текст - Строка - форматированный текст надписи с навигационными ссылками.
//  МассивСсылок - Массив - список ссылок на объекты.
//
Процедура ПриВыводеНавигационнойСсылкиВФормеОбъектаИБ(ДополнительнаяИнформация, МассивСсылок) Экспорт
	
	Если Не ЗначениеЗаполнено(МассивСсылок) Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПлатежногоПоручения = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении(
					"ПлатежноеПоручениеВМетаданных");
	
	Если МассивСсылок[0].Метаданные().Имя = ИмяПлатежногоПоручения
		И ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСБанками") Тогда
		
		МодульОбменСБанками = ОбщегоНазначения.ОбщийМодуль("ОбменСБанками");
		МодульОбменСБанками.ПриВыводеНавигационнойСсылкиВФормеОбъектаИБ(ДополнительнаяИнформация, МассивСсылок);
		
	ИначеЕсли ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСКонтрагентами") Тогда
		
		МодульОбменСКонтрагентамиКлиентСервер = ОбщегоНазначения.ОбщийМодуль("ОбменСКонтрагентамиКлиентСервер");
		МодульОбменСКонтрагентамиКлиентСервер.ПриВыводеНавигационнойСсылкиВФормеОбъектаИБ(ДополнительнаяИнформация, МассивСсылок);
		
	КонецЕсли;
	
КонецПроцедуры

// Заполнения списка шаблонов для разделенного режима работы.
// См. ОчередьЗаданийПереопределяемый.ПриПолученииСпискаШаблонов().
//
// Параметры:
//  ШаблоныЗаданий - Массив - в параметр следует добавить имена предопределенных
//   неразделенных регламентных заданий, которые должны использоваться в качестве
//   шаблонов для заданий очереди.
//
Процедура ПриПолученииСпискаШаблонов(ШаблоныЗаданий) Экспорт
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСКонтрагентами") Тогда
		ШаблоныЗаданий.Добавить("ПолучениеЭлектронныхДокументов");
		ШаблоныЗаданий.Добавить("ОтправкаЭлектронныхДокументов");
		ШаблоныЗаданий.Добавить("ПроверкаНовыхЭлектронныхДокументов");
		ШаблоныЗаданий.Добавить("ПроверкаКонтрагентовБЭД");
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	// ЭлектронноеВзаимодействие.ОбменССайтами
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменССайтами") Тогда
		ШаблоныЗаданий.Добавить("ОбменССайтом");
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.ОбменССайтами
	
	// ЭлектронноеВзаимодействие.ТорговыеПредложения
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ТорговыеПредложения") Тогда
		ШаблоныЗаданий.Добавить("СинхронизацияТорговыхПредложений");
		ШаблоныЗаданий.Добавить("ОбновлениеПодсказокТорговыеПредложения");
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.ТорговыеПредложения
	
	// ЭлектронноеВзаимодействие.ИнтеграцияСЯндексКассой
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ИнтеграцияСЯндексКассой") Тогда
		МодульИнтеграцияСЯндексКассой = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияСЯндексКассой");
		МодульИнтеграцияСЯндексКассой.ПриПолученииСпискаШаблонов(ШаблоныЗаданий);
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.ИнтеграцияСЯндексКассой
	
	// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.РаботаСНоменклатурой") Тогда
		ШаблоныЗаданий.Добавить("ОбновлениеНоменклатурыРаботаСНоменклатурой");
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	
КонецПроцедуры

// Заполняет соответствие имен методов их псевдонимам для вызова из очереди заданий.
// См. ОчередьЗаданийПереопределяемый.ПриОпределенииПсевдонимовОбработчиков().
//
// Параметры:
//  СоответствиеИменПсевдонимам - Соответствие - 
//    * Ключ - псевдоним метода, например ОчиститьОбластьДанных.
//    * Значение - имя метода для вызова, например РаботаВМоделиСервиса.ОчиститьОбластьДанных.
//        В качестве значения можно указать Неопределено, в этом случае считается что имя 
//        совпадает с псевдонимом.
//
Процедура ПриОпределенииПсевдонимовОбработчиков(СоответствиеИменПсевдонимам) Экспорт
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСКонтрагентами") Тогда
		СоответствиеИменПсевдонимам.Вставить("ПроверкаНовыхЭлектронныхДокументов");
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	// ЭлектронноеВзаимодействие.ОбменССайтами
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменССайтами") Тогда
		СоответствиеИменПсевдонимам.Вставить("ОбменССайтомСобытия.ЗаданиеВыполнитьОбмен", "ОбменССайтомСобытия.ЗаданиеВыполнитьОбмен");
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.ОбменССайтами
	
	// ЭлектронноеВзаимодействие.ТорговыеПредложения
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ТорговыеПредложения") Тогда
		СоответствиеИменПсевдонимам.Вставить("СинхронизацияТорговыхПредложений");
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.ТорговыеПредложения	
	
	// ЭлектронноеВзаимодействие.ИнтеграцияСЯндексКассой
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ИнтеграцияСЯндексКассой") Тогда
		МодульИнтеграцияСЯндексКассой = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияСЯндексКассой");
		МодульИнтеграцияСЯндексКассой.ПриОпределенииПсевдонимовОбработчиков(СоответствиеИменПсевдонимам);
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.ИнтеграцияСЯндексКассой
	
	// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.РаботаСНоменклатурой") Тогда
		СоответствиеИменПсевдонимам.Вставить("ОбновлениеНоменклатуры");
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	
КонецПроцедуры

// См. процедуру ОбщегоНазначенияПереопределяемый.ПриДобавленииОбработчиковУстановкиПараметровСеанса.
Процедура ПриДобавленииОбработчиковУстановкиПараметровСеанса(Обработчики) Экспорт
	
	// ОбменСБанками
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСБанками") Тогда
		МодульОбменСБанками = ОбщегоНазначения.ОбщийМодуль("ОбменСБанками");
		МодульОбменСБанками.ПриДобавленииОбработчиковУстановкиПараметровСеанса(Обработчики);
	КонецЕсли;
	// Конец ОбменСБанками
	
КонецПроцедуры

// См. процедуру ОбщегоНазначенияПереопределяемый.ПараметрыРаботыКлиентаПриЗапуске.
Процедура ПараметрыРаботыКлиентаПриЗапуске(Параметры) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСДеревомДанных

// Формирует дерево данных для заполнения или чтения прикладным решением.
//
// Параметры:
//  Макет - Макет - Макет с описанием структуры дерева (см. Обработка.ОбменСКонтрагентами.Макеты).
//
// Возвращаемое значение:
//  ДеревоЗначений - Дерево документа.
//
Функция ДеревоДокумента(Макет) Экспорт
	
	ВысотаТаблицы = Макет.ВысотаТаблицы;
	ШиринаТаблицы = Макет.ШиринаТаблицы;
	ТаблицаЗначений = Новый ТаблицаЗначений;
	
	// Считаем колонки из макета и добавим их в таблицу значений.
	Для НомерКолонки = 1 По ШиринаТаблицы Цикл
		ОбластьШапки = Макет.Область(5,НомерКолонки);
		Если Не ЗначениеЗаполнено(ОбластьШапки.Текст) Тогда
			ОбластьШапки = Макет.Область(4,НомерКолонки);
		КонецЕсли;
		НазваниеКолонки = ОбластьШапки.Текст;
		ТаблицаЗначений.Колонки.Добавить(НазваниеКолонки);
	КонецЦикла;
	
	// Считаем все перечисленные в макете реквизиты и добавим в таблицу значений.
	Для НомерСтроки = 7 По ВысотаТаблицы Цикл
		НоваяСтрока = ТаблицаЗначений.Добавить();
		Для НомерКолонки = 0 По ШиринаТаблицы-1 Цикл
			НоваяСтрока.Установить(НомерКолонки, Макет.Область(НомерСтроки, НомерКолонки + 1).Текст);
		КонецЦикла
	КонецЦикла;
	
	// Добавим служебные колонки, не описанные в макете.
	ДобавитьСлужебныеКолонкиВТаблицуДереваДокумента(ТаблицаЗначений);
	
	// Добавим некоторые колонки при необходимости (для совместимости с макетами старого формата).
	КолонкиДереваДокумента = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.КолонкиДереваДокумента();
	Для Каждого Колонка Из КолонкиДереваДокумента Цикл
		Если ТаблицаЗначений.Колонки.Найти(Колонка) = Неопределено Тогда
			ТаблицаЗначений.Колонки.Добавить(Колонка);
		КонецЕсли;
	КонецЦикла;
	
	КолУровней = 0;
	
	// Перенесем колонки в дерево значений и заодно вычислим максимальный уровень.
	ДеревоЗначений = Новый ДеревоЗначений;
	Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл
		ДеревоЗначений.Колонки.Добавить(Колонка.Имя);
		Если СтрНайти(Колонка.Имя, "Уровень") > 0 Тогда
			НомерУровня = Число(Сред(Колонка.Имя, 8, 2));
			Если НомерУровня > КолУровней Тогда
				КолУровней = НомерУровня;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Рекурсивно заполним дерево по таблице.
	ПолныйПуть = "";
	РекурсивноЗаполнитьСтрокиДерева(ДеревоЗначений, 1, КолУровней, ПолныйПуть, ТаблицаЗначений, 0);
	
	Возврат ДеревоЗначений;
	
КонецФункции

// Сохраняет значение в дереве данных.
//
// Параметры:
//  Дерево - ДеревоЗначений - дерево, содержащее данные электронного документа
//                            (см. ЭлектронноеВзаимодействие.ДеревоДокумента).
//  Реквизит - Строка - содержит полный путь к реквизиту дерева.
//  Значение - Произвольный - сохраняемое в дереве значение.
//  ПараметрыОбработкиОшибок - Строка - используется, чтобы переопределить текст выводимой по реквизиту ошибки
//                                      без указания параметров действий по клику на ошибке.
//                           - Структура - используется для указания указания параметров действий по клику на ошибке,
//                                         формируется функцией ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки.
//
Процедура ЗаполнитьЗначениеРеквизитаВДереве(Дерево, Реквизит, Значение, ПараметрыОбработкиОшибок = Неопределено) Экспорт
	
	// Заполним значение в нужной строке дерева.
	СтрокаДерева = Дерево.Строки.Найти(Реквизит, "ПолныйПуть", Истина);
	СтрокаДерева.Значение = Значение;
	Если СтрокаДерева.Признак = "Группа" ИЛИ СтрокаДерева.Признак = "Выбор" Тогда
		Возврат;
	КонецЕсли;

	// Заполним параметры обработки ошибок.
	ЗаполнитьПараметрыОбработкиОшибокВСтрокеДерева(СтрокаДерева, ПараметрыОбработкиОшибок);
	
	// Проставим значения в строках-родителях. При этом, если передана строка дерева, а не его корень, нужно пропустить
	// элементы пути вплоть до переданной строки.
	ПриведенныйПуть = Реквизит;
	Путь = "";
	Если ТипЗнч(Дерево) = Тип("СтрокаДереваЗначений") Тогда
		ПриведенныйПуть = Сред(Реквизит, СтрДлина(Дерево.ПолныйПуть) + 2);
		Путь = Путь + Дерево.ПолныйПуть;
	КонецЕсли;
	МассивРеквизитов = ОбщегоНазначенияКлиентСервер.РазложитьСтрокуПоТочкамИСлэшам(ПриведенныйПуть);
	Если МассивРеквизитов.Количество() = 1 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Элемент Из МассивРеквизитов Цикл
		Путь = ?(ЗначениеЗаполнено(Путь), Путь + "." + Элемент, Элемент);
		СтрокаДерева = Дерево.Строки.Найти(Путь, "ПолныйПуть", Истина);
		Если СтрокаДерева.Признак =  "Группа" Тогда
			СтрокаДерева.Значение = Истина;
		ИначеЕсли СтрокаДерева.Признак = "Выбор" Тогда
			ТекИндекс = МассивРеквизитов.Найти(Элемент);
			СтрокаДерева.Значение = МассивРеквизитов[ТекИндекс+1];
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Записывает данные из таблицы значений в дерево значений.
//
// Параметры:
//  Дерево - ДеревоЗначений - дерево данных, в котором необходимо сохранить данные.
//  ТаблицаДанных - таблицаЗначений - записываемые в дерево данные. Таблица может предварительно дополняться данными
//                                    для обработки ошибок:
//                                    см. ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю,
//                                    ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезНавигационнуюСсылку,
//                                    ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезОткрытиеФормы.
//  
//  НазваниеТаблицы - Строка - название таблицы в дереве.
//
Процедура ЗагрузитьТаблицуВДерево(Дерево, ТаблицаДанных, НазваниеТаблицы) Экспорт
	
	СтрокаТаблицы = Дерево.Строки.Найти(НазваниеТаблицы, "ПолныйПуть", Истина);
	НомерСтроки = 0;
	Для Каждого СтрокаДанных Из ТаблицаДанных Цикл
		НомерСтроки = НомерСтроки + 1;
		Если НомерСтроки = 1 Тогда
			ТекСтрока = СтрокаТаблицы.Строки[0];
		Иначе
			ПерваяСтрока = СтрокаТаблицы.Строки[0];
			ТекСтрока = СтрокаТаблицы.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(ТекСтрока, ПерваяСтрока);
			СкопироватьСтрокиДереваДляТаблицыРекурсивно(ТекСтрока, ПерваяСтрока);
		КонецЕсли;
		ТекСтрока.Значение = НомерСтроки;
		Для Каждого Колонка Из ТаблицаДанных.Колонки Цикл
			Если Колонка.Имя = "ДопДанныеПодписанные" ИЛИ Колонка.Имя = "ДопДанныеНеПодписанные" Тогда
				
				СтрокаДопДанных = ТекСтрока.Строки.Найти(ТекСтрока.ПолныйПуть + "." + Колонка.Имя, "ПолныйПуть");
				СтруктураДопДанных = СтрокаДанных[Колонка.Имя];
				
				Если ЗначениеЗаполнено(СтруктураДопДанных) Тогда
					ДобавитьДопДанныеВДерево(
							СтрокаДопДанных,
							СтруктураДопДанных,
							?(Колонка.Имя = "ДопДанныеПодписанные", Истина, Ложь));
				КонецЕсли;
				
				Продолжить;	
			ИначеЕсли Колонка.Имя = "НаборПараметровОбработкиОшибок" Тогда
				// Для каждого реквизита, для которого заданы параметры обработки ошибок, запишем их в соответствующую
				// строку дерева (строку реквизита).
				НаборПараметровОбработкиОшибок = СтрокаДанных[Колонка.Имя];
				Если НаборПараметровОбработкиОшибок <> Неопределено Тогда
					Для Каждого ЭлементНабораОбработкиОшибок Из НаборПараметровОбработкиОшибок Цикл
						ПолныйПуть = НазваниеТаблицы + ".НомерСтроки." + ЭлементНабораОбработкиОшибок.ИмяКолонки;
						СтрокаРеквизита = ТекСтрока.Строки.Найти(ПолныйПуть, "ПолныйПуть");
						
						Если СтрокаРеквизита <> Неопределено Тогда
							ЗаполнитьПараметрыОбработкиОшибокВСтрокеДерева(СтрокаРеквизита,
								ЭлементНабораОбработкиОшибок.ПараметрыОбработкиОшибок);
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			Иначе
				ПолныйПуть = НазваниеТаблицы + ?(Колонка.Имя = "НомерСтроки", ".", ".НомерСтроки.") + Колонка.Имя;
				СтрокаРеквизита = ТекСтрока.Строки.Найти(ПолныйПуть, "ПолныйПуть");
				Если СтрокаРеквизита <> Неопределено Тогда
					Если СтрокаРеквизита.Признак = "Таблица" И НЕ СтрокаДанных[Колонка.Имя] = Неопределено Тогда
						ЗагрузитьТаблицуВДерево(ТекСтрока, СтрокаДанных[Колонка.Имя], ПолныйПуть);
					ИначеЕсли СтрокаРеквизита.Признак = "Группа" И НЕ СтрокаДанных[Колонка.Имя] = Неопределено Тогда
						ЗагрузитьСтруктуруВГруппуДерева(ТекСтрока, СтрокаДанных[Колонка.Имя], ПолныйПуть);
						СтрокаРеквизита.Значение = Истина;
					Иначе
						СтрокаРеквизита.Значение = СтрокаДанных[Колонка.Имя];
					КонецЕсли
				КонецЕсли;
			КонецЕсли;
		КонецЦикла
		
	КонецЦикла;
	СтрокаТаблицы.Значение = ТаблицаДанных.Количество();
	
	МассивРеквизитов = ОбщегоНазначенияКлиентСервер.РазложитьСтрокуПоТочкамИСлэшам(НазваниеТаблицы);
	Если МассивРеквизитов.Количество() = 1 Тогда
		Возврат;
	КонецЕсли;
	Путь = "";
	Для Каждого Элемент Из МассивРеквизитов Цикл
		Путь = ?(ЗначениеЗаполнено(Путь), Путь + "." + Элемент, Элемент);
		
		СтрокаДерева = Дерево.Строки.Найти(Путь, "ПолныйПуть", Истина);
		Если СтрокаДерева <> Неопределено Тогда
			Если СтрокаДерева.Признак = "Группа" Тогда
				СтрокаДерева.Значение = Истина;
			ИначеЕсли СтрокаДерева.Признак = "Выбор" Тогда
				ТекИндекс = МассивРеквизитов.Найти(Элемент);
				СтрокаДерева.Значение = МассивРеквизитов[ТекИндекс+1];
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Записывает данные из структуры в группу дерева значений
//
// Параметры:
//  Дерево - ДеревоЗначений - дерево данных, в котором необходимо сохранить данные
//  СтруктураДанных - Структура - данные, которые должны быть загружены в группу
//  Путь - Строка - название группы в дереве.
//
Процедура ЗагрузитьСтруктуруВГруппуДерева(Дерево, СтруктураДанных, Путь) Экспорт
	
	СтрокаТаблицы = Дерево.Строки.Найти(Путь, "ПолныйПуть", Истина);
	Для Каждого Элемент Из СтруктураДанных Цикл
		ПолныйПуть = Путь + "." + Элемент.Ключ;
		СтрокаРеквизита = СтрокаТаблицы.Строки.Найти(ПолныйПуть, "ПолныйПуть");
		Если СтрокаРеквизита <> Неопределено Тогда
			Если СтрокаРеквизита.Признак = "Таблица" И НЕ Элемент.Значение = Неопределено Тогда
				ЗагрузитьТаблицуВДерево(СтрокаТаблицы, Элемент.Значение, ПолныйПуть);
			ИначеЕсли СтрокаРеквизита.Признак = "Группа" И НЕ Элемент.Значение = Неопределено Тогда
				ЗагрузитьСтруктуруВГруппуДерева(СтрокаТаблицы, Элемент.Значение, ПолныйПуть);
			Иначе
				СтрокаРеквизита.Значение = Элемент.Значение;
			КонецЕсли
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Добавляет строку в таблицу из коллекции свойств
//
// Параметры:
//  Дерево - ДеревоЗначений - дерево данных, в котором необходимо сохранить данные
//  Коллекция - Структура, Выборка, СтрокаТаблицыЗначений - коллекция для сохранения в дереве
//  НазваниеТаблицы - Строка - название таблицы в дереве.
//
Процедура ДобавитьЗаписьВТаблицуДерева(Дерево, Коллекция, НазваниеТаблицы) Экспорт
	
	ШапкаТаблицы = Дерево.Строки.Найти(НазваниеТаблицы, "ПолныйПуть", Истина);
	ШапкаТаблицы.Значение = ?(ЗначениеЗаполнено(ШапкаТаблицы.Значение), ШапкаТаблицы.Значение + 1, 1);
	
	СтруктураКолонок = СтруктураКолонокТаблицыДерева(Дерево, НазваниеТаблицы);
	ЗаполнитьЗначенияСвойств(СтруктураКолонок, Коллекция);
	ПерваяСтрокаДерева = Дерево.Строки.Найти(НазваниеТаблицы + ".НомерСтроки", "ПолныйПуть", Истина);
	
	Если ПустаяСтрока(ПерваяСтрокаДерева.Значение) Тогда
		НоваяСтрока = ПерваяСтрокаДерева;
		НомерСтроки = 1;
	Иначе
		Таблица = Дерево.Строки.Найти(НазваниеТаблицы, "ПолныйПуть", Истина);
		НомерСтроки = Таблица.Строки.Количество() + 1;
		НоваяСтрока = Таблица.Строки.Добавить();
		НоваяСтрока.ПолныйПуть = НазваниеТаблицы + ".НомерСтроки";
		СкопироватьСтрокиДереваДляТаблицыРекурсивно(НоваяСтрока, ПерваяСтрокаДерева);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ПерваяСтрокаДерева);
	НоваяСтрока.Значение = НомерСтроки;
	
	Для Каждого Элемент Из НоваяСтрока.Строки Цикл
		
		Если НазваниеКолонки(Элемент.ПолныйПуть) = "ДопДанные" Тогда
			Если СтруктураКолонок.Свойство("ДопДанныеПодписанные") Тогда
				ДобавитьДопДанныеВДерево(Элемент, СтруктураКолонок.ДопДанныеПодписанные, Истина);
			КонецЕсли;
			Если СтруктураКолонок.Свойство("ДопДанныеНеПодписанные") Тогда
				ДобавитьДопДанныеВДерево(Элемент, СтруктураКолонок.ДопДанныеНеПодписанные);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		Элемент.Значение = СтруктураКолонок[НазваниеКолонки(Элемент.ПолныйПуть)];
		
	КонецЦикла;
	
	МассивРеквизитов = ОбщегоНазначенияКлиентСервер.РазложитьСтрокуПоТочкамИСлэшам(НазваниеТаблицы);
	Если МассивРеквизитов.Количество() = 1 Тогда
		Возврат;
	КонецЕсли;
	Путь = "";
	Для Каждого Элемент Из МассивРеквизитов Цикл
		Путь = ?(ЗначениеЗаполнено(Путь), Путь + "." + Элемент, Элемент);
		
		СтрокаДерева = Дерево.Строки.Найти(Путь, "ПолныйПуть", Истина);
		Если СтрокаДерева <> Неопределено Тогда
			Если СтрокаДерева.Признак = "Группа" Тогда
				СтрокаДерева.Значение = Истина;
			ИначеЕсли СтрокаДерева.Признак = "Выбор" Тогда
				ТекИндекс = МассивРеквизитов.Найти(Элемент);
				СтрокаДерева.Значение = МассивРеквизитов[ТекИндекс+1];
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает таблицу значений с данными дерева значений
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - дерево с данными
//  НазваниеТаблицы - Строка - название таблицы в дереве, если необходимо получить данные табличной части.
//
// Возвращаемое значение:
//  ТаблицаЗначений - содержит данные дерева.
//
Функция ДанныеДерева(ДеревоДанных, НазваниеТаблицы = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(НазваниеТаблицы) Тогда
		Возврат ДанныеТаблицыДерева(ДеревоДанных, НазваниеТаблицы);
	Иначе
		Возврат ДанныеШапкиДерева(ДеревоДанных);
	КонецЕсли;
	
КонецФункции

// Возвращает строку дерева значений для заполнения в прикладном решении
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - дерево с данными
//  НазваниеПоля - Строка - название поля в дереве, содержащее полный путь к реквизиту
//  НайтиРекурсивно - Булево, Истина - если требуется рекурсивный поиск.
//
// Возвращаемое значение:
//  Строка таблицы значений - содержит строку дерева.
//
Функция СтрокаДерева(ДеревоДанных, НазваниеПоля, НайтиРекурсивно = Ложь) Экспорт
	
	СтрокаВозврата = ДеревоДанных.Строки.Найти(НазваниеПоля, "ПолныйПуть", НайтиРекурсивно);
	Если СтрокаВозврата.Признак = "Группа" Тогда
		СтрокаВозврата.Значение = Истина;
	КонецЕсли;
	Возврат СтрокаВозврата;

КонецФункции

// Копирует строки дерева значений
//
// Параметры:
//  СтрокаПолучатель - СтрокаДереваЗначений - строка дерева значений, в которую будут скопированы строки
//  СтрокаИсточник - СтрокаДереваЗначений - строка дерева значений, из которой будут скопированы строки.
Процедура СкопироватьСтрокиДереваРекурсивно(СтрокаПолучатель, СтрокаИсточник) Экспорт
	
	Для Каждого Реквизит Из СтрокаИсточник.Строки Цикл
		НовСтрока = СтрокаПолучатель.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтрока, Реквизит);
		Если Реквизит.Строки.Количество() > 0 Тогда
			СкопироватьСтрокиДереваРекурсивно(НовСтрока, Реквизит);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

// В процедуре происходит добавление данных из СтруктурыДанных в ДеревоЗначений.
//
// Параметры:
//  СтрокаДерева - ДеревоЗначений, СтрокаДереваЗначений - содержит данные.
//  СтруктураДопДанных - Структура - данные, которые необходимо поместить в дерево.
//  ЮридическиЗначимый - Булево - если Истина - то текущие данные необходимо по возможности поместить в основной ЭД.
//
Процедура ДобавитьДопДанныеВДерево(СтрокаДерева, СтруктураДопДанных, ЮридическиЗначимый = Ложь) Экспорт
	
	Если СтруктураДопДанных.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрокаДерева.Строки.Родитель = Неопределено Тогда
		СтрокаДереваЗначений = СтрокаДерева(СтрокаДерева, "ДопДанные");
		Если ЮридическиЗначимый Тогда
			СтрокаДопДанные = СтрокаДереваЗначений.Строки.Найти("ДопДанные.Подписанные", "ПолныйПуть");
		Иначе
			СтрокаДопДанные = СтрокаДереваЗначений.Строки.Найти("ДопДанные.НеПодписанные", "ПолныйПуть");
		КонецЕсли;
	ИначеЕсли НазваниеКолонки(СтрокаДерева.ПолныйПуть) = "ДопДанныеПодписанные"
			ИЛИ НазваниеКолонки(СтрокаДерева.ПолныйПуть) = "ДопДанныеНеПодписанные" Тогда
		СтрокаДопДанные = СтрокаДерева;
	Иначе
		СтрокаДерева.Значение = Истина;
		СтрокаДопДанные = СтрокаДерева.Строки[?(ЮридическиЗначимый, 0 ,1)];
	КонецЕсли;
	СтрокаДопДанные.Значение = Истина;
	
	Для Каждого Элемент Из СтруктураДопДанных Цикл
		НовСтрока = СтрокаДопДанные.Строки.Найти(СтрокаДопДанные.ПолныйПуть + "." + Элемент.Ключ, "ПолныйПуть");
		Если НовСтрока = Неопределено Тогда
			НовСтрока = СтрокаДопДанные.Строки.Добавить();
			НовСтрока.ПолныйПуть = СтрокаДопДанные.ПолныйПуть + "." + Элемент.Ключ;
			НомерУровня = СтрЧислоВхождений(НовСтрока.ПолныйПуть, ".") + 1;
			НовСтрока["Уровень" + НомерУровня] = НазваниеКолонки(НовСтрока.ПолныйПуть);
		КонецЕсли;
		НовСтрока.Значение = Элемент.Значение;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает название реквизита из полного пути
//
// Параметры:
//  ПолныйПуть - Строка - Полный путь до реквизита в дереве.
//
// Возвращаемое значение:
//  Строка - Название реквизита
//
Функция НазваниеКолонки(ПолныйПуть) Экспорт
	
	ПозицияРазделителя = СтрНайти(ПолныйПуть, ".", НаправлениеПоиска.СКонца);
	Если ПозицияРазделителя = 0 Тогда
		Результат = СокрЛП(ПолныйПуть);
	Иначе
		Результат = СокрЛП(Сред(ПолныйПуть, ПозицияРазделителя + 1));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Проверяет существование реквизита в дереве, по указанному пути.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - область данных поиска.
//  ПолныйПуть - Строка - значение поиска.
// 
// Возвращаемое значение:
//  Булево - Истина, если реквизит существует.
//
Функция СуществуетРеквизитВДереве(ДеревоДанных, ПолныйПуть) Экспорт
	
	Существует = Ложь;
	НайденнаяСтрока = ДеревоДанных.Строки.Найти(ПолныйПуть, "ПолныйПуть", Истина);
	Если НайденнаяСтрока <> Неопределено Тогда
		Существует = Истина;
	КонецЕсли;
	
	Возврат Существует;
	
КонецФункции

// Возвращает значение реквизита в дереве.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - объект поиска.
//  ПолныйПуть - Произвольный - значение поиска.
//  СообщатьОбОшибке - Булево - если Истина сообщать об ошибках поиска.
// 
// Возвращаемое значение:
//  Произвольный - результат поиска.
//
Функция ЗначениеРеквизитаВДереве(ДеревоДанных, ПолныйПуть, СообщатьОбОшибке = Истина) Экспорт
	
	Возврат ЭлектронноеВзаимодействиеСлужебный.ЗначениеРеквизитаВДереве(ДеревоДанных, ПолныйПуть, СообщатьОбОшибке);
	
КонецФункции

// Значение реквизита строки дерева разбора.
//
// Параметры:
//  ДеревоРазбора	 - ДеревоЗначений - объект поиска.
//  СтрокаДерева	 - КоллекцияСтрокДереваЗначений - строка дерева значений.
//  ПутьКРеквизиту	 - Строка - путь к реквизиту.
// 
// Возвращаемое значение:
//  Произвольный - результат поиска.
//
Функция ЗначениеРеквизитаСтрокиДереваРазбора(ДеревоРазбора, СтрокаДерева, ПутьКРеквизиту) Экспорт
	
	Возврат ЭлектронноеВзаимодействиеСлужебный.ЗначениеРеквизитаСтрокиДереваРазбора(
		ДеревоРазбора, СтрокаДерева, ПутьКРеквизиту);
	
КонецФункции

// Задает параметры обработки ошибок для реквизита дерева электронного документа.
//
// Параметры:
//  Дерево - ДеревоЗначений - дерево, содержащее данные электронного документа
//                            (см. ЭлектронноеВзаимодействие.ДеревоДокумента).
//  Реквизит - Строка - содержит полный путь к реквизиту дерева.
//  ПараметрыОбработкиОшибок - Строка - используется, чтобы переопределить текст выводимой по реквизиту ошибки
//                                      без указания параметров действий по клику на ошибке.
//                           - Структура - используется для указания указания параметров действий по клику на ошибке,
//                                         формируется функцией ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки.
//
Процедура ДобавитьВРеквизитОбработкуОшибки(Дерево, Реквизит, ПараметрыОбработкиОшибок) Экспорт
	
	СтрокиДерева = Дерево.Строки.НайтиСтроки(Новый Структура("ПолныйПуть", Реквизит), Истина);
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		ЗаполнитьПараметрыОбработкиОшибокВСтрокеДерева(СтрокаДерева, ПараметрыОбработкиОшибок);
	КонецЦикла;
	
КонецПроцедуры

// Сохраняет в таблице значений, подлежащей загрузке в дерево электронного документа, параметры ошибок
// для обработки через механизм сообщений пользователю.
//
// Параметры:
//  СтрокиТаблицы	 - Массив - строки таблицы значений, в которых нужно заполнить параметры обработки ошибок.
//                   - ТаблицаЗначений - таблица значений, в каждой строке которой нужно сохранить переданные
//                                       параметры обработки ошибок.
//  ПолеТаблицы		 - Строка - имя колонки таблицы, для которой указываются параметры обработки ошибок.
//  КлючДанных		 - Строка - имя колонки таблицы, значение которой будет использовано в качестве ключа данных
//                              при выводе сообщения пользователю.
//                   - ЛюбаяСсылка - ссылка, значение которой будет использовано в качестве ключа при выводе
//                                   сообщения пользователю.
//  ПутьКДанным		 - Строка - путь к данным, который будет указан в сообщении пользователю. Если указывается путь
//                              к реквизиту таблицы, скобки ("[", "]") указывать не следует. Передача индекса строки
//                              производится через параметр "ПолеНомераСтроки".
//  ТекстОшибки		 - Строка - используется, чтобы переопределить текст выводимой по реквизиту ошибки.
//  ПолеНомераСтроки - Строка - имя колонки таблицы, из которой будет подставляться в параметр "ПутьКДанным"
//                              индекс строки.
//
Процедура ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокиТаблицы, ПолеТаблицы, КлючДанных = "",
	ПутьКДанным = "", ТекстОшибки = "", ПолеНомераСтроки = Неопределено) Экспорт
	
	НаборСтрок = Неопределено;
	ПодготовитьВходныеПараметрыДляДобавленияОбработкиОшибки(СтрокиТаблицы, НаборСтрок);
	
	// Для каждой строки получим уникальные параметры обработки ошибки и добавим их в набор строки.
	Для Каждого СтрокаТаблицыДанных Из НаборСтрок Цикл
		// В первый раз нужно инициализировать значение служебного реквизита "НаборПараметровОбработкиОшибок".
		Если СтрокаТаблицыДанных.НаборПараметровОбработкиОшибок = Неопределено Тогда
			СтрокаТаблицыДанных.НаборПараметровОбработкиОшибок = Новый Массив;
		КонецЕсли;
		
		// Подготовим значения параметров.
		Если ЗначениеЗаполнено(КлючДанных) Тогда
			// В качестве ключа можно передавать либо путь к полю таблицы, содержащему ключ, либо сам ключ.
			Если ТипЗнч(КлючДанных) = Тип("Строка") Тогда
			 	ЗначениеКлюча = СтрокаТаблицыДанных[КлючДанных];
			Иначе
				ЗначениеКлюча = КлючДанных;
			КонецЕсли;
		Иначе
			ЗначениеКлюча = Неопределено;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПутьКДанным) Тогда
			// Если в таблице содержится номер строки, его нужно подставить в путь к полю.
			Если ПолеНомераСтроки <> Неопределено Тогда
				ИндексСтроки = СтрокаТаблицыДанных[ПолеНомераСтроки] - 1;
				
				ПозицияКонцаОсновногоПути = СтрНайти(ПутьКДанным, ".", НаправлениеПоиска.СКонца);
				Если ПозицияКонцаОсновногоПути <> 0 Тогда
					ОсновнойПуть = Лев(ПутьКДанным, ПозицияКонцаОсновногоПути - 1);
					ДополнительныйПуть = Сред(ПутьКДанным, ПозицияКонцаОсновногоПути + 1);
					ЗначениеПутиКДанным = СтрШаблон("%1[%2].%3", ОсновнойПуть, ИндексСтроки, ДополнительныйПуть);
				Иначе
					ЗначениеПутиКДанным = ПутьКДанным;
				КонецЕсли;
			Иначе
				ЗначениеПутиКДанным = ПутьКДанным;
			КонецЕсли;
		Иначе
			ЗначениеПутиКДанным = "";
		КонецЕсли;
		
		// Добавим параметры обработки ошибки в набор строки.
		ПараметрыОбработкиОшибки = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(ЗначениеКлюча,
			ЗначениеПутиКДанным,,,, ТекстОшибки);
		ДобавитьПараметрыОбработкиОшибкиВНабор(СтрокаТаблицыДанных, ПолеТаблицы, ПараметрыОбработкиОшибки);
	КонецЦикла;

КонецПроцедуры

// Сохраняет в таблице значений, подлежащей загрузке в дерево электронного документа, параметры ошибок
// для обработки через механизм навигационных ссылок.
// Примечание: в данной версии библиотеки не используется.
//
// Параметры:
//  СтрокиТаблицы	        - Массив - строки таблицы значений, в которых нужно заполнить параметры обработки ошибок.
//                          - ТаблицаЗначений - таблица значений, в каждой строке которой нужно сохранить переданные
//                                              параметры обработки ошибок.
//  ПолеТаблицы		        - Строка - имя колонки таблицы, для которой указываются параметры обработки ошибок.
//  ПолеНавигационнойСсылки - Строка - имя колонки таблицы, из которой будет извлекаться навигационная ссылка для
//                                     открытия формы при клике на ошибку.
//
Процедура ДобавитьВТаблицуОбработкуОшибкиЧерезНавигационнуюСсылку(СтрокиТаблицы, ПолеТаблицы, ПолеНавигационнойСсылки) Экспорт

	НаборСтрок = Неопределено;
	ПодготовитьВходныеПараметрыДляДобавленияОбработкиОшибки(СтрокиТаблицы, НаборСтрок);
	
	// Для каждой строки получим уникальные параметры обработки ошибки и добавим их в набор строки.
	Для Каждого СтрокаТаблицыДанных Из НаборСтрок Цикл
		// В первый раз нужно инициализировать значение служебного реквизита "НаборПараметровОбработкиОшибок".
		Если СтрокаТаблицыДанных.НаборПараметровОбработкиОшибок = Неопределено Тогда
			СтрокаТаблицыДанных.НаборПараметровОбработкиОшибок = Новый Массив;
		КонецЕсли;
		
		// Добавим параметры обработки ошибки в набор строки.
		ПараметрыОбработкиОшибки = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(,, СтрокаТаблицыДанных[ПолеНавигационнойСсылки]);
		ДобавитьПараметрыОбработкиОшибкиВНабор(СтрокаТаблицыДанных, ПолеТаблицы, ПараметрыОбработкиОшибки);
	КонецЦикла;

КонецПроцедуры

// Сохраняет в таблице значений, подлежащей загрузке в дерево электронного документа, параметры ошибок
// для обработки через открытие формы.
// Примечание: в данной версии библиотеки не используется.
//
// Параметры:
//  СтрокиТаблицы	           - Массив - строки таблицы значений, в которых нужно заполнить параметры обработки ошибок.
//                             - ТаблицаЗначений - таблица значений, в каждой строке которой нужно сохранить переданные
//                                              параметры обработки ошибок.
//  ПолеТаблицы		           - Строка - имя колонки таблицы, для которой указываются параметры обработки ошибок.
//  ИмяФормы                   - Строка - полное имя формы, которую нужно открыть при клике по ошибке.
//  СтатичныеПараметрыФормы    - Структура - структура, которая будет передана в форму в качестве параметров.
//  ДинамическиеПараметрыФормы - Структура - описывает параметры, передаваемые в открываемую форму, значения по которым
//                                           формируются для каждой строки таблицы индивидуально:
//                                 * Ключ     - Строка - содержит имя параметра.
//                                 * Значение - Строка - имя поля таблицы, содержащего значение параметра.
//
Процедура ДобавитьВТаблицуОбработкуОшибкиЧерезОткрытиеФормы(СтрокиТаблицы, ПолеТаблицы, ИмяФормы,
	Знач СтатичныеПараметрыФормы = Неопределено, ДинамическиеПараметрыФормы = Неопределено) Экспорт

	НаборСтрок = Неопределено;
	ПодготовитьВходныеПараметрыДляДобавленияОбработкиОшибки(СтрокиТаблицы, НаборСтрок);
	
	// Для каждой строки получим уникальные параметры обработки ошибки и добавим их в набор строки.
	Для Каждого СтрокаТаблицыДанных Из НаборСтрок Цикл
		// В первый раз нужно инициализировать значение служебного реквизита "НаборПараметровОбработкиОшибок".
		Если СтрокаТаблицыДанных.НаборПараметровОбработкиОшибок = Неопределено Тогда
			СтрокаТаблицыДанных.НаборПараметровОбработкиОшибок = Новый Массив;
		КонецЕсли;
		
		// Подготовим значения параметров.
		Если СтатичныеПараметрыФормы = Неопределено Тогда
			ЗначениеПараметровФормы = Новый Структура;
		Иначе
			ЗначениеПараметровФормы = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(СтатичныеПараметрыФормы);
		КонецЕсли;
		
		// Могли быть переданы динамические параметры, значения которых будут разными для каждой строки.
		Если ДинамическиеПараметрыФормы <> Неопределено Тогда
			Для Каждого КлючИЗначение Из ДинамическиеПараметрыФормы Цикл
				ЗначениеПараметровФормы.Вставить(КлючИЗначение.Ключ, СтрокаТаблицыДанных[КлючИЗначение.Значение]);
			КонецЦикла;
		КонецЕсли;
		
		// Добавим параметры обработки ошибки в набор строки.
		ПараметрыОбработкиОшибки = ЭлектронноеВзаимодействиеКлиентСервер.НовыеПараметрыОбработкиОшибки(,,, ИмяФормы, ЗначениеПараметровФормы);
		ДобавитьПараметрыОбработкиОшибкиВНабор(СтрокаТаблицыДанных, ПолеТаблицы, ПараметрыОбработкиОшибки);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ИнтеграцияСБиблиотекойТехнологияСервиса

// См. процедуру ТарификацияПереопределяемый.ПриФормированииСпискаУслуг
//
Процедура ПриФормированииСпискаУслуг(ПоставщикиУслуг) Экспорт
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСКонтрагентами") Тогда
		МодульОбменСКонтрагентами = ОбщегоНазначения.ОбщийМодуль("ОбменСКонтрагентами");
		МодульОбменСКонтрагентами.ПриФормированииСпискаУслуг(ПоставщикиУслуг);
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	// ЭлектронноеВзаимодействие.БизнесСеть
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.БизнесСеть") Тогда
		МодульОбменБизнесСеть = ОбщегоНазначения.ОбщийМодуль("БизнесСеть");
		МодульОбменБизнесСеть.ПриФормированииСпискаУслуг(ПоставщикиУслуг);
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.БизнесСеть
	
КонецПроцедуры

#КонецОбласти

// Обработчик обновления БЭД 1.3.10.24.
// 
// Параметры:
//  Параметры - Структура - параметры.
//
Процедура УстановитьПризнакИспользованияНесколькихОрганизаций(Параметры) Экспорт
	
	ТипОрганизации = Неопределено;
	МассивТипов = Метаданные.ОпределяемыеТипы.Организация.Тип.Типы();
	Для Каждого ТипСправочника Из МассивТипов Цикл
		
		Если ТипСправочника <> Тип("СправочникСсылка.ИдентификаторыОбъектовМетаданных")
			И Справочники.ТипВсеСсылки().СодержитТип(ТипСправочника) Тогда
			ТипОрганизации = ТипСправочника;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТипОрганизации = Неопределено Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	ОрганизацияСсылка = Новый(ТипОрганизации);
	ИмяСправочникаОрганизации = ОрганизацияСсылка.Метаданные().Имя;
	
	Количество = 0;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(*) КАК Количество
		|ИЗ
		|	&СправочникОрганизации КАК Организации
		|ГДЕ
		|	НЕ Организации.Предопределенный
		|	И НЕ Организации.ПометкаУдаления";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СправочникОрганизации", 
		"Справочник." + ИмяСправочникаОрганизации);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Количество = Выборка.Количество;
	КонецЕсли;
	
	Если Количество > 1 Тогда
		Константы.ИспользоватьНесколькоОрганизацийБЭД.Установить(Истина);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Параметры.ОбработкаЗавершена = Истина;
	
КонецПроцедуры

// Используется при добавлении отложенных обработчиков обновления,
// когда не требуется регистрировать данные к обработке. Не заполнять.
// 
// Параметры:
//  Параметры - Структура - параметры.
//
Процедура ЗаполнениеДанныхОбновления(Параметры) Экспорт
КонецПроцедуры

// См. процедуру ИнтернетПоддержкаПользователейПереопределяемый.ПриОпределенииСервисовСопровождения.
//
Процедура ПриОпределенииСервисовСопровождения(МодулиСервисов) Экспорт
	
	// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.РаботаСНоменклатурой") Тогда 
		ОбщийМодуль = ОбщегоНазначения.ОбщийМодуль("РаботаСНоменклатурой");
		ОбщийМодуль.ПриОпределенииСервисовСопровождения(МодулиСервисов);	
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьСлужебныеКолонкиВТаблицуДереваДокумента(ТаблицаДерева)

	Колонки = Новый Массив;
	
	Колонки.Добавить("ТипЗначения");
	Колонки.Добавить("Значение");
	Колонки.Добавить("ПолныйПуть");
	Колонки.Добавить("ТекстОшибки");
	Колонки.Добавить("ПутьКДаннымСообщения");
	Колонки.Добавить("КлючСообщения");
	Колонки.Добавить("НавигационнаяСсылка");
	Колонки.Добавить("ИмяФормы");
	Колонки.Добавить("ПараметрыФормы");
	
	Для Каждого Колонка Из Колонки Цикл
		Если ТаблицаДерева.Колонки.Найти(Колонка) = Неопределено Тогда
			ТаблицаДерева.Колонки.Добавить(Колонка);
		КонецЕсли;
	КонецЦикла;
	
	// Сдвинем колонки "Значение" и "ПолныйПуть" в начало для удобства отладки.
	КолонкаЗначение = ТаблицаДерева.Колонки.Найти("Значение");
	ТаблицаДерева.Колонки.Сдвинуть("Значение", -ТаблицаДерева.Колонки.Индекс(КолонкаЗначение));
	КолонкаПолныйПуть = ТаблицаДерева.Колонки.Найти("ПолныйПуть");
	ТаблицаДерева.Колонки.Сдвинуть("ПолныйПуть", -ТаблицаДерева.Колонки.Индекс(КолонкаПолныйПуть));

КонецПроцедуры


Функция ДанныеШапкиДерева(ДеревоДанных)
	
	ТаблицаВозврата = Новый ТаблицаЗначений;
	Для Каждого Колонка Из ДеревоДанных.Колонки Цикл
		ТаблицаВозврата.Колонки.Добавить(Колонка.Имя);
	КонецЦикла;
	
	ЗаполнитьТаблицуРекурсивно(ТаблицаВозврата, ДеревоДанных.Строки);
	
	Возврат ТаблицаВозврата;
	
КонецФункции

Функция ДанныеТаблицыДерева(ДеревоДанных, НазваниеТаблицы)
	
	ТаблицаВозврата = Новый ТаблицаЗначений;
	СтрокаТаблицы = ДеревоДанных.Строки.Найти(НазваниеТаблицы, "ПолныйПуть");
	НомерСтроки = СтрокаТаблицы.Строки[0];
	Для Каждого Строка Из НомерСтроки.Строки Цикл
		ТаблицаВозврата.Колонки.Добавить(НазваниеКолонки(Строка.ПолныйПуть));
	КонецЦикла;
	
	Для Каждого Строка Из СтрокаТаблицы.Строки Цикл
		НовСтрока = ТаблицаВозврата.Добавить();
		Для Каждого СтрокаРеквизита Из Строка.Строки Цикл
			НовСтрока[НазваниеКолонки(СтрокаРеквизита.ПолныйПуть)] = СтрокаРеквизита.Значение;
		КонецЦикла;
	КонецЦикла;
		
	Возврат ТаблицаВозврата;
	
КонецФункции

Процедура ЗаполнитьТаблицуРекурсивно(ТаблицаЗначений, СтрокиДерева, НазваниеТаблицы = Неопределено)
	
	Для Каждого Строка Из СтрокиДерева Цикл
		Если НЕ Строка.Признак = "Таблица" Тогда
			НовСтрока = ТаблицаЗначений.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрока, Строка);
			Если Строка.Строки.Количество()>0 Тогда
				ЗаполнитьТаблицуРекурсивно(ТаблицаЗначений, Строка.Строки);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура СкопироватьСтрокиДереваДляТаблицыРекурсивно(СтрокаПолучатель, СтрокаИсточник)
	
	СлужебныеСвойства = "ТекстОшибки, ПутьКДаннымСообщения, КлючСообщения, НавигационнаяСсылка, ИмяФормы, ПараметрыФормы";
	
	Для Каждого Реквизит Из СтрокаИсточник.Строки Цикл
		Если НазваниеКолонки(Реквизит.ПолныйПуть) = "НомерСтроки" И ТипЗнч(Реквизит.Значение) = Тип("Число")
				И Реквизит.Значение > 1 Тогда
			Прервать;
		КонецЕсли;
		НовСтрока = СтрокаПолучатель.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтрока, Реквизит,, СлужебныеСвойства);
		НовСтрока.Значение = "";
		Если Реквизит.Строки.Количество() > 0 Тогда
			СкопироватьСтрокиДереваДляТаблицыРекурсивно(НовСтрока, Реквизит);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Функция СтруктураКолонокТаблицыДерева(Дерево, НазваниеТаблицы)
	
	СтруктураВозврата = Новый Структура;
	
	СтрокаДерева = Дерево.Строки.Найти(НазваниеТаблицы + ".НомерСтроки", "ПолныйПуть", Истина);
	Для Каждого Подстрока Из СтрокаДерева.Строки Цикл
		НазваниеКолонки = НазваниеКолонки(Подстрока.ПолныйПуть);
		Если НазваниеКолонки = "ДопДанные" Тогда
			СтруктураВозврата.Вставить("ДопДанныеПодписанные");
			СтруктураВозврата.Вставить("ДопДанныеНеПодписанные");
		Иначе
			СтруктураВозврата.Вставить(НазваниеКолонки);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтруктураВозврата;
	
КонецФункции

Процедура РекурсивноЗаполнитьСтрокиДерева(ДеревоЗначений, Знач НомерУровня, КолУровней, Знач ПолныйПутьВДереве, ТЗ, НомерСтрокиТЗ)
	
	ЛокальныйПуть = ПолныйПутьВДереве;
	СтрокаТекУровня = Неопределено;
	Пока НомерСтрокиТЗ < ТЗ.Количество() Цикл
		СтрокаТЗ = ТЗ[НомерСтрокиТЗ];
		Для Сч = НомерУровня По КолУровней Цикл
			ИдТекУровня = "Уровень" + Сч;
			Если ТЗ.Колонки.Найти(ИдТекУровня) <> Неопределено И ЗначениеЗаполнено(СтрокаТЗ[ИдТекУровня]) Тогда
				Если НомерУровня < Сч Тогда
					РекурсивноЗаполнитьСтрокиДерева(СтрокаТекУровня, Сч, КолУровней, ЛокальныйПуть, ТЗ, НомерСтрокиТЗ);
				ИначеЕсли НомерУровня = Сч Тогда
					СтрокаТекУровня = ДеревоЗначений.Строки.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТекУровня, СтрокаТЗ);
					СтрокаТекУровня.ТипЗначения = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ОписаниеТипаПоПараметрам(СтрокаТЗ.Типы, СтрокаТЗ.КвалификаторыЧисла, СтрокаТЗ.КвалификаторыСтроки, СтрокаТЗ.КвалификаторыДаты);
					ЛокальныйПуть = ?(ПолныйПутьВДереве = "", "", ПолныйПутьВДереве + ".") + СтрокаТекУровня["Уровень" + Сч];
					СтрокаТекУровня.ПолныйПуть = ЛокальныйПуть;
				КонецЕсли;
				НомерСтрокиТЗ = НомерСтрокиТЗ + 1;
				Прервать;
			КонецЕсли;
			Если Сч >= КолУровней Тогда
				НомерСтрокиТЗ = НомерСтрокиТЗ - 1;
				Возврат;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыОбработкиОшибокВСтрокеДерева(СтрокаДерева, ПараметрыОбработкиОшибок)
	
	Если ТипЗнч(ПараметрыОбработкиОшибок) = Тип("Структура") Тогда
		ПараметрыОбработкиОшибок.Свойство("ТекстОшибки", СтрокаДерева.ТекстОшибки);
		ПараметрыОбработкиОшибок.Свойство("КлючСообщения", СтрокаДерева.КлючСообщения);
		ПараметрыОбработкиОшибок.Свойство("ПутьКДаннымСообщения", СтрокаДерева.ПутьКДаннымСообщения);
		ПараметрыОбработкиОшибок.Свойство("НавигационнаяСсылка", СтрокаДерева.НавигационнаяСсылка);
		ПараметрыОбработкиОшибок.Свойство("ИмяФормы", СтрокаДерева.ИмяФормы);
		ПараметрыОбработкиОшибок.Свойство("ПараметрыФормы", СтрокаДерева.ПараметрыФормы);
	ИначеЕсли ТипЗнч(ПараметрыОбработкиОшибок) = Тип("Строка") Тогда
		СтрокаДерева.ТекстОшибки = ПараметрыОбработкиОшибок;
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьПараметрыОбработкиОшибкиВНабор(СтрокаТаблицыДанных, ПолеТаблицы, ПараметрыОбработкиОшибки)
	
	ЭлементНабораПараметров = Новый Структура("ИмяКолонки, ПараметрыОбработкиОшибок",
		ПолеТаблицы, ПараметрыОбработкиОшибки);
	СтрокаТаблицыДанных.НаборПараметровОбработкиОшибок.Добавить(ЭлементНабораПараметров);

КонецПроцедуры

Процедура ПодготовитьВходныеПараметрыДляДобавленияОбработкиОшибки(СтрокиТаблицы, НаборСтрок)
	
	// Получим единую типизацию.
	Если ТипЗнч(СтрокиТаблицы) = Тип("Массив") Тогда
		ТаблицаДанных = СтрокиТаблицы[0].Владелец();
		НаборСтрок = СтрокиТаблицы;
	ИначеЕсли ТипЗнч(СтрокиТаблицы) = Тип("СтрокаТаблицыЗначений") Тогда
		ТаблицаДанных = СтрокиТаблицы.Владелец();
		НаборСтрок = Новый Массив;
		НаборСтрок.Добавить(СтрокиТаблицы);
	Иначе // таблица значений
		ТаблицаДанных = СтрокиТаблицы;
		НаборСтрок = СтрокиТаблицы;
	КонецЕсли;
	
	// Если в таблице нет служебной колонки, добавим ее.
	Если ТаблицаДанных.Колонки.Найти("НаборПараметровОбработкиОшибок") = Неопределено Тогда
		ТаблицаДанных.Колонки.Добавить("НаборПараметровОбработкиОшибок");
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

