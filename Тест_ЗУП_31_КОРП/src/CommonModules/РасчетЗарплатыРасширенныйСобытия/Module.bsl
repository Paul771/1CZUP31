
#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиПодписокНаСобытия

Процедура РасчетЗарплатыРасчетныеДокументыНачисленияПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	СуммаНачислено = 0;
	МетаданныеДокумента = Источник.Метаданные();
	Если МетаданныеДокумента.ТабличныеЧасти.Найти("ОплатаТруда") <> Неопределено Тогда 
		СуммаНачислено = Источник.ОплатаТруда.Итог("Результат");
	ИначеЕсли МетаданныеДокумента.ТабличныеЧасти.Найти("Начисления") <> Неопределено Тогда
		СуммаНачислено = Источник.Начисления.Итог("Результат");
	КонецЕсли; 
	Если МетаданныеДокумента.ТабличныеЧасти.Найти("НачисленияПерерасчет") <> Неопределено Тогда
		СуммаНачислено = СуммаНачислено + Источник.НачисленияПерерасчет.Итог("Результат");
	КонецЕсли; 
	Если МетаданныеДокумента.ТабличныеЧасти.Найти("НачисленияПоДоговорам") <> Неопределено Тогда
		СуммаНачислено = СуммаНачислено + Источник.НачисленияПоДоговорам.Итог("Результат");
	КонецЕсли; 
	Если МетаданныеДокумента.ТабличныеЧасти.Найти("Пособия") <> Неопределено Тогда
		СуммаНачислено = СуммаНачислено + Источник.Пособия.Итог("Результат");
	КонецЕсли;     
	Источник.Начислено = СуммаНачислено;
	
КонецПроцедуры

Процедура РасчетЗарплатыРасчетныеДокументыУдержанияПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	СуммаУдержано = 0;
	МетаданныеДокумента = Источник.Метаданные();
	Если МетаданныеДокумента.ТабличныеЧасти.Найти("Удержания") <> Неопределено Тогда
		СуммаУдержано = Источник.Удержания.Итог("Результат");
	КонецЕсли; 
	Если МетаданныеДокумента.ТабличныеЧасти.Найти("НДФЛ") <> Неопределено Тогда
		СуммаУдержано = СуммаУдержано + Источник.НДФЛ.Итог("Налог");
	КонецЕсли; 
	Если МетаданныеДокумента.ТабличныеЧасти.Найти("ПогашениеЗаймов") <> Неопределено Тогда
		СуммаУдержано = СуммаУдержано + Источник.ПогашениеЗаймов.Итог("ПогашениеПроцентов");
		СуммаУдержано = СуммаУдержано + Источник.ПогашениеЗаймов.Итог("ПогашениеЗайма");
		СуммаУдержано = СуммаУдержано + Источник.ПогашениеЗаймов.Итог("НалогНаМатериальнуюВыгоду");
	КонецЕсли; 
	Источник.Удержано = СуммаУдержано;
	
КонецПроцедуры

Процедура ПроверитьФормулуРасчетаОбработкаПроверкиЗаполнения(Источник, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	Если Не РасчетЗарплатыРасширенныйКлиентСервер.СпособРасчетаИспользуетФормулу(Источник.СпособРасчета) Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ФормулаРасчета");
	КонецЕсли;
	
КонецПроцедуры

Процедура ИспользоватьСтатьиФинансированияЗарплатаПриЗаписи(Источник, Отказ) Экспорт

	Константы.ИспользоватьРасчетЗарплатыИСтатьиФинансированияЗарплата.Установить(Константы.ИспользоватьНачислениеЗарплаты.Получить() И Источник.Значение);

КонецПроцедуры

Процедура ЗаполнитьНастройкиРасчетаЗарплатыПриЗаписи(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	РасчетЗарплатыРасширенный.ЗаполнитьНастройкиРасчетаЗарплаты();
	
КонецПроцедуры

Процедура ЗаполнитьОрганизацииНабораЗаписейПоПодразделениямПередЗаписью(Источник, Отказ, Замещение) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ОрганизацииПодразделений = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Источник.ВыгрузитьКолонку("Подразделение"), "Владелец");
	
	Для Каждого СтрокаНабора Из Источник Цикл
		СтрокаНабора.Организация = ОрганизацииПодразделений.Получить(СтрокаНабора.Подразделение);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьИспользованиеМногофункциональностиВДокументахПриЗаписи(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ЗарплатаКадрыРасширенный.УстановитьИспользованиеМногофункциональностиВДокументах();
	
КонецПроцедуры

Процедура ПроверитьФОТНачислений(Источник, Отказ, Замещение) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли; 
	
	// Запись набора в служебных целях.
	Если Источник.ДополнительныеСвойства.Свойство("ЭтоВторичныйНабор") Тогда
		Возврат;
	КонецЕсли; 
	
	Если Источник.ДополнительныеСвойства.Свойство("МенеджерВременныхТаблиц") Тогда
		Возврат;
	КонецЕсли; 
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	// Подготовка данных для перезаполнения планового ФОТ
	Запрос.УстановитьПараметр("Регистратор", Источник.Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЗаписываемыйНабор", Источник.Выгрузить());
	Запрос.УстановитьПараметр("НачисленияВходящиеВСоставФОТ", РасчетЗарплатыРасширенный.НачисленияВходящиеВСоставФОТ());
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТекущийНабор.Период,
		|	ТекущийНабор.Сотрудник,
		|	ТекущийНабор.Начисление
		|ПОМЕСТИТЬ ВТТекущийНаборПлановыхНачислений
		|ИЗ
		|	РегистрСведений.ПлановыеНачисления КАК ТекущийНабор
		|ГДЕ
		|	ТекущийНабор.Регистратор = &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаписываемыйНабор.Период,
		|	ЗаписываемыйНабор.Сотрудник,
		|	ЗаписываемыйНабор.Начисление
		|ПОМЕСТИТЬ ВТЗаписываемыйНаборПлановыхНачислений
		|ИЗ
		|	&ЗаписываемыйНабор КАК ЗаписываемыйНабор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(ЕСТЬNULL(ТекущийНабор.Период, ЗаписываемыйНабор.Период)) КАК Период,
		|	ЕСТЬNULL(ТекущийНабор.Сотрудник, ЗаписываемыйНабор.Сотрудник) КАК Сотрудник,
		|	ЕСТЬNULL(ТекущийНабор.Начисление, ЗаписываемыйНабор.Начисление) КАК Начисление,
		|	МИНИМУМ(ВЫБОР
		|			КОГДА ЗаписываемыйНабор.Начисление ЕСТЬ NULL 
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ) КАК ДвижениеУдаляется
		|ПОМЕСТИТЬ ВТДатыИзмененияНачислений
		|ИЗ
		|	ВТТекущийНаборПлановыхНачислений КАК ТекущийНабор
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТЗаписываемыйНаборПлановыхНачислений КАК ЗаписываемыйНабор
		|		ПО ТекущийНабор.Период = ЗаписываемыйНабор.Период
		|			И ТекущийНабор.Сотрудник = ЗаписываемыйНабор.Сотрудник
		|			И ТекущийНабор.Начисление = ЗаписываемыйНабор.Начисление
		|ГДЕ
		|	ЕСТЬNULL(ТекущийНабор.Начисление, ЗаписываемыйНабор.Начисление) В (&НачисленияВходящиеВСоставФОТ)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕСТЬNULL(ТекущийНабор.Сотрудник, ЗаписываемыйНабор.Сотрудник),
		|	ЕСТЬNULL(ТекущийНабор.Начисление, ЗаписываемыйНабор.Начисление)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДатыИзмененияНачислений.Начисление
		|ИЗ
		|	ВТДатыИзмененияНачислений КАК ДатыИзмененияНачислений";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Источник.ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПроверитьФОТНачисленийПериодическихПоказателейРасчетаЗарплатыСотрудников(Источник, Отказ, Замещение) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли; 
	
	// Запись набора в служебных целях.
	Если Источник.ДополнительныеСвойства.Свойство("ЭтоВторичныйНабор") Тогда
		Возврат;
	КонецЕсли; 
	
	Если Источник.ДополнительныеСвойства.Свойство("МенеджерВременныхТаблиц") Тогда
		Возврат;
	КонецЕсли; 
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	// Подготовка данных для перезаполнения планового ФОТ
	Запрос.УстановитьПараметр("Регистратор", Источник.Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЗаписываемыйНабор", Источник.Выгрузить());
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗначенияПоказателей.Период,
		|	ЗначенияПоказателей.Сотрудник,
		|	ЗначенияПоказателей.Показатель,
		|	ЗначенияПоказателей.Значение
		|ПОМЕСТИТЬ ВТТекущийНаборПоказателей
		|ИЗ
		|	РегистрСведений.ЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудников КАК ЗначенияПоказателей
		|ГДЕ
		|	ЗначенияПоказателей.Регистратор = &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗначенияПоказателей.Период,
		|	ЗначенияПоказателей.Сотрудник,
		|	ЗначенияПоказателей.Показатель,
		|	ЗначенияПоказателей.Значение
		|ПОМЕСТИТЬ ВТЗаписываемыйНаборПоказателей
		|ИЗ
		|	&ЗаписываемыйНабор КАК ЗначенияПоказателей
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МИНИМУМ(ЕСТЬNULL(ТекущийНаборПоказателей.Период, ЗаписываемыйНаборПоказателей.Период)) КАК Период,
		|	ЕСТЬNULL(ТекущийНаборПоказателей.Сотрудник, ЗаписываемыйНаборПоказателей.Сотрудник) КАК Сотрудник,
		|	ЕСТЬNULL(ТекущийНаборПоказателей.Показатель, ЗаписываемыйНаборПоказателей.Показатель) КАК Показатель,
		|	ЛОЖЬ КАК ДвижениеУдаляется
		|ПОМЕСТИТЬ ВТИзменяемыеПоказатели
		|ИЗ
		|	ВТТекущийНаборПоказателей КАК ТекущийНаборПоказателей
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТЗаписываемыйНаборПоказателей КАК ЗаписываемыйНаборПоказателей
		|		ПО ТекущийНаборПоказателей.Сотрудник = ЗаписываемыйНаборПоказателей.Сотрудник
		|			И ТекущийНаборПоказателей.Показатель = ЗаписываемыйНаборПоказателей.Показатель
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕСТЬNULL(ТекущийНаборПоказателей.Сотрудник, ЗаписываемыйНаборПоказателей.Сотрудник),
		|	ЕСТЬNULL(ТекущийНаборПоказателей.Показатель, ЗаписываемыйНаборПоказателей.Показатель)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИзменяемыеПоказатели.Показатель
		|ИЗ
		|	ВТИзменяемыеПоказатели КАК ИзменяемыеПоказатели";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Запрос.УстановитьПараметр("НачисленияВходящиеВСоставФОТ", РасчетЗарплатыРасширенный.НачисленияВходящиеВСоставФОТ());
		Запрос.Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ИзменяемыеПоказатели.Период,
			|	ИзменяемыеПоказатели.Сотрудник,
			|	НачисленияПоказатели.Ссылка КАК Начисление,
			|	ИзменяемыеПоказатели.ДвижениеУдаляется
			|ПОМЕСТИТЬ ВТДатыИзмененияНачислений
			|ИЗ
			|	ВТИзменяемыеПоказатели КАК ИзменяемыеПоказатели
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.Показатели КАК НачисленияПоказатели
			|		ПО ИзменяемыеПоказатели.Показатель = НачисленияПоказатели.Показатель
			|			И (НачисленияПоказатели.Ссылка В (&НачисленияВходящиеВСоставФОТ))";
		
		Запрос.Выполнить();
		Источник.ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗапомнитьИзмененияСтажейФизическихЛицПередЗаписью(Источник, Отказ, Замещение) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СтажиФизическихЛиц", Источник.Выгрузить());
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СтажиФизическихЛиц.Период КАК Период,
		|	СтажиФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СтажиФизическихЛиц.ВидСтажа КАК ВидСтажа,
		|	СтажиФизическихЛиц.ДатаОтсчета КАК ДатаОтсчета,
		|	СтажиФизическихЛиц.РазмерМесяцев КАК РазмерМесяцев,
		|	СтажиФизическихЛиц.РазмерДней КАК РазмерДней,
		|	СтажиФизическихЛиц.Прерван КАК Прерван
		|ПОМЕСТИТЬ ВТЗаписываемыйНабор
		|ИЗ
		|	&СтажиФизическихЛиц КАК СтажиФизическихЛиц
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтажиФизическихЛиц.Период КАК Период,
		|	СтажиФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СтажиФизическихЛиц.ВидСтажа КАК ВидСтажа,
		|	СтажиФизическихЛиц.ДатаОтсчета КАК ДатаОтсчета,
		|	СтажиФизическихЛиц.РазмерМесяцев КАК РазмерМесяцев,
		|	СтажиФизическихЛиц.РазмерДней КАК РазмерДней,
		|	СтажиФизическихЛиц.Прерван КАК Прерван
		|ПОМЕСТИТЬ ВТТекущийНабор
		|ИЗ
		|	РегистрСведений.СтажиФизическихЛиц КАК СтажиФизическихЛиц
		|ГДЕ
		|	&ОтборЗаписываемогоНабора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МИНИМУМ(ЕСТЬNULL(ТекущийНабор.Период, ЗаписываемыйНабор.Период)) КАК Период,
		|	ЕСТЬNULL(ТекущийНабор.ФизическоеЛицо, ЗаписываемыйНабор.ФизическоеЛицо) КАК ФизическоеЛицо,
		|	ЕСТЬNULL(ТекущийНабор.ВидСтажа, ЗаписываемыйНабор.ВидСтажа) КАК ВидСтажа
		|ИЗ
		|	ВТТекущийНабор КАК ТекущийНабор
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТЗаписываемыйНабор КАК ЗаписываемыйНабор
		|		ПО ТекущийНабор.Период = ЗаписываемыйНабор.Период
		|			И ТекущийНабор.ФизическоеЛицо = ЗаписываемыйНабор.ФизическоеЛицо
		|			И ТекущийНабор.ВидСтажа = ЗаписываемыйНабор.ВидСтажа
		|ГДЕ
		|	(ЕСТЬNULL(ЗаписываемыйНабор.ДатаОтсчета, ЕСТЬNULL(ТекущийНабор.ДатаОтсчета, 0)) <> ЕСТЬNULL(ТекущийНабор.ДатаОтсчета, 0)
		|			ИЛИ ЕСТЬNULL(ЗаписываемыйНабор.РазмерМесяцев, ЕСТЬNULL(ТекущийНабор.ДатаОтсчета, 0)) <> ЕСТЬNULL(ТекущийНабор.РазмерМесяцев, 0)
		|			ИЛИ ЕСТЬNULL(ЗаписываемыйНабор.РазмерДней, ЕСТЬNULL(ТекущийНабор.ДатаОтсчета, 0)) <> ЕСТЬNULL(ТекущийНабор.РазмерДней, 0)
		|			ИЛИ ЕСТЬNULL(ЗаписываемыйНабор.Прерван, ЕСТЬNULL(ТекущийНабор.Прерван, ЛОЖЬ)) <> ЕСТЬNULL(ТекущийНабор.Прерван, ЛОЖЬ))
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕСТЬNULL(ТекущийНабор.ФизическоеЛицо, ЗаписываемыйНабор.ФизическоеЛицо),
		|	ЕСТЬNULL(ТекущийНабор.ВидСтажа, ЗаписываемыйНабор.ВидСтажа)";
	
	ОтборЗаписываемогоНабора = "";
	Для каждого ЭлементОтбора Из Источник.Отбор Цикл
		
		Если Не ЭлементОтбора.Использование Тогда
			Продолжить;
		КонецЕсли; 
		
		УсловиеОтбора = "СтажиФизическихЛиц." + ЭлементОтбора.Имя + " = &СтажиФизическихЛиц" + ЭлементОтбора.Имя;
		ОтборЗаписываемогоНабора = ?(ПустаяСтрока(ОтборЗаписываемогоНабора), "", ОтборЗаписываемогоНабора + Символы.ПС + "И ") + УсловиеОтбора;
		
		Запрос.УстановитьПараметр("СтажиФизическихЛиц" + ЭлементОтбора.Имя, ЭлементОтбора.Значение);
		
	КонецЦикла;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборЗаписываемогоНабора", "(" + ?(ПустаяСтрока(ОтборЗаписываемогоНабора), "ИСТИНА", ОтборЗаписываемогоНабора) + ")");
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Источник.ДополнительныеСвойства.Вставить("ОбновляемыеВидыСтажей", РезультатЗапроса.Выгрузить());
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПерезаполнитьВторичныеЗаписиНачисленийЗависящихОтСтажаПриЗаписи(Источник, Отказ, Замещение) Экспорт
	// merge
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Источник.ДополнительныеСвойства.Свойство("ОбновляемыеВидыСтажей") Тогда
		Возврат;
	КонецЕсли; 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ОбновляемыеВидыСтажей", Источник.ДополнительныеСвойства.ОбновляемыеВидыСтажей);
	Запрос.УстановитьПараметр("НачислениеСевернаяНадбавка", РасчетЗарплаты.НачислениеСевернаяНадбавка());
	Запрос.УстановитьПараметр("НачисленияВходящиеВСоставФОТ", РасчетЗарплатыРасширенный.НачисленияВходящиеВСоставФОТ());
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОбновляемыеВидыСтажей.ФизическоеЛицо,
		|	ОбновляемыеВидыСтажей.ВидСтажа
		|ПОМЕСТИТЬ ВТОбновляемыеВидыСтажей
		|ИЗ
		|	&ОбновляемыеВидыСтажей КАК ОбновляемыеВидыСтажей
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОбновляемыеВидыСтажей.ФизическоеЛицо,
		|	НачисленияПоказатели.Ссылка КАК Начисление
		|ПОМЕСТИТЬ ВТВсеНачисленияЗависящиеОтСтажа
		|ИЗ
		|	ПланВидовРасчета.Начисления.Показатели КАК НачисленияПоказатели
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОбновляемыеВидыСтажей КАК ОбновляемыеВидыСтажей
		|		ПО НачисленияПоказатели.Показатель.ВидСтажа = ОбновляемыеВидыСтажей.ВидСтажа
		|			И (НачисленияПоказатели.Показатель.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейРасчетаЗарплаты.ЧисловойЗависящийОтСтажа))
		|ГДЕ
		|	НачисленияПоказатели.Ссылка В(&НачисленияВходящиеВСоставФОТ)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОбновляемыеВидыСтажей.ФизическоеЛицо,
		|	&НачислениеСевернаяНадбавка
		|ИЗ
		|	ВТОбновляемыеВидыСтажей КАК ОбновляемыеВидыСтажей
		|ГДЕ
		|	ВЫРАЗИТЬ(ОбновляемыеВидыСтажей.ВидСтажа КАК Справочник.ВидыСтажа).КатегорияСтажа = ЗНАЧЕНИЕ(Перечисление.КатегорииСтажа.Северный)
		|	И &НачислениеСевернаяНадбавка В (&НачисленияВходящиеВСоставФОТ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(ПлановыеНачисления.Период) КАК ДатаНачала,
		|	ПлановыеНачисления.Сотрудник КАК Сотрудник,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаИзмененияНачислений,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаИзмененияГрафика,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаИзмененияПоказателей,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаИзмененияКоличестваСтавок,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДействуетДо,
		|	ЛОЖЬ КАК ИзменениеКоличестваСтавок,
		|	ЛОЖЬ КАК ИзменениеГрафика,
		|	ЛОЖЬ КАК ИзменениеЗначенийПоказателей,
		|	ЛОЖЬ КАК ИзменениеНачислений,
		|	ЛОЖЬ КАК ИзменениеДанныхГрафика,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаИзмененияДанныхГрафика,
		|	ИСТИНА КАК ИзменениеДанныхСтажа,
		|	МИНИМУМ(ПлановыеНачисления.Период) КАК ДатаИзмененияДанныхСтажа,
		|	ЛОЖЬ КАК УдалениеДанных
		|ПОМЕСТИТЬ ВТПериодыОбновленияВторичныхДанных
		|ИЗ
		|	РегистрСведений.ПлановыеНачисления КАК ПлановыеНачисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВсеНачисленияЗависящиеОтСтажа КАК НачисленияЗависящиеОтСтажа
		|		ПО ПлановыеНачисления.ФизическоеЛицо = НачисленияЗависящиеОтСтажа.ФизическоеЛицо
		|			И ПлановыеНачисления.Начисление = НачисленияЗависящиеОтСтажа.Начисление
		|
		|СГРУППИРОВАТЬ ПО
		|	ПлановыеНачисления.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	СотрудникиПериодыКРасчету.Сотрудник
		|ИЗ
		|	ВТПериодыОбновленияВторичныхДанных КАК СотрудникиПериодыКРасчету";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ПлановыеНачисленияСотрудников.СформироватьДвиженияВторичныхДанных(Запрос.МенеджерВременныхТаблиц);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Обработчик подписки на событие "ПриЗаписи" констант ИспользоватьНачислениеЗарплаты и
// ИспользоватьНесколькоОрганизаций.
// Заполняет значение константы ИспользоватьНачислениеЗарплатыНесколькихОрганизаций.
//
Процедура УстановитьЗначениеКонстантыИспользоватьНачислениеЗарплатыНесколькихОрганизаций(Источник, Отказ) Экспорт
				
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Константы.ИспользоватьНачислениеЗарплатыВНесколькихОрганизациях.Установить(ЗарплатаКадрыРасширенный.ИспользоватьНачислениеЗарплатыНесколькихОрганизаций());
	
КонецПроцедуры

// Обработчик подписки на событие "ПриЗаписи" констант РаботаВХозрасчетнойОрганизации и
// ИспользоватьНесколькоОрганизаций.
// Заполняет значение константы ИспользоватьНачислениеЗарплатыНесколькихОрганизаций.
//
Процедура УстановитьЗначениеКонстантыРаботаВНесколькихХозрасчетныхОрганизациях(Источник, Отказ) Экспорт
										
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Константы.РаботаВНесколькихХозрасчетныхОрганизациях.Установить(ЗарплатаКадрыРасширенный.РаботаВНесколькихХозрасчетныхОрганизациях());
	
КонецПроцедуры

Процедура УстановитьИспользованиеРасчетаПервойПоловиныМесяца(Источник, Отказ, Замещение) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	РасчетЗарплатыРасширенный.УстановитьИспользованиеРасчетаПервойПоловиныМесяца();
	
КонецПроцедуры

Процедура ЗаполнитьНачисленияВнутреннихСовместителейИПодработок(Источник, Отказ, Замещение) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаСотрудников = Новый ТаблицаЗначений;
	ТаблицаСотрудников.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	ТаблицаСотрудников.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	
	ТаблицаВнутреннихСовместителей = Новый ТаблицаЗначений;
	ТаблицаВнутреннихСовместителей.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	ТаблицаВнутреннихСовместителей.Колонки.Добавить("ГоловнаяОрганизация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаВнутреннихСовместителей.Колонки.Добавить("ФизическиеЛица");
	
	ТаблицаПодработок = Новый ТаблицаЗначений;
	ТаблицаПодработок.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	ТаблицаПодработок.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
		Метаданные.РегистрыСведений.ВидыЗанятостиСотрудников.Имя,
		Запрос.МенеджерВременныхТаблиц,
		Ложь,
		ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(Источник.Выгрузить(, "Период,Сотрудник")));
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВидыЗанятостиСотрудников.Период КАК Период,
		|	ВидыЗанятостиСотрудников.Сотрудник КАК Сотрудник,
		|	ВидыЗанятостиСотрудников.Сотрудник КАК ГоловнаяОрганизация,
		|	ВидыЗанятостиСотрудников.Сотрудник КАК ФизическоеЛицо,
		|	ВидыЗанятостиСотрудников.Сотрудник КАК ВидЗанятости
		|ИЗ
		|	ВТВидыЗанятостиСотрудниковСрезПоследних КАК ВидыЗанятостиСотрудников";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ВидЗанятости = Перечисления.ВидыЗанятости.ВнутреннееСовместительство Тогда
			
			СтруктураПоиска = Новый Структура("Период,ГоловнаяОрганизация", Выборка.Период, Выборка.ГоловнаяОрганизация);
			НайденныеСтроки = ТаблицаВнутреннихСовместителей.НайтиСтроки(СтруктураПоиска);
			Если НайденныеСтроки.Количество() > 0 Тогда
				НайденнаяСтрока = НайденныеСтроки[0];
			Иначе
				
				НайденнаяСтрока = ТаблицаВнутреннихСовместителей.Добавить();
				ЗаполнитьЗначенияСвойств(НайденнаяСтрока, СтруктураПоиска);
				
				НайденнаяСтрока.ФизическиеЛица = Новый Соответствие;
				
			КонецЕсли;
			
			НайденнаяСтрока.ФизическиеЛица.Вставить(Выборка.ФизическоеЛицо, Истина);
			
		ИначеЕсли Выборка.ВидЗанятости = Перечисления.ВидыЗанятости.Подработка Тогда
			
			НоваяСтрока = ТаблицаПодработок.Добавить();
			НоваяСтрока.Период = Выборка.Период;
			НоваяСтрока.Сотрудник = Выборка.Сотрудник;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТаблицаВнутреннихСовместителей.Количество() > 0 Тогда
		
		УничтожитьВТСотрудникиОрганизации = Ложь;
		Для каждого СтрокаТаблицыВнутреннихСовместителей Из ТаблицаВнутреннихСовместителей Цикл
			
			ПараметрыПолучения = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
			ПараметрыПолучения.Организация = СтрокаТаблицыВнутреннихСовместителей.ГоловнаяОрганизация;
			ПараметрыПолучения.ОтбиратьПоГоловнойОрганизации = Истина;
			ПараметрыПолучения.НачалоПериода = СтрокаТаблицыВнутреннихСовместителей.Период;
			ПараметрыПолучения.ОкончаниеПериода = СтрокаТаблицыВнутреннихСовместителей.Период;
			ПараметрыПолучения.КадровыеДанные = "ВидЗанятости";
			ПараметрыПолучения.СписокФизическихЛиц = ОбщегоНазначения.ВыгрузитьКолонку(
				СтрокаТаблицыВнутреннихСовместителей.ФизическиеЛица, "Ключ");
			
			Если УничтожитьВТСотрудникиОрганизации Тогда
				Запрос.Текст = "Уничтожить ВТСотрудникиОрганизации";
				Запрос.Выполнить();
			Иначе
				УничтожитьВТСотрудникиОрганизации = Истина;
			КонецЕсли;
			
			КадровыйУчет.СоздатьВТСотрудникиОрганизации(Запрос.МенеджерВременныхТаблиц, Ложь, ПараметрыПолучения);
			
			Запрос.Текст =
				"ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	МИНИМУМ(СотрудникиОрганизации.Период) КАК Период,
				|	СотрудникиОрганизации.Сотрудник
				|ИЗ
				|	ВТСотрудникиОрганизации КАК СотрудникиОрганизации
				|ГДЕ
				|	СотрудникиОрганизации.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы)
				|
				|СГРУППИРОВАТЬ ПО
				|	СотрудникиОрганизации.Сотрудник";
			
			РезультатЗапросаПоПериодам = Запрос.Выполнить();
			Если Не РезультатЗапросаПоПериодам.Пустой() Тогда
				
				ВыборкаПоПериодам = РезультатЗапросаПоПериодам.Выбрать();
				Пока ВыборкаПоПериодам.Следующий() Цикл
					ЗаполнитьЗначенияСвойств(ТаблицаСотрудников.Добавить(), ВыборкаПоПериодам);
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТаблицаПодработок.Количество() > 0 Тогда
		
		ГоловныеСотрудники = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ТаблицаПодработок.ВыгрузитьКолонку("Сотрудник"), "ГоловнойСотрудник");
		Для каждого СтрокаТаблицыПодработок Из ТаблицаПодработок Цикл
			
			ГоловнойСотрудник = ГоловныеСотрудники.Получить(СтрокаТаблицыПодработок.Сотрудник);
			
			СтруктураПоиска = Новый Структура("Сотрудник", ГоловнойСотрудник);
			НайденныеСтроки = ТаблицаСотрудников.НайтиСтроки(СтруктураПоиска);
			Если НайденныеСтроки.Количество() > 0 Тогда
				
				НайденнаяСтрока = НайденныеСтроки[0];
				Если НайденнаяСтрока.Период > СтрокаТаблицыПодработок.Период Тогда
					НайденнаяСтрока.Период = СтрокаТаблицыПодработок.Период;
				КонецЕсли;
				
			Иначе
				
				НоваяСтрока = ТаблицаСотрудников.Добавить();
				НоваяСтрока.Период = СтрокаТаблицыПодработок.Период;
				НоваяСтрока.Сотрудник = ГоловнойСотрудник;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТаблицаСотрудников.Количество() > 0 Тогда
		
		Запрос = Новый Запрос;
		
		Запрос.УстановитьПараметр("ТаблицаСотрудников", ТаблицаСотрудников);
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ТаблицаСотрудников.Период,
			|	ТаблицаСотрудников.Сотрудник
			|ПОМЕСТИТЬ ВТТаблицаСотрудников
			|ИЗ
			|	&ТаблицаСотрудников КАК ТаблицаСотрудников
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Начисления.Регистратор
			|ИЗ
			|	ВТТаблицаСотрудников КАК ТаблицаСотрудников
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления КАК Начисления
			|		ПО (НАЧАЛОПЕРИОДА(ТаблицаСотрудников.Период, МЕСЯЦ) <= Начисления.ПериодДействияНачало)
			|			И ТаблицаСотрудников.Сотрудник = Начисления.Сотрудник
			|			И (НЕ Начисления.Регистратор ССЫЛКА Документ.ПереносДанных)
			|ГДЕ
			|	(ВЫРАЗИТЬ(Начисления.ВидРасчета КАК ПланВидовРасчета.Начисления).ДублироватьДляВнутреннихСовместителейИПодработок
			|			ИЛИ ВЫРАЗИТЬ(Начисления.ВидРасчета КАК ПланВидовРасчета.Начисления).ДублироватьДляПодработок)";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				НаборЗаписей = РегистрыРасчета.Начисления.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
				НаборЗаписей.Прочитать();
				
				Если РегистрыРасчета.Начисления.ДополнитьНаборЗаписейНачислениямиСовместителейИПодработок(НаборЗаписей) Тогда
					НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
					НаборЗаписей.Записать();
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьИзменениеГрафиковРаботыСотрудников(Источник, Отказ, Замещение) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли; 
	
	// Запись набора в служебных целях.
	Если Источник.ДополнительныеСвойства.Свойство("ЭтоВторичныйНабор") Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("МенеджерВременныхТаблиц") Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	// Подготовка данных для перезаполнения планового ФОТ
	Запрос.УстановитьПараметр("Регистратор", Источник.Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЗаписываемыйНабор", Источник.Выгрузить());
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ГрафикРаботыСотрудников.Период,
		|	ГрафикРаботыСотрудников.Сотрудник,
		|	ГрафикРаботыСотрудников.ГрафикРаботы,
		|	ГрафикРаботыСотрудников.ДействуетДо
		|ПОМЕСТИТЬ ВТГрафикРаботыСотрудниковЗаписываемыйНабор
		|ИЗ
		|	&ЗаписываемыйНабор КАК ГрафикРаботыСотрудников
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГрафикРаботыСотрудников.Период,
		|	ГрафикРаботыСотрудников.Сотрудник,
		|	ГрафикРаботыСотрудников.ГрафикРаботы,
		|	ГрафикРаботыСотрудников.ДействуетДо
		|ПОМЕСТИТЬ ВТГрафикРаботыСотрудниковТекущийНабор
		|ИЗ
		|	РегистрСведений.ГрафикРаботыСотрудников КАК ГрафикРаботыСотрудников
		|ГДЕ
		|	ГрафикРаботыСотрудников.Регистратор = &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЕСТЬNULL(ТекущийНабор.Период, ЗаписываемыйНабор.Период) КАК Период,
		|	ЕСТЬNULL(ТекущийНабор.Сотрудник, ЗаписываемыйНабор.Сотрудник) КАК Сотрудник
		|ПОМЕСТИТЬ ВТСотрудникиСИзменениямиПредварительно
		|ИЗ
		|	ВТГрафикРаботыСотрудниковТекущийНабор КАК ТекущийНабор
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТГрафикРаботыСотрудниковЗаписываемыйНабор КАК ЗаписываемыйНабор
		|		ПО ТекущийНабор.Период = ЗаписываемыйНабор.Период
		|			И ТекущийНабор.Сотрудник = ЗаписываемыйНабор.Сотрудник
		|ГДЕ
		|	ЕСТЬNULL(ТекущийНабор.ГрафикРаботы, ЗНАЧЕНИЕ(Справочник.ГрафикиРаботыСотрудников.ПустаяСсылка)) <> ЕСТЬNULL(ЗаписываемыйНабор.ГрафикРаботы, ЗНАЧЕНИЕ(Справочник.ГрафикиРаботыСотрудников.ПустаяСсылка))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(СотрудникиСИзменениямиПредварительно.Период) КАК Период,
		|	СотрудникиСИзменениямиПредварительно.Сотрудник
		|ПОМЕСТИТЬ ВТСотрудникиСИзменениями
		|ИЗ
		|	ВТСотрудникиСИзменениямиПредварительно КАК СотрудникиСИзменениямиПредварительно
		|ГДЕ
		|	СотрудникиСИзменениямиПредварительно.Период <> ДАТАВРЕМЯ(1, 1, 1)
		|
		|СГРУППИРОВАТЬ ПО
		|	СотрудникиСИзменениямиПредварительно.Сотрудник";
	
	Запрос.Выполнить();
	УстановитьДополнительноеСвойствоДляРасчетаФОТНачислений(Источник, МенеджерВременныхТаблиц);
	
КонецПроцедуры

Процедура ПроверитьИзменениеКадровыхДанныхСотрудников(Источник, Отказ, Замещение) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли; 
	
	// Запись набора в служебных целях.
	Если Источник.ДополнительныеСвойства.Свойство("ЭтоВторичныйНабор") Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("МенеджерВременныхТаблиц") Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	// Подготовка данных для перезаполнения планового ФОТ
	Запрос.УстановитьПараметр("Регистратор", Источник.Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЗаписываемыйНабор", Источник.Выгрузить());
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КадроваяИсторияСотрудников.Период,
		|	КадроваяИсторияСотрудников.Сотрудник,
		|	КадроваяИсторияСотрудников.КоличествоСтавок
		|ПОМЕСТИТЬ ВТКадроваяИсторияСотрудниковЗаписываемыйНабор
		|ИЗ
		|	&ЗаписываемыйНабор КАК КадроваяИсторияСотрудников
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КадроваяИсторияСотрудников.Период,
		|	КадроваяИсторияСотрудников.Сотрудник,
		|	КадроваяИсторияСотрудников.КоличествоСтавок
		|ПОМЕСТИТЬ ВТКадроваяИсторияСотрудниковТекущийНабор
		|ИЗ
		|	РегистрСведений.КадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
		|ГДЕ
		|	КадроваяИсторияСотрудников.Регистратор = &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЕСТЬNULL(ТекущийНабор.Период, ЗаписываемыйНабор.Период) КАК Период,
		|	ЕСТЬNULL(ТекущийНабор.Сотрудник, ЗаписываемыйНабор.Сотрудник) КАК Сотрудник,
		|	ЛОЖЬ КАК ВозвратноеСобытие
		|ПОМЕСТИТЬ ВТСотрудникиСИзменениямиПредварительно
		|ИЗ
		|	ВТКадроваяИсторияСотрудниковТекущийНабор КАК ТекущийНабор
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТКадроваяИсторияСотрудниковЗаписываемыйНабор КАК ЗаписываемыйНабор
		|		ПО ТекущийНабор.Период = ЗаписываемыйНабор.Период
		|			И ТекущийНабор.Сотрудник = ЗаписываемыйНабор.Сотрудник
		|ГДЕ
		|	ЕСТЬNULL(ТекущийНабор.КоличествоСтавок, НЕОПРЕДЕЛЕНО) <> ЕСТЬNULL(ЗаписываемыйНабор.КоличествоСтавок, НЕОПРЕДЕЛЕНО)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(СотрудникиСИзменениямиПредварительно.Период) КАК Период,
		|	СотрудникиСИзменениямиПредварительно.Сотрудник
		|ПОМЕСТИТЬ ВТСотрудникиСИзменениями
		|ИЗ
		|	ВТСотрудникиСИзменениямиПредварительно КАК СотрудникиСИзменениямиПредварительно
		|
		|СГРУППИРОВАТЬ ПО
		|	СотрудникиСИзменениямиПредварительно.Сотрудник";
	
	Запрос.Выполнить();
	УстановитьДополнительноеСвойствоДляРасчетаФОТНачислений(Источник, МенеджерВременныхТаблиц);
	
КонецПроцедуры

Процедура ПроверитьИзменениеСреднемесячныхНормВремениГрафиковРаботыСотрудников(Источник, Отказ, Замещение) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли; 
	
	// Запись набора в служебных целях.
	Если Источник.ДополнительныеСвойства.Свойство("ЭтоВторичныйНабор") Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("МенеджерВременныхТаблиц") Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если Источник.Отбор.ГрафикРаботыСотрудников.Использование
		И Источник.Отбор.Год.Использование Тогда
		
		Запрос.УстановитьПараметр("ГрафикРаботыСотрудников", Источник.Отбор.ГрафикРаботыСотрудников.Значение);
		Запрос.УстановитьПараметр("Год", Источник.Отбор.Год.Значение);
		
	Иначе
		Возврат;
	КонецЕсли;
	
	// Подготовка данных для перезаполнения планового ФОТ
	Запрос.УстановитьПараметр("ЗаписываемыйНабор", Источник.Выгрузить());
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СреднемесячныеНормыВремени.ГрафикРаботыСотрудников,
		|	СреднемесячныеНормыВремени.Год,
		|	СреднемесячныеНормыВремени.СреднемесячноеЧислоЧасов,
		|	СреднемесячныеНормыВремени.СреднемесячноеЧислоДней
		|ПОМЕСТИТЬ ВТСреднемесячныеНормыВремениЗаписываемыйНабор
		|ИЗ
		|	&ЗаписываемыйНабор КАК СреднемесячныеНормыВремени
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СреднемесячныеНормыВремени.ГрафикРаботыСотрудников,
		|	СреднемесячныеНормыВремени.Год,
		|	СреднемесячныеНормыВремени.СреднемесячноеЧислоЧасов,
		|	СреднемесячныеНормыВремени.СреднемесячноеЧислоДней
		|ПОМЕСТИТЬ ВТСреднемесячныеНормыВремениТекущийНабор
		|ИЗ
		|	РегистрСведений.СреднемесячныеНормыВремениГрафиковРаботыСотрудников КАК СреднемесячныеНормыВремени
		|ГДЕ
		|	СреднемесячныеНормыВремени.ГрафикРаботыСотрудников = &ГрафикРаботыСотрудников
		|	И СреднемесячныеНормыВремени.Год = &Год";
	
	Запрос.Выполнить();
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЕСТЬNULL(ТекущийНабор.ГрафикРаботыСотрудников, ЗаписываемыйНабор.ГрафикРаботыСотрудников) КАК ГрафикРаботыСотрудников,
		|	ЕСТЬNULL(ТекущийНабор.Год, ЗаписываемыйНабор.Год) КАК Год,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ТекущийНабор.СреднемесячноеЧислоЧасов, 0) <> ЗаписываемыйНабор.СреднемесячноеЧислоЧасов
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ИзмененоЧислоЧасов,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ТекущийНабор.СреднемесячноеЧислоДней, 0) <> ЗаписываемыйНабор.СреднемесячноеЧислоДней
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ИзмененоЧислоДней
		|ПОМЕСТИТЬ ВТИзменениямГрафиковПредварительно
		|ИЗ
		|	ВТСреднемесячныеНормыВремениТекущийНабор КАК ТекущийНабор
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТСреднемесячныеНормыВремениЗаписываемыйНабор КАК ЗаписываемыйНабор
		|		ПО ТекущийНабор.ГрафикРаботыСотрудников = ЗаписываемыйНабор.ГрафикРаботыСотрудников
		|			И ТекущийНабор.Год = ЗаписываемыйНабор.Год
		|ГДЕ
		|	НЕ ЗаписываемыйНабор.ГрафикРаботыСотрудников ЕСТЬ NULL 
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИзменениямГрафиковПредварительно.ГрафикРаботыСотрудников,
		|	ДОБАВИТЬКДАТЕ(ДАТАВРЕМЯ(1, 1, 1), ГОД, ИзменениямГрафиковПредварительно.Год - 1) КАК Период,
		|	ИзменениямГрафиковПредварительно.ИзмененоЧислоЧасов,
		|	ИзменениямГрафиковПредварительно.ИзмененоЧислоДней
		|ИЗ
		|	ВТИзменениямГрафиковПредварительно КАК ИзменениямГрафиковПредварительно
		|ГДЕ
		|	(ИзменениямГрафиковПредварительно.ИзмененоЧислоЧасов
		|			ИЛИ ИзменениямГрафиковПредварительно.ИзмененоЧислоДней)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		НачисленияСЧасовойИлиДневнойСтавкой = РасчетЗарплатыРасширенный.НачисленияСЧасовойИлиДневнойТарифнойСтавкой();
		Если НачисленияСЧасовойИлиДневнойСтавкой.Количество() > 0 Тогда
			
			Запрос.УстановитьПараметр("НачисленияСЧасовойИлиДневнойСтавкой", НачисленияСЧасовойИлиДневнойСтавкой);
			
			// Сбор сведений о сотрудниках
			ТаблицаСотрудников = Новый ТаблицаЗначений;
			ТаблицаСотрудников.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
			ТаблицаСотрудников.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
			
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.СледующийПоЗначениюПоля("Период") Цикл
				
				МассивГрафиковРаботы = Новый Массив;
				Пока Выборка.Следующий() Цикл
					МассивГрафиковРаботы.Добавить(Выборка.ГрафикРаботыСотрудников);
				КонецЦикла;
				
				ПараметрыПолученияСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
				ПараметрыПолученияСотрудников.НачалоПериода = Выборка.Период;
				ПараметрыПолученияСотрудников.ОкончаниеПериода = Выборка.Период;
				
				Отборы = Новый Массив;
				ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(Отборы, "ГрафикРаботы", "В", МассивГрафиковРаботы);
				
				ПараметрыПолученияСотрудников.Отборы = Отборы;
				
				СотрудникОрганизаций = КадровыйУчет.СотрудникиОрганизации(Ложь, ПараметрыПолученияСотрудников);
				Для каждого СтрокаСотрудника Из СотрудникОрганизаций Цикл
					НоваяСтрокаТаблицыСотрудников = ТаблицаСотрудников.Добавить();
					НоваяСтрокаТаблицыСотрудников.Период = Выборка.Период;
					НоваяСтрокаТаблицыСотрудников.Сотрудник = СтрокаСотрудника.Сотрудник;
				КонецЦикла;
				
			КонецЦикла;
			
			Если ТаблицаСотрудников.Количество() > 0 Тогда
				
				Запрос.УстановитьПараметр("ТаблицаСотрудников", ТаблицаСотрудников);
				Запрос.Текст =
					"ВЫБРАТЬ
					|	ТаблицаСотрудников.Период,
					|	ТаблицаСотрудников.Сотрудник
					|ПОМЕСТИТЬ ВТСотрудникиПериодыПредварительно
					|ИЗ
					|	&ТаблицаСотрудников КАК ТаблицаСотрудников
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	СотрудникиПериодыПредварительно.Период,
					|	СотрудникиПериодыПредварительно.Сотрудник,
					|	НачисленияСЧасовымиИДневнымиТарифнымиСтавками.Ссылка КАК Начисление
					|ПОМЕСТИТЬ ВТСотрудникиПериоды
					|ИЗ
					|	ВТСотрудникиПериодыПредварительно КАК СотрудникиПериодыПредварительно
					|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
					|			Начисления.Ссылка КАК Ссылка
					|		ИЗ
					|			ПланВидовРасчета.Начисления КАК Начисления
					|		ГДЕ
					|			Начисления.Ссылка В(&НачисленияСЧасовойИлиДневнойСтавкой)) КАК НачисленияСЧасовымиИДневнымиТарифнымиСтавками
					|		ПО (ИСТИНА)";
				
				Запрос.Выполнить();
				
				ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
					"ПлановыеНачисления",
					Запрос.МенеджерВременныхТаблиц,
					Ложь,
					ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
						"ВТСотрудникиПериоды", "Сотрудник,Начисление"),
					,
					"ВТПлановыеНачисленияСотрудников");
				
				Запрос.Текст =
					"ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	ПлановыеНачисленияСотрудников.Период,
					|	ПлановыеНачисленияСотрудников.Сотрудник,
					|	ПлановыеНачисленияСотрудников.Начисление,
					|	ЛОЖЬ КАК ДвижениеУдаляется
					|ПОМЕСТИТЬ ВТДатыИзмененияНачислений
					|ИЗ
					|	ВТПлановыеНачисленияСотрудников КАК ПлановыеНачисленияСотрудников
					|ГДЕ
					|	ПлановыеНачисленияСотрудников.Используется
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ ПЕРВЫЕ 1
					|	ДатыИзмененияНачислений.Сотрудник
					|ИЗ
					|	ВТДатыИзмененияНачислений КАК ДатыИзмененияНачислений";
					
				РезультатЗапроса = Запрос.Выполнить();
				Если Не РезультатЗапроса.Пустой() Тогда
					Источник.ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиРегламентныхЗаданий

Процедура РасчетФОТНачисленийЗависящихОтСтажа(ОбрабатываемыйМесяц = Неопределено) Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания();
	
	Если НЕ ЗначениеЗаполнено(ОбрабатываемыйМесяц) Тогда
		ОбрабатываемыйМесяц = НачалоМесяца(ТекущаяДатаСеанса());
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ОбрабатываемыйМесяц", ОбрабатываемыйМесяц);
	
	РасчетЗарплатыРасширенный.СоздатьВТНачисленияЗависящиеОтСтажа(Запрос.МенеджерВременныхТаблиц);
	
	Запрос.УстановитьПараметр("НачисленияВходящиеВСоставФОТ", РасчетЗарплатыРасширенный.НачисленияВходящиеВСоставФОТ());
	Запрос.Текст =
		"ВЫБРАТЬ
		|	&ОбрабатываемыйМесяц КАК Период,
		|	НачисленияЗависящиеОтСтажа.Начисление
		|ПОМЕСТИТЬ ВТИзмеренияДатыНачисленийЗависящихОтСтажа
		|ИЗ
		|	ВТНачисленияЗависящиеОтСтажа КАК НачисленияЗависящиеОтСтажа
		|ГДЕ
		|	НачисленияЗависящиеОтСтажа.Начисление В(&НачисленияВходящиеВСоставФОТ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИзмеренияДатыНачисленийЗависящихОтСтажа.Начисление
		|ИЗ
		|	ВТИзмеренияДатыНачисленийЗависящихОтСтажа КАК ИзмеренияДатыНачисленийЗависящихОтСтажа";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли; 
	
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
		"ПлановыйФОТ",
		Запрос.МенеджерВременныхТаблиц,
		Ложь,
		ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
			"ВТИзмеренияДатыНачисленийЗависящихОтСтажа",
			"Начисление"));
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПлановыйФОТ.Сотрудник,
		|	ПлановыйФОТ.Период,
		|	ПлановыйФОТ.Начисление
		|ПОМЕСТИТЬ ВТОтборНачислений
		|ИЗ
		|	ВТПлановыйФОТСрезПоследних КАК ПлановыйФОТ
		|ГДЕ
		|	ПлановыйФОТ.Используется
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОтборНачислений.Сотрудник,
		|	ДОБАВИТЬКДАТЕ(ОтборНачислений.Период, МЕСЯЦ, -1) КАК Период,
		|	КОНЕЦПЕРИОДА(ОтборНачислений.Период, МЕСЯЦ) КАК ДатаОкончания
		|ПОМЕСТИТЬ ВТСотрудникиПериодыДляПериодовИзмененияСтажа
		|ИЗ
		|	ВТОтборНачислений КАК ОтборНачислений";
	
	Запрос.Выполнить();
	
	КадровыйУчетРасширенный.СоздатьВТПериодыИзмененияСтажа(Запрос.МенеджерВременныхТаблиц, "ВТСотрудникиПериодыДляПериодовИзмененияСтажа");
	
	ОписательТаблиц = РасчетЗарплатыРасширенный.ОписательВременныхТаблицДляСоздатьВТПериодыИзмененияПоказателейЗависящихОтСтажа(
		Запрос.МенеджерВременныхТаблиц, "ВТОтборНачислений", "ВТПериодыИзмененияСтажа");
	
	РасчетЗарплатыРасширенный.СоздатьВТПериодыИзмененияЗначенийПоказателяСевернаяНадбавка(ОписательТаблиц);
	РасчетЗарплатыРасширенный.СоздатьВТПериодыИзмененияЗначенийСтажевыхПоказателей(ОписательТаблиц);
	
	Запрос.Текст = "УНИЧТОЖИТЬ ВТПериодыИзмененияСтажа";
	Запрос.Выполнить();
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОтборНачислений.Период,
		|	ОтборНачислений.Сотрудник,
		|	ОтборНачислений.Начисление
		|ПОМЕСТИТЬ ВТСотрудниковКРасчетуПредварительно
		|ИЗ
		|	ВТОтборНачислений КАК ОтборНачислений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПериодыИзмененияЗначенийПоказателяСевернаяНадбавка КАК ПериодыИзмененияЗначенийПоказателя
		|		ПО ОтборНачислений.Сотрудник = ПериодыИзмененияЗначенийПоказателя.Сотрудник
		|			И ОтборНачислений.Начисление = ПериодыИзмененияЗначенийПоказателя.Начисление
		|			И ОтборНачислений.Период <= ПериодыИзмененияЗначенийПоказателя.Период
		|ГДЕ
		|	ПериодыИзмененияЗначенийПоказателя.КоэффициентПересчета <> 1
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОтборНачислений.Период,
		|	ОтборНачислений.Сотрудник,
		|	ОтборНачислений.Начисление
		|ИЗ
		|	ВТОтборНачислений КАК ОтборНачислений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПериодыИзмененияЗначенийСтажевыхПоказателей КАК ПериодыИзмененияЗначенийПоказателя
		|		ПО ОтборНачислений.Сотрудник = ПериодыИзмененияЗначенийПоказателя.Сотрудник
		|			И ОтборНачислений.Начисление = ПериодыИзмененияЗначенийПоказателя.Начисление
		|			И ОтборНачислений.Период <= ПериодыИзмененияЗначенийПоказателя.Период
		|ГДЕ
		|	ПериодыИзмененияЗначенийПоказателя.КоэффициентПересчета <> 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МИНИМУМ(СотрудниковКРасчетуПредварительно.Период) КАК ДатаНачала,
		|	СотрудниковКРасчетуПредварительно.Сотрудник КАК Сотрудник,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаИзмененияНачислений,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаИзмененияГрафика,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаИзмененияПоказателей,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаИзмененияКоличестваСтавок,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДействуетДо,
		|	ЛОЖЬ КАК ИзменениеКоличестваСтавок,
		|	ЛОЖЬ КАК ИзменениеГрафика,
		|	ЛОЖЬ КАК ИзменениеЗначенийПоказателей,
		|	ЛОЖЬ КАК ИзменениеНачислений,
		|	ЛОЖЬ КАК ИзменениеДанныхГрафика,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаИзмененияДанныхГрафика,
		|	ЛОЖЬ КАК ИзменениеДанныхСтажа,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаИзмененияДанныхСтажа,
		|	ЛОЖЬ КАК УдалениеДанных
		|ПОМЕСТИТЬ ВТПериодыОбновленияВторичныхДанных
		|ИЗ
		|	ВТСотрудниковКРасчетуПредварительно КАК СотрудниковКРасчетуПредварительно
		|
		|СГРУППИРОВАТЬ ПО
		|	СотрудниковКРасчетуПредварительно.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПериодыИзмененияЗначенийПоказателяСевернаяНадбавка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПериодыИзмененияЗначенийСтажевыхПоказателей
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТСотрудниковКРасчетуПредварительно
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТОтборНачислений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТСотрудникиПериодыДляПериодовИзмененияСтажа";
	
	Запрос.Выполнить();
	
	УстановитьПривилегированныйРежим(Истина);
	ПлановыеНачисленияСотрудников.СформироватьДвиженияВторичныхДанных(Запрос.МенеджерВременныхТаблиц,,,,, КонецМесяца(ОбрабатываемыйМесяц));
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ИспользоватьСтатьиФинансированияЗарплатаПриЗаписиПриЗаписи(Источник, Отказ) Экспорт
	// Вставить содержимое обработчика.
КонецПроцедуры

#КонецОбласти

Процедура УстановитьДополнительноеСвойствоДляРасчетаФОТНачислений(Источник, МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СотрудникиСИзменениями.Сотрудник
		|ИЗ
		|	ВТСотрудникиСИзменениями КАК СотрудникиСИзменениями";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра("ВТСотрудникиСИзменениями", "Сотрудник");
		
		ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
		
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(
			ПараметрыПостроения.Отборы, "Начисление", "В", РасчетЗарплатыРасширенный.НачисленияВходящиеВСоставФОТ());
		
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(
			ПараметрыПостроения.Отборы, "Начисление.ВидВремени", "В", "ЗНАЧЕНИЕ(Перечисление.ВидыРабочегоВремениСотрудников.ОтработанноеВПределахНормы), ЗНАЧЕНИЕ(Перечисление.ВидыРабочегоВремениСотрудников.ЧасовоеОтработанноеВПределахНормы)");
		
		ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
			"ПлановыеНачисления",
			МенеджерВременныхТаблиц,
			Ложь,
			ОписаниеФильтра,
			ПараметрыПостроения,
			"ВТДатыИзмененияНачисленийПредварительно");
		
		Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ПлановыеНачисления.Сотрудник
			|ИЗ
			|	ВТДатыИзмененияНачисленийПредварительно КАК ПлановыеНачисления
			|ГДЕ
			|	ПлановыеНачисления.Используется";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Запрос.Текст =
				"ВЫБРАТЬ
				|	ПлановыеНачисления.Период,
				|	ПлановыеНачисления.Сотрудник,
				|	ПлановыеНачисления.Начисление,
				|	ЛОЖЬ КАК ДвижениеУдаляется
				|ПОМЕСТИТЬ ВТДатыИзмененияНачислений
				|ИЗ
				|	ВТДатыИзмененияНачисленийПредварительно КАК ПлановыеНачисления
				|ГДЕ
				|	ПлановыеНачисления.Используется";
			
			Запрос.Выполнить();
			
			Источник.ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
