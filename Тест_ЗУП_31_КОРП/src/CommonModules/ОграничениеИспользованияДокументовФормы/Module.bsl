////////////////////////////////////////////////////////////////////////////////
// Подсистема "Ограничение использования документов".
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Устанавливает доступность документа для редактирования и располагает команду ограничения/освобождения документа.
//
// Параметры:
//  Форма - УправляемаяФорма - форма документа.
//  ИмяДействия - Строка - имя действия команды ограничения/освобождения.
//
Процедура ПриПолученииДанныхНаСервере(Форма, ИмяДействия = "ОграничитьДокумент") Экспорт
	
	ДополнитьФормуДокумента(Форма, ИмяДействия);
	УстановитьДоступностьДокументаДляРедактирования(Форма);
	
КонецПроцедуры

// Из формы ограничивает/освобождает документ для редактирования и выполнения прочих команд.
//
// Параметры:
//  Форма - УправляемаяФорма - форма документа.
//
Процедура ОграничитьДокумент(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если Форма.Объект.Проведен Тогда
		
		Элементы.ОграничитьДокумент.Пометка = Не Элементы.ОграничитьДокумент.Пометка;
		
		ОграничитьОсвободитьДокумент(Форма, Элементы.ОграничитьДокумент.Пометка);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьДоступностьДокументаДляРедактирования(Форма)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
		
	Если НЕ Объект.Проведен Тогда
		
		Элементы.СообщениеОНевозможностиИзмененияГруппа.Видимость = Ложь;
		Элементы.ОграничитьДокумент.Видимость = Ложь;
		
	Иначе
		
		ДокументОграничен = РегистрыСведений.ОграниченныеДокументы.ДокументОграничен(Объект.Ссылка);
		ОграничениеИспользованияДокументовКлиентСервер.УстановитьДоступностьДанныхФормы(Форма, ДокументОграничен);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьФормуДокумента(Форма, ИмяДействия)
	
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("РежимТолькоПросмотр", Новый ОписаниеТипов("Булево")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("РежимТолькоПросмотрПрежняя", Новый ОписаниеТипов("Булево")));
	
	МассивИменРеквизитовФормы = Новый Массив;
	ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(Форма, МассивИменРеквизитовФормы);
	ЗарплатаКадры.ИзменитьРеквизитыФормы(Форма, ДобавляемыеРеквизиты, МассивИменРеквизитовФормы);

	ИмяКоманды = "ОграничитьДокумент";
	
	ПредставлениеПометки = ОграничениеИспользованияДокументов.ПредставлениеПометкиОграниченияДокумента(Форма.Объект.Ссылка);
		
	КомандаЗакрытияДокумента = Форма.Команды.Найти(ИмяКоманды);
	
	Если КомандаЗакрытияДокумента = Неопределено Тогда
		
		НоваяКоманда = Форма.Команды.Добавить(ИмяКоманды);
		НоваяКоманда.Действие = "Подключаемый_" + ИмяДействия;
		
		ЭлементКнопкаПодменю = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), Форма.КоманднаяПанель);
		ЭлементКнопкаПодменю.Заголовок				= ПредставлениеПометки;
		ЭлементКнопкаПодменю.Вид					= ВидКнопкиФормы.КнопкаКоманднойПанели;
		ЭлементКнопкаПодменю.ИмяКоманды				= ИмяКоманды;
		ЭлементКнопкаПодменю.ТолькоВоВсехДействиях 	= Истина;
		
		КомандаЗакрытияДокумента = НоваяКоманда;
		
	КонецЕсли;
	
	КомандаЗакрытияДокумента.Заголовок = ПредставлениеПометки;
	
	ИмяЭлементаНадпись = "СообщениеОНевозможностиИзмененияНадпись";
	ЭлементНадпись = Форма.Элементы.Найти(ИмяЭлементаНадпись);
	
	Если ЭлементНадпись = Неопределено Тогда
		
		ГруппаСообщения = Форма.Элементы.Найти("СообщениеОНевозможностиИзмененияГруппа");
		
		Если ГруппаСообщения <> Неопределено Тогда
			
			ЭлементКартинка = Форма.Элементы.Добавить("СообщениеОНевозможностиИзмененияКартинка", Тип("ДекорацияФормы"), ГруппаСообщения);
			ЭлементКартинка.Вид = ВидДекорацииФормы.Картинка;
			ЭлементКартинка.Заголовок = НСтр("ru = 'Сообщение о невозможности изменения (картинка)'");
			ЭлементКартинка.Картинка = БиблиотекаКартинок.Предупреждение;
			
			ЭлементНадпись = Форма.Элементы.Добавить(ИмяЭлементаНадпись, Тип("ДекорацияФормы"), ГруппаСообщения);
			ЭлементНадпись.Вид = ВидДекорацииФормы.Надпись;
			ЭлементНадпись.АвтоМаксимальнаяШирина = Ложь;
			ЭлементНадпись.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
			ЭлементНадпись.Подсказка = НСтр("ru = 'Для снятия запрета редактирования, снимите пометку в меню «Еще».'");
			ЭлементНадпись.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстЗаголовкаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Документ недоступен для редактирования, т.к. он %1'"),
		НРег(ПредставлениеПометки));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		ИмяЭлементаНадпись,
		"Заголовок",
		ТекстЗаголовкаСообщения);
		
КонецПроцедуры

Процедура ОграничитьОсвободитьДокумент(Форма, Ограничить = Истина)
	
	ДокументСсылка = Форма.Объект.Ссылка;
		
	Если Ограничить Тогда
		РегистрыСведений.ОграниченныеДокументы.ОграничитьДокумент(ДокументСсылка);
	Иначе
		РегистрыСведений.ОграниченныеДокументы.ОсвободитьДокумент(ДокументСсылка);
	КонецЕсли;
	
	УстановитьДоступностьДокументаДляРедактирования(Форма);
	
КонецПроцедуры

#КонецОбласти
