#Область СлужебныйПрограммныйИнтерфейс

// Заполняет вторичные данные ТЧ Кадровый резерв
//
Процедура ЗаполнитьВторичныеДанныеКадровогоРезерва(КадровыйРезерв) Экспорт

	Для каждого КадровыйРезервСтрока Из КадровыйРезерв Цикл
		ЗаполнитьОписаниеДействия(КадровыйРезервСтрока);
		ЗаполнитьОписаниеСтатуса(КадровыйРезервСтрока);
	КонецЦикла; 

КонецПроцедуры

// Заполняет текстовое поле "ОписаниеДействия" строки по полю "Действие"
//
Процедура ЗаполнитьОписаниеДействия(ТекущаяСтрока, СоСтороныИсключения = Истина) Экспорт

	Если НЕ ТекущаяСтрока.Свойство("ОписаниеДействия") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ДействияСКадровымиРезервистами.Оставить") Тогда
		ТекущаяСтрока.ОписаниеДействия = НСтр("ru = 'Сохранить в кадровом резерве'"); 
	ИначеЕсли ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ДействияСКадровымиРезервистами.Отклонить") Тогда
		Если СоСтороныИсключения Тогда
			ТекущаяСтрока.ОписаниеДействия = НСтр("ru = 'Исключить'");
		Иначе
			ТекущаяСтрока.ОписаниеДействия = НСтр("ru = 'Отклонить'"); 
		КонецЕсли;
	ИначеЕсли ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ДействияСКадровымиРезервистами.Утвердить") Тогда
		ТекущаяСтрока.ОписаниеДействия = НСтр("ru = 'Включить в кадровый резерв'"); 
	КонецЕсли;
	
КонецПроцедуры

// Заполняет текстовое поле "ОписаниеСтатуса" строки по полю "Действие"
//
Процедура ЗаполнитьОписаниеСтатуса(ТекущаяСтрока) Экспорт

	Если НЕ ТекущаяСтрока.Свойство("ОписаниеСтатуса") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущаяСтрока.Статус = ПредопределенноеЗначение("Перечисление.СостоянияСогласования.ПустаяСсылка") Тогда
		ТекущаяСтрока.ОписаниеСтатуса = НСтр("ru = 'Исключен'"); 
	ИначеЕсли ТекущаяСтрока.Статус = ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Рассматривается") Тогда
		ТекущаяСтрока.ОписаниеСтатуса = НСтр("ru = 'Кандидат'"); 
	ИначеЕсли ТекущаяСтрока.Статус = ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Согласовано") Тогда
		ТекущаяСтрока.ОписаниеСтатуса = НСтр("ru = 'Включен в резерв'"); 
	Иначе
		ТекущаяСтрока.ОписаниеСтатуса = ""; 
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьДоступностьКомандКадровогоРезерваДокументовДляОбщихДокументов(Форма, ТекущиеДанные) Экспорт

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"КадровыйРезервКадровыйРезервОставить",
		"Доступность",
		ТекущиеДанные.Действие = ПредопределенноеЗначение("Перечисление.ДействияСКадровымиРезервистами.Отклонить"));
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"КадровыйРезервКадровыйРезервИсключить",
		"Доступность",
		ТекущиеДанные.Действие = ПредопределенноеЗначение("Перечисление.ДействияСКадровымиРезервистами.Оставить"));
		
КонецПроцедуры

#КонецОбласти