////////////////////////////////////////////////////////////////////////////////
// Подсистема "Обмен с банками по зарплатным проектам".
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Процедура ВыгрузитьФайлыДляОбменаСБанком(СтруктураПараметров, АдресХранилища) Экспорт
	
	СтруктураПараметров.Вставить("МассивОписанийФайлов", Новый Массив);
		
	МетаданныеПлатежногоДокумента = ОбменСБанкамиПоЗарплатнымПроектам.МетаданныеПлатежногоДокументаПеречисленияЗарплаты();
	
	ДокументыПоТипам = ОбщегоНазначенияБЗК.ОбъектыПоТипам(СтруктураПараметров.МассивДокументов);
	Для Каждого ДокументыПоТипу Из ДокументыПоТипам Цикл
		
		МетаданныеДокумента = Метаданные.НайтиПоТипу(ДокументыПоТипу.Ключ);
		Если (МетаданныеПлатежногоДокумента = Неопределено
			Или МетаданныеДокумента.ПолноеИмя() <> МетаданныеПлатежногоДокумента.ПолноеИмя()) Тогда			
			СтруктураПараметров.Вставить("МассивДокументов", ДокументыПоТипу.Значение);
			МенеджерДокумента = ОбщегоНазначенияБЗК.МенеджерОбъектаПоТипу(ДокументыПоТипу.Ключ);
			МенеджерДокумента.ВыгрузитьФайлыДляОбменаСБанком(СтруктураПараметров)
		Иначе
			Для Каждого ПлатежныйДокумент Из ДокументыПоТипу.Значение Цикл
				СтруктураПараметров.Вставить("МассивДокументов", ОбменСБанкамиПоЗарплатнымПроектам.ВедомостиПлатежногоДокументаПеречисленияЗарплаты(ПлатежныйДокумент));
				СтруктураПараметров.Вставить("ПлатежныйДокумент", ПлатежныйДокумент);
				ОбменСБанкамиПоЗарплатнымПроектам.ВыгрузитьФайлыДляОбменаСБанкомПоВедомости(СтруктураПараметров);
			КонецЦикла	
		КонецЕсли;
		
	КонецЦикла;
		
	ПоместитьВоВременноеХранилище(СтруктураПараметров.МассивОписанийФайлов, АдресХранилища);
	
КонецПроцедуры

// см. ОбменСБанкамиПоЗарплатнымПроектам.ПолучитьСтруктуруКлючевыхРеквизитовОбъекта
Процедура ПолучитьСтруктуруКлючевыхРеквизитовОбъекта(ИмяОбъекта, СтруктураКлючевыхРеквизитов) Экспорт
	
	Если ИмяОбъекта = "Документ.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников" Тогда
		// шапка
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, ЗарплатныйПроект, НомерРеестра, ПометкаУдаления");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		
		// ТЧ
		СтрокаРеквизитовОбъекта = "";
		Для Каждого РеквизитДокумента Из Метаданные.Документы.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.ТабличныеЧасти.Сотрудники.Реквизиты Цикл
			СтрокаРеквизитовОбъекта = СтрокаРеквизитовОбъекта + РеквизитДокумента.Имя + ", ";
		КонецЦикла;
		СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(СтрокаРеквизитовОбъекта, 2);
		СтруктураКлючевыхРеквизитов.Вставить("Сотрудники", СтрокаРеквизитовОбъекта);
		
	ИначеЕсли ИмяОбъекта = "Документ.ВедомостьНаВыплатуЗарплатыВБанк" Тогда
		// шапка
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, ЗарплатныйПроект, НомерРеестра, ПометкаУдаления");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		
		// ТЧ
		СтрокаРеквизитовОбъекта = ("Сотрудник, ФизическоеЛицо, КВыплате, КомпенсацияЗаЗадержкуЗарплаты, НомерЛицевогоСчета");
		СтруктураКлючевыхРеквизитов.Вставить("Зарплата", СтрокаРеквизитовОбъекта);
		
	ИначеЕсли ИмяОбъекта = "Документ.ПлатежноеПоручение" Тогда
		// шапка
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, НомерРеестра, ПометкаУдаления");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		
		// ТЧ
		СтрокаРеквизитовОбъекта = ("Ведомость");
		СтруктураКлючевыхРеквизитов.Вставить("Ведомости", СтрокаРеквизитовОбъекта);
		
	ИначеЕсли ИмяОбъекта = "Документ.ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудников" Тогда
		// шапка
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, ЗарплатныйПроект, НомерРеестра, ПометкаУдаления");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		
		// ТЧ
		СтрокаРеквизитовОбъекта = ("Сотрудник, ФизическоеЛицо, ДатаЗакрытия, ЛицевойСчет");
		СтруктураКлючевыхРеквизитов.Вставить("Сотрудники", СтрокаРеквизитовОбъекта);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
