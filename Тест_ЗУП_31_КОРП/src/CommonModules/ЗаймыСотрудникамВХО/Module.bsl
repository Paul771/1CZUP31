////////////////////////////////////////////////////////////////////////////////
// Подсистема "Займы сотрудникам".
// Процедуры и функции, предназначенные для обслуживания выданных сотрудникам займов.
//  
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Определяет необходимость заполнения формы расчетов в документах выдачи/погашения займов.
//
// Параметры:
//	Организация
//
// Возвращаемое значение - булево.
//
Функция УточнятьФормуРасчетовПриВыдачеПогашенииЗайма(Организация) Экспорт
	
	// Уточняем форму расчетов, если используется обмен с бухгалтерией.
	
	// ЗарплатаКадрыПриложения.ОбменЗарплата3Бухгалтерия3
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОбменЗарплата3Бухгалтерия3") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиЗарплата3Бухгалтерия3");
		Возврат Модуль.ОбменИспользуется(Организация);
	КонецЕсли;
	// Конец ЗарплатаКадрыПриложения.ОбменЗарплата3Бухгалтерия3
	
	Возврат Ложь;
	
КонецФункции

// Возвращает массив документов ВыдачаЗаймаСотруднику, оформленных по договору.
//
// Параметры:
//	ДоговорЗайма - ссылка на документ ДоговорЗаймаСотруднику.
//
Функция ДокументыВыдачиЗайма(ДоговорЗайма) Экспорт 
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ДоговорЗайма", ДоговорЗайма);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВыдачаЗаймаСотруднику.Ссылка
	               |ИЗ
	               |	Документ.ВыдачаЗаймаСотруднику КАК ВыдачаЗаймаСотруднику
	               |ГДЕ
	               |	ВыдачаЗаймаСотруднику.Проведен
	               |	И ВыдачаЗаймаСотруднику.ДоговорЗайма = &ДоговорЗайма";
				   
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

Процедура ЗаполнитьФормуРасчетовВДокументахЗаймаСотруднику() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВыдачаЗаймаСотруднику.Ссылка
	|ИЗ
	|	Документ.ВыдачаЗаймаСотруднику КАК ВыдачаЗаймаСотруднику
	|ГДЕ
	|	ВыдачаЗаймаСотруднику.ФормаРасчетов = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПогашениеЗаймаСотруднику.Ссылка
	|ИЗ
	|	Документ.ПогашениеЗаймаСотруднику КАК ПогашениеЗаймаСотруднику
	|ГДЕ
	|	ПогашениеЗаймаСотруднику.ФормаРасчетов = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.ПустаяСсылка)";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДокОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ДокОбъект.ОбменДанными.Загрузка = Истина;
		ДокОбъект.ФормаРасчетов = Перечисления.ФормыОплаты.Наличная;
		ДокОбъект.Записать();
	КонецЦикла;
	
КонецПроцедуры

// Определяет имя таблицы документа внеочередного погашения займа сотрудником.
//
Процедура ЗаполнитьИмяДокументаПогашенияЗайма(ДокументПогашения) Экспорт
	ДокументПогашения = Метаданные.Документы.ПогашениеЗаймаСотруднику.ПолноеИмя();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Определяет объекты, в которых есть процедура ДобавитьКомандыПечати().
// Подробнее см. УправлениеПечатьюПереопределяемый.
//
// Параметры:
//  СписокОбъектов - Массив - список менеджеров объектов.
//
Процедура ПриОпределенииОбъектовСКомандамиПечати(СписокОбъектов) Экспорт
	
	СписокОбъектов.Добавить(Документы.ВыдачаЗаймаСотруднику);
	СписокОбъектов.Добавить(Документы.ПогашениеЗаймаСотруднику);
	
КонецПроцедуры

Процедура ЗаполнитьКатегориюДоходаВДвиженияхПогашениеЗаймаСотрудникуПоРегиструСведенияОДоходахНДФЛ(ПараметрыОбновления = Неопределено) Экспорт 

	УчетФактическиПолученныхДоходовРасширенный.ЗаполнитьКатегориюДоходаВДвиженияхОсобогоДокументаПоРегиструСведенияОДоходахНДФЛ("ПогашениеЗаймаСотруднику", Перечисления.КатегорииДоходовНДФЛ.ПрочиеНатуральныеДоходы, ПараметрыОбновления)	

КонецПроцедуры

Процедура ЗаполнитьКатегориюДоходаВДвиженияхПогашениеЗаймаСотрудникуПоРегиструРасчетыНалогоплательщиковСБюджетомПоНДФЛ(ПараметрыОбновления = Неопределено) Экспорт 

	УчетФактическиПолученныхДоходовРасширенный.ЗаполнитьКатегориюДоходаВДвиженияхОсобогоДокументаПоРегиструРасчетыНалогоплательщиковСБюджетомПоНДФЛ("ПогашениеЗаймаСотруднику", Перечисления.КатегорииДоходовНДФЛ.ПрочиеНатуральныеДоходы, ПараметрыОбновления)	

КонецПроцедуры

#КонецОбласти