
#Область СлужебныйПрограммныйИнтерфейс

Процедура ОткрытьФормуЗамещенияПоСсылке(Форма, НавигационнаяСсылка, СтандартнаяОбработка) Экспорт 

	СтандартнаяОбработка = Ложь;
	
	Если НЕ ЦепочкиДокументовВызовСервера.ИспользоватьЦепочкиДокументов() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураСсылки = ЦепочкиДокументовКлиентСервер.СтруктураНавигационнойСсылки(НавигационнаяСсылка);
	
	Если СтруктураСсылки.Режим = "ВызовФормыЗамещения" Тогда
		ОткрытьФормуПомощникаЗамещения(Форма, СтруктураСсылки);
	Иначе	
		ОткрытьФормуДокументаЗамещения(Форма, СтруктураСсылки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьФормуДокументаЗамещения(Форма, СтруктураСсылки) Экспорт 
	
	Если НЕ ЦепочкиДокументовВызовСервера.ИспользоватьЦепочкиДокументов() Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураСсылки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяОткрываемойФормы = СтрЗаменить("Документ.%1.ФормаОбъекта", "%1", СтруктураСсылки.ИмяМетаданных);
	ДокументСсылка = ЦепочкиДокументовВызовСервера.ДокументПоСсылке(СтруктураСсылки.ИмяМетаданных, СтруктураСсылки.НавигационнаяСсылка);
	
	ОткрытьФорму(ИмяОткрываемойФормы, Новый Структура("Ключ", ДокументСсылка));

КонецПроцедуры

Процедура ОповеститьЗаписанПодчиненныйДокумент(Форма) Экспорт 

	Если НЕ ЦепочкиДокументовВызовСервера.ИспользоватьЦепочкиДокументов() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Форма.ЗамещениеДокументОснование) Тогда
		Оповестить("ЗаписанПодчиненныйДокумент", , Форма.ЗамещениеДокументОснование);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПараметрыОткрытияФормыЗамещения()

	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("ДокументСсылка");
	ПараметрыФормы.Вставить("Сотрудник");
	ПараметрыФормы.Вставить("ПозицияШР");
	ПараметрыФормы.Вставить("ДатаНачала");
	ПараметрыФормы.Вставить("ДатаОкончания");
	ПараметрыФормы.Вставить("ТекущаяПозицияШР");
	ПараметрыФормы.Вставить("ФормироватьПараметрыПоСсылке");
	
	Возврат ПараметрыФормы;

КонецФункции

Процедура ОткрытьФормуПомощникаЗамещения(Форма, СтруктураСсылки) 

	ДополнительныеПараметры = Новый Структура("Форма, СтруктураСсылки", Форма, СтруктураСсылки);
	Если ЗначениеЗаполнено(Форма.Объект.Ссылка) Тогда
		ОткрытьФормуПомощникаЗамещенияЗавершение(Неопределено, ДополнительныеПараметры);
	Иначе
		Оповещение = Новый ОписаниеОповещения("ОткрытьФормуПомощникаЗамещенияЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ТекстВопроса = НСтр("ru = 'Ввести замещение можно только для записанного документа. Записать?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьФормуПомощникаЗамещенияЗавершение(Результат, ДополнительныеПараметры) Экспорт 

	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ДополнительныеПараметры.Форма;
	СтруктураСсылки = ДополнительныеПараметры.СтруктураСсылки;
	Объект = Форма.Объект;
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Форма.Записать();
	КонецЕсли;
	
	ДокументСсылка = ЦепочкиДокументовВызовСервера.ДокументПоСсылке(СтруктураСсылки.ИмяМетаданных, СтруктураСсылки.НавигационнаяСсылка);
	Если Не ЗначениеЗаполнено(ДокументСсылка) Тогда
		ДокументСсылка = Объект.Ссылка;
	КонецЕсли;
	
	ПараметрыФормы = ПараметрыОткрытияФормыЗамещения();
	
	ПараметрыФормы.ДокументСсылка = ДокументСсылка;
	ПараметрыФормы.ФормироватьПараметрыПоСсылке = СтруктураСсылки.ФормироватьПараметрыПоСсылке;
	
	Если НЕ СтруктураСсылки.ФормироватьПараметрыПоСсылке Тогда
		ОписаниеДокумента = ЦепочкиДокументовВызовСервера.ОписаниеДокументовЗамещения(ДокументСсылка);
		ПараметрыФормы.Сотрудник = Объект[ОписаниеДокумента.ИмяРеквизитаСотрудник];
		ПараметрыФормы.ДатаНачала = Объект[ОписаниеДокумента.ИмяРеквизитаДатаНачалаСобытия];
		ПараметрыФормы.ДатаОкончания = Объект[ОписаниеДокумента.ИмяРеквизитаДатаОкончанияСобытия];
		Если ЗначениеЗаполнено(ОписаниеДокумента.ИмяРеквизитаФормыТекущаяПозицияШР) Тогда
			ПараметрыФормы.ТекущаяПозицияШР = Форма[ОписаниеДокумента.ИмяРеквизитаФормыТекущаяПозицияШР];
		КонецЕсли;
	КонецЕсли;
	
	ОткрытьФорму("Обработка.ЦепочкиКадровыхДокументов.Форма.ПомощникЗамещения", ПараметрыФормы, Форма, Истина);
	
КонецПроцедуры

#КонецОбласти