////////////////////////////////////////////////////////////////////////////////
// Подсистема "Обмен с банками по зарплатным проектам".
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает данные документов для обмена с банком по зарплатным проектам.
//
// 	Параметры:
// 		Документы - Массив - ссылки на документы, по которым требуется получить данные.
// 		                     Поддерживаемые документы:
// 		                         ОпределяемыйТип.ВедомостьВБанкЗарплатаКадры
// 		                         ДокументСсылка.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников
// 		                         ДокументСсылка.ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудников
// 		ДатаПолученияДанных - Дата           - дата формирования файла.
// 		ПлатежныйДокумент   - ДокументСсылка - платежный документ, в который входят ведомости.
//
// Возвращаемое значение:
//		Соответствие - где Ключ - ссылка на документ, Значение - структура документа.
//		Неопределено - если нет документов по которым требуется получить данные.
//
Функция ДанныеДокументовДляОбменаСБанком(Документы, ДатаПолученияДанных, ПлатежныйДокумент = Неопределено) Экспорт
	
	Если Документы.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТипДокументов = ТипЗнч(Документы[0]);
	
	Если ТипДокументов = Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплатыВБанк") Тогда
		Возврат Документы.ВедомостьНаВыплатуЗарплатыВБанк.ДанныеВедомостиНаВыплатуЗарплатыВБанк(Документы, ДатаПолученияДанных, ПлатежныйДокумент);
	ИначеЕсли ТипДокументов = Тип("ДокументСсылка.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников") Тогда
		Возврат Документы.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.ДанныеОткрытияЛицевыхСчетов(Документы, ДатаПолученияДанных);
	ИначеЕсли ТипДокументов = Тип("ДокументСсылка.ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудников") Тогда
		Возврат Документы.ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудников.ДанныеЗакрытияЛицевыхСчетов(Документы, ДатаПолученияДанных);
	КонецЕсли;
	
	Возврат Неопределено
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Получает значение использования электронного обмена информацией с банком по зарплатному проекту.
//
// Параметры:
//		ЗарплатныйПроект - ссылка на зарплатный проект.
//
// Возвращаемое значение:
//		ИспользоватьЭОИСБанком - 
//				принимает значение Истина, если по зарплатному проекту
//				ведется обмен электронными документами с банком, 
//				Ложь - если не ведется обмен электронными документами с банком.
//
Функция ИспользоватьЭОИСБанком(ЗарплатныйПроект) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ЗарплатныйПроект", ЗарплатныйПроект);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗарплатныеПроекты.ИспользоватьЭлектронныйДокументооборотСБанком КАК ИспользоватьЭОИСБанком
	|ИЗ
	|	Справочник.ЗарплатныеПроекты КАК ЗарплатныеПроекты
	|ГДЕ
	|	ЗарплатныеПроекты.Ссылка = &ЗарплатныйПроект";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		Возврат Выборка.ИспользоватьЭОИСБанком;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ИспользоватьТиповойЭОИСБанком(ЗарплатныйПроект) Экспорт
	
	ИспользоватьТиповойОбмен = Истина;
	ОбменСБанкамиПоЗарплатнымПроектамПереопределяемый.ПриОпределенииИспользованияТиповогоЭОИСБанком(ЗарплатныйПроект, ИспользоватьТиповойОбмен);
	
	Возврат ИспользоватьТиповойОбмен;
	
КонецФункции

// Получает первый зарплатный проект по организации.
//
// Параметры:
//		Организация - ссылка на организацию.
//		ПолучитьЕслиЕдинственный - Если (Истина), то проверяется, чтобы зарплатный проект был единственным в организации,
//				Если зарплатный проект не единственный, тогда возвращается пустая ссылка.
//				Иначе (Ложь) проверка не делается и возвращается первый.
//
// Возвращаемое значение:
//		ЗарплатныйПроект
//		Если зарплатный проект не найден - возвращается пустая ссылка.
//
Функция ЗарплатныйПроектПоОрганизации(Организация, ПолучитьЕслиЕдинственный = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(ЗарплатныеПроекты.Ссылка) КАК Количество
	|ПОМЕСТИТЬ ВТЗарплатныеПроекты
	|ИЗ
	|	Справочник.ЗарплатныеПроекты КАК ЗарплатныеПроекты
	|ГДЕ
	|	ЗарплатныеПроекты.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ЗарплатныеПроекты.Ссылка КАК ЗарплатныйПроект,
	|	ВТЗарплатныеПроекты.Количество КАК Количество
	|ИЗ
	|	Справочник.ЗарплатныеПроекты КАК ЗарплатныеПроекты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗарплатныеПроекты КАК ВТЗарплатныеПроекты
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ЗарплатныеПроекты.Организация = &Организация";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Если ПолучитьЕслиЕдинственный Тогда
			Если Выборка.Количество = 1 Тогда
				Возврат Выборка.ЗарплатныйПроект;
			Иначе
				Возврат Справочники.ЗарплатныеПроекты.ПустаяСсылка();
			КонецЕсли;
		Иначе
			Возврат Выборка.ЗарплатныйПроект;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Справочники.ЗарплатныеПроекты.ПустаяСсылка();
	
КонецФункции

// Получает первый зарплатный проект по организации и банку.
//
// Параметры:
//		Организация - ссылка на организацию,
//		Банк - ссылка на банк.
//
// Возвращаемое значение:
//		ЗарплатныйПроект
//		Если зарплатный проект не найден - возвращается пустая ссылка.
//
Функция ЗарплатныйПроектПоОрганизацииИБанку(Организация, Банк) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Банк", Банк);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗарплатныеПроекты.Ссылка КАК ЗарплатныйПроект
	|ИЗ
	|	Справочник.ЗарплатныеПроекты КАК ЗарплатныеПроекты
	|ГДЕ
	|	ЗарплатныеПроекты.Организация = &Организация
	|	И ЗарплатныеПроекты.Банк = &Банк";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Возврат Выборка.ЗарплатныйПроект;
		
	КонецЕсли;
	
	Возврат Справочники.ЗарплатныеПроекты.ПустаяСсылка();
	
КонецФункции

// Создает новый зарплатный проект, используя наименование банка.
//
// Параметры:
//		Банк - банк, используя наименование которого создается зарплатный проект.
//
// Возвращаемое значение:
//		Зарплатный проект - ссылка на созданный зарплатный проект.
//		Или пустая ссылка, если зарплатный проект не создан.
//
Функция НовыйЗарплатныйПроектПоОрганизацииИБанку(Организация, Банк) Экспорт
	
	Если Не ЗначениеЗаполнено(Организация) ИЛИ Не ЗначениеЗаполнено(Банк) Тогда
		Возврат Справочники.ЗарплатныеПроекты.ПустаяСсылка();
	КонецЕсли;
	
	ЗарплатныйПроектОбъект = Справочники.ЗарплатныеПроекты.СоздатьЭлемент();
	ЗарплатныйПроектОбъект.Наименование = Банк.Наименование;
	ЗарплатныйПроектОбъект.Организация = Организация;
	ЗарплатныйПроектОбъект.Банк = Банк;
	
	ЗарплатныйПроектОбъект.Записать();
	
	Возврат ЗарплатныйПроектОбъект.Ссылка;
	
КонецФункции

// Возвращает структуру, описывающую временную таблицу отборов физических лиц.
//
// Параметры:
//				ИмяВременнойТаблицыОтборов - Строка, имя временной таблицы
//								в менеджере запросов.
//				ИмяПоляФизическоеЛицо - Строка, указывает имя поля временной таблицы,
//								содержащего ссылку на физическое лицо.
//				ИмяПоляПериод - Строка, указывает имя поля временной таблицу, содержащее
//								период, на который предполагается получить лицевые счета.
//
Функция ОписаниеВременнойТаблицыДляСоздатьВТЛицевыеСчетаСотрудников(МенеджерВременныхТаблиц, ИмяВременнойТаблицыОтборов, ИмяПоляФизическоеЛицо = "ФизическоеЛицо", ИмяПоляПериод = "Период", ДатаПолученияДанных = Неопределено) Экспорт
	
	ОписаниеВременнойТаблицы = Новый Структура;
	ОписаниеВременнойТаблицы.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
	Если ДатаПолученияДанных = Неопределено Тогда
		ОписаниеВременнойТаблицы.Вставить("ИмяВременнойТаблицыОтборов", ИмяВременнойТаблицыОтборов);
		ОписаниеВременнойТаблицы.Вставить("ИмяПоляФизическоеЛицо", ИмяПоляФизическоеЛицо);
		ОписаниеВременнойТаблицы.Вставить("ИмяПоляПериод", ИмяПоляПериод);
	ИначеЕсли МенеджерВременныхТаблиц <> Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("ДатаПолученияДанных", ДатаПолученияДанных);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	&ДатаПолученияДанных КАК Период,
		|	Сотрудники.*
		|ПОМЕСТИТЬ ВТСписокСотрудниковДляПолученияЛицевыхСчетовСотрудников
		|ИЗ
		|	ВТСотрудники КАК Сотрудники";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТСотрудники", ИмяВременнойТаблицыОтборов);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Сотрудники.ФизическоеЛицо", "Сотрудники." + ИмяПоляФизическоеЛицо);
		Запрос.Выполнить();
		
		ОписаниеВременнойТаблицы.Вставить("ИмяВременнойТаблицыОтборов", "ВТСписокСотрудниковДляПолученияЛицевыхСчетовСотрудников");
		ОписаниеВременнойТаблицы.Вставить("ИмяПоляФизическоеЛицо", "ФизическоеЛицо");
		ОписаниеВременнойТаблицы.Вставить("ИмяПоляПериод", "Период");
		
	КонецЕсли;
	
	Возврат ОписаниеВременнойТаблицы;
	
КонецФункции

Функция ТекстЗапросаВТЛицевыеСчетаСотрудников(Запрос, ТолькоРазрешенные, ОписаниеВременнойТаблицы, ПоляОтбораПериодическихДанных, ИмяВТЛицевыеСчетаСотрудниковПоЗарплатнымПроектам = "ВТЛицевыеСчетаСотрудников") Экспорт
	
	ЗапросВТ = ЗапросВТСведенияОЗарплатныхПроектах(ТолькоРазрешенные, ОписаниеВременнойТаблицы, ПоляОтбораПериодическихДанных);
	
	ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросВТ);
	
	ТекстЗапросаУникальныхЗаписей = 
		"ВЫБРАТЬ
		|	СведенияОЗарплатныхПроектах.Период,
		|	МАКСИМУМ(СведенияОЗарплатныхПроектах.ПериодЗаписи) КАК ПериодЗаписи,
		|	СведенияОЗарплатныхПроектах.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	СведенияОЗарплатныхПроектах.ФизическоеЛицо
		|ПОМЕСТИТЬ ВТМаксимальныеПериодыЛицевыхСчетов
		|ИЗ
		|	ВТСведенияОЗарплатныхПроектах КАК СведенияОЗарплатныхПроектах
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОЗарплатныхПроектах.Период,
		|	СведенияОЗарплатныхПроектах.Организация.ГоловнаяОрганизация,
		|	СведенияОЗарплатныхПроектах.ФизическоеЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СведенияОЗарплатныхПроектах.Период,
		|	СведенияОЗарплатныхПроектах.ПериодЗаписи,
		|	СведенияОЗарплатныхПроектах.Организация,
		|	СведенияОЗарплатныхПроектах.ФизическоеЛицо,
		|	СведенияОЗарплатныхПроектах.НомерЛицевогоСчета,
		|	СведенияОЗарплатныхПроектах.ЗарплатныйПроект,
		|	ЗарплатныеПроекты.Банк КАК Банк,
		|	СведенияОЗарплатныхПроектах.ДокументОснование
		|ПОМЕСТИТЬ ВТЛицевыеСчетаСотрудниковПоЗарплатнымПроектам
		|ИЗ
		|	ВТМаксимальныеПериодыЛицевыхСчетов КАК МаксимальныеПериодыЛицевыхСчетов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСведенияОЗарплатныхПроектах КАК СведенияОЗарплатныхПроектах
		|		ПО МаксимальныеПериодыЛицевыхСчетов.Период = СведенияОЗарплатныхПроектах.Период
		|			И МаксимальныеПериодыЛицевыхСчетов.ПериодЗаписи = СведенияОЗарплатныхПроектах.ПериодЗаписи
		|			И МаксимальныеПериодыЛицевыхСчетов.ГоловнаяОрганизация = СведенияОЗарплатныхПроектах.Организация.ГоловнаяОрганизация
		|			И МаксимальныеПериодыЛицевыхСчетов.ФизическоеЛицо = СведенияОЗарплатныхПроектах.ФизическоеЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗарплатныеПроекты КАК ЗарплатныеПроекты
		|		ПО (СведенияОЗарплатныхПроектах.ЗарплатныйПроект = ЗарплатныеПроекты.Ссылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТСведенияОЗарплатныхПроектах
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТМаксимальныеПериодыЛицевыхСчетов";
		
	ТекстЗапросаУникальныхЗаписей = СтрЗаменить(ТекстЗапросаУникальныхЗаписей, "ВТЛицевыеСчетаСотрудниковПоЗарплатнымПроектам", ИмяВТЛицевыеСчетаСотрудниковПоЗарплатнымПроектам);
	
	ТекстЗапроса = ЗапросВТ.Текст
		+ ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов()
		+ ТекстЗапросаУникальныхЗаписей;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ЗапросВТЛицевыеСчетаСотрудниковПоВременнойТаблице(ТолькоРазрешенные, ОписаниеВременнойТаблицы, ПоляОтбораПериодическихДанных, ИмяВТЛицевыеСчетаСотрудниковПоЗарплатнымПроектам = "ВТЛицевыеСчетаСотрудников") Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаВТЛицевыеСчетаСотрудников(Запрос, ТолькоРазрешенные, ОписаниеВременнойТаблицы, ПоляОтбораПериодическихДанных, ИмяВТЛицевыеСчетаСотрудниковПоЗарплатнымПроектам);
	
	Возврат Запрос;
	
КонецФункции

// Формирует временную таблицу ВТЛицевыеСчетаСотрудников сотрудников с номерами лицевых счетов 
// по переданной организации и временной таблице, в которой находятся сотрудники.
//
// Параметры:
//		ОписаниеВременнойТаблицы - структура полученная функцией
//		                           ОписаниеВременнойТаблицыДляСоздатьПоВременнойТаблицеВТЛицевыеСчетаСотрудников.
//			Содержит следующие поля:
//					МенеджерВременныхТаблиц.
//					ИмяВременнойТаблицыОтборов - имя временной таблицы.
//		ТолькоРазрешенные - Булево
//		Организация - организация, по которой нужно получить лицевые счета.
//		ЗарплатныйПроект - зарплатный проект, если не задан, то по всем зарплатным проектам.
//
// Создает в МенеджерВременныхТаблиц временную таблицу с именем ВТЛицевыеСчетаСотрудников.
// Поля временной таблицы
//		МесяцОткрытия
//		Сотрудник
//		ФизическоеЛицо
//		ЗарплатныйПроект
//		Банк
//		НомерЛицевогоСчета
//		Документ - Документ-подтверждение из банка, при получении которого был зарегистрирован номер лицевого счета
//				Если не указан, то номер лицевого счета введен вручную.
//
Процедура СоздатьПоВременнойТаблицеВТЛицевыеСчетаСотрудников(ОписаниеВременнойТаблицы, ТолькоРазрешенные, Организация, ЗарплатныйПроект = Неопределено) Экспорт
	
	Отбор = Новый Массив;
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(Отбор, "Организация", "=", Организация);
	Если ЗарплатныйПроект <> Неопределено Тогда
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(Отбор, "ЗарплатныйПроект", "=", ЗарплатныйПроект);
	КонецЕсли;
	ПоляОтбораПериодическихДанных = Новый Структура;
	ПоляОтбораПериодическихДанных.Вставить("ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам", Отбор);
	
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		ОписаниеВременнойТаблицы.МенеджерВременныхТаблиц, ОписаниеВременнойТаблицы.ИмяВременнойТаблицыОтборов);
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, ТолькоРазрешенные, "ЗарплатныйПроектПериодРегистрации, ЗарплатныйПроект, НомерЛицевогоСчета, ЗарплатныйПроектРегистратор", ПоляОтбораПериодическихДанных);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ОписаниеВременнойТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КадровыеДанныеСотрудников.ЗарплатныйПроектПериодРегистрации КАК МесяцОткрытия,
	|	КадровыеДанныеСотрудников.Сотрудник КАК Сотрудник,
	|	КадровыеДанныеСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
	|	КадровыеДанныеСотрудников.ЗарплатныйПроект КАК ЗарплатныйПроект,
	|	ЗарплатныеПроекты.Банк КАК Банк,
	|	КадровыеДанныеСотрудников.НомерЛицевогоСчета КАК НомерЛицевогоСчета,
	|	КадровыеДанныеСотрудников.ЗарплатныйПроектРегистратор КАК ДокументОснование
	|ПОМЕСТИТЬ ВТЛицевыеСчетаСотрудников
	|ИЗ
	|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗарплатныеПроекты КАК ЗарплатныеПроекты
	|		ПО КадровыеДанныеСотрудников.ЗарплатныйПроект = ЗарплатныеПроекты.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТКадровыеДанныеСотрудников";
	
	Если Не ТолькоРазрешенные Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВЫБРАТЬ РАЗРЕШЕННЫЕ", "ВЫБРАТЬ");
	КонецЕсли;
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Формирует временную таблицу ВТЛицевыеСчетаСотрудников сотрудников с номерами лицевых счетов 
// по переданной организации и списку сотрудников.
//
// Параметры:
//		МенеджерВременныхТаблиц
//		ТолькоРазрешенные - Булево
//		СписокСотрудников - список сотрудников, по которым нужно получить лицевые счета.
//		Организация - организация, по которой нужно получить лицевые счета.
//		ДатаПолученияДанных - дата на которую необходимо получить данные о номерах лицевых счетов,
//						если дату не указывать, данные будут получены на текущую дату.
//		ЗарплатныйПроект - зарплатный проект, если не задан, то по всем зарплатным проектам.
//
// Создает в МенеджерВременныхТаблиц временную таблицу с именем ВТЛицевыеСчетаСотрудников.
// Возвращаемое значение:
//		Таблица значений с колонками, описанными в процедуре СоздатьПоВременнойТаблицеВТЛицевыеСчетаСотрудников.
//
Процедура СоздатьВТЛицевыеСчетаСотрудников(МенеджерВременныхТаблиц, ТолькоРазрешенные, СписокСотрудников, Организация, ДатаПолученияДанных = Неопределено, ЗарплатныйПроект = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("СписокСотрудников", СписокСотрудников);
	Запрос.УстановитьПараметр("ДатаПолученияДанных", ?(ДатаПолученияДанных = Неопределено, ТекущаяДатаСеанса(), ДатаПолученияДанных));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&ДатаПолученияДанных КАК Период,
	|	Сотрудники.Ссылка КАК Сотрудник,
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТСписокСотрудниковДляПолученияЛицевыхСчетовСотрудников
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Ссылка В(&СписокСотрудников)";
	
	Если Не ТолькоРазрешенные Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВЫБРАТЬ РАЗРЕШЕННЫЕ", "ВЫБРАТЬ");
	КонецЕсли;
	
	Запрос.Выполнить();
	
	ОписаниеВременнойТаблицы = ОписаниеВременнойТаблицыДляСоздатьВТЛицевыеСчетаСотрудников(МенеджерВременныхТаблиц, "ВТСписокСотрудниковДляПолученияЛицевыхСчетовСотрудников");
	СоздатьПоВременнойТаблицеВТЛицевыеСчетаСотрудников(ОписаниеВременнойТаблицы, ТолькоРазрешенные, Организация, ЗарплатныйПроект);
	
	Запрос.Текст = "УНИЧТОЖИТЬ ВТСписокСотрудниковДляПолученияЛицевыхСчетовСотрудников";
	Запрос.Выполнить();
	
КонецПроцедуры

// Возвращает таблицу значений с сотрудниками и их лицевыми счетами.
//
// Параметры:
//		СписокСотрудников - список сотрудников, по которым нужно получить лицевые счета.
//		ТолькоРазрешенные - Булево
//		Организация - организация, по которой нужно получить лицевые счета.
//		ДатаПолученияДанных - дата на которую необходимо получить данные о номерах лицевых счетов,
//						если дату не указывать, данные будут получены на текущую дату.
//		ЗарплатныйПроект - зарплатный проект, если не задан, то по всем зарплатным проектам.
//
// Возвращаемое значение:
//		Таблица значений с колонками, описанными в процедуре СоздатьПоВременнойТаблицеВТЛицевыеСчетаСотрудников.
//
Функция ЛицевыеСчетаСотрудников(СписокСотрудников, ТолькоРазрешенные, Организация, ДатаПолученияДанных = Неопределено, ЗарплатныйПроект = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	СоздатьВТЛицевыеСчетаСотрудников(Запрос.МенеджерВременныхТаблиц, ТолькоРазрешенные, СписокСотрудников, Организация, ДатаПолученияДанных, ЗарплатныйПроект);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	*
	|ИЗ
	|	ВТЛицевыеСчетаСотрудников КАК ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Возвращает таблицу значений с физическими лицами и их лицевыми счетами в указанной организации.
//
// Параметры:
//		СписокФизическихЛиц - список физических лиц, по которым нужно получить лицевые счета.
//		ТолькоРазрешенные - Булево
//		Организация - организация, по которой нужно получить лицевые счета.
//		ДатаПолученияДанных - дата на которую необходимо получить данные о номерах лицевых счетов,
//						если дату не указывать, данные будут получены на текущую дату.
//		ЗарплатныйПроект - зарплатный проект, если не задан, то по всем зарплатным проектам.
//
// Возвращаемое значение:
//		Таблица значений с колонками, описанными в процедуре СоздатьПоВременнойТаблицеВТЛицевыеСчетаСотрудников.
//
Функция ЛицевыеСчетаФизическихЛиц(СписокФизическихЛиц, ТолькоРазрешенные, Организация, ДатаПолученияДанных, ЗарплатныйПроект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ФизическиеЛица", СписокФизическихЛиц);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаПолученияДанных", ДатаПолученияДанных);
	Запрос.УстановитьПараметр("ЗарплатныйПроект", ЗарплатныйПроект);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЛицевыеСчета.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЛицевыеСчета.НомерЛицевогоСчета КАК НомерЛицевогоСчета
	|ИЗ
	|	РегистрСведений.ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.СрезПоследних(
	|			&ДатаПолученияДанных,
	|			Организация = &Организация
	|				И ЗарплатныйПроект = &ЗарплатныйПроект
	|				И ФизическоеЛицо В (&ФизическиеЛица)) КАК ЛицевыеСчета";
	
	Если Не ТолькоРазрешенные Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВЫБРАТЬ РАЗРЕШЕННЫЕ", "ВЫБРАТЬ");
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Возвращает структуру пояснения для номера лицевого счета.
//
// Параметры:
//		ФизическоеЛицо
//		ЗарплатныйПроект
//		НомерЛицевогоСчета - Строка, проверяемый номер лицевого счета.
//		ЛицевыеСчета - Структура лицевого счета.
//		ЛицевыеСчетаПрежняя - Структура лицевого счета до исправления.
//
// Возвращаемое значение:
//		Структура:
//			СообщенияПроверки - Строка, текст сообщения проверки номера лицевого счета, показывается при нажатии на
//			                    гиперссылку надписи.
//			Картинка - картинка, может принимать значения "Предупреждение" и "ОперацияВыполненаУспешно".
//			ТекстНадписи - Строка, текст, который будет показан пользователю на форме.
//			ЭлементЦветТекста - Цвет текста надписи.
//			ВведенДокументом - Булево, Истина, если лицевой счет введен документом подтверждения, иначе Ложь.
//
Функция СтруктураПоясненияКНомеруЛицевогоСчета(ФизическоеЛицо, ЗарплатныйПроект, НомерЛицевогоСчета, ЛицевыеСчета, ЛицевыеСчетаПрежняя) Экспорт
	
	ИспользоватьЭОИСБанком = ИспользоватьЭОИСБанком(ЗарплатныйПроект);
	
	РезультатПроверки = Новый Структура;
	РезультатПроверки.Вставить(
		"СообщениеПроверки",
		?(ИспользоватьЭОИСБанком,
			СообщениеПроверкиНомераЛицевогоСчетаНаСоответствиеТребованиям(ФизическоеЛицо, ЗарплатныйПроект, НомерЛицевогоСчета),
			""));
	
	СтруктураЛицевыеСчета = Новый Структура;
	Для Каждого ЭлементСтруктуры Из ЛицевыеСчетаПрежняя Цикл
		СтруктураЛицевыеСчета.Вставить(ЭлементСтруктуры.Ключ, ЛицевыеСчета[ЭлементСтруктуры.Ключ]);
	КонецЦикла;
	СтруктураЛицевыеСчета = Новый ФиксированнаяСтруктура(СтруктураЛицевыеСчета);
	
	РезультатПроверки.Вставить(
		"ЗначениеЛСИзменено",
		Не ОбщегоНазначения.КоллекцииИдентичны(
			ЛицевыеСчетаПрежняя,
			СтруктураЛицевыеСчета));
	
	ЛСУказанПравильно = ?(ПустаяСтрока(РезультатПроверки.СообщениеПроверки), Истина, Ложь);
	
	СообщенияПроверки = ?(ПустаяСтрока(РезультатПроверки.СообщениеПроверки) И ИспользоватьЭОИСБанком,
			НСтр("ru = 'Номер лицевого счета указан правильно'"), РезультатПроверки.СообщениеПроверки);
	
	ВведенДокументом = Ложь;
	ТекстНадписи = "";
	Если Не ИспользоватьЭОИСБанком Тогда
		Картинка = Новый Картинка;
		ЭлементЦветТекста = ЦветаСтиля.ЦветТекстаПоля;
	Иначе
		Если ЛСУказанПравильно Или (Не РезультатПроверки.ЗначениеЛСИзменено И ЗначениеЗаполнено(ЛицевыеСчета.ДокументОснование)) Тогда
			Картинка = БиблиотекаКартинок.ОперацияВыполненаУспешно;
			Если Не РезультатПроверки.ЗначениеЛСИзменено И ЗначениеЗаполнено(ЛицевыеСчета.ДокументОснование) Тогда
				ПредставлениеДокумента = Новый ФорматированнаяСтрока(
					Строка(ЛицевыеСчета.ДокументОснование), , , , ПолучитьНавигационнуюСсылку(ЛицевыеСчета.ДокументОснование));
				ТекстНадписи = Новый ФорматированнаяСтрока(НСтр("ru='Номер лицевого счета введен документом (см.'") + " ", ПредставлениеДокумента, ")");
				ВведенДокументом = Истина;
			КонецЕсли;
			ЭлементЦветТекста = ЦветаСтиля.ЦветТекстаПоля;
		Иначе
			Картинка = БиблиотекаКартинок.Предупреждение;
			ТекстНадписи = НСтр("ru = 'Предупреждение'");
			ЭлементЦветТекста = ЦветаСтиля.РезультатПредупреждениеЦвет;
		КонецЕсли;
	КонецЕсли;
	
	СтруктураПояснения = Новый Структура;
	СтруктураПояснения.Вставить("СообщенияПроверки", СообщенияПроверки);
	СтруктураПояснения.Вставить("Картинка", Картинка);
	СтруктураПояснения.Вставить("ТекстНадписи", ТекстНадписи);
	СтруктураПояснения.Вставить("ЭлементЦветТекста", ЭлементЦветТекста);
	СтруктураПояснения.Вставить("ВведенДокументом", ВведенДокументом);
	
	Возврат СтруктураПояснения;
	
КонецФункции

// Возвращает номера реестров для документов.
//
// Параметры:
// 	СоответствиеДокументов - Соответствие
// 	ГодыВыгрузки - Массив
// 	РеквизитыПлатежногоДокумента - Структура
//		* ПлатежныйДокумент - ДокументСсылка
//		* Номер             - Число
//		* Дата              - Дата
//		* Организация       - СправочникСсылка.Организации
//
// Возвращаемое значение:
// 	Соответствие - номера реестров документов.
//
Функция НомераРеестровДокументов(СоответствиеДокументов, ГодыВыгрузки, РеквизитыПлатежногоДокумента) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ГодыВыгрузки", ГодыВыгрузки);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СостоянияДокументовЗачисленияЗарплаты.ДокументЗачисленияЗарплаты,
	|	СостоянияДокументовЗачисленияЗарплаты.Год,
	|	СостоянияДокументовЗачисленияЗарплаты.НомерРеестра
	|ПОМЕСТИТЬ ВТВыгруженныеДокументы
	|ИЗ
	|	РегистрСведений.СостоянияДокументовЗачисленияЗарплаты КАК СостоянияДокументовЗачисленияЗарплаты
	|ГДЕ
	|	СостоянияДокументовЗачисленияЗарплаты.Год В(&ГодыВыгрузки)";
	Запрос.Выполнить();
	
	ШаблонТекстаЗапроса = 
	"ВЫБРАТЬ
	|	Ведомость.Организация КАК Организация,
	|	ВыгруженныеДокументы.ДокументЗачисленияЗарплаты КАК ДокументЗачисленияЗарплаты,
	|	ВыгруженныеДокументы.Год КАК Год,
	|	ВыгруженныеДокументы.НомерРеестра КАК НомерРеестра
	|ИЗ
	|	ВТВыгруженныеДокументы КАК ВыгруженныеДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ИмяТаблицы КАК Ведомость
	|		ПО ВыгруженныеДокументы.ДокументЗачисленияЗарплаты = Ведомость.Ссылка";
	
	ТекстыЗапросов = Новый Массив;
	Для Каждого ТипВедомости Из Метаданные.ОпределяемыеТипы.ВедомостьВБанкЗарплатаКадры.Тип.Типы() Цикл
		ТекстыЗапросов.Добавить(
			СтрЗаменить(
				ШаблонТекстаЗапроса, 
				"#ИмяТаблицы", 
				Метаданные.НайтиПоТипу(ТипВедомости).ПолноеИмя()));
	КонецЦикла;
	
	СхемаЗапроса = Новый СхемаЗапроса();
	СхемаЗапроса.УстановитьТекстЗапроса(
		СтрСоединить(
			ТекстыЗапросов, 
			"
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|"));
	СхемаЗапроса.ПакетЗапросов[0].ТаблицаДляПомещения = "ВТДанныеВедомостей";
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеквизитыПлатежныхДокументовПеречисленияЗарплаты.Организация,
	|	ВыгруженныеДокументы.ДокументЗачисленияЗарплаты,
	|	ВыгруженныеДокументы.Год,
	|	ВыгруженныеДокументы.НомерРеестра
	|ПОМЕСТИТЬ ВТДокументыПоОрганизациям
	|ИЗ
	|	ВТВыгруженныеДокументы КАК ВыгруженныеДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РеквизитыПлатежныхДокументовПеречисленияЗарплаты КАК РеквизитыПлатежныхДокументовПеречисленияЗарплаты
	|		ПО ВыгруженныеДокументы.ДокументЗачисленияЗарплаты = РеквизитыПлатежныхДокументовПеречисленияЗарплаты.ПлатежныйДокумент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВыгруженныеДокументы.Организация,
	|	ВыгруженныеДокументы.ДокументЗачисленияЗарплаты,
	|	ВыгруженныеДокументы.Год,
	|	ВыгруженныеДокументы.НомерРеестра
	|ИЗ
	|	ВТДанныеВедомостей КАК ВыгруженныеДокументы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыПоОрганизациям.Организация,
	|	ДокументыПоОрганизациям.Год,
	|	МАКСИМУМ(ДокументыПоОрганизациям.НомерРеестра) КАК НомерРеестра
	|ИЗ
	|	ВТДокументыПоОрганизациям КАК ДокументыПоОрганизациям
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыПоОрганизациям.Организация,
	|	ДокументыПоОрганизациям.Год";
	
	НомераРеестровОрганизаций = Запрос.Выполнить().Выгрузить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	НомераРеестров = Новый Соответствие;
	
	Если РеквизитыПлатежногоДокумента = Неопределено Тогда
		Для Каждого СтрокаСоответствия Из СоответствиеДокументов Цикл 
			ДанныеДокумента = СтрокаСоответствия.Значение;
			Год = Год(ДанныеДокумента.Дата);
			ДанныеРеестра = Новый Структура("Год, НомерРеестра", Год, Неопределено);
			
			Если Не ЗначениеЗаполнено(ДанныеДокумента.НомерРеестра) Тогда
				ДанныеДокумента.Вставить("Год", Год);
				СтруктураПоиска = Новый Структура("Организация, Год", ДанныеДокумента.Организация, Год);
				НайденныеСтроки = НомераРеестровОрганизаций.НайтиСтроки(СтруктураПоиска);
				Если НайденныеСтроки.Количество() = 0 Тогда
					ДанныеДокумента.Вставить("НомерРеестра", СтрокаВЧисло(ДанныеДокумента.Номер));
					Если ДанныеДокумента.НомерРеестра = 0 Тогда
						ДанныеДокумента.НомерРеестра = 1;
					КонецЕсли;
					ЗаполнитьЗначенияСвойств(НомераРеестровОрганизаций.Добавить(), ДанныеДокумента);
				Иначе 
					ДанныеДокумента.Вставить("НомерРеестра", НайденныеСтроки[0].НомерРеестра + 1);
					НайденныеСтроки[0].НомерРеестра = ДанныеДокумента.НомерРеестра;
				КонецЕсли;
			КонецЕсли;
			ДанныеРеестра.НомерРеестра = ДанныеДокумента.НомерРеестра;
			
			НомераРеестров.Вставить(СтрокаСоответствия.Ключ, ДанныеРеестра);
		КонецЦикла;
	Иначе
		Год = Год(РеквизитыПлатежногоДокумента.Дата);
		ДанныеРеестра = Новый Структура("Год, НомерРеестра", Год, Неопределено);
		
		ДанныеРеестра.НомерРеестра = НомерРеестраПлатежногоДокумента(РеквизитыПлатежногоДокумента.ПлатежныйДокумент);
		Если Не ЗначениеЗаполнено(ДанныеРеестра.НомерРеестра) Тогда
			РеквизитыПлатежногоДокумента.Вставить("Год", Год);
			СтруктураПоиска = Новый Структура("Организация, Год", РеквизитыПлатежногоДокумента.Организация, Год);
			НайденныеСтроки = НомераРеестровОрганизаций.НайтиСтроки(СтруктураПоиска);
			Если НайденныеСтроки.Количество() = 0 Тогда
				РеквизитыПлатежногоДокумента.Вставить("НомерРеестра", СтрокаВЧисло(РеквизитыПлатежногоДокумента.Номер));
				Если РеквизитыПлатежногоДокумента.НомерРеестра = 0 Тогда
					РеквизитыПлатежногоДокумента.НомерРеестра = 1;
				КонецЕсли;
				ЗаполнитьЗначенияСвойств(НомераРеестровОрганизаций.Добавить(), РеквизитыПлатежногоДокумента);
			Иначе
				РеквизитыПлатежногоДокумента.Вставить("НомерРеестра", НайденныеСтроки[0].НомерРеестра + 1);
				НайденныеСтроки[0].НомерРеестра = РеквизитыПлатежногоДокумента.НомерРеестра;
			КонецЕсли;
			ДанныеРеестра.НомерРеестра = РеквизитыПлатежногоДокумента.НомерРеестра;
		КонецЕсли;
		
		НомераРеестров.Вставить(РеквизитыПлатежногоДокумента.ПлатежныйДокумент, ДанныеРеестра);
	КонецЕсли;
	
	Возврат НомераРеестров;
	
КонецФункции

// Формирует и прикрепляет файл обмена к документам с помощью подсистемы "Файлы".
//
// Параметры:
//		СтруктураПараметровДляФормированияФайла - Структура - параметры формирования файла:
//			* МассивДокументов - Массив - ссылки на документы, по которым требуется сформировать файл.
//			* МассивОписанийФайлов - Массив - описания сформированных файлов.
//
Процедура ВыгрузитьФайлыДляОбменаСБанкомПоВедомости(СтруктураПараметровДляФормированияФайла) Экспорт
	
	Если Не СтруктураПараметровДляФормированияФайла.Свойство("СтруктураПараметровЭД") Тогда
		СтруктураПараметровДляФормированияФайла.Вставить("СтруктураПараметровЭД", Неопределено);
	КонецЕсли;
	Если Не СтруктураПараметровДляФормированияФайла.Свойство("ПлатежныйДокумент") Тогда
		СтруктураПараметровДляФормированияФайла.Вставить("ПлатежныйДокумент", Неопределено);
	КонецЕсли;
	
	Отказ = Ложь;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивДокументов", СтруктураПараметровДляФормированияФайла.МассивДокументов);
	Запрос.УстановитьПараметр("ПлатежныйДокумент", СтруктураПараметровДляФормированияФайла.ПлатежныйДокумент);
	
	ШаблонТекстаЗапроса = 
	"ВЫБРАТЬ
	|	ВедомостьВБанк.Ссылка КАК Ведомость,
	|	ВедомостьВБанк.ЗарплатныйПроект КАК ЗарплатныйПроект,
	|	ВедомостьВБанк.Проведен КАК Проведен,
	|	ПлатежныеДокументыПеречисленияЗарплаты.ПлатежныйДокумент КАК ПлатежныйДокумент
	|ИЗ
	|	#ИмяТаблицы КАК ВедомостьВБанк
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлатежныеДокументыПеречисленияЗарплаты КАК ПлатежныеДокументыПеречисленияЗарплаты
	|		ПО ВедомостьВБанк.Ссылка = ПлатежныеДокументыПеречисленияЗарплаты.Ведомость
	|			И (ПлатежныеДокументыПеречисленияЗарплаты.ПлатежныйДокумент <> &ПлатежныйДокумент)
	|ГДЕ
	|	ВедомостьВБанк.Ссылка В(&МассивДокументов)";
	
	ДокументСсылка = СтруктураПараметровДляФормированияФайла.МассивДокументов[0];
	ИмяТаблицы = "Документ."+ДокументСсылка.Метаданные().Имя;
	ТекстЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "#ИмяТаблицы", ИмяТаблицы);
	Запрос.Текст = ТекстЗапроса;
	
	ЗарплатныйПроект = Неопределено;
	ВДокументахОдинЗарплатныйПроект = Истина;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.ПлатежныйДокумент) Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 уже включена в %2'"),
				Выборка.Ведомость, Выборка.ПлатежныйДокумент);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Выборка.ПлатежныйДокумент, , , Отказ);
		КонецЕсли;
		Если Не Выборка.Проведен Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Документ %1 не проведен.'"), Выборка.Ведомость);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Выборка.Ведомость,,, Отказ);
		КонецЕсли;
		Если ЗарплатныйПроект = Неопределено Тогда
			ЗарплатныйПроект = Выборка.ЗарплатныйПроект;
		ИначеЕсли ЗарплатныйПроект <> Выборка.ЗарплатныйПроект Тогда
			ВДокументахОдинЗарплатныйПроект = Ложь;
		КонецЕсли;
		Если НЕ ОбменСБанкамиПоЗарплатнымПроектам.ИспользоватьЭОИСБанком(Выборка.ЗарплатныйПроект) Тогда
			Если ЗначениеЗаполнено(Выборка.ЗарплатныйПроект) Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'По зарплатному проекту %1 не используется обмен электронными документами с банком.'"),
					Выборка.ЗарплатныйПроект);
			Иначе
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'В документе %1 не выбран зарплатный проект.'"), Выборка.Ведомость);
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения, Выборка.ЗарплатныйПроект, "ИспользоватьЭлектронныйДокументооборотСБанком",,Отказ);
		КонецЕсли;
	КонецЦикла;
	Если СтруктураПараметровДляФормированияФайла.ПлатежныйДокумент <> Неопределено Тогда
		Если НЕ СтруктураПараметровДляФормированияФайла.ПлатежныйДокумент.Проведен Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Документ %1 не проведен.'"), СтруктураПараметровДляФормированияФайла.ПлатежныйДокумент);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, СтруктураПараметровДляФормированияФайла.ПлатежныйДокумент,,, Отказ);
		КонецЕсли;
		
		Если Не ВДокументахОдинЗарплатныйПроект Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'В документе %1 присутствуют ведомости с разными зарплатными проектами. Файл по документу не сформирован.'"),
					СтруктураПараметровДляФормированияФайла.ПлатежныйДокумент);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Выборка.Ведомость,,, Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ВидОперации = "ВедомостьНаВыплатуЗарплатыВБанк";
	ДатаПолученияДанных = ТекущаяДатаСеанса();
	
	ДокументМенеджер = ОбщегоНазначения.МенеджерОбъектаПоСсылке(ДокументСсылка);
	ДанныеДокументов = ДокументМенеджер.ДанныеВедомостиНаВыплатуЗарплатыВБанк(СтруктураПараметровДляФормированияФайла.МассивДокументов, ДатаПолученияДанных, СтруктураПараметровДляФормированияФайла.ПлатежныйДокумент);
	
	СтруктураПараметровДляФормированияФайла.Вставить("ВидОперации", ВидОперации);
	СтруктураПараметровДляФормированияФайла.Вставить("ДанныеДокументов", ДанныеДокументов);
	СтруктураПараметровДляФормированияФайла.Вставить("КорневыеСвойства", КорневыеСвойства(ВидОперации));
	СтруктураПараметровДляФормированияФайла.Вставить("ОбязательныеПоляФайла", ОбязательныеПоляФайла(ВидОперации));
	СтруктураПараметровДляФормированияФайла.Вставить("СоответствиеПреобразованияЗначений", СоответствиеПреобразованияЗначений(ВидОперации));
	СтруктураПараметровДляФормированияФайла.Вставить("СоответствиеТипов", СоответствиеТипов(ВидОперации));
	
	СоздатьФайлыДляОбменаСБанком(СтруктураПараметровДляФормированияФайла);
	
КонецПроцедуры

// Метод предназначен для установки константы использования электронного обмена с банками по зарплатным проектам.
//
Процедура УстановитьИспользованиеЭлектронногоОбменаСБанками(ИспользоватьНачислениеЗарплаты = Неопределено) Экспорт
	
	Если ИспользоватьНачислениеЗарплаты = Неопределено Тогда
		ИспользоватьНачислениеЗарплаты = Константы.ИспользоватьНачислениеЗарплаты.Получить();
	КонецЕсли;

	// Включаем использование в том случае, 
	// если используется расчет зарплаты и есть хотя бы зарплатный проект, для которого используется ЭОИ.
	
	УстановитьПривилегированныйРежим(Истина);
	ИспользованиеЭлектронногоОбменаСБанками = Ложь;
	Если ИспользоватьНачислениеЗарплаты Тогда
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК Поле1
		|ИЗ
		|	Справочник.ЗарплатныеПроекты КАК ЗарплатныеПроекты
		|ГДЕ
		|	ЗарплатныеПроекты.ИспользоватьЭлектронныйДокументооборотСБанком
		|	И НЕ ЗарплатныеПроекты.ПометкаУдаления");
		ИспользованиеЭлектронногоОбменаСБанками = Не Запрос.Выполнить().Пустой();
	КонецЕсли;
	
	ТекущееИспользование = Константы.ИспользоватьЭлектронныйОбменСБанкамиПоЗарплатнымПроектам.Получить();
	Если ТекущееИспользование <> ИспользованиеЭлектронногоОбменаСБанками Тогда
		Константы.ИспользоватьЭлектронныйОбменСБанкамиПоЗарплатнымПроектам.Установить(ИспользованиеЭлектронногоОбменаСБанками);
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#Область ПроцедурыИФункцииРаботыСФормами

// Создает элементы формы для группы Подтверждение из банка
//		Открыть
//		Загрузить
//
Процедура ГруппаПодтверждениеИзБанкаДополнитьФорму(Форма) Экспорт
	
	МетаданныеДокумента = Форма.Объект.Ссылка.Метаданные();
	
	ИспользоватьЭОИ = Ложь;
	ЕстьРеквизитЗарплатныйПроект = ОбщегоНазначения.ЕстьРеквизитОбъекта("ЗарплатныйПроект", МетаданныеДокумента);
	Если ЕстьРеквизитЗарплатныйПроект Тогда
		ИспользоватьЭОИ = ИспользоватьЭОИСБанком(Форма.Объект.ЗарплатныйПроект);
	Иначе
		МетаданныеПлатежногоДокумента = МетаданныеПлатежногоДокументаПеречисленияЗарплаты();
		Если МетаданныеПлатежногоДокумента <> Неопределено И МетаданныеПлатежногоДокумента.ПолноеИмя() = МетаданныеДокумента.ПолноеИмя() Тогда
			ВедомостиПлатежногоДокумента = ВедомостиПлатежногоДокументаПеречисленияЗарплаты(Форма.Объект.Ссылка);
			Если ВедомостиПлатежногоДокумента.Количество() > 0 Тогда
				ЗарплатныйПроектПлатежногоДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВедомостиПлатежногоДокумента[0], "ЗарплатныйПроект");
				ИспользоватьЭОИ = ИспользоватьЭОИСБанком(ЗарплатныйПроектПлатежногоДокумента);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьЭлектронныйОбменСБанкамиПоЗарплатнымПроектам")
		Или НЕ ИспользоватьЭОИ Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", Форма.Объект.Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПодтверждениеЗачисленияЗарплаты.Ссылка КАК ДокументПодтверждение,
	|	ПодтверждениеЗачисленияЗарплаты.МоментВремени КАК МоментВремени
	|ИЗ
	|	Документ.ПодтверждениеЗачисленияЗарплаты КАК ПодтверждениеЗачисленияЗарплаты
	|ГДЕ
	|	ПодтверждениеЗачисленияЗарплаты.ПервичныйДокумент = &Ссылка
	|	И ПодтверждениеЗачисленияЗарплаты.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПодтверждениеОткрытияЛицевыхСчетовСотрудников.Ссылка,
	|	ПодтверждениеОткрытияЛицевыхСчетовСотрудников.МоментВремени
	|ИЗ
	|	Документ.ПодтверждениеОткрытияЛицевыхСчетовСотрудников КАК ПодтверждениеОткрытияЛицевыхСчетовСотрудников
	|ГДЕ
	|	ПодтверждениеОткрытияЛицевыхСчетовСотрудников.ПервичныйДокумент = &Ссылка
	|	И ПодтверждениеОткрытияЛицевыхСчетовСотрудников.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПодтверждениеЗачисленияЗарплаты.Ссылка,
	|	ПодтверждениеЗачисленияЗарплаты.МоментВремени
	|ИЗ
	|	РегистрСведений.ПлатежныеДокументыПеречисленияЗарплаты КАК ПлатежныеДокументыПеречисленияЗарплаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПодтверждениеЗачисленияЗарплаты КАК ПодтверждениеЗачисленияЗарплаты
	|		ПО ПлатежныеДокументыПеречисленияЗарплаты.ПлатежныйДокумент = ПодтверждениеЗачисленияЗарплаты.ПервичныйДокумент
	|			И (ПодтверждениеЗачисленияЗарплаты.Проведен)
	|ГДЕ
	|	ПлатежныеДокументыПеречисленияЗарплаты.Ведомость = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	МоментВремени УБЫВ";
	
	ТипДокументаПодтвержденияИзБанка = Неопределено;
	СписокДокументовПодтверждений = Новый СписокЗначений;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокДокументовПодтверждений.Добавить(Выборка.ДокументПодтверждение);
		ТипДокументаПодтвержденияИзБанка = Выборка.ДокументПодтверждение.Метаданные().ПолноеИмя();
	КонецЦикла;
	
	Элементы = Форма.Элементы;
	ГруппаПодтверждениеИзБанка = Элементы.ГруппаПодтверждениеИзБанка;
	
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ПодтверждениеИнфоНадпись", Новый ОписаниеТипов("Строка")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ДокументПодтверждениеИзБанка", Новый ОписаниеТипов("СписокЗначений")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ТипДокументаПодтвержденияИзБанка", ОбщегоНазначения.ОписаниеТипаСтрока(150)));
	
	МассивИменРеквизитовФормы = Новый Массив;
	ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(Форма, МассивИменРеквизитовФормы);
	
	ЗарплатаКадры.ИзменитьРеквизитыФормы(Форма, ДобавляемыеРеквизиты, МассивИменРеквизитовФормы);
	
	Форма.ТипДокументаПодтвержденияИзБанка = ТипДокументаПодтвержденияИзБанка;
	
	// Надпись
	Если Элементы.Найти("ПодтверждениеИнфоНадпись") = Неопределено Тогда
		Элемент = Элементы.Добавить("ПодтверждениеИнфоНадпись", Тип("ПолеФормы"), ГруппаПодтверждениеИзБанка);
		Элемент.ПутьКДанным = "ПодтверждениеИнфоНадпись";
		Элемент.Вид = ВидПоляФормы.ПолеНадписи;
		Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элемент.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
		Элемент.Высота = 1;
		Элемент.РастягиватьПоВертикали = Ложь;
		Элемент.АвтоМаксимальнаяШирина = Ложь;
	КонецЕсли;
	
	Если СписокДокументовПодтверждений.Количество() > 0 Тогда
		
		// Команда Открыть
		ИмяКоманды = "ОткрытьДокументПодтверждение";
		Если Форма.Команды.Найти(ИмяКоманды) = Неопределено Тогда
			КомандаФормы = Форма.Команды.Добавить(ИмяКоманды);
			КомандаФормы.Заголовок = НСтр("ru = 'Подтверждения из банка'");
			КомандаФормы.Действие = "Подключаемый_" + ИмяКоманды;
		КонецЕсли;
		Если Элементы.Найти(ИмяКоманды) = Неопределено Тогда
			Элемент = Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ГруппаПодтверждениеИзБанка);
			Элемент.Вид = ВидКнопкиФормы.Гиперссылка;
			Элемент.ИмяКоманды = ИмяКоманды;
		КонецЕсли;
		Форма.ПодтверждениеИнфоНадпись = НСтр("ru = 'По документу получены подтверждения из банка, редактирование невозможно'");
		Форма.ДокументПодтверждениеИзБанка = СписокДокументовПодтверждений;
		
		ЭлементЗагрузитьДокументПодтверждение = Элементы.Найти("ЗагрузитьДокументПодтверждение");
		Если ЭлементЗагрузитьДокументПодтверждение <> Неопределено Тогда
			Элементы.Удалить(ЭлементЗагрузитьДокументПодтверждение);
		КонецЕсли;
	Иначе
		// Команда Загрузить
		ИмяКоманды = "ЗагрузитьДокументПодтверждение";
		Если Форма.Команды.Найти(ИмяКоманды) = Неопределено Тогда
			КомандаФормы = Форма.Команды.Добавить(ИмяКоманды);
			КомандаФормы.Заголовок = НСтр("ru = 'Загрузить подтверждение из банка'");
			КомандаФормы.Действие = "Подключаемый_" + ИмяКоманды;
		КонецЕсли;
		Если Элементы.Найти(ИмяКоманды) = Неопределено Тогда
			Элемент = Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ГруппаПодтверждениеИзБанка);
			Элемент.Вид = ВидКнопкиФормы.Гиперссылка;
			Элемент.ИмяКоманды = ИмяКоманды;
		КонецЕсли;
		Форма.ПодтверждениеИнфоНадпись = НСтр("ru = 'По документу не получены подтверждения из банка'");
		Форма.ДокументПодтверждениеИзБанка = Неопределено;
		
		ЭлементОткрытьДокументПодтверждение = Элементы.Найти("ОткрытьДокументПодтверждение");
		Если ЭлементОткрытьДокументПодтверждение <> Неопределено Тогда
			Элементы.Удалить(ЭлементОткрытьДокументПодтверждение);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура КомандыОбменаДополнитьФорму(Форма) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗарплатныеПроекты.Ссылка КАК ЗарплатныйПроект
	|ИЗ
	|	Справочник.ЗарплатныеПроекты КАК ЗарплатныеПроекты";
	
	КомандыОбмена = Новый Структура;
	КомандыОбмена.Вставить("ВыгрузитьФайлДляОбменаСБанком", Новый Массив);
	КомандыОбмена.Вставить("ОтправитьВБанк", Новый Массив);
	КомандыОбмена.Вставить("ЗагрузитьПодтвержденияБанка", Новый Массив);
	КомандыОбмена.Вставить("ВыполнитьСинхронизациюСБанком", Новый Массив);
	КомандыОбмена.Вставить("ПоказатьОтправленныйДокумент", Новый Массив);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ОбменСБанкамиПоЗарплатнымПроектам.ИспользоватьПрямойЭОИСБанком(Выборка.ЗарплатныйПроект) Тогда
			Если КомандыОбмена.ОтправитьВБанк.Найти(Выборка.ЗарплатныйПроект) = Неопределено Тогда
				КомандыОбмена.ОтправитьВБанк.Добавить(Выборка.ЗарплатныйПроект);
			КонецЕсли;
			Если КомандыОбмена.ВыполнитьСинхронизациюСБанком.Найти(Выборка.ЗарплатныйПроект) = Неопределено Тогда
				КомандыОбмена.ВыполнитьСинхронизациюСБанком.Добавить(Выборка.ЗарплатныйПроект);
			КонецЕсли;
			Если КомандыОбмена.ПоказатьОтправленныйДокумент.Найти(Выборка.ЗарплатныйПроект) = Неопределено Тогда
				КомандыОбмена.ПоказатьОтправленныйДокумент.Добавить(Выборка.ЗарплатныйПроект);
			КонецЕсли;
		Иначе
			Если КомандыОбмена.ВыгрузитьФайлДляОбменаСБанком.Найти(Выборка.ЗарплатныйПроект) = Неопределено Тогда
				КомандыОбмена.ВыгрузитьФайлДляОбменаСБанком.Добавить(Выборка.ЗарплатныйПроект);
			КонецЕсли;
			Если КомандыОбмена.ЗагрузитьПодтвержденияБанка.Найти(Выборка.ЗарплатныйПроект) = Неопределено Тогда
				КомандыОбмена.ЗагрузитьПодтвержденияБанка.Добавить(Выборка.ЗарплатныйПроект);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("КомандыОбмена", Новый ОписаниеТипов(Неопределено)));
	
	МассивИменРеквизитовФормы = Новый Массив;
	ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(Форма, МассивИменРеквизитовФормы);
	ЗарплатаКадры.ИзменитьРеквизитыФормы(Форма, ДобавляемыеРеквизиты, МассивИменРеквизитовФормы);
	
	Форма.КомандыОбмена = КомандыОбмена;
	
КонецПроцедуры

#КонецОбласти

#Область ПрямойОбменСБанками

// Получает значение использования прямого обмена электронной информацией с банком по зарплатному проекту.
//
// Параметры:
//		ЗарплатныйПроект - ссылка на зарплатный проект.
//
// Возвращаемое значение:
//		ИспользоватьПрямойЭОИСБанком - 
//				принимает значение Истина, если по зарплатному проекту
//				ведется прямой обмен электронными документами с банком, 
//				Ложь - если не ведется прямой обмен электронными документами с банком.
//
Функция ИспользоватьПрямойЭОИСБанком(ЗарплатныйПроект) Экспорт
	
	Если Не ЗначениеЗаполнено(ЗарплатныйПроект) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗарплатныйПроект, "Банк, ИспользоватьЭлектронныйДокументооборотСБанком");
	ИспользоватьПрямойЭОИСБанком = ЗначенияРеквизитов.ИспользоватьЭлектронныйДокументооборотСБанком
		И ОбменСБанками.ВозможенПрямойОбменСБанком(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗначенияРеквизитов.Банк, "Код"), 2)
		И ПолучитьФункциональнуюОпцию("ИспользоватьОбменСБанками");
	
	Возврат ИспользоватьПрямойЭОИСБанком;
	
КонецФункции

// Заполняет массив актуальными видами электронных документов
//
// Параметры:
//  Массив - виды актуальных ЭД.
//
Процедура ЗаполнитьАктуальныеВидыЭД(Массив) Экспорт
	
	Массив.Добавить(Перечисления.ВидыЭДОбменСБанками.СписокНаОткрытиеСчетовПоЗарплатномуПроекту);
	Массив.Добавить(Перечисления.ВидыЭДОбменСБанками.СписокУволенныхСотрудников);
	Массив.Добавить(Перечисления.ВидыЭДОбменСБанками.СписокНаЗачислениеДенежныхСредствНаСчетаСотрудников);
	Массив.Добавить(Перечисления.ВидыЭДОбменСБанками.ПодтверждениеОткрытияСчетовПоЗарплатномуПроекту);
	Массив.Добавить(Перечисления.ВидыЭДОбменСБанками.ПодтверждениеЗачисленияДенежныхСредствНаСчетаСотрудников);
	
КонецПроцедуры

// Определяет параметры электронного документа по типу владельца.
//
// Параметры:
//  Источник - объекта либо ссылка документа/справочника-источника.
//  ПараметрыЭД - структура параметров источника, необходимых для определения
//                настроек обмена ЭД. Обязательные параметры: ВидЭД, Банк, Организация.
//
Процедура ЗаполнитьПараметрыЭДПоИсточнику(Источник, ПараметрыЭД) Экспорт
	
	ТипИсточника = ТипЗнч(Источник);
	
	Если ТипИсточника = Тип("ДокументСсылка.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников")
		ИЛИ ТипИсточника = Тип("ДокументОбъект.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников") Тогда
		
		ПараметрыЭД.ВидЭД = Перечисления.ВидыЭДОбменСБанками.СписокНаОткрытиеСчетовПоЗарплатномуПроекту;
		ПараметрыЭД.Направление = Перечисления.НаправленияЭД.Исходящий;
		ПараметрыЭД.Организация = Источник.Организация;
		ПараметрыЭД.Банк = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.ЗарплатныйПроект, "Банк");
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплатыВБанк")
		ИЛИ ТипИсточника = Тип("ДокументОбъект.ВедомостьНаВыплатуЗарплатыВБанк") Тогда
		
		ПараметрыЭД.ВидЭД = Перечисления.ВидыЭДОбменСБанками.СписокНаЗачислениеДенежныхСредствНаСчетаСотрудников;
		ПараметрыЭД.Направление = Перечисления.НаправленияЭД.Исходящий;
		ПараметрыЭД.Организация = Источник.Организация;
		ПараметрыЭД.Банк = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.ЗарплатныйПроект, "Банк");
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудников")
		ИЛИ ТипИсточника = Тип("ДокументОбъект.ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудников") Тогда
		
		ПараметрыЭД.ВидЭД = Перечисления.ВидыЭДОбменСБанками.СписокУволенныхСотрудников;
		ПараметрыЭД.Направление = Перечисления.НаправленияЭД.Исходящий;
		ПараметрыЭД.Организация = Источник.Организация;
		ПараметрыЭД.Банк = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.ЗарплатныйПроект, "Банк");
		
	Иначе	
		
		ОбменСБанкамиПоЗарплатнымПроектамВнутренний.ЗаполнитьПараметрыЭДПоИсточнику(Источник, ПараметрыЭД);
		
	КонецЕсли;
	
КонецПроцедуры

// Формирует табличный документ на основании файла XML для визуального отображения электронного документа.
//
// Параметры:
//  ИмяФайла - Строка - полный путь к файлу XML
//  ТабличныйДокумент - ТабличныйДокумент - возвращаемое значение, визуальное отображение данных файла.
//
Процедура ЗаполнитьТабличныйДокументПоПрямомуОбменуСБанками(ИмяФайла, ТабличныйДокумент) Экспорт
	
	ТабличныйДокумент = Неопределено;
	
	Пакет = ФабрикаXDTO.Пакеты.Получить(Метаданные.ПакетыXDTO.ФорматОбменаСБанкамиПоЗарплатнымПроектам.ПространствоИмен);
	ТипОбъектаXDTO = Пакет.КорневыеСвойства[0].Тип;
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ИмяФайла);
	Попытка
		ОбъектXDTOВременный = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML);
		
		ОбъектXDTO = ФабрикаXDTO.Создать(ТипОбъектаXDTO);
		ЗаполнитьОбъектXDTO(ОбъектXDTO, ОбъектXDTOВременный);
		
	Исключение
		ТекстСообщенияОбОшибке = НСтр("ru = 'Неверный формат файла.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщенияОбОшибке);
		
		Возврат;
	КонецПопытки;
	ЧтениеXML.Закрыть();
	
	Если ОбъектXDTO.Получить("ЗачислениеЗарплаты") <> Неопределено Тогда
		// Печать списка перечислений
		ТабличныйДокумент = Документы.ВедомостьНаВыплатуЗарплатыВБанк.ПечатьСпискаПеречисленийПоXML(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОбъектXDTO), Новый СписокЗначений);
	ИначеЕсли ОбъектXDTO.Получить("ОткрытиеСчетов") <> Неопределено Тогда
		// Печать открытия л/с
		ТабличныйДокумент = Документы.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.ПечатьСпискаНаОткрытиеЛицевыхСчетовПоXML(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОбъектXDTO), Новый СписокЗначений);
	ИначеЕсли ОбъектXDTO.Получить("СписокУвольнений") <> Неопределено Тогда
		// Печать закрытия л/с
	ИначеЕсли ОбъектXDTO.Получить("РезультатЗачисленияЗарплаты") <> Неопределено Тогда
		// Печать подтверждения зачисления зарплаты
	ИначеЕсли ОбъектXDTO.Получить("РезультатОткрытияСчетов") <> Неопределено Тогда
		// Печать подтверждения открытия лицевых счетов
		ТабличныйДокумент = Документы.ПодтверждениеОткрытияЛицевыхСчетовСотрудников.ПечатьПодтвержденияСпискаНаОткрытиеЛицевыхСчетовПоXML(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОбъектXDTO), Новый СписокЗначений);
	КонецЕсли;
	
КонецПроцедуры

// Вызывается для формирования XML файла
//
// Параметры:
//    ОбъектДляВыгрузки = ДокументСсылка - ссылка на документ, на основании которого будет сформирован ЭД
//    ИмяФайла - Строка - имя сформированного файла
//    АдресФайла - АдресВременногоХранилища - содержит двоичные данные файла.
//
Процедура ПриФормированииXMLФайла(ОбъектДляВыгрузки, ИмяФайла, АдресФайла) Экспорт
	
	УникальныйИдентификаторXMLФайла = Новый УникальныйИдентификатор;
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификаторXMLФайла);
	СтруктураПараметров = Новый Структура("МассивДокументов, УникальныйИдентификаторФормы",
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОбъектДляВыгрузки), УникальныйИдентификаторXMLФайла);
	ВыгрузитьФайлыДляОбменаСБанком(СтруктураПараметров, АдресХранилища);
	МассивОписанийПередаваемыхФайлов = МассивОписанийПередаваемыхФайлов(АдресХранилища, УникальныйИдентификаторXMLФайла);
	
	Для Каждого Описание Из МассивОписанийПередаваемыхФайлов Цикл
		ИмяФайла = Описание.Имя;
		АдресФайла = Описание.Хранение;
		Прервать;
	КонецЦикла
	
КонецПроцедуры

// Вызывается при получении файла из банка
//
// Параметры:
// АдресДанныхФайла - Строка - адрес временного хранилища с двоичными данными файла
// ИмяФайла - Строка - формализованное имя файла данных
// ИдентификаторЭДВладельца - Строка - (возвращаемый параметр) идентификатор ЭД, на основании которого был получен ответ
//                                     из банка.
// ОбъектВладелец - ДокументСсылка - (возвращаемый параметр) ссылка на документ, который был создан на основании ЭД
// ДанныеОповещения - Структура - (возвращаемый параметр) данные для вызова метода Оповестить на клиенте
//                 * Ключ - Строка - имя события
//                 * Значение - Произвольный - параметр сообщения.
Процедура ПриПолученииXMLФайла(АдресДанныхФайла, ИмяФайла, ОбъектВладелец, ДанныеОповещения) Экспорт
	
	СозданныеДокументы = ЗагрузитьПодтвержденияИзБанка(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(АдресДанныхФайла));
	
	ПриЗагрузкеБылиОшибки = Ложь;
	ДанныеОповещения = Новый Структура();
	Для каждого СозданныйДокумент Из СозданныеДокументы Цикл
		
		Если СозданныйДокумент = Неопределено Тогда
			ПриЗагрузкеБылиОшибки = Истина;
			Продолжить;
		КонецЕсли;
		ДанныеОповещения.Вставить(ОбменСБанкамиПоЗарплатнымПроектамКлиентСервер.ПривестиСтрокуКИдентификатору(СозданныйДокумент.Ключ), ТипЗнч(СозданныйДокумент.Ключ));
		ДанныеОповещения.Вставить(ОбменСБанкамиПоЗарплатнымПроектамКлиентСервер.ПривестиСтрокуКИдентификатору(СозданныйДокумент.Значение), ТипЗнч(СозданныйДокумент.Значение));
		Если ТипЗнч(СозданныйДокумент.Ключ) = Тип("ДокументСсылка.ПодтверждениеОткрытияЛицевыхСчетовСотрудников") Тогда
			ДанныеОповещения.Вставить("ЗагруженоПодтверждениеОткрытияЛицевыхСчетов", Неопределено);
		ИначеЕсли ТипЗнч(СозданныйДокумент.Ключ) = Тип("ДокументСсылка.ПодтверждениеЗачисленияЗарплаты") Тогда
			ДанныеОповещения.Вставить("ЗагруженоПодтверждениеЗачисленияЗарплаты", Неопределено);
		КонецЕсли;
		
		ОбъектВладелец = СозданныйДокумент.Ключ;
		
	КонецЦикла;
	
	Если ПриЗагрузкеБылиОшибки Тогда
		ТекстСообщения = НСтр("ru = 'При загрузке файлов подтверждений банка были ошибки'");
	Иначе
		ТекстСообщения = НСтр("ru = 'Все файлы успешно загружены'");
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

// См. ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьСтруктуруКлючевыхРеквизитовОбъекта.
Процедура ПолучитьСтруктуруКлючевыхРеквизитовОбъекта(ИмяОбъекта, СтруктураКлючевыхРеквизитов) Экспорт
	ОбменСБанкамиПоЗарплатнымПроектамВнутренний.ПолучитьСтруктуруКлючевыхРеквизитовОбъекта(ИмяОбъекта, СтруктураКлючевыхРеквизитов)	
КонецПроцедуры

// Заполняет список команд ЭДО.
// 
// Параметры:
//  СоставКоманд - Массив - например "Документ.ВедомостьНаВыплатуЗарплатыВБанк".
//
Процедура ПодготовитьСтруктуруОбъектовКомандЭДО(СоставКомандЭДО) Экспорт
	
	СоставКомандЭДО.Добавить("Документ.ВедомостьНаВыплатуЗарплатыВБанк");
	СоставКомандЭДО.Добавить("Документ.ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудников");
	СоставКомандЭДО.Добавить("Документ.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников");
	
	ОбменСБанкамиПоЗарплатнымПроектамВнутренний.ДополнитьСоставКомандЭДО(СоставКомандЭДО);
	
	МетаданныеПлатежногоДокумента = МетаданныеПлатежногоДокументаПеречисленияЗарплаты();
	Если МетаданныеПлатежногоДокумента <> Неопределено Тогда
		СоставКомандЭДО.Добавить(МетаданныеПлатежногоДокумента.ПолноеИмя());
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриСозданииНаСервереФормыСДинамическимСписком(Форма) Экспорт
	
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ПрямыеОбменыСБанкомПоЗарплатномуПроекту", Новый ОписаниеТипов("ТаблицаЗначений")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ЗарплатныйПроект", Новый ОписаниеТипов("СправочникСсылка.ЗарплатныеПроекты"), "ПрямыеОбменыСБанкомПоЗарплатномуПроекту"));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ИспользоватьПрямойОбмен", Новый ОписаниеТипов("Булево"), "ПрямыеОбменыСБанкомПоЗарплатномуПроекту"));
	
	МассивИменРеквизитовФормы = Новый Массив;
	ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(Форма, МассивИменРеквизитовФормы);
	ЗарплатаКадры.ИзменитьРеквизитыФормы(Форма, ДобавляемыеРеквизиты, МассивИменРеквизитовФормы);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗарплатныеПроекты.Ссылка
	|ИЗ
	|	Справочник.ЗарплатныеПроекты КАК ЗарплатныеПроекты";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрокаТаблицы = Форма.ПрямыеОбменыСБанкомПоЗарплатномуПроекту.Добавить();
		НоваяСтрокаТаблицы.ЗарплатныйПроект = Выборка.Ссылка;
		НоваяСтрокаТаблицы.ИспользоватьПрямойОбмен = ИспользоватьПрямойЭОИСБанком(Выборка.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииДляРаботыСПлатежнымДокументом

// Возвращает доступность платежного документа для изменения.
//
// Параметры:
//		ПлатежныйДокумент - Ссылка на платежный документ.
//
Функция ДоступностьПлатежногоДокумента(ПлатежныйДокумент) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ПлатежныйДокумент", ПлатежныйДокумент);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СостоянияДокументовЗачисленияЗарплаты.ДокументЗачисленияЗарплаты
	|ИЗ
	|	РегистрСведений.СостоянияДокументовЗачисленияЗарплаты КАК СостоянияДокументовЗачисленияЗарплаты
	|ГДЕ
	|	СостоянияДокументовЗачисленияЗарплаты.ДокументЗачисленияЗарплаты = &ПлатежныйДокумент
	|	И (СостоянияДокументовЗачисленияЗарплаты.Состояние = ЗНАЧЕНИЕ(Перечисление.СостояниеЗачисленияЗарплаты.ЗачисленоПолностью)
	|			ИЛИ СостоянияДокументовЗачисленияЗарплаты.Состояние = ЗНАЧЕНИЕ(Перечисление.СостояниеЗачисленияЗарплаты.ЗачисленоСОшибками)
	|			ИЛИ СостоянияДокументовЗачисленияЗарплаты.Состояние = ЗНАЧЕНИЕ(Перечисление.СостояниеЗачисленияЗарплаты.НеЗачислено))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СостоянияДокументовЗачисленияЗарплаты.ДокументЗачисленияЗарплаты
	|ИЗ
	|	РегистрСведений.ПлатежныеДокументыПеречисленияЗарплаты КАК ПлатежныеДокументыПеречисленияЗарплаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияДокументовЗачисленияЗарплаты КАК СостоянияДокументовЗачисленияЗарплаты
	|		ПО ПлатежныеДокументыПеречисленияЗарплаты.ПлатежныйДокумент = СостоянияДокументовЗачисленияЗарплаты.ДокументЗачисленияЗарплаты
	|			И (СостоянияДокументовЗачисленияЗарплаты.Состояние = ЗНАЧЕНИЕ(Перечисление.СостояниеЗачисленияЗарплаты.ЗачисленоПолностью)
	|				ИЛИ СостоянияДокументовЗачисленияЗарплаты.Состояние = ЗНАЧЕНИЕ(Перечисление.СостояниеЗачисленияЗарплаты.ЗачисленоСОшибками)
	|				ИЛИ СостоянияДокументовЗачисленияЗарплаты.Состояние = ЗНАЧЕНИЕ(Перечисление.СостояниеЗачисленияЗарплаты.НеЗачислено))
	|ГДЕ
	|	ПлатежныеДокументыПеречисленияЗарплаты.Ведомость = &ПлатежныйДокумент";
	
	Возврат Запрос.Выполнить().Пустой();
	
КонецФункции

Функция СведенияОПлатежномДокументе(МассивВедомостей, ЗначенияДляЗаполнения = Неопределено) Экспорт
	
	Сведения = Новый Структура("
	|Организация,
	|ЗарплатныйПроект,
	|Получатель,
	|РасчетныйСчет,
	|СуммаПлатежа,
	|СпособВыплаты,
	|СпособВыплатыПредставление,
	|МесяцВыплатыПредставление");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивВедомостей", МассивВедомостей);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВедомостьНаВыплатуЗарплатыВБанк.Организация КАК Организация,
	|	ВедомостьНаВыплатуЗарплатыВБанк.ЗарплатныйПроект КАК ЗарплатныйПроект,
	|	ВедомостьНаВыплатуЗарплатыВБанк.СпособВыплаты.Наименование КАК СпособВыплатыНаименование,
	|	ВедомостьНаВыплатуЗарплатыВБанк.СпособВыплаты КАК СпособВыплаты,
	|	ВедомостьНаВыплатуЗарплатыВБанк.ПериодРегистрации КАК МесяцВыплаты
	|ИЗ
	|	Документ.ВедомостьНаВыплатуЗарплатыВБанк КАК ВедомостьНаВыплатуЗарплатыВБанк
	|ГДЕ
	|	ВедомостьНаВыплатуЗарплатыВБанк.Ссылка В(&МассивВедомостей)
	|
	|УПОРЯДОЧИТЬ ПО
	|	СпособВыплатыНаименование";
	ТаблицаВедомостей = Запрос.Выполнить().Выгрузить();
	ОбменСБанкамиПоЗарплатнымПроектамВнутренний.ДополнитьТаблицуВедомостей(ТаблицаВедомостей, МассивВедомостей);
	
	Если ТаблицаВедомостей.Количество() = 0 Тогда
		Возврат Сведения;
	КонецЕсли;
	
	Сведения.Организация 	  = ТаблицаВедомостей[0].Организация;
	Сведения.ЗарплатныйПроект = ТаблицаВедомостей[0].ЗарплатныйПроект;
	Сведения.СпособВыплаты 	  = ?(ТаблицаВедомостей.Количество()>1, Неопределено, ТаблицаВедомостей[0].СпособВыплаты);
	
	РеквизитыПолучателя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Сведения.ЗарплатныйПроект, "Банк,РасчетныйСчет");
	Сведения.Получатель    = РеквизитыПолучателя.Банк;   
	Сведения.РасчетныйСчет = РеквизитыПолучателя.РасчетныйСчет;
	
	СпособыВыплаты = ТаблицаВедомостей.ВыгрузитьКолонку("СпособВыплатыНаименование");
	ОбщегоНазначенияКлиентСервер.СвернутьМассив(СпособыВыплаты);
	СпособВыплатыПредставление = "";
	Для каждого СпособВыплатыНаименование Из СпособыВыплаты Цикл
		СпособВыплатыПредставление = СпособВыплатыПредставление + СпособВыплатыНаименование + ", ";
	КонецЦикла;
	СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(СпособВыплатыПредставление, 2);
	Сведения.СпособВыплатыПредставление = СпособВыплатыПредставление;
	
	ТаблицаВедомостей.Свернуть("МесяцВыплаты");
	ТаблицаВедомостей.Сортировать("МесяцВыплаты");
	МесяцВыплатыПредставление = "";
	Для каждого СтрокаТЗ Из ТаблицаВедомостей Цикл
		МесяцВыплатыПредставление = МесяцВыплатыПредставление + Формат(СтрокаТЗ.МесяцВыплаты, "ДФ='ММММ гггг'") + ", ";
	КонецЦикла;
	СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(МесяцВыплатыПредставление, 2);
	Сведения.МесяцВыплатыПредставление = МесяцВыплатыПредставление;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ВедомостьНаВыплатуЗарплатыВБанкЗарплата.КВыплате + ВедомостьНаВыплатуЗарплатыВБанкЗарплата.КомпенсацияЗаЗадержкуЗарплаты), 0) КАК СуммаПоДокументу
	|ИЗ
	|	Документ.ВедомостьНаВыплатуЗарплатыВБанк.Зарплата КАК ВедомостьНаВыплатуЗарплатыВБанкЗарплата
	|ГДЕ
	|	ВедомостьНаВыплатуЗарплатыВБанкЗарплата.Ссылка В(&МассивВедомостей)";
	Выборка = Запрос.Выполнить().Выбрать();
	
	СуммаПоДокументу = 0;
	Если Выборка.Следующий() Тогда
		СуммаПоДокументу = Выборка.СуммаПоДокументу;
	КонецЕсли;
	ОбменСБанкамиПоЗарплатнымПроектамВнутренний.ДополнитьСуммуПоДокументу(СуммаПоДокументу, МассивВедомостей);
	
	Сведения.СуммаПлатежа = СуммаПоДокументу;
	
	Возврат Сведения;
	
КонецФункции

Процедура ЗаполнитьРеквизитыНаФормеПлатежногоДокумента(Форма, Организация, ЗначенияДляЗаполнения, МассивВедомостей) Экспорт
	
	СведенияОПлатежномДокументе = СведенияОПлатежномДокументе(МассивВедомостей, ЗначенияДляЗаполнения);
	
	Если ЗначенияДляЗаполнения.Свойство("Получатель") Тогда
		Форма[ЗначенияДляЗаполнения.Получатель] = СведенияОПлатежномДокументе.Получатель;
	КонецЕсли;
	
	Если ЗначенияДляЗаполнения.Свойство("РасчетныйСчет") Тогда
		Форма[ЗначенияДляЗаполнения.РасчетныйСчет] = СведенияОПлатежномДокументе.РасчетныйСчет;
	КонецЕсли;
	
	Если ЗначенияДляЗаполнения.Свойство("СуммаПлатежа") Тогда
		Форма[ЗначенияДляЗаполнения.СуммаПлатежа] = СведенияОПлатежномДокументе.СуммаПлатежа;
	КонецЕсли;
	
	Если ЗначенияДляЗаполнения.Свойство("ЗарплатныйПроект") Тогда
		Форма[ЗначенияДляЗаполнения.ЗарплатныйПроект] = СведенияОПлатежномДокументе.ЗарплатныйПроект;
	КонецЕсли;
	
	Если ЗначенияДляЗаполнения.Свойство("ИНН") Или ЗначенияДляЗаполнения.Свойство("КПП") Тогда
		СписокПоказателей = Новый Массив;
		СписокПоказателей.Добавить("ИННЮЛ");
		СписокПоказателей.Добавить("КППЮЛ");
		СписокПоказателей.Добавить("ТипНП");
		Если ЗначениеЗаполнено(Организация) Тогда
			СведенияОбОрганизации = ЗарплатаКадры.ПолучитьСведенияОбОрганизации(Организация, , СписокПоказателей);
		Иначе
			СведенияОбОрганизации = Новый Структура("ИННЮЛ, КППЮЛ, ТипНП");
		КонецЕсли;
		
		Если ЗначенияДляЗаполнения.Свойство("ИНН") Тогда
			Форма[ЗначенияДляЗаполнения.ИНН] = ?(ЗначениеЗаполнено(СведенияОбОрганизации.ИННЮЛ), СведенияОбОрганизации.ИННЮЛ, НСтр("ru = '<не указан>'"));
		КонецЕсли;
		Если ЗначенияДляЗаполнения.Свойство("КПП") Тогда
			Если СведенияОбОрганизации.ТипНП = 3 Тогда
				// Физическое лицо
				Форма[ЗначенияДляЗаполнения.КПП] = НСтр("ru = '<не требуется>'");
			Иначе
				// Юридическое лицо
				Форма[ЗначенияДляЗаполнения.КПП] = ?(ЗначениеЗаполнено(СведенияОбОрганизации.КППЮЛ), СведенияОбОрганизации.КППЮЛ, НСтр("ru = '<не указан>'"));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ВедомостиПлатежногоДокументаПеречисленияЗарплаты(ПлатежныйДокумент) Экспорт
	
	СтандартнаяОбработка = Истина;
	МассивВедомостей = Новый Массив;
	ОбменСБанкамиПоЗарплатнымПроектамПереопределяемый.ВедомостиПлатежногоДокументаПеречисленияЗарплаты(
		ПлатежныйДокумент, МассивВедомостей, СтандартнаяОбработка);
	
	Если Не СтандартнаяОбработка Тогда
		Возврат МассивВедомостей;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ВнешниеХозяйственныеОперации.ВзаиморасчетыССотрудникамиВХО") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ВзаиморасчетыССотрудникамиВХО");
		Модуль.ВедомостиПлатежногоДокументаПеречисленияЗарплаты(ПлатежныйДокумент, МассивВедомостей);
	КонецЕсли;
	
	Возврат МассивВедомостей;
	
КонецФункции

Функция ПечатьСпискаПеречисленийПоДокументам(МассивОбъектов, ОбъектыПечати, ПлатежныйДокумент = Неопределено) Экспорт
	
	Если МассивОбъектов.Количество() = 0 Тогда
		ДокументМенеджер = Документы.ВедомостьНаВыплатуЗарплатыВБанк;
	Иначе	
		ДокументМенеджер = ОбщегоНазначения.МенеджерОбъектаПоСсылке(МассивОбъектов[0]);
	КонецЕсли;
	Возврат ДокументМенеджер.ПечатьСпискаПеречисленийПоДокументам(МассивОбъектов, ОбъектыПечати, ПлатежныйДокумент);
	
КонецФункции

Процедура ДополнитьПлатежныйДокумент(ПлатежныйДокумент, МассивВедомостей) Экспорт
	
	СтандартнаяОбработка = Истина;
	ОбменСБанкамиПоЗарплатнымПроектамПереопределяемый.ДополнитьПлатежныйДокумент(ПлатежныйДокумент, МассивВедомостей, СтандартнаяОбработка);
	
	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ВнешниеХозяйственныеОперации.ВзаиморасчетыССотрудникамиВХО") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ВзаиморасчетыССотрудникамиВХО");
		Модуль.ДополнитьПлатежныйДокумент(ПлатежныйДокумент, МассивВедомостей);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик ПередЗаписью для платежного документа
//
// Параметры:
//		Источник - записываемый объект.
//
Процедура ПередЗаписьюПлатежногоДокументаПеречисленияЗарплаты(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Истина;
	ОбменСБанкамиПоЗарплатнымПроектамПереопределяемый.ПередЗаписьюПлатежногоДокументаПеречисленияЗарплаты(Источник, Отказ, СтандартнаяОбработка);
	
	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеПлатежногоДокумента = МетаданныеПлатежногоДокументаПеречисленияЗарплаты();
	Если МетаданныеПлатежногоДокумента.Реквизиты.Найти("НомерРеестра") <> Неопределено Тогда
		Если Не ЗначениеЗаполнено(Источник.НомерРеестра) Тогда
			РеквизитыПлатежногоДокумента = Новый Структура;
			РеквизитыПлатежногоДокумента.Вставить("ПлатежныйДокумент", Источник);
			РеквизитыПлатежногоДокумента.Вставить("Номер", Источник.Номер);
			РеквизитыПлатежногоДокумента.Вставить("Дата", Источник.Дата);
			РеквизитыПлатежногоДокумента.Вставить("Организация", Источник.Организация);
			
			ГодыВыгрузки = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Год(Источник.Дата));
			НомераРеестров = НомераРеестровДокументов(Неопределено, ГодыВыгрузки, РеквизитыПлатежногоДокумента);
			ДанныеРеестра = НомераРеестров.Получить(Источник);
			Если ДанныеРеестра <> Неопределено Тогда
				Источник.НомерРеестра = ДанныеРеестра.НомерРеестра;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Источник.ДополнительныеСвойства.Вставить("МассивВедомостейПередЗаписью", ВедомостиПлатежногоДокументаПеречисленияЗарплаты(Источник.Ссылка));
	Источник.ДополнительныеСвойства.Вставить("Проведен", Источник.Проведен);
	
КонецПроцедуры

// Обработчик ПриЗаписи для платежного документа
// Регистрирует состояние документа.
//
// Параметры:
//		Источник - записываемый объект, который влияет на состояния документов зачисления зарплаты.
//
Процедура ПриЗаписиПлатежногоДокументаПеречисленияЗарплаты(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Истина;
	ОбменСБанкамиПоЗарплатнымПроектамПереопределяемый.ПриЗаписиПлатежногоДокументаПеречисленияЗарплаты(Источник, Отказ, СтандартнаяОбработка);
	
	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	МассивВедомостей = ВедомостиПлатежногоДокументаПеречисленияЗарплаты(Источник.Ссылка);
	
	// Заполняем сведения о реквизитах платежного документа и составе включенных в него ведомостей.
	
	НаборЗаписей = РегистрыСведений.РеквизитыПлатежныхДокументовПеречисленияЗарплаты.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПлатежныйДокумент.Установить(Источник.Ссылка);
	НаборЗаписей.Записать();
	
	НаборЗаписей = РегистрыСведений.ПлатежныеДокументыПеречисленияЗарплаты.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПлатежныйДокумент.Установить(Источник.Ссылка);
	НаборЗаписей.Записать();
	
	СведенияОПлатежномДокументе = СведенияОПлатежномДокументе(МассивВедомостей);
	
	НаборЗаписей = РегистрыСведений.РеквизитыПлатежныхДокументовПеречисленияЗарплаты.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПлатежныйДокумент.Установить(Источник.Ссылка);
	
	НоваяЗаписьНабора = НаборЗаписей.Добавить();
	НоваяЗаписьНабора.Организация = СведенияОПлатежномДокументе.Организация;
	НоваяЗаписьНабора.ПлатежныйДокумент = Источник.Ссылка;
	НоваяЗаписьНабора.ЗарплатныйПроект = СведенияОПлатежномДокументе.ЗарплатныйПроект;
	НоваяЗаписьНабора.Номер = Источник.Номер;
	НоваяЗаписьНабора.Дата = Источник.Дата;
	НоваяЗаписьНабора.СуммаПоДокументу = СведенияОПлатежномДокументе.СуммаПлатежа;
	НоваяЗаписьНабора.СпособВыплаты = СведенияОПлатежномДокументе.СпособВыплаты;
	НоваяЗаписьНабора.СпособВыплатыПредставление = СведенияОПлатежномДокументе.СпособВыплатыПредставление;
	НоваяЗаписьНабора.МесяцВыплатыПредставление = СведенияОПлатежномДокументе.МесяцВыплатыПредставление;
	
	НаборЗаписей.Записать();
	
	НаборЗаписей = РегистрыСведений.ПлатежныеДокументыПеречисленияЗарплаты.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПлатежныйДокумент.Установить(Источник.Ссылка);
	Для Каждого Ведомость Из МассивВедомостей Цикл
		НоваяЗаписьНабора = НаборЗаписей.Добавить();
		НоваяЗаписьНабора.Организация = СведенияОПлатежномДокументе.Организация;
		НоваяЗаписьНабора.ПлатежныйДокумент = Источник.Ссылка;
		НоваяЗаписьНабора.Ведомость = Ведомость;
	КонецЦикла;
	Если Источник.ДополнительныеСвойства.Свойство("ПроверкаВыполнена") И Источник.ДополнительныеСвойства.ПроверкаВыполнена Тогда
		НаборЗаписей.ДополнительныеСвойства.Вставить("ВыполнятьПроверкуПередЗаписьюНабора", Ложь);
	КонецЕсли;
	НаборЗаписей.Записать();
	
	// Проверим - был ли изменен документ в составе ведомостей в банк
	
	ВедомостиПриЗаписи = Новый ТаблицаЗначений;
	ВедомостиПриЗаписи.Колонки.Добавить("Ссылка", Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ВедомостьВБанкЗарплатаКадры.Тип));
	ВедомостиПередЗаписью = ВедомостиПриЗаписи.СкопироватьКолонки();
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицуИзМассива(ВедомостиПриЗаписи, МассивВедомостей, "Ссылка");
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицуИзМассива(ВедомостиПередЗаписью, Источник.ДополнительныеСвойства.МассивВедомостейПередЗаписью, "Ссылка");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВедомостиПередЗаписью", ВедомостиПередЗаписью);
	Запрос.УстановитьПараметр("ВедомостиПриЗаписи", ВедомостиПриЗаписи);
	Запрос.УстановитьПараметр("Проведен", Источник.Проведен);
	Запрос.УстановитьПараметр("ПроведенПередЗаписью", Источник.ДополнительныеСвойства.Проведен);
	Запрос.УстановитьПараметр("ПервичныйДокумент", Источник.Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВедомостьНаВыплатуЗарплатыВБанкПриЗаписи.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТВедомостиПриЗаписи
	|ИЗ
	|	&ВедомостиПриЗаписи КАК ВедомостьНаВыплатуЗарплатыВБанкПриЗаписи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВедомостьНаВыплатуЗарплатыВБанкПередЗаписью.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТВедомостиПередЗаписью
	|ИЗ
	|	&ВедомостиПередЗаписью КАК ВедомостьНаВыплатуЗарплатыВБанкПередЗаписью
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВедомостиПриЗаписи.Ссылка КАК ВедомостьПриЗаписи,
	|	ВедомостиПередЗаписью.Ссылка КАК ВедомостьПередЗаписью,
	|	СостоянияДокументовЗачисленияЗарплаты.ДокументЗачисленияЗарплаты КАК ДокументЗачисленияЗарплаты
	|ИЗ
	|	ВТВедомостиПриЗаписи КАК ВедомостиПриЗаписи
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТВедомостиПередЗаписью КАК ВедомостиПередЗаписью
	|		ПО ВедомостиПриЗаписи.Ссылка = ВедомостиПередЗаписью.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияДокументовЗачисленияЗарплаты КАК СостоянияДокументовЗачисленияЗарплаты
	|		ПО (&ПервичныйДокумент = СостоянияДокументовЗачисленияЗарплаты.ДокументЗачисленияЗарплаты)
	|ГДЕ
	|	(ВедомостиПриЗаписи.Ссылка ЕСТЬ NULL
	|			ИЛИ ВедомостиПередЗаписью.Ссылка ЕСТЬ NULL
	|			ИЛИ &Проведен <> &ПроведенПередЗаписью
	|				И СостоянияДокументовЗачисленияЗарплаты.ДокументЗачисленияЗарплаты ЕСТЬ NULL)";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		// Регистрируем состояние платежного документа, если документ был изменен
		// Платежный документ может быть выгружен, в этом случае не нужно менять состояние, если он не был изменен.
		ДанныеРеестра = Неопределено;
		МетаданныеПлатежногоДокумента = МетаданныеПлатежногоДокументаПеречисленияЗарплаты();
		Если МетаданныеПлатежногоДокумента.Реквизиты.Найти("НомерРеестра") <> Неопределено Тогда
			ДанныеПлатежногоДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник.Ссылка, "Дата, НомерРеестра");
			Если Не ПустаяСтрока(ДанныеПлатежногоДокумента.НомерРеестра) Тогда
				ДанныеРеестра = Новый Структура("Год, НомерРеестра", Год(ДанныеПлатежногоДокумента.Дата), ДанныеПлатежногоДокумента.НомерРеестра);
			КонецЕсли;
		КонецЕсли;
		ЗарегистрироватьСостояниеЗачисленияЗарплатыПоДокументу(Источник.Ссылка, Отказ, , ДанныеРеестра);
	КонецЕсли;
	
КонецПроцедуры

// Определяет возможность передачи в банк реестров на зачисление зарплаты.
//
// Возвращаемое значение - булево.
//
Функция ИспользоватьПередачуВБанкРеестровНаЗачислениеЗарплаты() Экспорт
	
	Использование = Истина;
	
	ОбменСБанкамиПоЗарплатнымПроектамПереопределяемый.ОпределитьДоступностьПередачиВБанкРеестровНаЗачислениеЗарплаты(Использование);
	
	Возврат Использование;
	
КонецФункции

#КонецОбласти

#Область ДанныеДляПечатиПоXML

Функция ДанныеСпискаПеречисленийПоXML(МассивОбъектов) Экспорт
	
	ДанныеДокументов = Новый Соответствие;
	
	Для Каждого ОбъектПечати Из МассивОбъектов Цикл
		
		СуммаПоДокументу = 0;
		УникальнаяСсылка = Документы.ВедомостьНаВыплатуЗарплатыВБанк.ПолучитьСсылку(Новый УникальныйИдентификатор);
		ДанныеДокумента = Документы.ВедомостьНаВыплатуЗарплатыВБанк.ДанныеЗаполненияВедомости();
		ДанныеДокумента.Документ = УникальнаяСсылка;
		ДанныеДокумента.ДатаДокумента = ОбъектПечати.ДатаФормирования;
		ДанныеДокумента.НомерДоговора = ОбъектПечати.НомерДоговора;
		ДанныеДокумента.ПолноеНаименованиеОрганизации = ОбъектПечати.НаименованиеОрганизации;
		ДанныеДокумента.НомерРасчетногоСчетаОрганизации = ОбъектПечати.РасчетныйСчетОрганизации;
		Для каждого Сотрудник Из ОбъектПечати.ЗачислениеЗарплаты.Сотрудник Цикл
			
			СуммаПоДокументу = СуммаПоДокументу + Сотрудник.Сумма;
			
			ДанныеСтрокиДокумента = Документы.ВедомостьНаВыплатуЗарплатыВБанк.ДанныеЗаполненияСтрокиВедомости();
			ДанныеСтрокиДокумента.НомерЛицевогоСчета = Сотрудник.ЛицевойСчет;
			ДанныеСтрокиДокумента.Фамилия = Сотрудник.Фамилия;
			ДанныеСтрокиДокумента.Имя = Сотрудник.Имя;
			ДанныеСтрокиДокумента.Отчество = Сотрудник.Отчество;
			ДанныеСтрокиДокумента.СуммаКВыплате = Сотрудник.Сумма;
			
			ДанныеДокумента.Сотрудники.Добавить(ДанныеСтрокиДокумента);
			
		КонецЦикла;
		ДанныеДокумента.СуммаПоДокументу = СуммаПоДокументу;
		ДанныеДокументов.Вставить(УникальнаяСсылка, ДанныеДокумента);
		
	КонецЦикла;
	
	Возврат ДанныеДокументов;
	
КонецФункции

Функция ДанныеСпискаНаОткрытиеЛицевыхСчетовПоXML(МассивОбъектов) Экспорт
	
	ДанныеДокументов = Новый Соответствие;
	
	Для Каждого ОбъектПечати Из МассивОбъектов Цикл
		
		УникальнаяСсылка = Документы.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.ПолучитьСсылку(Новый УникальныйИдентификатор);
		ДанныеДокумента = Документы.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.ДанныеЗаполненияОткрытияЛицевыхСчетов();
		ДанныеДокумента.Документ = УникальнаяСсылка;
		ДанныеДокумента.ДатаДокумента = ОбъектПечати.ДатаФормирования;
		ДанныеДокумента.НомерДоговора = ОбъектПечати.НомерДоговора;
		ДанныеДокумента.ПолноеНаименованиеОрганизации = ОбъектПечати.НаименованиеОрганизации;
		ДанныеДокумента.НомерРасчетногоСчетаОрганизации = ОбъектПечати.РасчетныйСчетОрганизации;
		
		Для каждого Сотрудник Из ОбъектПечати.ОткрытиеСчетов.Сотрудник Цикл
			
			ДанныеСтрокиДокумента = Документы.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.ДанныеЗаполненияСтрокиОткрытияЛицевыхСчетов();
			ДанныеСтрокиДокумента.Фамилия = Сотрудник.Фамилия;
			ДанныеСтрокиДокумента.Имя = Сотрудник.Имя;
			ДанныеСтрокиДокумента.Отчество = Сотрудник.Отчество;
			ДанныеСтрокиДокумента.ЭмбоссированныйТекст1 = Сотрудник.ЭмбоссированныйТекст.Поле1;
			ДанныеСтрокиДокумента.ЭмбоссированныйТекст2 = Сотрудник.ЭмбоссированныйТекст.Поле2;
			ДанныеСтрокиДокумента.ЭмбоссированныйТекст3 = Сотрудник.ЭмбоссированныйТекст.Поле3;
			ДанныеСтрокиДокумента.ДатаРождения = Сотрудник.ДатаРождения;
			ДанныеСтрокиДокумента.Пол = Сотрудник.Пол;
			ДанныеСтрокиДокумента.ОтделениеБанка = Сотрудник.ОтделениеБанка;
			ДанныеСтрокиДокумента.ФилиалОтделенияБанка = Сотрудник.ФилиалОтделенияБанка;
			ДанныеСтрокиДокумента.КодВидаВклада = Сотрудник.ВидВклада.КодВидаВклада;
			ДанныеСтрокиДокумента.СуммаПервоначальногоПополнения = Сотрудник.Сумма;
			
			ДанныеСтрокиДокумента.АдресПоПропискеПредставление = ПредставлениеАдресаИзЭДXML(
				Сотрудник.АдресПрописки, Справочники.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица);
			ДанныеСтрокиДокумента.АдресМестаПроживанияПредставление = ПредставлениеАдресаИзЭДXML(
				Сотрудник.АдресПроживания, Справочники.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица);
			ДанныеСтрокиДокумента.АдресМестаРаботыПредставление = ПредставлениеАдресаИзЭДXML(
				Сотрудник.АдресМестаРаботы, Справочники.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица);
			
			ДанныеСтрокиДокумента.ТелефонРабочийПредставление = Сотрудник.РабочийТелефон;
			ДанныеСтрокиДокумента.ДокументПредставление = ПредставлениеУдостоверенияЛичностиИзЭДXML(Сотрудник.УдостоверениеЛичности);
			ДанныеДокумента.Сотрудники.Добавить(ДанныеСтрокиДокумента);
		КонецЦикла;
		
		ДанныеДокументов.Вставить(УникальнаяСсылка, ДанныеДокумента);
		
	КонецЦикла;
	
	Возврат ДанныеДокументов;
	
КонецФункции

Функция ДанныеПодтвержденияОткрытияЛицевыхСчетовПоXML(МассивОбъектов) Экспорт
	
	ДанныеДокументов = Новый Соответствие;
	
	Для Каждого ОбъектПечати Из МассивОбъектов Цикл
		
		УникальнаяСсылка = Документы.ПодтверждениеОткрытияЛицевыхСчетовСотрудников.ПолучитьСсылку(Новый УникальныйИдентификатор);
		ДанныеДокумента = Документы.ПодтверждениеОткрытияЛицевыхСчетовСотрудников.ДанныеЗаполненияПодтвержденияОткрытияЛицевыхСчетов();
		ДанныеДокумента.Ссылка = УникальнаяСсылка;
		ДанныеДокумента.Дата = ОбъектПечати.ДатаФормирования;
		ДанныеДокумента.НомерДоговора = ОбъектПечати.НомерДоговора;
		ДанныеДокумента.ПолноеНаименованиеОрганизации = ОбъектПечати.НаименованиеОрганизации;
		ДанныеДокумента.НомерРасчетногоСчетаОрганизации = ОбъектПечати.РасчетныйСчетОрганизации;
		
		ИдПервичногоДокумента = Новый УникальныйИдентификатор(ОбъектПечати.ИдПервичногоДокумента);
		СсылкаНаПервичныйДокумент = Документы.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.ПолучитьСсылку(ИдПервичногоДокумента);
		ДанныеДокумента.ПервичныйДокумент = СсылкаНаПервичныйДокумент;
		
		Для каждого Сотрудник Из ОбъектПечати.РезультатОткрытияСчетов.Сотрудник Цикл
			
			ДанныеСтрокиДокумента = Документы.ПодтверждениеОткрытияЛицевыхСчетовСотрудников.ДанныеЗаполненияСтрокиПодтвержденияОткрытияЛицевыхСчетов();
			ДанныеСтрокиДокумента.Фамилия = Сотрудник.Фамилия;
			ДанныеСтрокиДокумента.Имя = Сотрудник.Имя;
			ДанныеСтрокиДокумента.Отчество = Сотрудник.Отчество;
			ДанныеСтрокиДокумента.НомерЛицевогоСчета = Сотрудник.ЛицевойСчет;
			ДанныеСтрокиДокумента.Сумма = Сотрудник.Сумма;
			ДанныеСтрокиДокумента.РезультатОткрытияСчета = Сотрудник.Результат;
			ДанныеСтрокиДокумента.КомментарийРезультата = Сотрудник.РасшифровкаРезультата;
			
			ДанныеДокумента.Сотрудники.Добавить(ДанныеСтрокиДокумента);
			
		КонецЦикла;
		ДанныеДокументов.Вставить(УникальнаяСсылка, ДанныеДокумента);
		
	КонецЦикла;
	
	Возврат ДанныеДокументов;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция МетаданныеПлатежногоДокументаПеречисленияЗарплаты() Экспорт
	
	МетаданныеПлатежногоДокумента = Неопределено;
	ОбменСБанкамиПоЗарплатнымПроектамПереопределяемый.ЗаполнитьМетаданныеПлатежногоДокументаПеречисленияЗарплаты(МетаданныеПлатежногоДокумента);
	
	Если МетаданныеПлатежногоДокумента <> Неопределено Тогда
		Возврат МетаданныеПлатежногоДокумента;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ВнешниеХозяйственныеОперации.ВзаиморасчетыССотрудникамиВХО") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ВзаиморасчетыССотрудникамиВХО");
		Модуль.ЗаполнитьМетаданныеПлатежногоДокументаПеречисленияЗарплаты(МетаданныеПлатежногоДокумента);
	КонецЕсли;
	
	Возврат МетаданныеПлатежногоДокумента;
	
КонецФункции

// Возвращает номер реестра платежного документа, выгружаемого в банк
//
// Параметры:
//		ПлатежныйДокумент - Ссылка на платежный документ.
//
Функция НомерРеестраПлатежногоДокумента(ПлатежныйДокумент)
	
	НомерРеестра = Неопределено;
	ОбменСБанкамиПоЗарплатнымПроектамПереопределяемый.ЗаполнитьНомерРеестраПлатежногоДокумента(ПлатежныйДокумент, НомерРеестра);
	
	Если НомерРеестра = Неопределено Тогда
		МетаданныеПлатежногоДокумента = МетаданныеПлатежногоДокументаПеречисленияЗарплаты();
		Если МетаданныеПлатежногоДокумента.Реквизиты.Найти("НомерРеестра") <> Неопределено Тогда
			Если ТипЗнч(ПлатежныйДокумент) = Тип("ДокументОбъект." + МетаданныеПлатежногоДокумента.Имя) Тогда
				НомерРеестра = ПлатежныйДокумент.НомерРеестра;
			Иначе
				НомерРеестра = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПлатежныйДокумент, "НомерРеестра");
			КонецЕсли;
			Если ПустаяСтрока(НомерРеестра) Тогда
				НомерРеестра = Неопределено;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат НомерРеестра;
	
КонецФункции

Функция СтрокаВЧисло(Знач ИсходнаяСтрока) Экспорт
	
	Результат = 0;
	Длина = СтрДлина(ИсходнаяСтрока);
	Для НомерСимвола = 1 По Длина Цикл
		КодСимвола = КодСимвола(ИсходнаяСтрока, НомерСимвола);
		Если КодСимвола > 47 И КодСимвола < 58 Тогда // Число.
			Число = КодСимвола - 48;
			Результат = Результат * 10 + Число;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Проверяет правильность заполнения платежного документа.
//
// Параметры:
//		ПлатежныйДокумент - Ссылка на платежный документ.
//		МассивВедомостей - Массив - массив ссылок ведомостей в банк.
//		МассивОшибок - Массив - Массив структур ошибок.
//								ТекстСообщения - Строка - Текст ошибки.
//								Ведомость - Ссылка на ведомость, к которой относится ошибка,
//									Если Неопределено, тогда ошибка относится к Платежному документу.
//
Процедура ПроверитьЗаполнениеПлатежногоДокумента(ПлатежныйДокумент, МассивВедомостей, МассивОшибок) Экспорт
	
	Если Не ДоступностьПлатежногоДокумента(ПлатежныйДокумент) Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'По документу %1 приняты файлы подтверждения зачисления зарплаты. Изменение документа невозможно.'"), ПлатежныйДокумент);
		МассивОшибок.Добавить(Новый Структура("ТекстСообщения, Ведомость", ТекстОшибки, Неопределено));
	КонецЕсли;
	
	МассивНеПовторяющихсяВедомостей = Новый Массив;
	МассивПовторяющихсяВедомостей = Новый Массив;
	Для каждого Ведомость Из МассивВедомостей Цикл
		Если МассивНеПовторяющихсяВедомостей.Найти(Ведомость) = Неопределено Тогда
			МассивНеПовторяющихсяВедомостей.Добавить(Ведомость);
		Иначе
			Если МассивПовторяющихсяВедомостей.Найти(Ведомость) = Неопределено Тогда
				МассивПовторяющихсяВедомостей.Добавить(Ведомость);
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 уже включена в %2'"), Ведомость, ПлатежныйДокумент);
				МассивОшибок.Добавить(Новый Структура("ТекстСообщения, Ведомость", ТекстОшибки, Ведомость));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ведомости", МассивВедомостей);
	Запрос.УстановитьПараметр("Ссылка", ПлатежныйДокумент);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПлатежныеДокументыПеречисленияЗарплаты.Ведомость,
	|	ПлатежныеДокументыПеречисленияЗарплаты.ПлатежныйДокумент
	|ИЗ
	|	РегистрСведений.ПлатежныеДокументыПеречисленияЗарплаты КАК ПлатежныеДокументыПеречисленияЗарплаты
	|ГДЕ
	|	ПлатежныеДокументыПеречисленияЗарплаты.ПлатежныйДокумент <> &Ссылка
	|	И ПлатежныеДокументыПеречисленияЗарплаты.Ведомость В(&Ведомости)";
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Пока Выборка.Следующий() Цикл
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 уже включена в %2'"),
			Выборка.Ведомость, Выборка.ПлатежныйДокумент);
		МассивОшибок.Добавить(Новый Структура("ТекстСообщения, Ведомость", ТекстОшибки, Выборка.Ведомость));
	КонецЦикла;
	
	ВедомостиПоТипамДокументов = Новый Соответствие;	
	Для каждого Ведомость Из МассивВедомостей Цикл
		
		Если Не ЗначениеЗаполнено(Ведомость) Тогда
			Продолжить;
		КонецЕсли;
		
		ТипВедомости = ТипЗнч(Ведомость);
		ВедомостиПоТипу = ВедомостиПоТипамДокументов[ТипВедомости];
		Если ВедомостиПоТипу = Неопределено Тогда
			ВедомостиПоТипу = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ведомость);
		Иначе
			ВедомостиПоТипу.Добавить(Ведомость);
		КонецЕсли;
		ВедомостиПоТипамДокументов[ТипВедомости] = ВедомостиПоТипу;
	
	КонецЦикла;
	
	ВедомостиТаблица = Новый ТаблицаЗначений;
	ВедомостиТаблица.Колонки.Добавить("Ссылка", Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ВедомостьВБанкЗарплатаКадры.Тип));
	ВедомостиТаблица.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ВедомостиТаблица.Колонки.Добавить("ЗарплатныйПроект", Новый ОписаниеТипов("СправочникСсылка.ЗарплатныеПроекты"));
	ВедомостиТаблица.Колонки.Добавить("Проведен", Новый ОписаниеТипов("Булево"));
	
	УстановитьПривилегированныйРежим(Истина);
	Для каждого КлючИЗначение Из ВедомостиПоТипамДокументов Цикл
		ВедомостиПоТипу = КлючИЗначение.Значение;
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ВедомостиПоТипу, "Организация,ЗарплатныйПроект,Проведен");
		Для каждого ЭлементКоллекции Из ЗначенияРеквизитов Цикл
			НоваяСтрока = ВедомостиТаблица.Добавить();
			НоваяСтрока.Ссылка = ЭлементКоллекции.Ключ;
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементКоллекции.Значение);
		КонецЦикла;
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
	
	Запрос.УстановитьПараметр("ВедомостиТаблица", ВедомостиТаблица);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВедомостьНаВыплатуЗарплатыВБанк.Ссылка КАК Ведомость,
	|	ВедомостьНаВыплатуЗарплатыВБанк.Организация КАК Организация,
	|	ВедомостьНаВыплатуЗарплатыВБанк.ЗарплатныйПроект КАК ЗарплатныйПроект,
	|	ВедомостьНаВыплатуЗарплатыВБанк.Проведен КАК Проведен
	|ПОМЕСТИТЬ ВТВедомостиПлатежногоДокумента
	|ИЗ
	|	&ВедомостиТаблица КАК ВедомостьНаВыплатуЗарплатыВБанк
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВедомостиПлатежногоДокумента.Организация КАК Организация,
	|	ВедомостиПлатежногоДокумента.ЗарплатныйПроект КАК ЗарплатныйПроект
	|ИЗ
	|	ВТВедомостиПлатежногоДокумента КАК ВедомостиПлатежногоДокумента";
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);

	Если Результат.Количество() > 1 Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В документе %1 должны присутствовать ведомости только из одной организации и зарплатного проекта.'"), ПлатежныйДокумент);
		МассивОшибок.Добавить(Новый Структура("ТекстСообщения, Ведомость", ТекстОшибки, Неопределено));
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВедомостиПлатежногоДокумента.Ведомость
	|ИЗ
	|	ВТВедомостиПлатежногоДокумента КАК ВедомостиПлатежногоДокумента
	|ГДЕ
	|	НЕ ВедомостиПлатежногоДокумента.Проведен";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Ведомость %1 не проведена.'"), Выборка.Ведомость);
		МассивОшибок.Добавить(Новый Структура("ТекстСообщения, Ведомость", ТекстОшибки, Выборка.Ведомость));
	КонецЦикла;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СостоянияДокументовЗачисленияЗарплаты.ДокументЗачисленияЗарплаты КАК Ведомость
	|ИЗ
	|	РегистрСведений.СостоянияДокументовЗачисленияЗарплаты КАК СостоянияДокументовЗачисленияЗарплаты
	|ГДЕ
	|	СостоянияДокументовЗачисленияЗарплаты.ДокументЗачисленияЗарплаты В(&Ведомости)
	|	И (СостоянияДокументовЗачисленияЗарплаты.Состояние = ЗНАЧЕНИЕ(Перечисление.СостояниеЗачисленияЗарплаты.ЗачисленоПолностью)
	|			ИЛИ СостоянияДокументовЗачисленияЗарплаты.Состояние = ЗНАЧЕНИЕ(Перечисление.СостояниеЗачисленияЗарплаты.ЗачисленоСОшибками)
	|			ИЛИ СостоянияДокументовЗачисленияЗарплаты.Состояние = ЗНАЧЕНИЕ(Перечисление.СостояниеЗачисленияЗарплаты.НеЗачислено))";
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);

	Пока Выборка.Следующий() Цикл
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'По ведомости %1 уже получено подтверждение зачисления зарплаты.'"), Выборка.Ведомость);
		МассивОшибок.Добавить(Новый Структура("ТекстСообщения, Ведомость", ТекстОшибки, Выборка.Ведомость));
	КонецЦикла;
	
КонецПроцедуры

// Включает функциональную опцию "ИспользоватьЭлектронныйОбменСБанкамиПоЗарплатнымПроектам",
// если хоть в одном зарплатном проекте установлен флаг "ИспользоватьЭлектронныйДокументооборотСБанком",
// иначе отключает функциональную опцию.
//
Процедура УстановитьИспользованиеЭлектронногоОбменаСБанкамиПриЗаписи(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьИспользованиеЭлектронногоОбменаСБанками();
	
КонецПроцедуры

// Получает первую систему расчетов по банковским картам из зарплатного проекта.
//
// Параметры:
//		ЗарплатныйПроект
//
// Возвращаемое значение:
//		СистемаРасчетовПоБанковскимКартам.
//		Если система расчетов по банковским картам не найдена - возвращается Неопределено.
//
Функция СистемаРасчетовПоБанковскимКартам(ЗарплатныйПроект)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗарплатныеПроектыСистемыРасчетовПоБанковскимКартам.Ссылка КАК ЗарплатныйПроект,
	|	ЗарплатныеПроектыСистемыРасчетовПоБанковскимКартам.СистемаРасчетовПоБанковскимКартам КАК СистемаРасчетовПоБанковскимКартам
	|ИЗ
	|	Справочник.ЗарплатныеПроекты.СистемыРасчетовПоБанковскимКартам КАК ЗарплатныеПроектыСистемыРасчетовПоБанковскимКартам
	|ГДЕ
	|	ЗарплатныеПроектыСистемыРасчетовПоБанковскимКартам.Ссылка = &ЗарплатныйПроект
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗарплатныеПроектыСистемыРасчетовПоБанковскимКартам.НомерСтроки";
	
	Запрос.УстановитьПараметр("ЗарплатныйПроект", ЗарплатныйПроект);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.СистемаРасчетовПоБанковскимКартам;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Возвращает таблицу значений с физическими лицами по номерам лицевых счетов.
//
// Параметры:
//		ЛицевыеСчета - Таблица значений с номерами лицевых счетов, для которых требуется найти физических лиц.
//				НомерЛицевогоСчета - строка, номер лицевого счета.
//		Организация - Организация, по которой отбираются лицевые счета.
//		ЗарплатныйПроект - Зарплатный проект, по которому отбираются лицевые счета.
//
// Возвращаемое значение:
//		Таблица значений
//			НомерЛицевогоСчета - строка, номер лицевого счета.
//			ФизическоеЛицо - ссылка на физическое лицо, которому принадлежит номер лицевого счета.
//
Функция ФизическиеЛицаПоНомерамЛицевыхСчетов(ЛицевыеСчета, Организация, ЗарплатныйПроект = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ЛицевыеСчета", ЛицевыеСчета);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ЗарплатныйПроект", ?(ЗарплатныйПроект = Неопределено,
					Справочники.ЗарплатныеПроекты.ПустаяСсылка(), ЗарплатныйПроект));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЛицевыеСчета.НомерЛицевогоСчета
	|ПОМЕСТИТЬ ВТЛицевыеСчета
	|ИЗ
	|	&ЛицевыеСчета КАК ЛицевыеСчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЛицевыеСчета.НомерЛицевогоСчета КАК НомерЛицевогоСчета,
	|	ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	ВТЛицевыеСчета КАК ЛицевыеСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам КАК ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам
	|		ПО (ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.Организация = &Организация)
	|			И ЛицевыеСчета.НомерЛицевогоСчета = ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.НомерЛицевогоСчета
	|			И (ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.ЗарплатныйПроект = &ЗарплатныйПроект
	|				ИЛИ &ЗарплатныйПроект = ЗНАЧЕНИЕ(Справочник.ЗарплатныеПроекты.ПустаяСсылка))
	|
	|СГРУППИРОВАТЬ ПО
	|	ЛицевыеСчета.НомерЛицевогоСчета,
	|	ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.ФизическоеЛицо";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Возвращает таблицу значений с физическими лицами по документам удостоверяющим личность.
//
// Параметры:
//		ДанныеДокументов - таблица значений, в которой находятся данные документов удостоверяющих личность.
//				ДокументВид - наименование вида документа.
//				КодВидаДокумента - код МВД документа.
//				ДокументСерия - серия документа.
//				ДокументНомер - номер документа.
//
// Возвращаемое значение:
//		Таблица значений
//			ДокументВид - наименование вида документа.
//			КодВидаДокумента - код МВД документа.
//			ДокументСерия - серия документа.
//			ДокументНомер - номер документа.
//			ФизическоеЛицо - ссылка на физическое лицо, которому принадлежит документ удостоверяющий личность.
//
Функция ФизическиеЛицаПоДокументамУдостоверяющимЛичность(ПервичныйДокумент, ДанныеДокументов) Экспорт
	
	Если ДанныеДокументов.Колонки.Найти("ДокументНомерСерия") = Неопределено Тогда
		ДанныеДокументов.Колонки.Добавить("ДокументНомерСерия", ОбщегоНазначения.ОписаниеТипаСтрока(28));
	КонецЕсли;
	Для Каждого СтрокаТаблицы Из ДанныеДокументов Цикл
		СтрокаТаблицы.ДокументНомерСерия = СтрокаТаблицы.ДокументСерия + СтрокаТаблицы.ДокументНомер;
		СтрокаТаблицы.ДокументНомерСерия = СтрЗаменить(СтрокаТаблицы.ДокументНомерСерия, " ", "");
	КонецЦикла;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ДанныеДокументов", ДанныеДокументов);
	Запрос.УстановитьПараметр("ПервичныйДокумент", ПервичныйДокумент);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокументов.ДокументВид КАК ДокументВид,
	|	ДанныеДокументов.КодВидаДокумента КАК КодВидаДокумента,
	|	ДанныеДокументов.ДокументНомерСерия КАК ДокументНомерСерия,
	|	ДанныеДокументов.ДокументСерия КАК ДокументСерия,
	|	ДанныеДокументов.ДокументНомер КАК ДокументНомер
	|ПОМЕСТИТЬ ВТДанныеДокументов
	|ИЗ
	|	&ДанныеДокументов КАК ДанныеДокументов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокументов.ДокументВид КАК ДокументВид,
	|	ДанныеДокументов.КодВидаДокумента КАК КодВидаДокумента,
	|	ДанныеДокументов.ДокументСерия КАК ДокументСерия,
	|	ДанныеДокументов.ДокументНомер КАК ДокументНомер,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	ВТДанныеДокументов КАК ДанныеДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыДокументовФизическихЛиц КАК ВидыДокументовФизическихЛиц
	|		ПО (ДанныеДокументов.КодВидаДокумента = ВидыДокументовФизическихЛиц.КодМВД
	|				ИЛИ ДанныеДокументов.ДокументВид = ВидыДокументовФизическихЛиц.Наименование)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.Сотрудники КАК ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников
	|		ПО (ВидыДокументовФизическихЛиц.Ссылка = ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.ДокументВид)
	|			И ДанныеДокументов.ДокументНомерСерия = ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.ДокументНомерСерия
	|ГДЕ
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.Ссылка = &ПервичныйДокумент
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокументов.ДокументВид,
	|	ДанныеДокументов.КодВидаДокумента,
	|	ДанныеДокументов.ДокументСерия,
	|	ДанныеДокументов.ДокументНомер,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.ФизическоеЛицо";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Возвращает массив описаний передаваемых файлов.
//
// Параметры:
//		АдресХранилища - Строка - адрес временного хранилища, куда помещен массив документов, по которым выгружаются файлы.
//
// Возвращаемое значение:
//		ПолучаемыеФайлы - Массив - описания передаваемых файлов (см. ОписаниеПередаваемогоФайла).
//
Функция МассивОписанийПередаваемыхФайлов(АдресХранилища, УникальныйИдентификаторФормы) Экспорт
	
	ПолучаемыеФайлы = Новый Массив;
	МассивОписанийФайлов = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	Если ТипЗнч(МассивОписанийФайлов) <> Тип("Массив") Или МассивОписанийФайлов.Количество() = 0 Тогда
		Возврат ПолучаемыеФайлы;
	КонецЕсли;
	
	Для Каждого ОписаниеФайла Из МассивОписанийФайлов Цикл
		
		Если ОписаниеФайла = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		АдресФайлаВоВременномХранилище = ПоместитьВоВременноеХранилище(ОписаниеФайла.ДвоичныеДанные, УникальныйИдентификаторФормы);
		ПолучаемыйФайл = Новый ОписаниеПередаваемогоФайла(ОписаниеФайла.ИмяФайла, АдресФайлаВоВременномХранилище);
		ПолучаемыеФайлы.Добавить(ПолучаемыйФайл);
		
	КонецЦикла;
	
	Возврат ПолучаемыеФайлы;
	
КонецФункции

// Заполняет список выбора у элемента формы системами расчетов по банковским картам зарплатного проекта.
//
// Параметры:
//		ЗарплатныйПроект - зарплатный проект, по которому получаются системы расчетов по банковским картам.
//		Элементы - Элементы формы
//		ИмяЭлемента - Наименование элемента формы, в котором заполняется список выбора.
//
Процедура ЗаполнитьСписокСистемРасчетов(ЗарплатныйПроект, Элементы, ИмяЭлемента) Экспорт
	
	ЭлементФормы = Элементы.Найти(ИмяЭлемента);
	Если ЭлементФормы = Неопределено  Тогда
		Возврат;
	КонецЕсли;
	
	СписокВыбора = ЭлементФормы.СписокВыбора;
	СписокВыбора.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗарплатныеПроектыСистемыРасчетовПоБанковскимКартам.СистемаРасчетовПоБанковскимКартам КАК СистемаРасчетовПоБанковскимКартам
	|ИЗ
	|	Справочник.ЗарплатныеПроекты.СистемыРасчетовПоБанковскимКартам КАК ЗарплатныеПроектыСистемыРасчетовПоБанковскимКартам
	|ГДЕ
	|	ЗарплатныеПроектыСистемыРасчетовПоБанковскимКартам.Ссылка = &ЗарплатныйПроект";
	
	Запрос.УстановитьПараметр("ЗарплатныйПроект", ЗарплатныйПроект);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокВыбора.Добавить(Выборка.СистемаРасчетовПоБанковскимКартам);
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает видимость элементов в коллекции по доступности в зарплатном проекте.
//
// Параметры:
//		ФорматФайла - Формат файла зарплатного проекта, для которого получаются доступные элементы.
//		ЭлементыКоллекции - элементы коллекции, которым нужно установить видимость.
//		НаименованиеКоллекции - наименование коллекции формы, в которой присутствуют доступные элементы.
//		ДоступныеЭлементыЗарплатногоПроекта - доступные элементы зарплатного проекта.
//
Процедура УстановитьВидимостьЭлементов(ФорматФайла, ЭлементыКоллекции, НаименованиеКоллекции, ДоступныеЭлементыЗарплатногоПроекта = Неопределено) Экспорт
	
	Если ДоступныеЭлементыЗарплатногоПроекта = Неопределено Тогда
		ДоступныеЭлементыЗарплатногоПроекта = ДоступныеЭлементыЗарплатногоПроекта(ФорматФайла, НаименованиеКоллекции);
	КонецЕсли;
	
	Для Каждого ЭлементФормы Из ЭлементыКоллекции Цикл
		
		Если ТипЗнч(ЭлементФормы) = Тип("КнопкаФормы") Тогда
			Продолжить;
		ИначеЕсли ТипЗнч(ЭлементФормы) = Тип("ГруппаФормы")
			Или ТипЗнч(ЭлементФормы) = Тип("ТаблицаФормы") Тогда
			
			УстановитьВидимостьЭлементов(ФорматФайла, ЭлементФормы.ПодчиненныеЭлементы, НаименованиеКоллекции, ДоступныеЭлементыЗарплатногоПроекта);
			
			Продолжить;
			
		КонецЕсли;
		
		Если ДоступныеЭлементыЗарплатногоПроекта.Найти(ЭлементФормы.Имя) = Неопределено Тогда
			ЭлементФормы.Видимость = Ложь;
		Иначе
			ЭлементФормы.Видимость = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция СоответствиеФорматовФайла() Экспорт
	
	СоответствиеФорматаФайла = Новый Соответствие;
	СоответствиеФорматаФайла.Вставить(Перечисления.ФорматыФайловОбменаПоЗарплатномуПроекту.Версия1, 1);
	СоответствиеФорматаФайла.Вставить(Перечисления.ФорматыФайловОбменаПоЗарплатномуПроекту.Версия3, 3);
	СоответствиеФорматаФайла.Вставить(Перечисления.ФорматыФайловОбменаПоЗарплатномуПроекту.Версия32, 3.2);
	СоответствиеФорматаФайла.Вставить(Перечисления.ФорматыФайловОбменаПоЗарплатномуПроекту.Версия33, 3.3);
	СоответствиеФорматаФайла.Вставить(Перечисления.ФорматыФайловОбменаПоЗарплатномуПроекту.Версия34, 3.4);
	СоответствиеФорматаФайла.Вставить(Перечисления.ФорматыФайловОбменаПоЗарплатномуПроекту.Версия35, 3.5);
	
	Возврат СоответствиеФорматаФайла;
	
КонецФункции

// Получает доступные элементы для зарплатного проекта.
//
// Параметры:
//		ФорматФайла - Формат файла зарплатного проекта, для которого получаются доступные элементы.
//		НаименованиеКоллекции - наименование коллекции формы, в которой присутствуют доступные элементы.
//
// Возвращаемое значение
//		МассивЭлементов - массив с доступными элементами.
//
Функция ДоступныеЭлементыЗарплатногоПроекта(ФорматФайла, НаименованиеКоллекции)
	
	СоответствиеФорматаФайла = СоответствиеФорматовФайла();
	
	МассивЭлементов = Новый Массив;
	
	МассивЭлементов.Добавить(НаименованиеКоллекции + "НомерСтроки");
	МассивЭлементов.Добавить(НаименованиеКоллекции + "ФизическоеЛицо");
	МассивЭлементов.Добавить(НаименованиеКоллекции + "ЭмбоссированныйТекст1");
	МассивЭлементов.Добавить(НаименованиеКоллекции + "ЭмбоссированныйТекст2");
	МассивЭлементов.Добавить(НаименованиеКоллекции + "ЭмбоссированныйТекст3");
	МассивЭлементов.Добавить(НаименованиеКоллекции + "СистемаРасчетовПоБанковскимКартам");
	МассивЭлементов.Добавить(НаименованиеКоллекции + "СуммаПервоначальногоПополнения");
	МассивЭлементов.Добавить(НаименованиеКоллекции + "КодВидаВклада");
	МассивЭлементов.Добавить(НаименованиеКоллекции + "КодПодвидаВклада");
	МассивЭлементов.Добавить(НаименованиеКоллекции + "ВалютаВклада");
	МассивЭлементов.Добавить("ДокументыФизическихЛицВидДокумента");
	МассивЭлементов.Добавить("ДокументыФизическихЛицСерия");
	МассивЭлементов.Добавить("ДокументыФизическихЛицНомер");
	МассивЭлементов.Добавить("ДокументыФизическихЛицКемВыдан");
	МассивЭлементов.Добавить("ДокументыФизическихЛицДатаВыдачи");
	МассивЭлементов.Добавить("ДокументыФизическихЛицСрокДействия");
	МассивЭлементов.Добавить("ДокументыФизическихЛицКодПодразделения");
	МассивЭлементов.Добавить(НаименованиеКоллекции + "ДокументУдостоверяющийЛичность");
	МассивЭлементов.Добавить(НаименованиеКоллекции + "Пол");
	МассивЭлементов.Добавить(НаименованиеКоллекции + "ДатаРождения");
	МассивЭлементов.Добавить(НаименованиеКоллекции + "МестоРождения");
	МассивЭлементов.Добавить(НаименованиеКоллекции + "АдресМестаРаботы");
	МассивЭлементов.Добавить(НаименованиеКоллекции + "АдресПоПрописке");
	МассивЭлементов.Добавить(НаименованиеКоллекции + "АдресМестаПроживания");
	МассивЭлементов.Добавить(НаименованиеКоллекции + "ТелефонДомашний");
	МассивЭлементов.Добавить(НаименованиеКоллекции + "ТелефонРабочий");
	
	МассивЭлементов.Добавить(НаименованиеКоллекции + "Состояние");
	МассивЭлементов.Добавить(НаименованиеКоллекции + "Команда");
	МассивЭлементов.Добавить(НаименованиеКоллекции + "СводныеСведенияКартинка");
	МассивЭлементов.Добавить(НаименованиеКоллекции + "СводныеСведения");
	
	Если СоответствиеФорматаФайла.Получить(ФорматФайла) >= 3 Тогда
		
		МассивЭлементов.Добавить(НаименованиеКоллекции + "Должность");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ЯвляетсяЗарплатнойКартой");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ОплатаЗарплатнойКарты");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ЯвляетсяРезидентом");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "Гражданство");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "КатегорияСотрудника");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ИспользуетсяОвердрафт");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ЯвляетсяСотрудникомБанка");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ЯвляетсяУчастникомБонуснойПрограммы");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "БонуснаяПрограмма");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "НомерУчастникаБонуснойПрограммы");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ТарифСледующийГод");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ТарифТекущийГод");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "СпособРассылкиОтчета");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "АдресЭлектроннойПочты");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "СчетДебета");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "НомерМобильногоТелефона");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ОператорСвязи");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ИспользованиеМобильногоБанка");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ТарифМобильногоБанка");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "РазрешитьПередачуИнформацииВБКИ");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "КонтрольнаяИнформация");
		
	КонецЕсли;
	
	Если СоответствиеФорматаФайла.Получить(ФорматФайла) >= 3.2 Тогда
		
		МассивЭлементов.Добавить(НаименованиеКоллекции + "НомерМиграционнойКарты");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ДатаНачалаПребыванияМиграционнойКарты");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ДатаОкончанияПребыванияМиграционнойКарты");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ВидМиграционногоДокумента");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "НомерМиграционногоДокумента");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ДатаНачалаПребыванияМиграционногоДокумента");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ДатаОкончанияПребыванияМиграционногоДокумента");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ТабельныйНомер");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ДатаПриема");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ПрогнозируемыйМесячныйДоход");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ДеньВыплатыЗарплаты");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "АдресДляИнформирования");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ИспользованиеДоставкиКорреспонденции");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "СпособДоставкиЭлектроннойПочтой");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "СпособДоставкиПочтой");
		
	КонецЕсли;
	
	Если СоответствиеФорматаФайла.Получить(ФорматФайла) >= 3.3 Тогда
		
		МассивЭлементов.Добавить(НаименованиеКоллекции + "Сотрудник");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ДатаЗакрытия");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ЛицевойСчет");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "УволенныхСводныеСведенияКартинка");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "УволенныхСводныеСведения");
		
	КонецЕсли;
	
	Если СоответствиеФорматаФайла.Получить(ФорматФайла) >= 3.4 Тогда
		
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ИдентификаторДизайна");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ПВК");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ПриложениеКартаКод");
		МассивЭлементов.Добавить(НаименованиеКоллекции + "ПриложениеКартаПараметры");
		
	КонецЕсли;
	
	Если СоответствиеФорматаФайла.Получить(ФорматФайла) >= 3.5 Тогда
		
		МассивЭлементов.Добавить(НаименованиеКоллекции + "СтраховойНомерПФР");
		
	КонецЕсли;
	
	Возврат МассивЭлементов;
	
КонецФункции

// Регистрирует лицевые счета по физическим лицам.
//
// Параметры:
//		ДанныеЛицевыхСчетов - Таблица значений с колонками
//				МесяцОткрытия
//				Организация
//				ФизическоеЛицо
//				НомерЛицевогоСчета
//				ЗарплатныйПроект
//				ДокументОснование
//
Процедура ЗарегистрироватьЛицевыеСчетаФизическихЛиц(ДанныеЛицевыхСчетов) Экспорт
	
	Для каждого ДанныеФизическогоЛица Из ДанныеЛицевыхСчетов Цикл
		
		НаборЗаписей = РегистрыСведений.ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Период.Установить(ДанныеФизическогоЛица.МесяцОткрытия);
		НаборЗаписей.Отбор.Организация.Установить(ДанныеФизическогоЛица.Организация);
		НаборЗаписей.Отбор.ФизическоеЛицо.Установить(ДанныеФизическогоЛица.ФизическоеЛицо);
		
		Если ЗначениеЗаполнено(ДанныеФизическогоЛица.ЗарплатныйПроект)
			И Не ПустаяСтрока(ДанныеФизическогоЛица.НомерЛицевогоСчета) Тогда
			НаборЗаписей.Прочитать();
			Если НаборЗаписей.Количество() = 0 Тогда
				Запись = НаборЗаписей.Добавить();
			Иначе
				Запись = НаборЗаписей[НаборЗаписей.Количество() - 1];
			КонецЕсли;
			// Перезаписываем записи, созданные документами, только записями, которые также были созданы документами.
			Если ЗначениеЗаполнено(Запись.ДокументОснование) И Не ЗначениеЗаполнено(ДанныеФизическогоЛица.ДокументОснование) Тогда
				Продолжить;
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(Запись, ДанныеФизическогоЛица);
			
			Запись.Период = ДанныеФизическогоЛица.МесяцОткрытия;
		КонецЕсли;
		
		НаборЗаписей.Записать(Истина);
		
	КонецЦикла;
	
КонецПроцедуры

// Отменяет регистрацию лицевых счетов по физическим лицам.
//
// Параметры:
//		ДокументОснование - Документ, по которому отменяется регистрация лицевых счетов.
//
Процедура ОтменитьРегистрациюЛицевыхСчетовФизическихЛиц(ДокументОснование) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.Период КАК МесяцОткрытия,
	|	ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.Организация,
	|	ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.ФизическоеЛицо,
	|	ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.Банк,
	|	ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.НомерЛицевогоСчета,
	|	ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.ЗарплатныйПроект,
	|	ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.ДокументОснование
	|ИЗ
	|	РегистрСведений.ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам КАК ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам
	|ГДЕ
	|	ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.ДокументОснование = &ДокументОснование";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Период.Установить(Выборка.МесяцОткрытия);
		НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
		НаборЗаписей.Отбор.ФизическоеЛицо.Установить(Выборка.ФизическоеЛицо);
		
		НаборЗаписей.ДополнительныеСвойства.Вставить("ОтменаРегистрацииЛицевыхСчетов", Истина);
		
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает сообщение об ошибке в номере лицевого счета сотрудника,
// если номер лицевого счета не соответствует стандарту.
//
// Параметры:
//		ФизическоеЛицо - Физическое лицо, чей лицевой счет проверяется.
//		НомерЛицевогоСчета - Строка, номер лицевого счета сотрудника.
//		ЗарплатныйПроект - зарплатный проект, в котором открыт лицевой счет.
//
Функция СообщениеПроверкиНомераЛицевогоСчетаНаСоответствиеТребованиям(ФизическоеЛицо, ЗарплатныйПроект, Знач НомерЛицевогоСчета)
	
	Если Не ЗначениеЗаполнено(ЗарплатныйПроект) Тогда
		ТекстОшибки = НСтр("ru = 'Не указан зарплатный проект.'");
		Возврат ТекстОшибки;
	КонецЕсли;
	
	Если СтрДлина(СокрЛП(НомерЛицевогоСчета)) <> 20 Или Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(НомерЛицевогоСчета) Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'У сотрудника %1 неверный номер лицевого счета (%2), он должен содержать 20 цифр.'"),
				ФизическоеЛицо,
				СокрЛП(НомерЛицевогоСчета));
		Возврат ТекстОшибки;
	КонецЕсли;
	
	СоответствиеСимволов = Новый Соответствие;
	// латиница
	СоответствиеСимволов.Вставить("A", 0);
	СоответствиеСимволов.Вставить("B", 1);
	СоответствиеСимволов.Вставить("C", 2);
	СоответствиеСимволов.Вставить("E", 3);
	СоответствиеСимволов.Вставить("H", 4);
	СоответствиеСимволов.Вставить("K", 5);
	СоответствиеСимволов.Вставить("M", 6);
	СоответствиеСимволов.Вставить("P", 7);
	СоответствиеСимволов.Вставить("T", 8);
	СоответствиеСимволов.Вставить("X", 9);
	
	ШестойРазряд = Сред(НомерЛицевогоСчета, 6, 1);
	Если СоответствиеСимволов[ШестойРазряд] <> Неопределено Тогда
		НомерЛицевогоСчета = Лев(НомерЛицевогоСчета, 5) + СоответствиеСимволов[ШестойРазряд] + Сред(НомерЛицевогоСчета, 7);
	КонецЕсли;
	
	БИК = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗарплатныйПроект, "Банк.Код");
	Если БИК = Null Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не указан банк в зарплатном проекте ""%1"".'"),
				ЗарплатныйПроект);
		Возврат ТекстОшибки;
	КонецЕсли;
	
	УсловныйНомер = Прав(БИК, 3);
	Если УсловныйНомер = "000" Или УсловныйНомер = "001" Или УсловныйНомер = "002" Тогда
		// Это БИК РКЦ
		УсловныйНомер = "0" + Сред(БИК, 5, 2);
	КонецЕсли;
	
	МассивВесовыхКоэффициентов = Новый Массив;
	МассивВесовыхКоэффициентов.Добавить(7);
	МассивВесовыхКоэффициентов.Добавить(1);
	МассивВесовыхКоэффициентов.Добавить(3);
	
	Коэффициент = 0;
	СуммаМладшихРазрядов = 0;
	Для Шаг = 1 По 3 Цикл
		
		ЦифраРазряда = Число(Сред(УсловныйНомер, Шаг, 1));
		
		Произведение = ЦифраРазряда * МассивВесовыхКоэффициентов.Получить(Коэффициент);
		МладшийРазряд = Произведение - Цел(Произведение/10)*10;
		СуммаМладшихРазрядов = СуммаМладшихРазрядов + МладшийРазряд;
		
		Коэффициент = Коэффициент + 1;
		Если Коэффициент = 3 Тогда
			Коэффициент = 0;
		КонецЕсли;
		
	КонецЦикла;
	
	Для Шаг = 1 По МИН(СтрДлина(НомерЛицевогоСчета), 20) Цикл
		
		ЦифраРазряда = Число(Сред(НомерЛицевогоСчета, Шаг, 1));
		
		Произведение = ЦифраРазряда * МассивВесовыхКоэффициентов.Получить(Коэффициент);
		МладшийРазряд = Произведение - Цел(Произведение/10)*10;
		СуммаМладшихРазрядов = СуммаМладшихРазрядов + МладшийРазряд;
		
		Коэффициент = Коэффициент + 1;
		Если Коэффициент = 3 Тогда
			Коэффициент = 0;
		КонецЕсли;
		
	КонецЦикла;
	
	Если СуммаМладшихРазрядов <> Цел(СуммаМладшихРазрядов/10)*10 Тогда
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'У сотрудника %1 указан неверный номер лицевого счета (%2), либо указан неверный БИК банка.
					|Возможно, лицевой счет открывался в другом отделении банка.'"),
				ФизическоеЛицо,
				СокрЛП(НомерЛицевогоСчета));
		
		Возврат ТекстОшибки;
		
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

Процедура ЗарегистрироватьИзмененияЛицевыхСчетов(ЛицевыеСчетаФизическихЛиц, Организация, ДатаНачала) Экспорт
	
	ОбменСБанкамиПоЗарплатнымПроектамВнутренний.ЗарегистрироватьИзмененияЛицевыхСчетов(ЛицевыеСчетаФизическихЛиц, Организация, ДатаНачала);
	
КонецПроцедуры

Функция ДеньВыплатыЗарплатыВОрганизации(Организация)
	Возврат ОбменСБанкамиПоЗарплатнымПроектамВнутренний.ДеньВыплатыЗарплатыВОрганизации(Организация);
КонецФункции

// Регистрирует состояние Заявки на открытие лицевых счетов.
//
// Параметры:
//		ДокументОткрытияЛицевыхСчетов - Ссылка на Заявку на открытие лицевых счетов, для которой требуется установить состояние.
//		Отказ - Булево - Истина, если по документу получено подтверждение, иначе Ложь
//		Состояние - Устанавливаемое состояние по документу
//		ПодтверждениеОткрытияЛицевыхСчетов - Ссылка на документ-подтверждение.
//
Процедура ЗарегистрироватьСостояниеОткрытияЛицевыхСчетов(ДокументОткрытияЛицевыхСчетов, Отказ, Состояние = Неопределено, ПодтверждениеОткрытияЛицевыхСчетов = Неопределено) Экспорт
	
	МетаданныеЗаявкиНаОткрытиеЛицевыхСчетов = Метаданные.Документы.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников;
	МетаданныеДокумента = ДокументОткрытияЛицевыхСчетов.Метаданные();
	Если МетаданныеДокумента.ПолноеИмя() <> МетаданныеЗаявкиНаОткрытиеЛицевыхСчетов.ПолноеИмя() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ДокументОткрытияЛицевыхСчетов", ДокументОткрытияЛицевыхСчетов);
	Запрос.УстановитьПараметр("ПодтверждениеОткрытияЛицевыхСчетов", ПодтверждениеОткрытияЛицевыхСчетов);
	ХешФайла = ?(ПодтверждениеОткрытияЛицевыхСчетов = Неопределено, "", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПодтверждениеОткрытияЛицевыхСчетов, "ХешФайла"));
	Запрос.УстановитьПараметр("ХешФайла", ХешФайла);
	Запрос.УстановитьПараметр("Состояние", ?(Состояние = Неопределено, Перечисления.СостояниеЗаявкиНаОткрытиеЛицевогоСчетаСотрудника.ПустаяСсылка(), Состояние));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.Ссылка КАК ДокументОткрытияЛицевыхСчетов,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ПодтверждениеОткрытияЛицевыхСчетовСотрудников.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПодтверждениеПолучено,
	|	ВЫБОР
	|		КОГДА &Состояние = ЗНАЧЕНИЕ(Перечисление.СостояниеЗаявкиНаОткрытиеЛицевогоСчетаСотрудника.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.Проведен
	|							И ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.ЗарплатныйПроект <> ЗНАЧЕНИЕ(Справочник.ЗарплатныеПроекты.ПустаяСсылка)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеЗаявкиНаОткрытиеЛицевогоСчетаСотрудника.ГотовКВыгрузке)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостояниеЗаявкиНаОткрытиеЛицевогоСчетаСотрудника.ПустаяСсылка)
	|				КОНЕЦ
	|		ИНАЧЕ &Состояние
	|	КОНЕЦ КАК Состояние
	|ИЗ
	|	Документ.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников КАК ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПодтверждениеОткрытияЛицевыхСчетовСотрудников КАК ПодтверждениеОткрытияЛицевыхСчетовСотрудников
	|		ПО (ПодтверждениеОткрытияЛицевыхСчетовСотрудников.ПервичныйДокумент = ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.Ссылка)
	|			И (ПодтверждениеОткрытияЛицевыхСчетовСотрудников.Проведен)
	|			И (ПодтверждениеОткрытияЛицевыхСчетовСотрудников.Ссылка <> &ПодтверждениеОткрытияЛицевыхСчетов)
	|			И (ПодтверждениеОткрытияЛицевыхСчетовСотрудников.ХешФайла = &ХешФайла)
	|ГДЕ
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.Ссылка = &ДокументОткрытияЛицевыхСчетов";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ПодтверждениеПолучено Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'По документу %1 получено подтверждение из банка. Изменение документа невозможно.'"),
				Выборка.ДокументОткрытияЛицевыхСчетов);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Выборка.ДокументОткрытияЛицевыхСчетов, , , Отказ);
			Прервать;
		КонецЕсли;
		
		МенеджерЗаписи = РегистрыСведений.СостоянияДокументовОткрытияЛицевыхСчетов.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ДокументОткрытияЛицевыхСчетов = Выборка.ДокументОткрытияЛицевыхСчетов;
		МенеджерЗаписи.Прочитать();
		Если ЗначениеЗаполнено(Выборка.Состояние) Тогда
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
			МенеджерЗаписи.Записать();
		Иначе
			Если МенеджерЗаписи.Выбран() Тогда
				МенеджерЗаписи.Удалить();
			КонецЕсли	
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Регистрирует состояние ведомости или платежного документа.
//
// Параметры:
//		ДокументЗачисленияЗарплаты - Ссылка на ведомость или платежный документ, для которого требуется установить
//									состояние.
//		Отказ - Булево - Истина, если по документу получено подтверждение, иначе Ложь
//		Состояние - Устанавливаемое состояние по документу
//		ДанныеРеестра - Структура
//				Год - год даты реестра
//				НомерРеестра - номер выгружаемого реестра
//		ПодтверждениеЗачисленияЗарплаты - Ссылка на документ-подтверждение.
//
Процедура ЗарегистрироватьСостояниеЗачисленияЗарплатыПоДокументу(ДокументЗачисленияЗарплаты, Отказ, Состояние = Неопределено, ДанныеРеестра = Неопределено, ПодтверждениеЗачисленияЗарплаты = Неопределено) Экспорт
	
	МетаданныеПлатежногоДокумента = МетаданныеПлатежногоДокументаПеречисленияЗарплаты();
	МетаданныеДокумента = ДокументЗачисленияЗарплаты.Метаданные();
	
	ВедомостьВБанкТипы = Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ВедомостьВБанкЗарплатаКадры.Тип);
	ЭтоВедомостьВБанк = ВедомостьВБанкТипы.СодержитТип(ТипЗнч(ДокументЗачисленияЗарплаты));
	
	Если (МетаданныеПлатежногоДокумента = Неопределено Или МетаданныеДокумента.ПолноеИмя() <> МетаданныеПлатежногоДокумента.ПолноеИмя())
		И Не ЭтоВедомостьВБанк Тогда 
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ДокументЗачисленияЗарплаты", ДокументЗачисленияЗарплаты);
	Запрос.УстановитьПараметр("ПодтверждениеЗачисленияЗарплаты", ПодтверждениеЗачисленияЗарплаты);
	ХешФайла = ?(ПодтверждениеЗачисленияЗарплаты = Неопределено, "", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПодтверждениеЗачисленияЗарплаты, "ХешФайла"));
	Запрос.УстановитьПараметр("ХешФайла", ХешФайла);
	Запрос.УстановитьПараметр("Состояние", ?(Состояние = Неопределено, Перечисления.СостояниеЗачисленияЗарплаты.ПустаяСсылка(), Состояние));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументЗачисленияЗарплаты.Ссылка КАК ДокументЗачисленияЗарплаты,
	|	ДокументЗачисленияЗарплаты.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ПодтверждениеЗачисленияЗарплаты.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПодтверждениеПолучено,
	|	ВЫБОР
	|		КОГДА &Состояние = ЗНАЧЕНИЕ(Перечисление.СостояниеЗачисленияЗарплаты.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА ДокументЗачисленияЗарплаты.Проведен
	|							И ДокументЗачисленияЗарплаты.ЗарплатныйПроект <> ЗНАЧЕНИЕ(Справочник.ЗарплатныеПроекты.ПустаяСсылка)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеЗачисленияЗарплаты.ГотовКВыгрузке)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостояниеЗачисленияЗарплаты.ПустаяСсылка)
	|				КОНЕЦ
	|		ИНАЧЕ &Состояние
	|	КОНЕЦ КАК Состояние
	|ИЗ
	|	#ДокументЗачисленияЗарплаты КАК ДокументЗачисленияЗарплаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПодтверждениеЗачисленияЗарплаты КАК ПодтверждениеЗачисленияЗарплаты
	|		ПО (ПодтверждениеЗачисленияЗарплаты.ПервичныйДокумент = ДокументЗачисленияЗарплаты.Ссылка)
	|			И (ПодтверждениеЗачисленияЗарплаты.Проведен)
	|			И (ПодтверждениеЗачисленияЗарплаты.ХешФайла = &ХешФайла)
	|			И (ПодтверждениеЗачисленияЗарплаты.Ссылка <> &ПодтверждениеЗачисленияЗарплаты)
	|ГДЕ
	|	ДокументЗачисленияЗарплаты.Ссылка = &ДокументЗачисленияЗарплаты";
	
	ИмяОбъекта = МетаданныеДокумента.ПолноеИмя();
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ДокументЗачисленияЗарплаты", ИмяОбъекта);
	Если МетаданныеПлатежногоДокумента <> Неопределено И МетаданныеПлатежногоДокумента.ПолноеИмя() = МетаданныеДокумента.ПолноеИмя() Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДокументЗачисленияЗарплаты.ЗарплатныйПроект <> ЗНАЧЕНИЕ(Справочник.ЗарплатныеПроекты.ПустаяСсылка)",
			"РеквизитыПлатежныхДокументовПеречисленияЗарплаты.ЗарплатныйПроект <> ЗНАЧЕНИЕ(Справочник.ЗарплатныеПроекты.ПустаяСсылка)
			|							И РеквизитыПлатежныхДокументовПеречисленияЗарплаты.ЗарплатныйПроект ЕСТЬ НЕ NULL");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И (ПодтверждениеЗачисленияЗарплаты.Ссылка <> &ПодтверждениеЗачисленияЗарплаты)",
			"И (ПодтверждениеЗачисленияЗарплаты.Ссылка <> &ПодтверждениеЗачисленияЗарплаты)
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеквизитыПлатежныхДокументовПеречисленияЗарплаты КАК РеквизитыПлатежныхДокументовПеречисленияЗарплаты
			|		ПО ДокументЗачисленияЗарплаты.Ссылка = РеквизитыПлатежныхДокументовПеречисленияЗарплаты.ПлатежныйДокумент");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ПодтверждениеПолучено Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'По документу %1 получено подтверждение из банка. Изменение документа невозможно.'"),
				Выборка.ДокументЗачисленияЗарплаты);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Выборка.ДокументЗачисленияЗарплаты, , , Отказ);
			Прервать;
		КонецЕсли;
		
		МенеджерЗаписи = РегистрыСведений.СостоянияДокументовЗачисленияЗарплаты.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ДокументЗачисленияЗарплаты = Выборка.ДокументЗачисленияЗарплаты;
		МенеджерЗаписи.Прочитать();
		Если ЗначениеЗаполнено(Выборка.Состояние) Тогда
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
			Если ДанныеРеестра <> Неопределено Тогда 
				ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ДанныеРеестра);
			КонецЕсли;
			МенеджерЗаписи.Записать();
		Иначе
			Если МенеджерЗаписи.Выбран() Тогда
				МенеджерЗаписи.Удалить();
			КонецЕсли	
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВедомостьНаВыплатуЗарплатыВБанкПередЗаписью(ВедомостьОбъект) Экспорт
	
	Если Не ЗначениеЗаполнено(ВедомостьОбъект.ЗарплатныйПроект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВедомостьОбъект.НомерРеестра) Тогда
		СтруктураВедомостьОбъект = Новый Структура;
		СтруктураВедомостьОбъект.Вставить("Дата", ВедомостьОбъект.Дата);
		СтруктураВедомостьОбъект.Вставить("Номер", ВедомостьОбъект.Номер);
		СтруктураВедомостьОбъект.Вставить("Организация", ВедомостьОбъект.Организация);
		СтруктураВедомостьОбъект.Вставить("НомерРеестра", ВедомостьОбъект.НомерРеестра);
		
		СоответствиеДокументов = Новый Соответствие;
		СоответствиеДокументов.Вставить(ВедомостьОбъект, СтруктураВедомостьОбъект);
		ГодыВыгрузки = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Год(ВедомостьОбъект.Дата));
		НомераРеестров = НомераРеестровДокументов(СоответствиеДокументов, ГодыВыгрузки, Неопределено);
		ДанныеРеестра = НомераРеестров.Получить(ВедомостьОбъект);
		Если ДанныеРеестра <> Неопределено Тогда
			ВедомостьОбъект.НомерРеестра = ДанныеРеестра.НомерРеестра;
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ведомость", ВедомостьОбъект.Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВедомостьНаВыплатуЗарплатыВБанк.Организация КАК Организация,
	|	ВедомостьНаВыплатуЗарплатыВБанк.ЗарплатныйПроект КАК ЗарплатныйПроект,
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СУММА(ВедомостьНаВыплатуЗарплатыВБанкЗарплата.КВыплате) КАК КВыплате,
	|	ВедомостьНаВыплатуЗарплатыВБанкЗарплата.НомерЛицевогоСчета КАК НомерЛицевогоСчета,
	|	ВедомостьНаВыплатуЗарплатыВБанк.Проведен
	|ИЗ
	|	Документ.ВедомостьНаВыплатуЗарплатыВБанк.Зарплата КАК ВедомостьНаВыплатуЗарплатыВБанкЗарплата
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВедомостьНаВыплатуЗарплатыВБанк КАК ВедомостьНаВыплатуЗарплатыВБанк
	|		ПО ВедомостьНаВыплатуЗарплатыВБанкЗарплата.Ссылка = ВедомостьНаВыплатуЗарплатыВБанк.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|		ПО ВедомостьНаВыплатуЗарплатыВБанкЗарплата.Сотрудник = Сотрудники.Ссылка
	|ГДЕ
	|	ВедомостьНаВыплатуЗарплатыВБанкЗарплата.Ссылка = &Ведомость
	|
	|СГРУППИРОВАТЬ ПО
	|	ВедомостьНаВыплатуЗарплатыВБанк.Организация,
	|	ВедомостьНаВыплатуЗарплатыВБанк.ЗарплатныйПроект,
	|	Сотрудники.ФизическоеЛицо,
	|	ВедомостьНаВыплатуЗарплатыВБанкЗарплата.НомерЛицевогоСчета,
	|	ВедомостьНаВыплатуЗарплатыВБанк.Проведен";
	
	ВедомостьОбъект.ДополнительныеСвойства.Вставить("ДанныеВедомостиПередЗаписью", Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ВедомостьНаВыплатуЗарплатыВБанкПриЗаписи(ВедомостьОбъект, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ВедомостьОбъект.ЗарплатныйПроект)
		Или Не ВедомостьОбъект.ДополнительныеСвойства.Свойство("ДанныеВедомостиПередЗаписью") Тогда
		ЗарегистрироватьСостояниеЗачисленияЗарплатыПоДокументу(ВедомостьОбъект.Ссылка, Отказ);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ведомость", ВедомостьОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДанныеВедомостиПередЗаписью", ВедомостьОбъект.ДополнительныеСвойства.ДанныеВедомостиПередЗаписью);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеВедомостиПередЗаписью.Организация КАК Организация,
	|	ДанныеВедомостиПередЗаписью.ЗарплатныйПроект КАК ЗарплатныйПроект,
	|	ДанныеВедомостиПередЗаписью.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеВедомостиПередЗаписью.КВыплате КАК КВыплате,
	|	ДанныеВедомостиПередЗаписью.НомерЛицевогоСчета КАК НомерЛицевогоСчета,
	|	ДанныеВедомостиПередЗаписью.Проведен КАК Проведен
	|ПОМЕСТИТЬ ВТДанныеВедомостиПередЗаписью
	|ИЗ
	|	&ДанныеВедомостиПередЗаписью КАК ДанныеВедомостиПередЗаписью
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВедомостьНаВыплатуЗарплатыВБанк.Организация КАК Организация,
	|	ВедомостьНаВыплатуЗарплатыВБанк.ЗарплатныйПроект КАК ЗарплатныйПроект,
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СУММА(ВедомостьНаВыплатуЗарплатыВБанкЗарплата.КВыплате) КАК КВыплате,
	|	ВедомостьНаВыплатуЗарплатыВБанкЗарплата.НомерЛицевогоСчета КАК НомерЛицевогоСчета,
	|	ВедомостьНаВыплатуЗарплатыВБанк.Проведен КАК Проведен
	|ПОМЕСТИТЬ ВТДанныеВедомостиПриЗаписи
	|ИЗ
	|	Документ.ВедомостьНаВыплатуЗарплатыВБанк.Зарплата КАК ВедомостьНаВыплатуЗарплатыВБанкЗарплата
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВедомостьНаВыплатуЗарплатыВБанк КАК ВедомостьНаВыплатуЗарплатыВБанк
	|		ПО ВедомостьНаВыплатуЗарплатыВБанкЗарплата.Ссылка = ВедомостьНаВыплатуЗарплатыВБанк.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|		ПО ВедомостьНаВыплатуЗарплатыВБанкЗарплата.Сотрудник = Сотрудники.Ссылка
	|ГДЕ
	|	ВедомостьНаВыплатуЗарплатыВБанкЗарплата.Ссылка = &Ведомость
	|
	|СГРУППИРОВАТЬ ПО
	|	ВедомостьНаВыплатуЗарплатыВБанк.Организация,
	|	ВедомостьНаВыплатуЗарплатыВБанк.ЗарплатныйПроект,
	|	Сотрудники.ФизическоеЛицо,
	|	ВедомостьНаВыплатуЗарплатыВБанкЗарплата.НомерЛицевогоСчета,
	|	ВедомостьНаВыплатуЗарплатыВБанк.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	1 КАК Поле
	|ИЗ
	|	ВТДанныеВедомостиПередЗаписью КАК ДанныеВедомостиПередЗаписью
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТДанныеВедомостиПриЗаписи КАК ДанныеВедомостиПриЗаписи
	|		ПО ДанныеВедомостиПередЗаписью.Организация = ДанныеВедомостиПриЗаписи.Организация
	|			И ДанныеВедомостиПередЗаписью.ЗарплатныйПроект = ДанныеВедомостиПриЗаписи.ЗарплатныйПроект
	|			И ДанныеВедомостиПередЗаписью.ФизическоеЛицо = ДанныеВедомостиПриЗаписи.ФизическоеЛицо
	|			И ДанныеВедомостиПередЗаписью.КВыплате = ДанныеВедомостиПриЗаписи.КВыплате
	|			И ДанныеВедомостиПередЗаписью.НомерЛицевогоСчета = ДанныеВедомостиПриЗаписи.НомерЛицевогоСчета
	|			И ДанныеВедомостиПередЗаписью.Проведен = ДанныеВедомостиПриЗаписи.Проведен
	|ГДЕ
	|	(ДанныеВедомостиПередЗаписью.ФизическоеЛицо ЕСТЬ NULL 
	|			ИЛИ ДанныеВедомостиПриЗаписи.ФизическоеЛицо ЕСТЬ NULL )";
	
	Если Не Запрос.Выполнить().Пустой() Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВедомостьНаВыплатуЗарплатыВБанк.Ссылка КАК Ведомость,
		|	ПлатежныеДокументыПеречисленияЗарплаты.ПлатежныйДокумент КАК ПлатежныйДокумент
		|ИЗ
		|	Документ.ВедомостьНаВыплатуЗарплатыВБанк КАК ВедомостьНаВыплатуЗарплатыВБанк
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПлатежныеДокументыПеречисленияЗарплаты КАК ПлатежныеДокументыПеречисленияЗарплаты
		|		ПО ВедомостьНаВыплатуЗарплатыВБанк.Ссылка = ПлатежныеДокументыПеречисленияЗарплаты.Ведомость
		|ГДЕ
		|	ВедомостьНаВыплатуЗарплатыВБанк.Ссылка = &Ведомость";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			ДанныеРеестра = Неопределено;
			Если Не ПустаяСтрока(ВедомостьОбъект.НомерРеестра) Тогда
				ДанныеРеестра = Новый Структура("Год, НомерРеестра", Год(ВедомостьОбъект.Дата), ВедомостьОбъект.НомерРеестра);
			КонецЕсли;
			ЗарегистрироватьСостояниеЗачисленияЗарплатыПоДокументу(ВедомостьОбъект.Ссылка, Отказ, , ДанныеРеестра);
		Иначе
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				ДанныеРеестра = Неопределено;
				МетаданныеПлатежногоДокумента = МетаданныеПлатежногоДокументаПеречисленияЗарплаты();
				Если МетаданныеПлатежногоДокумента.Реквизиты.Найти("НомерРеестра") <> Неопределено Тогда
					ДанныеПлатежногоДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Выборка.ПлатежныйДокумент, "Дата, НомерРеестра");
					Если Не ПустаяСтрока(ДанныеПлатежногоДокумента.НомерРеестра) Тогда
						ДанныеРеестра = Новый Структура("Год, НомерРеестра", Год(ДанныеПлатежногоДокумента.Дата), ДанныеПлатежногоДокумента.НомерРеестра);
					КонецЕсли;
				КонецЕсли;
				ЗарегистрироватьСостояниеЗачисленияЗарплатыПоДокументу(Выборка.ПлатежныйДокумент, Отказ, , ДанныеРеестра);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ЗапросВТСведенияОЗарплатныхПроектах(ТолькоРазрешенные, ОписаниеВременнойТаблицы, ПоляОтбораПериодическихДанных, ИмяВТСведенияОЗарплатныхПроектах = "ВТСведенияОЗарплатныхПроектах")
	
	ПоляОтбора = Неопределено;
	Если ПоляОтбораПериодическихДанных <> Неопределено Тогда
		ПоляОтбораПериодическихДанных.Свойство("ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам", ПоляОтбора);
	КонецЕсли;
	
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
		ОписаниеВременнойТаблицы.ИмяВременнойТаблицыОтборов, "ФизическоеЛицо");
	
	ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("Период", ОписаниеВременнойТаблицы.ИмяПоляПериод);
	ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("ФизическоеЛицо", ОписаниеВременнойТаблицы.ИмяПоляФизическоеЛицо);
	
	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	ПараметрыПостроения.Отборы = ПоляОтбора;
	ПараметрыПостроения.ИспользоватьРасширениеЯзыкаЗапросовДляСКД = Ложь;
	
	ЗапросВТИмяРегистраСрез = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам", 
		ТолькоРазрешенные,
		ОписаниеФильтра,
		ПараметрыПостроения,
		Истина,
		ИмяВТСведенияОЗарплатныхПроектах);
	
	Возврат ЗапросВТИмяРегистраСрез;
	
КонецФункции

Процедура УточнитьТекстЗапросаДинамическогоСпискаЗачислениеЗарплаты(ЗачислениеЗарплаты) Экспорт
	
	ОбменСБанкамиПоЗарплатнымПроектамВнутренний.УточнитьТекстЗапросаДинамическогоСпискаЗачислениеЗарплаты(ЗачислениеЗарплаты);
	
КонецПроцедуры


#Область ПервоначальноеЗаполнениеИОбновлениеИнформационнойБазы

// Добавляет в список Обработчики процедуры-обработчики обновления,
// необходимые данной подсистеме.
//
// Параметры:
//   Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//                                   общего модуля ОбновлениеИнформационнойБазы.
//
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.3.14";
	Обработчик.Процедура = "РегистрыСведений.ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.ЗаполнитьЗарплатныйПроект";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.5.15";
	Обработчик.Процедура = "РегистрыСведений.СостоянияДокументовЗачисленияЗарплаты.ЗаполнитьСостоянияДокументовЗачисленияЗарплаты";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.6.10";
	Обработчик.Процедура = "Документы.ПодтверждениеОткрытияЛицевыхСчетовСотрудников.ЗаполнитьПериодРегистрации";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.6.19";
	Обработчик.Процедура = "РегистрыСведений.ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.ЗаполнитьПериодРегистрации";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.12.13";
	Обработчик.Процедура = "ОбменСБанкамиПоЗарплатнымПроектам.СформироватьДвиженияПоДокументамОплаты";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.13.4";
	Обработчик.Процедура = "ОбменСБанкамиПоЗарплатнымПроектам.СформироватьДвиженияПоДокументамПодтверждениеЗачисленияЗарплаты";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.16.2";
	Обработчик.Процедура = "ОбменСБанкамиПоЗарплатнымПроектам.УточнитьВерсиюФорматаФайловОбмена";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.16.22";
	Обработчик.Процедура = "ОбменСБанкамиПоЗарплатнымПроектам.ЗаполнитьЗарплатныйПроектВПодтвержденияхИзБанка";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.17.6";
	Обработчик.Процедура = "ОбменСБанкамиПоЗарплатнымПроектам.ЗаполнитьНомераРеестровДокументовЗачисленияЗарплаты";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.18.9";
	Обработчик.Процедура = "ОбменСБанкамиПоЗарплатнымПроектам.ВосстановитьРеквизитыПлатежныхДокументов";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.18.31";
	Обработчик.Процедура = "ОбменСБанкамиПоЗарплатнымПроектам.ВосстановитьСостоянияДокументовЗачисленияЗарплаты";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.21.14";
	Обработчик.Процедура = "Справочники.ЗарплатныеПроекты.ЗаполнитьКодировкуФайловОбменаПоЗарплатнымПроектам";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.22.2";
	Обработчик.Процедура = "РегистрыСведений.СостоянияДокументовЗачисленияЗарплаты.ЗаполнитьОрганизациюВСостоянииДокументовЗачисленияЗарплаты";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.22.107";
	Обработчик.Процедура = "РегистрыСведений.СостоянияДокументовОткрытияЛицевыхСчетов.ЗаполнитьСостоянияДокументовОткрытияЛицевыхСчетов";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.2.19";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("99ed025c-0e99-431a-a4f1-56b0354600f4");
	Обработчик.Процедура = "Документы.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.ЗаполнитьДокументНомерСерияСотрудника";
	Обработчик.Комментарий = НСтр("ru = 'Заполнение серии-номера документа сотрудника в ""Заявка на открытие лицевых счетов"".'");
	
КонецПроцедуры

Процедура СформироватьДвиженияПоДокументамОплаты() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ВременнаяТаблицаОпределена = Ложь;
	ОбменСБанкамиПоЗарплатнымПроектамПереопределяемый.СформироватьДвиженияПоДокументамОплаты(Запрос.МенеджерВременныхТаблиц, ВременнаяТаблицаОпределена);
	Если Не ВременнаяТаблицаОпределена Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПодтверждениеЗачисленияЗарплаты.Ссылка КАК ПодтверждениеЗачисления
	|ИЗ
	|	Документ.ПодтверждениеЗачисленияЗарплаты КАК ПодтверждениеЗачисленияЗарплаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОплатаВедомостейНаВыплатуЗарплаты КАК ОплатаВедомостейНаВыплатуЗарплатыПодтверждение
	|		ПО ПодтверждениеЗачисленияЗарплаты.Ссылка = ОплатаВедомостейНаВыплатуЗарплатыПодтверждение.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|		ПО ПодтверждениеЗачисленияЗарплаты.Ссылка = РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыНалоговыхАгентовСБюджетомПоНДФЛ КАК РасчетыНалоговыхАгентовСБюджетомПоНДФЛ
	|		ПО ПодтверждениеЗачисленияЗарплаты.Ссылка = РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Регистратор
	|ГДЕ
	|	(ОплатаВедомостейНаВыплатуЗарплатыПодтверждение.Регистратор ЕСТЬ НЕ NULL 
	|			ИЛИ РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор ЕСТЬ НЕ NULL 
	|			ИЛИ РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Регистратор ЕСТЬ НЕ NULL )";
	
	// Очистим движения по документам подтверждения.
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НаборРегистра = РегистрыСведений.ОплатаВедомостейНаВыплатуЗарплаты.СоздатьНаборЗаписей();
		НаборРегистра.Отбор.Регистратор.Установить(Выборка.ПодтверждениеЗачисления);
		НаборРегистра.ОбменДанными.Загрузка = Истина;
		НаборРегистра.Записать();
		НаборРегистра = РегистрыНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СоздатьНаборЗаписей();
		НаборРегистра.Отбор.Регистратор.Установить(Выборка.ПодтверждениеЗачисления);
		НаборРегистра.ОбменДанными.Загрузка = Истина;
		НаборРегистра.Записать();
		НаборРегистра = РегистрыНакопления.РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.СоздатьНаборЗаписей();
		НаборРегистра.Отбор.Регистратор.Установить(Выборка.ПодтверждениеЗачисления);
		НаборРегистра.ОбменДанными.Загрузка = Истина;
		НаборРегистра.Записать();
	КонецЦикла;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВедомостьНаВыплатуЗарплатыВБанк.Организация КАК Организация,
	|	ДокументыОплаты.ДокументОплаты КАК ДокументОплаты,
	|	ВЫБОР
	|		КОГДА Организации.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА Организации.Ссылка
	|		ИНАЧЕ Организации.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	ДокументыОплаты.Дата КАК ДатаДокумента,
	|	КОНЕЦПЕРИОДА(ДокументыОплаты.Дата, ДЕНЬ) КАК ДатаОперации,
	|	КОНЕЦПЕРИОДА(ДокументыОплаты.Дата, МЕСЯЦ) КАК Период,
	|	ВедомостьНаВыплатуЗарплатыВБанкЗарплата.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СУММА(ВедомостьНаВыплатуЗарплатыВБанкЗарплата.КВыплате + ВедомостьНаВыплатуЗарплатыВБанкЗарплата.КомпенсацияЗаЗадержкуЗарплаты) КАК СуммаВыплаты
	|ПОМЕСТИТЬ ВТСуммыПоСотрудникам
	|ИЗ
	|	ВТСоответствиеВедомостейДокументамОплаты КАК ДокументыОплаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВедомостьНаВыплатуЗарплатыВБанк КАК ВедомостьНаВыплатуЗарплатыВБанк
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВедомостьНаВыплатуЗарплатыВБанк.Зарплата КАК ВедомостьНаВыплатуЗарплатыВБанкЗарплата
	|			ПО ВедомостьНаВыплатуЗарплатыВБанк.Ссылка = ВедомостьНаВыплатуЗарплатыВБанкЗарплата.Ссылка
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|			ПО ВедомостьНаВыплатуЗарплатыВБанк.Организация = Организации.Ссылка
	|		ПО ДокументыОплаты.ВедомостьВБанк = ВедомостьНаВыплатуЗарплатыВБанк.Ссылка
	|			И (ВедомостьНаВыплатуЗарплатыВБанк.Проведен)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗарплатныеПроекты КАК ЗарплатныеПроекты
	|		ПО (ВедомостьНаВыплатуЗарплатыВБанк.ЗарплатныйПроект = ЗарплатныеПроекты.Ссылка)
	|			И (ЗарплатныеПроекты.ИспользоватьЭлектронныйДокументооборотСБанком)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОплатаВедомостейНаВыплатуЗарплаты КАК ОплатаВедомостейНаВыплатуЗарплаты
	|		ПО ДокументыОплаты.ДокументОплаты = ОплатаВедомостейНаВыплатуЗарплаты.Регистратор
	|ГДЕ
	|	ОплатаВедомостейНаВыплатуЗарплаты.Регистратор ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	ВедомостьНаВыплатуЗарплатыВБанк.Организация,
	|	ДокументыОплаты.ДокументОплаты,
	|	ДокументыОплаты.Дата,
	|	ВедомостьНаВыплатуЗарплатыВБанкЗарплата.Сотрудник.ФизическоеЛицо,
	|	ВЫБОР
	|		КОГДА Организации.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА Организации.Ссылка
	|		ИНАЧЕ Организации.ГоловнаяОрганизация
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИзмеренияДаты.Период КАК ЗаданныйПериод,
	|	ИзмеренияДаты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	МАКСИМУМ(РегистрСведений.Период) КАК Период
	|ПОМЕСТИТЬ ВТМаксимальныеПериоды
	|ИЗ
	|	ВТСуммыПоСотрудникам КАК ИзмеренияДаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизическихЛицКакНалогоплательщиковНДФЛ КАК РегистрСведений
	|		ПО (РегистрСведений.Период <= ИзмеренияДаты.Период
	|				ИЛИ ИзмеренияДаты.Период = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0))
	|			И (РегистрСведений.ФизическоеЛицо = ИзмеренияДаты.ФизическоеЛицо)
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзмеренияДаты.Период,
	|	ИзмеренияДаты.ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаксимальныеПериоды.ЗаданныйПериод КАК Период,
	|	РегистрСведений.Период КАК ПериодЗаписи,
	|	МаксимальныеПериоды.ФизическоеЛицо КАК ФизическоеЛицо,
	|	РегистрСведений.Статус КАК Статус
	|ПОМЕСТИТЬ ВТСтатусФизическихЛицКакНалогоплательщиковНДФЛСрезПоследних
	|ИЗ
	|	ВТМаксимальныеПериоды КАК МаксимальныеПериоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизическихЛицКакНалогоплательщиковНДФЛ КАК РегистрСведений
	|		ПО (РегистрСведений.Период = МаксимальныеПериоды.Период)
	|			И (РегистрСведений.ФизическоеЛицо = МаксимальныеПериоды.ФизическоеЛицо)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПодтверждениеЗачисленияЗарплатыСотрудники.Ссылка КАК ПодтверждениеЗачисления,
	|	ПодтверждениеЗачисленияЗарплатыСотрудники.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	Документ.ПодтверждениеЗачисленияЗарплаты.Сотрудники КАК ПодтверждениеЗачисленияЗарплатыСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВедомостьНаВыплатуЗарплатыВБанк КАК ВедомостьНаВыплатуЗарплатыВБанк
	|		ПО ПодтверждениеЗачисленияЗарплатыСотрудники.Ссылка.ПервичныйДокумент = ВедомостьНаВыплатуЗарплатыВБанк.Ссылка
	|			И (ВедомостьНаВыплатуЗарплатыВБанк.Проведен)
	|			И (ПодтверждениеЗачисленияЗарплатыСотрудники.РезультатЗачисленияЗарплаты <> ЗНАЧЕНИЕ(Перечисление.РезультатыЗачисленияЗарплаты.Зачислено))
	|			И (ПодтверждениеЗачисленияЗарплатыСотрудники.Ссылка.Проведен)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗарплатныеПроекты КАК ЗарплатныеПроекты
	|		ПО (ВедомостьНаВыплатуЗарплатыВБанк.ЗарплатныйПроект = ЗарплатныеПроекты.Ссылка)
	|			И (ЗарплатныеПроекты.ИспользоватьЭлектронныйДокументооборотСБанком)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВзаиморасчетыССотрудниками КАК ВзаиморасчетыССотрудниками
	|		ПО ПодтверждениеЗачисленияЗарплатыСотрудники.Ссылка = ВзаиморасчетыССотрудниками.Регистратор
	|			И (ВзаиморасчетыССотрудниками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|ГДЕ
	|	ВзаиморасчетыССотрудниками.ФизическоеЛицо ЕСТЬ NULL 
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПодтверждениеЗачисленияЗарплатыСотрудники.Ссылка,
	|	ПодтверждениеЗачисленияЗарплатыСотрудники.ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СуммыНДФЛПоСотрудникам.Период КАК Период,
	|	СуммыНДФЛПоСотрудникам.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	СуммыНДФЛПоСотрудникам.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СуммыНДФЛПоСотрудникам.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	СуммыНДФЛПоСотрудникам.КодДохода КАК КодДохода,
	|	СуммыНДФЛПоСотрудникам.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	СуммыНДФЛПоСотрудникам.Организация КАК Организация,
	|	СуммыНДФЛПоСотрудникам.Подразделение КАК Подразделение,
	|	СуммыНДФЛПоСотрудникам.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	СУММА(СуммыНДФЛПоСотрудникам.СуммаОстаток) КАК СуммаОстаток,
	|	СуммыНДФЛПоСотрудникам.ДокументОплаты КАК ДокументОплаты,
	|	СуммыНДФЛПоСотрудникам.ДатаДокумента,
	|	СуммыНДФЛПоСотрудникам.ДатаОперации,
	|	СуммыНДФЛПоСотрудникам.СуммаВыплаты КАК СуммаВыплаты,
	|	ВЫБОР
	|		КОГДА СуммыНДФЛПоСотрудникам.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(Статусы.Статус, ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) = ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Нерезидент)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|				КОНЕЦ
	|		КОГДА ЕСТЬNULL(Статусы.Статус, ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) = ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|			ТОГДА ВЫБОР
	|					КОГДА СуммыНДФЛПоСотрудникам.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|					КОГДА СуммыНДФЛПоСотрудникам.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|					ИНАЧЕ """"
	|				КОНЕЦ
	|		КОГДА СуммыНДФЛПоСотрудникам.КодДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.Код1010)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ КАК Ставка
	|ИЗ
	|	(ВЫБРАТЬ
	|		РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Период КАК Период,
	|		РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|		РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|		РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|		РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КодДохода КАК КодДохода,
	|		РасчетыНалогоплательщиковСБюджетомПоНДФЛ.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|		РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация КАК Организация,
	|		РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение КАК Подразделение,
	|		СуммыПоСотрудникам.ДокументОплаты КАК ДокументОплаты,
	|		СуммыПоСотрудникам.СуммаВыплаты КАК СуммаВыплаты,
	|		СуммыПоСотрудникам.ДатаОперации КАК ДатаОперации,
	|		СуммыПоСотрудникам.ДатаДокумента КАК ДатаДокумента,
	|		СуммыПоСотрудникам.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|		ВЫБОР
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма
	|			ИНАЧЕ -РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма
	|		КОНЕЦ КАК СуммаОстаток
	|	ИЗ
	|		ВТСуммыПоСотрудникам КАК СуммыПоСотрудникам
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|			ПО СуммыПоСотрудникам.ФизическоеЛицо = РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо
	|				И (РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ГоловнаяОрганизация = СуммыПоСотрудникам.ГоловнаяОрганизация)
	|				И (НЕ РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор В
	|						(ВЫБРАТЬ
	|							ВТСуммыПоСотрудникам.ДокументОплаты
	|						ИЗ
	|							ВТСуммыПоСотрудникам))
	|				И (РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация = СуммыПоСотрудникам.Организация)
	|				И (РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Период <= СуммыПоСотрудникам.ДатаОперации)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		СуммыПоСотрудникам.ДокументОплаты,
	|		СуммыПоСотрудникам.ДатаДокумента,
	|		СуммыПоСотрудникам.СуммаВыплаты,
	|		СуммыПоСотрудникам.ДатаОперации,
	|		СуммыПоСотрудникам.ГоловнаяОрганизация,
	|		РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Период,
	|		РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КодДохода,
	|		РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода,
	|		РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение,
	|		РасчетыНалогоплательщиковСБюджетомПоНДФЛ.РегистрацияВНалоговомОргане,
	|		РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация,
	|		РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента,
	|		РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо,
	|		ВЫБОР
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма
	|			ИНАЧЕ -РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма
	|		КОНЕЦ) КАК СуммыНДФЛПоСотрудникам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусФизическихЛицКакНалогоплательщиковНДФЛСрезПоследних КАК Статусы
	|		ПО СуммыНДФЛПоСотрудникам.ФизическоеЛицо = Статусы.ФизическоеЛицо
	|
	|СГРУППИРОВАТЬ ПО
	|	СуммыНДФЛПоСотрудникам.ДокументОплаты,
	|	СуммыНДФЛПоСотрудникам.ДатаДокумента,
	|	СуммыНДФЛПоСотрудникам.ДатаОперации,
	|	СуммыНДФЛПоСотрудникам.СуммаВыплаты,
	|	СуммыНДФЛПоСотрудникам.Период,
	|	СуммыНДФЛПоСотрудникам.МесяцНалоговогоПериода,
	|	СуммыНДФЛПоСотрудникам.ФизическоеЛицо,
	|	СуммыНДФЛПоСотрудникам.СтавкаНалогообложенияРезидента,
	|	СуммыНДФЛПоСотрудникам.КодДохода,
	|	СуммыНДФЛПоСотрудникам.РегистрацияВНалоговомОргане,
	|	СуммыНДФЛПоСотрудникам.Организация,
	|	СуммыНДФЛПоСотрудникам.Подразделение,
	|	СуммыНДФЛПоСотрудникам.ГоловнаяОрганизация,
	|	ВЫБОР
	|		КОГДА СуммыНДФЛПоСотрудникам.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(Статусы.Статус, ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) = ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Нерезидент)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|				КОНЕЦ
	|		КОГДА ЕСТЬNULL(Статусы.Статус, ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) = ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|			ТОГДА ВЫБОР
	|					КОГДА СуммыНДФЛПоСотрудникам.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|					КОГДА СуммыНДФЛПоСотрудникам.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|					ИНАЧЕ """"
	|				КОНЕЦ
	|		КОГДА СуммыНДФЛПоСотрудникам.КодДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.Код1010)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	СУММА(СуммыНДФЛПоСотрудникам.СуммаОстаток) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	СуммыНДФЛПоСотрудникам.ДатаДокумента,
	|	СуммыНДФЛПоСотрудникам.ДокументОплаты,
	|	СуммыНДФЛПоСотрудникам.ФизическоеЛицо,
	|	СуммыНДФЛПоСотрудникам.МесяцНалоговогоПериода,
	|	СуммыНДФЛПоСотрудникам.Период,
	|	СуммыНДФЛПоСотрудникам.РегистрацияВНалоговомОргане,
	|	СуммыНДФЛПоСотрудникам.Организация,
	|	СуммыНДФЛПоСотрудникам.Подразделение,
	|	СуммыНДФЛПоСотрудникам.СтавкаНалогообложенияРезидента,
	|	СуммыНДФЛПоСотрудникам.КодДохода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыОплаты.ВедомостьВБанк КАК ВедомостьВБанк,
	|	СуммыПоСотрудникам.Организация КАК Организация,
	|	СуммыПоСотрудникам.ДокументОплаты КАК ДокументОплаты,
	|	СуммыПоСотрудникам.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	ВТСуммыПоСотрудникам КАК СуммыПоСотрудникам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеВедомостейДокументамОплаты КАК ДокументыОплаты
	|		ПО СуммыПоСотрудникам.ДокументОплаты = ДокументыОплаты.ДокументОплаты
	|
	|УПОРЯДОЧИТЬ ПО
	|	СуммыПоСотрудникам.ДокументОплаты";
	
	РезультатЗапросов = Запрос.ВыполнитьПакет();
	
	// Сформируем новые движения по документу "Подтверждение зачисления зарплаты".
	Выборка = РезультатЗапросов[РезультатЗапросов.Количество() - 3].Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ПодтверждениеЗачисления") Цикл
		Отказ = Ложь;
		ПодтверждениеЗачисленияОбъект = Выборка.ПодтверждениеЗачисления.ПолучитьОбъект();
		ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ПодтверждениеЗачисленияОбъект);
		
		ФизическиеЛицаДокумента = Новый Массив;
		Пока Выборка.Следующий() Цикл
			ФизическиеЛицаДокумента.Добавить(Выборка.ФизическоеЛицо);
		КонецЦикла;
		
		ВзаиморасчетыССотрудниками.ЗарегистрироватьНевыплатуПоВедомости(ПодтверждениеЗачисленияОбъект.Движения, Отказ, ПодтверждениеЗачисленияОбъект.ПервичныйДокумент, ФизическиеЛицаДокумента);
		Если Не Отказ Тогда
			УчетНДФЛ.ЗарегистрироватьНевыплатуДокументом(ПодтверждениеЗачисленияОбъект.Движения, Отказ, ПодтверждениеЗачисленияОбъект.ПервичныйДокумент, ФизическиеЛицаДокумента);
		КонецЕсли;
		Для каждого КоллекцияДвижения Из ПодтверждениеЗачисленияОбъект.Движения Цикл
			КоллекцияДвижения.ОбменДанными.Загрузка = Истина;
		КонецЦикла;
		ПодтверждениеЗачисленияОбъект.Записать();
	КонецЦикла;
	
	// Сформируем движения в документах оплаты.
	
	// УчетНДФЛ
	ОстаткиПоФизическимЛицам = Новый Соответствие;
	
	РезультатыРасчетов = Новый ТаблицаЗначений;
	РезультатыРасчетов.Колонки.Добавить("Период",Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	РезультатыРасчетов.Колонки.Добавить("ФизическоеЛицо",Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	РезультатыРасчетов.Колонки.Добавить("СтавкаНалогообложенияРезидента",Новый ОписаниеТипов("ПеречислениеСсылка.НДФЛСтавкиНалогообложенияРезидента"));
	РезультатыРасчетов.Колонки.Добавить("Ставка",Новый ОписаниеТипов("ПеречислениеСсылка.НДФЛСтавки"));
	РезультатыРасчетов.Колонки.Добавить("КодДохода",Новый ОписаниеТипов("СправочникСсылка.ВидыДоходовНДФЛ"));
	РезультатыРасчетов.Колонки.Добавить("МесяцНалоговогоПериода",Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	РезультатыРасчетов.Колонки.Добавить("РегистрацияВНалоговомОргане",Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
	РезультатыРасчетов.Колонки.Добавить("Подразделение",Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	РезультатыРасчетов.Колонки.Добавить("Сумма",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(13,0)));
	
	Выборка = РезультатЗапросов[РезультатЗапросов.Количество() - 2].Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ДокументОплаты") Цикл
		
		РезультатыРасчетов.Очистить();
		Пока Выборка.СледующийПоЗначениюПоля("ФизическоеЛицо") Цикл 
			СуммаКРаспределению = Выборка.СуммаВыплаты;
			
			Пока Выборка.Следующий() И СуммаКРаспределению > 0 Цикл
				ОстаткиПоМесяцам = ОстаткиПоФизическимЛицам.Получить(Выборка.ФизическоеЛицо);
				Если ОстаткиПоМесяцам = Неопределено Тогда
					ОстаткиПоМесяцам = Новый Соответствие;
					СуммаОстаток = Выборка.СуммаОстаток;
				Иначе
					СуммаОстаток = ОстаткиПоМесяцам.Получить(Выборка.МесяцНалоговогоПериода);
					Если СуммаОстаток = Неопределено Тогда
						СуммаОстаток = Выборка.СуммаОстаток;
					КонецЕсли;
				КонецЕсли;
				
				СписываемаяСумма = Мин(СуммаОстаток, СуммаКРаспределению);
				СуммаКРаспределению = СуммаКРаспределению - СписываемаяСумма;
				
				Если СписываемаяСумма <> 0 Тогда
					СтрокаТаблицыРезультатов = РезультатыРасчетов.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицыРезультатов, Выборка);
					СтрокаТаблицыРезультатов.Сумма = СписываемаяСумма;
				КонецЕсли;
				ОстаткиПоМесяцам.Вставить(Выборка.МесяцНалоговогоПериода, СуммаОстаток - СписываемаяСумма);
				ОстаткиПоФизическимЛицам.Вставить(Выборка.ФизическоеЛицо, ОстаткиПоМесяцам);
				
			КонецЦикла;
		КонецЦикла;
		
		Отказ = Ложь;
		НаборДвижений = ЗарплатаКадры.НаборыЗаписейРегистратора(Выборка.ДокументОплаты.Метаданные());
		НачатьТранзакцию();
		УчетНДФЛ.СформироватьУдержанныйНалогПоТаблицеЗначений(НаборДвижений, Отказ, Выборка.Организация, Выборка.ДатаОперации, РезультатыРасчетов, , Истина);
		УчетНДФЛ.СформироватьПеречисленныйНалогПоФизическимЛицам(Выборка.ДокументОплаты, НаборДвижений, Отказ, Выборка.Организация, Выборка.ДатаОперации, РезультатыРасчетов.ВыгрузитьКолонку("ФизическоеЛицо"));
		ОтменитьТранзакцию();

		// РасчетыНалоговыхАгентовСБюджетомПоНДФЛ
		Если Не Отказ И НаборДвижений.РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Количество() > 0 Тогда
			НаборЗаписей = РегистрыНакопления.РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.ДокументОплаты);
			Для каждого ЗаписьНабораДвижений Из НаборДвижений.РасчетыНалоговыхАгентовСБюджетомПоНДФЛ Цикл
				ЗаписьНабораЗаписей = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьНабораЗаписей, ЗаписьНабораДвижений);
			КонецЦикла;
			НаборЗаписей.ОбменДанными.Загрузка = Истина;
			НаборЗаписей.Записать();
		КонецЕсли;
		
		// РасчетыНалогоплательщиковСБюджетомПоНДФЛ
		Если Не Отказ И НаборДвижений.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Количество() > 0 Тогда
			НаборЗаписей = РегистрыНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.ДокументОплаты);
			Для каждого ЗаписьНабораДвижений Из НаборДвижений.РасчетыНалогоплательщиковСБюджетомПоНДФЛ Цикл
				ЗаписьНабораЗаписей = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьНабораЗаписей, ЗаписьНабораДвижений);
			КонецЦикла;
			НаборЗаписей.ОбменДанными.Загрузка = Истина;
			НаборЗаписей.Записать();
		КонецЕсли;
	КонецЦикла;
	
	// ОплатаВедомостейНаВыплатуЗарплаты
	Выборка = РезультатЗапросов[РезультатЗапросов.Количество() - 1].Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ДокументОплаты") Цикл
		НаборЗаписей = РегистрыСведений.ОплатаВедомостейНаВыплатуЗарплаты.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.ДокументОплаты);
		Пока Выборка.Следующий() Цикл
			Если Выборка.ВедомостьВБанк = Выборка.ДокументОплаты Тогда
				ОбменСБанкамиПоЗарплатнымПроектамВнутренний.СформироватьДвиженияПоДокументамОплаты(Выборка.Ведомость);
			Иначе
				НоваяСтрока = НаборЗаписей.Добавить();
				НоваяСтрока.Организация		= Выборка.Организация;
				НоваяСтрока.Ведомость 		= Выборка.ВедомостьВБанк;
				НоваяСтрока.ФизическоеЛицо	= Выборка.ФизическоеЛицо;
			КонецЕсли;
		КонецЦикла;
		Если НаборЗаписей.Количество() > 0 Тогда
			НаборЗаписей.ОбменДанными.Загрузка = Истина;
			НаборЗаписей.Записать();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьДвиженияПоДокументамПодтверждениеЗачисленияЗарплаты() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПодтверждениеЗачисленияЗарплаты.Ссылка КАК ПодтверждениеЗачисления
	|ИЗ
	|	Документ.ПодтверждениеЗачисленияЗарплаты КАК ПодтверждениеЗачисленияЗарплаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОплатаВедомостейНаВыплатуЗарплаты КАК ОплатаВедомостейНаВыплатуЗарплатыПодтверждение
	|		ПО ПодтверждениеЗачисленияЗарплаты.Ссылка = ОплатаВедомостейНаВыплатуЗарплатыПодтверждение.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|		ПО ПодтверждениеЗачисленияЗарплаты.Ссылка = РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыНалоговыхАгентовСБюджетомПоНДФЛ КАК РасчетыНалоговыхАгентовСБюджетомПоНДФЛ
	|		ПО ПодтверждениеЗачисленияЗарплаты.Ссылка = РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Регистратор
	|ГДЕ
	|	(ОплатаВедомостейНаВыплатуЗарплатыПодтверждение.Регистратор ЕСТЬ НЕ NULL 
	|			ИЛИ РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор ЕСТЬ НЕ NULL 
	|			ИЛИ РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Регистратор ЕСТЬ НЕ NULL )";
	
	// Очистим движения по документам подтверждения.
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НаборРегистра = РегистрыСведений.ОплатаВедомостейНаВыплатуЗарплаты.СоздатьНаборЗаписей();
		НаборРегистра.Отбор.Регистратор.Установить(Выборка.ПодтверждениеЗачисления);
		НаборРегистра.ОбменДанными.Загрузка = Истина;
		НаборРегистра.Записать();
		НаборРегистра = РегистрыНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СоздатьНаборЗаписей();
		НаборРегистра.Отбор.Регистратор.Установить(Выборка.ПодтверждениеЗачисления);
		НаборРегистра.ОбменДанными.Загрузка = Истина;
		НаборРегистра.Записать();
		НаборРегистра = РегистрыНакопления.РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.СоздатьНаборЗаписей();
		НаборРегистра.Отбор.Регистратор.Установить(Выборка.ПодтверждениеЗачисления);
		НаборРегистра.ОбменДанными.Загрузка = Истина;
		НаборРегистра.Записать();
	КонецЦикла;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПодтверждениеЗачисленияЗарплатыСотрудники.Ссылка КАК ПодтверждениеЗачисления,
	|	ПодтверждениеЗачисленияЗарплатыСотрудники.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	Документ.ПодтверждениеЗачисленияЗарплаты.Сотрудники КАК ПодтверждениеЗачисленияЗарплатыСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВедомостьНаВыплатуЗарплатыВБанк КАК ВедомостьНаВыплатуЗарплатыВБанк
	|		ПО ПодтверждениеЗачисленияЗарплатыСотрудники.Ссылка.ПервичныйДокумент = ВедомостьНаВыплатуЗарплатыВБанк.Ссылка
	|			И (ВедомостьНаВыплатуЗарплатыВБанк.Проведен)
	|			И (ПодтверждениеЗачисленияЗарплатыСотрудники.РезультатЗачисленияЗарплаты <> ЗНАЧЕНИЕ(Перечисление.РезультатыЗачисленияЗарплаты.Зачислено))
	|			И (ПодтверждениеЗачисленияЗарплатыСотрудники.Ссылка.Проведен)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗарплатныеПроекты КАК ЗарплатныеПроекты
	|		ПО (ВедомостьНаВыплатуЗарплатыВБанк.ЗарплатныйПроект = ЗарплатныеПроекты.Ссылка)
	|			И (ЗарплатныеПроекты.ИспользоватьЭлектронныйДокументооборотСБанком)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВзаиморасчетыССотрудниками КАК ВзаиморасчетыССотрудниками
	|		ПО ПодтверждениеЗачисленияЗарплатыСотрудники.Ссылка = ВзаиморасчетыССотрудниками.Регистратор
	|			И (ВзаиморасчетыССотрудниками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|ГДЕ
	|	ВзаиморасчетыССотрудниками.ФизическоеЛицо ЕСТЬ NULL 
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПодтверждениеЗачисленияЗарплатыСотрудники.Ссылка,
	|	ПодтверждениеЗачисленияЗарплатыСотрудники.ФизическоеЛицо";
	
	// Сформируем новые движения по документу "Подтверждение зачисления зарплаты".
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ПодтверждениеЗачисления") Цикл
		Отказ = Ложь;
		ПодтверждениеЗачисленияОбъект = Выборка.ПодтверждениеЗачисления.ПолучитьОбъект();
		ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ПодтверждениеЗачисленияОбъект);
		
		ФизическиеЛицаДокумента = Новый Массив;
		Пока Выборка.Следующий() Цикл
			ФизическиеЛицаДокумента.Добавить(Выборка.ФизическоеЛицо);
		КонецЦикла;
		
		ВзаиморасчетыССотрудниками.ЗарегистрироватьНевыплатуПоВедомости(ПодтверждениеЗачисленияОбъект.Движения, Отказ, ПодтверждениеЗачисленияОбъект.ПервичныйДокумент, ФизическиеЛицаДокумента);
		Если Не Отказ Тогда
			УчетНДФЛ.ЗарегистрироватьНевыплатуДокументом(ПодтверждениеЗачисленияОбъект.Движения, Отказ, ПодтверждениеЗачисленияОбъект.ПервичныйДокумент, ФизическиеЛицаДокумента);
		КонецЕсли;
		Для каждого КоллекцияДвижения Из ПодтверждениеЗачисленияОбъект.Движения Цикл
			КоллекцияДвижения.ОбменДанными.Загрузка = Истина;
		КонецЦикла;
		ПодтверждениеЗачисленияОбъект.Записать();
	КонецЦикла;
	
КонецПроцедуры

Процедура УточнитьВерсиюФорматаФайловОбмена() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗарплатныеПроекты.Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ФорматыФайловОбменаПоЗарплатномуПроекту.Версия33) КАК ФорматФайла
	|ИЗ
	|	Справочник.ЗарплатныеПроекты КАК ЗарплатныеПроекты
	|ГДЕ
	|	ЗарплатныеПроекты.ФорматФайла = ЗНАЧЕНИЕ(Перечисление.ФорматыФайловОбменаПоЗарплатномуПроекту.Версия34)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗарплатныйПроект = Выборка.Ссылка.ПолучитьОбъект();
		ЗарплатныйПроект.ФорматФайла = Выборка.ФорматФайла;
		ЗарплатныйПроект.ОбменДанными.Загрузка = Истина;
		ЗарплатныйПроект.Записать();
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьЗарплатныйПроектВПодтвержденияхИзБанка() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПодтверждениеЗачисленияЗарплаты.Ссылка,
	|	ПодтверждениеЗачисленияЗарплаты.ПервичныйДокумент
	|ПОМЕСТИТЬ ВТПодтвержденияЗачисленияЗарплаты
	|ИЗ
	|	Документ.ПодтверждениеЗачисленияЗарплаты КАК ПодтверждениеЗачисленияЗарплаты
	|ГДЕ
	|	ПодтверждениеЗачисленияЗарплаты.ЗарплатныйПроект = ЗНАЧЕНИЕ(Справочник.ЗарплатныеПроекты.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодтверждениеОткрытияЛицевыхСчетовСотрудников.Ссылка,
	|	ПодтверждениеОткрытияЛицевыхСчетовСотрудников.ПервичныйДокумент
	|ПОМЕСТИТЬ ВТПодтвержденияОткрытияЛицевыхСчетовСотрудников
	|ИЗ
	|	Документ.ПодтверждениеОткрытияЛицевыхСчетовСотрудников КАК ПодтверждениеОткрытияЛицевыхСчетовСотрудников
	|ГДЕ
	|	ПодтверждениеОткрытияЛицевыхСчетовСотрудников.ЗарплатныйПроект = ЗНАЧЕНИЕ(Справочник.ЗарплатныеПроекты.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодтвержденияЗачисленияЗарплаты.Ссылка,
	|	ВедомостьНаВыплатуЗарплатыВБанк.ЗарплатныйПроект
	|ИЗ
	|	ВТПодтвержденияЗачисленияЗарплаты КАК ПодтвержденияЗачисленияЗарплаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВедомостьНаВыплатуЗарплатыВБанк КАК ВедомостьНаВыплатуЗарплатыВБанк
	|		ПО ПодтвержденияЗачисленияЗарплаты.ПервичныйДокумент = ВедомостьНаВыплатуЗарплатыВБанк.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПодтвержденияОткрытияЛицевыхСчетовСотрудников.Ссылка,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.ЗарплатныйПроект
	|ИЗ
	|	ВТПодтвержденияОткрытияЛицевыхСчетовСотрудников КАК ПодтвержденияОткрытияЛицевыхСчетовСотрудников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников КАК ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников
	|		ПО ПодтвержденияОткрытияЛицевыхСчетовСотрудников.ПервичныйДокумент = ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.Ссылка";
	
	МетаданныеПлатежногоДокумента = МетаданныеПлатежногоДокументаПеречисленияЗарплаты();
	Если МетаданныеПлатежногоДокумента <> Неопределено Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПодтвержденияЗачисленияЗарплаты.Ссылка,
		|	РеквизитыПлатежныхДокументовПеречисленияЗарплаты.ЗарплатныйПроект
		|ИЗ
		|	ВТПодтвержденияЗачисленияЗарплаты КАК ПодтвержденияЗачисленияЗарплаты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ " + МетаданныеПлатежногоДокумента.ПолноеИмя() + " КАК ПлатежныеДокументы
		|		ПО ПодтвержденияЗачисленияЗарплаты.ПервичныйДокумент = ПлатежныеДокументы.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РеквизитыПлатежныхДокументовПеречисленияЗарплаты КАК РеквизитыПлатежныхДокументовПеречисленияЗарплаты
		|		ПО (ПлатежныеДокументы.Ссылка = РеквизитыПлатежныхДокументовПеречисленияЗарплаты.ПлатежныйДокумент)";
		
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДокументПодтверждение = Выборка.Ссылка.ПолучитьОбъект();
		ДокументПодтверждение.ЗарплатныйПроект = Выборка.ЗарплатныйПроект;
		ДокументПодтверждение.ОбменДанными.Загрузка = Истина;
		ДокументПодтверждение.Записать();
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьНомераРеестровДокументовЗачисленияЗарплаты() Экспорт 
	
	Запрос = Новый Запрос;
	
	Запрос.Текст  = 
	"ВЫБРАТЬ
	|	СостоянияДокументовЗачисленияЗарплаты.ДокументЗачисленияЗарплаты
	|ПОМЕСТИТЬ ВТВыгруженныеДокументы
	|ИЗ
	|	РегистрСведений.СостоянияДокументовЗачисленияЗарплаты КАК СостоянияДокументовЗачисленияЗарплаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВедомостьНаВыплатуЗарплатыВБанкПрисоединенныеФайлы КАК ПрисоединенныеФайлы
	|		ПО СостоянияДокументовЗачисленияЗарплаты.ДокументЗачисленияЗарплаты = ПрисоединенныеФайлы.ВладелецФайла
	|			И (СостоянияДокументовЗачисленияЗарплаты.Год = 0)";
	
	МетаданныеПлатежногоДокумента = МетаданныеПлатежногоДокументаПеречисленияЗарплаты();
	
	Если МетаданныеПлатежногоДокумента <> Неопределено Тогда
		
		ИмяДокумента = МетаданныеПлатежногоДокумента.ПолноеИмя();
		ИмяСправочника = СтрЗаменить(ИмяДокумента, "Документ.", "Справочник.") + "ПрисоединенныеФайлы";
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СостоянияДокументовЗачисленияЗарплаты.ДокументЗачисленияЗарплаты
		|ИЗ
		|	РегистрСведений.СостоянияДокументовЗачисленияЗарплаты КАК СостоянияДокументовЗачисленияЗарплаты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ИмяСправочника КАК ПрисоединенныеФайлы
		|		ПО СостоянияДокументовЗачисленияЗарплаты.ДокументЗачисленияЗарплаты = ПрисоединенныеФайлы.ВладелецФайла
		|			И (СостоянияДокументовЗачисленияЗарплаты.Год = 0)";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяСправочника", ИмяСправочника);
		
		Запрос.Текст = Запрос.Текст
		+ "
		|ОБЪЕДИНИТЬ ВСЕ
		|";
		
		Запрос.Текст = Запрос.Текст + ТекстЗапроса;
		
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РеквизитыПлатежныхДокументовПеречисленияЗарплаты.Дата,
	|	РеквизитыПлатежныхДокументовПеречисленияЗарплаты.Номер,
	|	ВыгруженныеДокументы.ДокументЗачисленияЗарплаты
	|ИЗ
	|	ВТВыгруженныеДокументы КАК ВыгруженныеДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РеквизитыПлатежныхДокументовПеречисленияЗарплаты КАК РеквизитыПлатежныхДокументовПеречисленияЗарплаты
	|		ПО ВыгруженныеДокументы.ДокументЗачисленияЗарплаты = РеквизитыПлатежныхДокументовПеречисленияЗарплаты.ПлатежныйДокумент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВедомостьНаВыплатуЗарплатыВБанк.Дата,
	|	ВедомостьНаВыплатуЗарплатыВБанк.Номер,
	|	ВыгруженныеДокументы.ДокументЗачисленияЗарплаты
	|ИЗ
	|	ВТВыгруженныеДокументы КАК ВыгруженныеДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВедомостьНаВыплатуЗарплатыВБанк КАК ВедомостьНаВыплатуЗарплатыВБанк
	|		ПО ВыгруженныеДокументы.ДокументЗачисленияЗарплаты = ВедомостьНаВыплатуЗарплатыВБанк.Ссылка";
	
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьТекстыЗапросов(Запрос.Текст, ТекстЗапроса);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		
		НаборЗаписей = РегистрыСведений.СостоянияДокументовЗачисленияЗарплаты.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ДокументЗачисленияЗарплаты.Установить(Выборка.ДокументЗачисленияЗарплаты);
		НаборЗаписей.Прочитать();
		
		НаборЗаписей[0].Год = Год(Выборка.Дата);
		Для Сч = 1 По 6 Цикл 
			КоличествоСимволов = 7 - Сч;
			НомерРеестраСтрока = Прав(Выборка.Номер, КоличествоСимволов);
			Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(НомерРеестраСтрока) Тогда 
				НаборЗаписей[0].НомерРеестра = Число(НомерРеестраСтрока);
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВосстановитьРеквизитыПлатежныхДокументов() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВедомостиПлатежныхДокументов.ПлатежныйДокумент,
	|	ВедомостиПлатежныхДокументов.Ведомость
	|ПОМЕСТИТЬ ВТВедомостиПлатежныхДокументов
	|ИЗ
	|	РегистрСведений.ПлатежныеДокументыПеречисленияЗарплаты КАК ВедомостиПлатежныхДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеквизитыПлатежныхДокументовПеречисленияЗарплаты КАК РеквизитыПлатежныхДокументов
	|		ПО (РеквизитыПлатежныхДокументов.ПлатежныйДокумент = ВедомостиПлатежныхДокументов.ПлатежныйДокумент)
	|ГДЕ
	|	РеквизитыПлатежныхДокументов.ПлатежныйДокумент ЕСТЬ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВедомостиПлатежныхДокументов.ПлатежныйДокумент,
	|	СУММА(Ведомости.КВыплате + Ведомости.КомпенсацияЗаЗадержкуЗарплаты) КАК СуммаПоДокументу
	|ПОМЕСТИТЬ ВТСуммаПоДокументу
	|ИЗ
	|	ВТВедомостиПлатежныхДокументов КАК ВедомостиПлатежныхДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВедомостьНаВыплатуЗарплатыВБанк.Зарплата КАК Ведомости
	|		ПО (Ведомости.Ссылка = ВедомостиПлатежныхДокументов.Ведомость)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВедомостиПлатежныхДокументов.ПлатежныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВедомостиПлатежныхДокументов.ПлатежныйДокумент,
	|	ВедомостиПлатежныхДокументов.ПлатежныйДокумент.Номер КАК Номер,
	|	ВедомостиПлатежныхДокументов.ПлатежныйДокумент.Дата КАК Дата,
	|	Ведомости.Организация КАК Организация,
	|	Ведомости.ЗарплатныйПроект КАК ЗарплатныйПроект,
	|	Ведомости.СпособВыплаты КАК СпособВыплаты,
	|	Ведомости.ПериодРегистрации КАК МесяцВыплаты,
	|	СуммаПлатежныхДокументов.СуммаПоДокументу
	|ИЗ
	|	ВТВедомостиПлатежныхДокументов КАК ВедомостиПлатежныхДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВедомостьНаВыплатуЗарплатыВБанк КАК Ведомости
	|		ПО (Ведомости.Ссылка = ВедомостиПлатежныхДокументов.Ведомость)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСуммаПоДокументу КАК СуммаПлатежныхДокументов
	|		ПО ВедомостиПлатежныхДокументов.ПлатежныйДокумент = СуммаПлатежныхДокументов.ПлатежныйДокумент
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВедомостиПлатежныхДокументов.ПлатежныйДокумент";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ПлатежныйДокумент") Цикл
		НаборЗаписей = РегистрыСведений.РеквизитыПлатежныхДокументовПеречисленияЗарплаты.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ПлатежныйДокумент.Установить(Выборка.ПлатежныйДокумент);
		СтрокаНабора = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаНабора, Выборка);
		// Если в ведомостях разные месяцы или способы выплаты, заполняем строку с представлением.
		ПредставленияСпособовВыплаты = Новый Массив;
		ПредставленияМесяцевВыплаты = Новый Массив;
		Пока Выборка.Следующий() Цикл
			ПредставленияСпособовВыплаты.Добавить(Строка(Выборка.СпособВыплаты));
			ПредставленияМесяцевВыплаты.Добавить(Формат(Выборка.МесяцВыплаты, "ДФ='ММММ гггг'"));
		КонецЦикла;
		СтрокаНабора.СпособВыплатыПредставление = СтрСоединить(ПредставленияСпособовВыплаты, ", ");
		СтрокаНабора.МесяцВыплатыПредставление = СтрСоединить(ПредставленияМесяцевВыплаты, ", ");
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Записать();
	КонецЦикла;
	
КонецПроцедуры

Процедура ВосстановитьСостоянияДокументовЗачисленияЗарплаты() Экспорт 
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УдалитьСостоянияДокументовЗачисленияЗарплаты.ДокументЗачисленияЗарплаты,
	|	УдалитьСостоянияДокументовЗачисленияЗарплаты.Состояние,
	|	УдалитьСостоянияДокументовЗачисленияЗарплаты.Год,
	|	УдалитьСостоянияДокументовЗачисленияЗарплаты.НомерРеестра
	|ИЗ
	|	РегистрСведений.УдалитьСостоянияДокументовЗачисленияЗарплаты КАК УдалитьСостоянияДокументовЗачисленияЗарплаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияДокументовЗачисленияЗарплаты КАК СостоянияДокументовЗачисленияЗарплаты
	|		ПО УдалитьСостоянияДокументовЗачисленияЗарплаты.ДокументЗачисленияЗарплаты = СостоянияДокументовЗачисленияЗарплаты.ДокументЗачисленияЗарплаты
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(УдалитьСостоянияДокументовЗачисленияЗарплаты.ДокументЗачисленияЗарплаты) <> ТИП(Справочник.УдалитьКомплектыВедомостейНаВыплатуЗарплатыЧерезБанк)
	|	И СостоянияДокументовЗачисленияЗарплаты.ДокументЗачисленияЗарплаты ЕСТЬ NULL ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		НаборЗаписей = РегистрыСведений.СостоянияДокументовЗачисленияЗарплаты.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ДокументЗачисленияЗарплаты.Установить(Выборка.ДокументЗачисленияЗарплаты);
		НоваяЗапись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииДляФормированияФайла

// Создает файлы обмена с банком по переданным документам и присоединяет их к этим документам.
// Помещает во временное хранилище массив документов, к которым были присоединены файлы.
//
// 	Параметры:
// 		СтруктураПараметровДляФормированияФайла - Структура - должна содержать значения:
// 			* МассивДокументов - Массив - документы, по которым создаются файлы.
// 			* АдресХранилища   - адрес временного хранилища, куда будет помещен массив документов с присоединенными файлами.
//
Процедура ВыгрузитьФайлыДляОбменаСБанком(СтруктураПараметров, АдресХранилища) Экспорт
	ОбменСБанкамиПоЗарплатнымПроектамВнутренний.ВыгрузитьФайлыДляОбменаСБанком(СтруктураПараметров, АдресХранилища);
КонецПроцедуры

// Формирует и прикрепляет файл обмена к объекту с помощью подсистемы "Файлы".
//
// Параметры:
//		МассивДокументов - Массив ссылок на документы, для которых формируются файлы обмена.
//		МассивОписанийФайлов - Массив описаний сформированных файлов.
//		СтруктураПараметровДляФормированияФайла - Структура
//				ВидОперации
//				ДанныеШапки
//				ДанныеСтрок
//				КорневыеСвойства
//				ОбязательныеПоляФайла
//				СоответствиеПреобразованияЗначений
//				СоответствиеТипов.
//
Процедура СоздатьФайлыДляОбменаСБанком(СтруктураПараметровДляФормированияФайла) Экспорт
	
	Для каждого ВыгружаемыйДокумент Из СтруктураПараметровДляФормированияФайла.ДанныеДокументов Цикл
		ДанныеШапкиДокумента = ВыгружаемыйДокумент.Значение;
		ДанныеСтрокДокумента = ВыгружаемыйДокумент.Значение.Сотрудники;
		ПрисоединитьФайлОбменаСБанкамиКОбъекту(ВыгружаемыйДокумент.Ключ, ДанныеШапкиДокумента, ДанныеСтрокДокумента, СтруктураПараметровДляФормированияФайла);
	КонецЦикла;
	
КонецПроцедуры

// Возвращает результат выгрузки файлов для обмена с банком.
//
// Параметры:
//		МассивДокументов - массив ссылок на документы, по которым будут выгружены файлы.
//
// Возвращаемое значение:
//		Результат - структура
//			ЗаданиеВыполнено - булево, Истина , если фоновое задание выполнено, иначе Ложь.
//			ПолучаемыеФайлы - Массив описаний передаваемых файлов.
//
Функция РезультатВыгрузкиФайловДляОбменаСБанком(Форма, МассивДокументов) Экспорт
	
	НаименованиеЗадания = НСтр("ru = 'Выгрузка файлов для обмена с банком'");
	ИмяЭкспортнойПроцедуры = "ОбменСБанкамиПоЗарплатнымПроектам.ВыгрузитьФайлыДляОбменаСБанком";
	Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
		Форма.УникальныйИдентификатор,
		ИмяЭкспортнойПроцедуры,
		Новый Структура("МассивДокументов, УникальныйИдентификаторФормы", МассивДокументов, Форма.УникальныйИдентификатор),
		НаименованиеЗадания);
	
	Форма.АдресХранилища = Результат.АдресХранилища;
	
	ПолучаемыеФайлы = Новый Массив;
	Если Результат.ЗаданиеВыполнено Тогда
		ПолучаемыеФайлы = МассивОписанийПередаваемыхФайлов(Форма.АдресХранилища, Форма.УникальныйИдентификатор);
	КонецЕсли;
	Результат.Вставить("ПолучаемыеФайлы", ПолучаемыеФайлы);
	
	Возврат Результат;
	
КонецФункции

// Получает наименование файла по переданным параметрам.
//
// Параметры:
//		Ссылка - документ, по которому формируется имя файла.
//		ОтделениеБанка - номер ОСБ Сбербанка России, в котором открыты счета физических лиц, 
//				на которые должны быть зачислены суммы, указанные в Реестре.
//		НомерДокумента - порядковый номер Реестра.
//		ПризнакТипаСписка - признак типа списка 
//				“z” - зачисление заработной платы, 
//				“o” - открытие лицевых счетов, 
//				“y” - результат зачисления заработной платы, 
//				“n” - результат открытия лицевых счетов
//				“u” - закрытие лицевых счетов.
//
// Возвращаемое значение:
//		ИмяФайла - сформированное название файла.
//
Функция ИмяФайлаОбменаСБанкамиПоЗарплатнымПроектам(Ссылка, ОтделениеБанка, НомерДокумента, ПризнакТипаСписка) Экспорт
	
	ИмяФайла = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1%2%3.xml",
		СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ОтделениеБанка, 4),
		Прав(СтроковыеФункцииКлиентСервер.ДополнитьСтроку(НомерДокумента, 3), 3),
		ПризнакТипаСписка);
	
	ОбменСБанкамиПоЗарплатнымПроектамПереопределяемый.ОпределитьИмяФайлаОбменаСБанкамиПоЗарплатнымПроектам(ИмяФайла, Ссылка);
	
	Возврат ИмяФайла;
	
КонецФункции

// Прикрепляет файл обмена с банками к документу, если к документу уже прикреплен файл, то он будет заменен.
//
// Параметры:
//		ВладелецФайла - Ссылка на документ, к которому будет прикреплен файл.
//		ДанныеШапкиДокумента - коллекция полей шапки документа.
//		ДанныеСтрокДокумента - коллекция строк документа.
//		СтруктураПараметровДляФормированияФайла - Структура
//				ВидОперации
//				ДанныеШапки
//				ДанныеСтрок
//				КорневыеСвойства
//				ОбязательныеПоляФайла
//				СоответствиеПреобразованияЗначений
//				СоответствиеТипов.
//		МассивОписанийФайлов - Массив описаний сформированных файлов.
//
Процедура ПрисоединитьФайлОбменаСБанкамиКОбъекту(ВладелецФайла, ДанныеШапкиДокумента, ДанныеСтрокДокумента, СтруктураПараметровДляФормированияФайла)
	
	Ошибки = Неопределено;
	ФайлОбменаСБанками = ФайлОбменаСБанкамиПоЗарплатнымПроектам(ДанныеШапкиДокумента, ДанныеСтрокДокумента, СтруктураПараметровДляФормированияФайла, Ошибки);
	
	Если Ошибки <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	ЗарегистрироватьСостояниеЗачисленияЗарплатыПоДокументу(ВладелецФайла, Отказ, Перечисления.СостояниеЗачисленияЗарплаты.ОжидаетПодтверждения, ДанныеШапкиДокумента.ДанныеРеестра);
	ЗарегистрироватьСостояниеОткрытияЛицевыхСчетов(ВладелецФайла, Отказ, Перечисления.СостояниеЗаявкиНаОткрытиеЛицевогоСчетаСотрудника.ОжидаетПодтверждения);
	
	Если Отказ Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки,
			"Объект.Номер",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'По документу %1 получено подтверждение из банка. Изменение документа невозможно'"), ВладелецФайла),
			ВладелецФайла,
			,
			НСтр("ru = 'По документам получены подтверждения из банка. Изменение документов невозможно.'"));
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыФайла = Новый Структура(
		"Автор,
		|ВладелецФайлов,
		|ИмяБезРасширения,
		|РасширениеБезТочки,
		|ВремяИзменения,
		|ВремяИзмененияУниверсальное");
		
	ПараметрыФайла.ВладелецФайлов = ВладелецФайла;
	ПараметрыФайла.ИмяБезРасширения = Лев(ДанныеШапкиДокумента.ИмяФайла, СтрДлина(ДанныеШапкиДокумента.ИмяФайла) - 4);
	ПараметрыФайла.РасширениеБезТочки = Прав(ДанныеШапкиДокумента.ИмяФайла, 3);
	
	ПрисоединенныйФайл = РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, ФайлОбменаСБанками.АдресФайла);
	
	ОписаниеФайла = РаботаСФайлами.ДанныеФайла(ПрисоединенныйФайл, СтруктураПараметровДляФормированияФайла.УникальныйИдентификаторФормы, Истина);
	ОписаниеФайла.Вставить("ВладелецФайла", ВладелецФайла);
	ОписаниеФайла.Вставить("Ссылка", ПрисоединенныйФайл);
	ОписаниеФайла.Вставить("ДвоичныеДанные", ФайлОбменаСБанками.ДвоичныеДанные);
	СтруктураПараметровДляФормированияФайла.МассивОписанийФайлов.Добавить(ОписаниеФайла);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ДополнитьМассивИменСвойствСумм(МассивИменСвойствСумм, ОбъектXDTO, ПолныйПутьСвойства)
	
	Для каждого СвойствоОбъекта Из ОбъектXDTO.Свойства() Цикл
		Если СвойствоОбъекта.Тип.Имя = "Сумма" И СвойствоОбъекта.Тип.БазовыйТип.Имя = "decimal" Тогда
			МассивИменСвойствСумм.Добавить(ПолныйПутьСвойства + "\" + СвойствоОбъекта.Имя);
		КонецЕсли;
		СвойствоОбъектаXDTO = ФабрикаXDTO.Создать(СвойствоОбъекта.Тип);
		Если СвойствоОбъектаXDTO <> Неопределено Тогда
			ДополнитьМассивИменСвойствСумм(МассивИменСвойствСумм, СвойствоОбъектаXDTO, ПолныйПутьСвойства + "\" + СвойствоОбъекта.Имя);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Удаляет пространство имен из текста xml-файла для совместимости с форматами версии 1.0 и 3.0.
//
// Параметры:
//		СтрокаXML - Строка, текст xml-файла.
//
// Возвращаемое значение:
//		СтрокаXML - Строка, новый текст xml-файла без пространства имен.
//
Функция УдалитьПространствоИмен(ТекстXML, ФорматФайла, КодировкаФайла)
	
	МассивИменСвойствСумм = Новый Массив;
	Пакет = ФабрикаXDTO.Пакеты.Получить(Метаданные.ПакетыXDTO.ФорматОбменаСБанкамиПоЗарплатнымПроектам.ПространствоИмен);
	Для каждого КорневоеСвойство Из Пакет.КорневыеСвойства Цикл
		КорневойОбъектXDTO = ФабрикаXDTO.Создать(КорневоеСвойство.Тип);
		ДополнитьМассивИменСвойствСумм(МассивИменСвойствСумм, КорневойОбъектXDTO, КорневоеСвойство.Имя);
	КонецЦикла;
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(ТекстXML);
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку(КодировкаФайла);
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ИмяЭлемента = "";
	Пока ЧтениеXML.Прочитать() Цикл
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			ИмяЭлемента = ИмяЭлемента + ?(ИмяЭлемента = "", "", "\") + ЧтениеXML.Имя;
			ЗаписьXML.ЗаписатьНачалоЭлемента(ЧтениеXML.Имя);
			Если ЧтениеXML.КоличествоАтрибутов() > 0 Тогда
				Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
					Если (ЧтениеXML.Имя = "xmlns"
							Или ЧтениеXML.Имя = "xmlns:xs"
							Или ЧтениеXML.Имя = "xmlns:xsi"
							Или ЧтениеXML.Имя = "xsi:type")
						И (ФорматФайла = Перечисления.ФорматыФайловОбменаПоЗарплатномуПроекту.Версия1
							Или ФорматФайла = Перечисления.ФорматыФайловОбменаПоЗарплатномуПроекту.Версия3)Тогда
						Продолжить;
					КонецЕсли;
					ЗаписьXML.ЗаписатьАтрибут(ЧтениеXML.Имя, ЧтениеXML.Значение);
				КонецЦикла;
			КонецЕсли;
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
			Если МассивИменСвойствСумм.Найти(ИмяЭлемента) = Неопределено Тогда
				ЗаписьXML.ЗаписатьТекст(ЧтениеXML.Значение);
			Иначе
				ЗаписьXML.ЗаписатьТекст(Формат(Число(ЧтениеXML.Значение), "ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧРГ=; ЧГ=0"));
			КонецЕсли;
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			ЗаписьXML.ЗаписатьКонецЭлемента();
			ИмяЭлемента = Лев(ИмяЭлемента, СтрДлина(ИмяЭлемента) - СтрДлина(ЧтениеXML.Имя) - 1);
		Иначе
			ЗаписьXML.ЗаписатьАтрибут(ЧтениеXML.Имя, ЧтениеXML.Значение);
		КонецЕсли;
	КонецЦикла;
	
	ЧтениеXML.Закрыть();
	
	Возврат ЗаписьXML.Закрыть();
	
КонецФункции

// Получает адрес временного хранилища записываемого файла.
//
// Параметры:
//		ДанныеШапкиДокумента - коллекция полей шапки документа.
//		ДанныеСтрокДокумента - коллекция строк документа.
//		СтруктураПараметровДляФормированияФайла - Структура
//				ВидОперации
//				ДанныеШапки
//				ДанныеСтрок
//				КорневыеСвойства
//				ОбязательныеПоляФайла
//				СоответствиеПреобразованияЗначений
//				СоответствиеТипов.
//		Ошибки - Структура ошибок, которые будут показаны пользователю.
//				Добавляются с помощью метода "ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю".
//
// Возвращаемое значение:
//		АдресФайла - адрес временного хранилища записываемого файла.
//
Функция ФайлОбменаСБанкамиПоЗарплатнымПроектам(ДанныеШапкиДокумента, ДанныеСтрокДокумента, СтруктураПараметровДляФормированияФайла, Ошибки)
	
	// Для каждой заявки на открытие л/с соответствие полей файла может различаться из-за версии формата, указанного в
	// зарплатном проекте.
	СоответствиеПолейФайла = ОбменСБанкамиПоЗарплатнымПроектам.СоответствиеПолейФайла(СтруктураПараметровДляФормированияФайла.ВидОперации, ДанныеШапкиДокумента.ФорматФайла);
	
	АдресФайла = "";
	ОбменСБанкамиПоЗарплатнымПроектамПереопределяемый.ОпределитьАдресФайлаОбменаСБанкамиПоЗарплатнымПроектам(АдресФайла,
		ДанныеШапкиДокумента, ДанныеСтрокДокумента, СтруктураПараметровДляФормированияФайла.КорневыеСвойства,
		СоответствиеПолейФайла, СтруктураПараметровДляФормированияФайла.ОбязательныеПоляФайла,
		СтруктураПараметровДляФормированияФайла.СоответствиеПреобразованияЗначений);
	Если ЭтоАдресВременногоХранилища(АдресФайла) Тогда
		ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(АдресФайла);
		Возврат Новый Структура("АдресФайла, ДвоичныеДанные", АдресФайла, ДвоичныеДанныеФайла);
	КонецЕсли;
	
	КодировкаФайла = Строка(ДанныеШапкиДокумента.КодировкаФайла);
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку(КодировкаФайла);
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	Пакет = ФабрикаXDTO.Пакеты.Получить(Метаданные.ПакетыXDTO.ФорматОбменаСБанкамиПоЗарплатнымПроектам.ПространствоИмен);
	
	Для каждого КорневоеСвойство Из СтруктураПараметровДляФормированияФайла.КорневыеСвойства Цикл
		
		КорневоеСвойствоXDTO = Пакет.КорневыеСвойства.Получить(КорневоеСвойство);
		КорневойОбъектXDTO = ФабрикаXDTO.Создать(КорневоеСвойствоXDTO.Тип);
		
		ЗаполнитьПоляФормата(КорневойОбъектXDTO, ДанныеШапкиДокумента, ДанныеСтрокДокумента,
							СоответствиеПолейФайла, СтруктураПараметровДляФормированияФайла.ОбязательныеПоляФайла,
							СтруктураПараметровДляФормированияФайла.СоответствиеПреобразованияЗначений, Ошибки);
		
		ФабрикаXDTO.ЗаписатьXML(ЗаписьXML, КорневойОбъектXDTO, КорневоеСвойствоXDTO.ЛокальноеИмя, КорневоеСвойствоXDTO.URIПространстваИмен, , НазначениеТипаXML.Явное);
		
	КонецЦикла;
	
	ТекстXML = ЗаписьXML.Закрыть();
	ТекстXML = УдалитьПространствоИмен(ТекстXML, ДанныеШапкиДокумента.ФорматФайла, КодировкаФайла);
	
	// Создание временного XML файла.
	Если СтруктураПараметровДляФормированияФайла.Свойство("СтруктураПараметровЭД") И СтруктураПараметровДляФормированияФайла.СтруктураПараметровЭД <> Неопределено Тогда
		ИмяВременногоФайла = СтруктураПараметровДляФормированияФайла.СтруктураПараметровЭД.ПолноеИмяФайла;
	Иначе
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	КонецЕсли;
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(ТекстXML);
	ТекстовыйДокумент.Записать(ИмяВременногоФайла, КодировкаФайла);
	
	ДвоичныеДанныеФайла = Новый ДвоичныеДанные(ИмяВременногоФайла);
	АдресФайла = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла);
	
	// Если прямой обмен, то временные файлы будут удалены в БЭД
	Если Не СтруктураПараметровДляФормированияФайла.Свойство("СтруктураПараметровЭД") Или СтруктураПараметровДляФормированияФайла.СтруктураПараметровЭД = Неопределено Тогда
		УдалитьФайлы(ИмяВременногоФайла);
	КонецЕсли;
	
	Возврат Новый Структура("АдресФайла, ДвоичныеДанные", АдресФайла, ДвоичныеДанныеФайла);
	
КонецФункции

// Заполняет поля ОбъектаXDTO по соответствию полей.
//
// Параметры:
//		ОбъектXDTO - заполняемый ОбъектXDTO.
//		ТекущиеДанные - Коллекция с набором текущих значений.
//		ДанныеСтрокДокумента - коллекция с набором значений строк документа.
//		СоответствиеПолей - соответствие между полями формата и полями выгружаемой коллекции.
//		ОбязательныеПоля - массив обязательных для заполнения полей формата.
//		СоответствиеПреобразованияЗначений - соответствие полей для преобразования.
//		Ошибки - Структура ошибок, которые будут показаны пользователю.
//				Добавляются с помощью метода "ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю".
//
Процедура ЗаполнитьПоляФормата(ОбъектXDTO, ТекущиеДанные, ДанныеСтрокДокумента, СоответствиеПолей, ОбязательныеПоля, СоответствиеПреобразованияЗначений, Ошибки)
	
	Для каждого СвойствоОбъектаXDTO Из ОбъектXDTO.Свойства() Цикл
		
		Если ТипЗнч(ОбъектXDTO[СвойствоОбъектаXDTO.Имя]) = Тип("СписокXDTO") И ТипЗнч(СвойствоОбъектаXDTO.Тип) = Тип("ТипОбъектаXDTO") Тогда
			
			СписокXDTO = ОбъектXDTO.ПолучитьСписок(СвойствоОбъектаXDTO.Имя);
			
			Для каждого СтрокаДокумента Из ДанныеСтрокДокумента Цикл
				ОбъектXDTOизСвойстваXDTO = ФабрикаXDTO.Создать(СвойствоОбъектаXDTO.Тип);
				ЗаполнитьПоляФормата(ОбъектXDTOизСвойстваXDTO, СтрокаДокумента, ДанныеСтрокДокумента, СоответствиеПолей, ОбязательныеПоля, СоответствиеПреобразованияЗначений, Ошибки);
				СписокXDTO.Добавить(ОбъектXDTOизСвойстваXDTO);
			КонецЦикла;
			
			Продолжить;
			
		КонецЕсли;
		
		ЗначениеСоответствияПолей = СоответствиеПолей.Получить(СвойствоОбъектаXDTO.Имя);
		Если ЗначениеСоответствияПолей = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЗначениеОбязательностиЗаполнения = ОбязательныеПоля.Получить(СвойствоОбъектаXDTO.Имя);
		Если ТипЗнч(ЗначениеОбязательностиЗаполнения) = Тип("Соответствие") Тогда
			ЗначениеОбязательноДляЗаполнения = ЗначениеОбязательностиЗаполнения.Получить("ЗначениеОбязательноДляЗаполнения") <> Неопределено;
		Иначе
			ЗначениеОбязательноДляЗаполнения = ЗначениеОбязательностиЗаполнения <> Неопределено;
		КонецЕсли;
		ЗначениеКорректно = Истина;
		
		Если ТипЗнч(ЗначениеСоответствияПолей) = Тип("Соответствие") Тогда
			
			Если Не СвойствоОбъектаXDTO = Неопределено Тогда
				ЗначениеПоля = ФабрикаXDTO.Создать(СвойствоОбъектаXDTO.Тип);
				ЗначениеОбязательностиЗаполнения = ?(ЗначениеОбязательностиЗаполнения = Неопределено, Новый Соответствие, ЗначениеОбязательностиЗаполнения);
				ЗаполнитьПоляФормата(ЗначениеПоля, ТекущиеДанные, ДанныеСтрокДокумента, ЗначениеСоответствияПолей, ЗначениеОбязательностиЗаполнения, СоответствиеПреобразованияЗначений, Ошибки);
				ЗначениеКорректно = ОбъектXDTOЗаполнен(ЗначениеПоля, ЗначениеПоля.Свойства());
			Иначе
				ЗначениеКорректно = Ложь;
			КонецЕсли;
			
		Иначе
			ЗначениеПоля = ТекущиеДанные[ЗначениеСоответствияПолей];
		КонецЕсли;
		
		ЗначениеПоля = ПреобразованноеЗначениеПоСоответствию(ЗначениеПоля, СоответствиеПреобразованияЗначений);
		Если (Не ЗначениеКорректно И Не ЗначениеОбязательноДляЗаполнения)
			ИЛИ ((ЗначениеОбязательностиЗаполнения = Неопределено ИЛИ ЗначениеОбязательностиЗаполнения = Ложь)
				И Не ЗначениеЗаполнено(ЗначениеПоля)) Тогда
			Продолжить;
		КонецЕсли;
		
		Если СвойствоОбъектаXDTO.Тип.Имя = "string" ИЛИ СвойствоОбъектаXDTO.Тип.БазовыйТип.Имя = "string" Тогда
			
			ЗначениеПоля = Строка(ЗначениеПоля);
			Для каждого Фасет Из СвойствоОбъектаXDTO.Тип.Фасеты Цикл
				
				Если Фасет.Вид = ВидФасетаXDTO.МаксДлина Тогда
					
					ЗначениеПоля = Лев(ЗначениеПоля, Число(Фасет.Значение));
					
				ИначеЕсли Фасет.Вид = ВидФасетаXDTO.МинДлина Тогда
					
					Если Число(Фасет.Значение) > СтрДлина(ЗначениеПоля) Тогда
						Для Шаг = СтрДлина(ЗначениеПоля) + 1 По Число(Фасет.Значение) Цикл
							ЗначениеПоля = " " + ЗначениеПоля;
						КонецЦикла;
					КонецЕсли;
					
				ИначеЕсли Фасет.Вид = ВидФасетаXDTO.Длина И СтрДлина(ЗначениеПоля) <> Число(Фасет.Значение) Тогда
					
					ЗначениеКорректно = Ложь;
					
					Если СвойствоОбъектаXDTO.Имя = "НомерСчета" Тогда
						ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки,
							"Объект.Сотрудники[%1]." + ЗначениеСоответствияПолей,
							СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'У сотрудника %1 неверный номер лицевого счета, он должен содержать 20 цифр - %2.'"), ТекущиеДанные.Сотрудник, ЗначениеПоля),
							СвойствоОбъектаXDTO.Имя,
							ТекущиеДанные.НомерСтроки,
							НСтр("ru = 'В строке %1 неверный номер лицевого счета, он должен содержать 20 цифр.'"));
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;
			
		ИначеЕсли СвойствоОбъектаXDTO.Тип.Имя = "date" ИЛИ СвойствоОбъектаXDTO.Тип.БазовыйТип.Имя = "date" Тогда
			
			ЗначениеПоля = Дата(ЗначениеПоля);
			
		КонецЕсли;
		
		Если ТипЗнч(ЗначениеПоля) = Тип("Строка") Или ТипЗнч(ЗначениеПоля) = Тип("Дата") Или ТипЗнч(ЗначениеПоля) = Тип("Число") Тогда
			Если Не ЗначениеЗаполнено(ЗначениеПоля) Тогда
				ЗначениеКорректно = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		ОбменСБанкамиПоЗарплатнымПроектамПереопределяемый.ОпределитьЗначениеПоля(ЗначениеПоля, СвойствоОбъектаXDTO,
			ЗначениеКорректно, ОбъектXDTO, ТекущиеДанные, ДанныеСтрокДокумента, СоответствиеПолей, ОбязательныеПоля, СоответствиеПреобразованияЗначений, Ошибки);
		
		Если ЗначениеКорректно ИЛИ ЗначениеОбязательноДляЗаполнения Тогда
			
			Если СвойствоОбъектаXDTO.Тип.Имя = "Счет" И Не ЗначениеКорректно Тогда
				Продолжить;
			КонецЕсли;
			ОбъектXDTO[СвойствоОбъектаXDTO.Имя] = ЗначениеПоля;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Проверяет есть ли заполненные свойства в объекте XDTO.
// Параметры:
//		ОбъектXDTO - проверяемый объект XDTO
//				может иметь тип:
//								ОбъектXDTO
//								СвойствоXDTO, у которого тип ТипОбъектаXDTO.
//		КоллекцияСвойств - коллекция свойств проверяемого объекта XDTO.
//
// Возвращаемое значение:
//		Если есть хоть одно заполненное свойство возвращает ИСТИНА, иначе ЛОЖЬ.
//
Функция ОбъектXDTOЗаполнен(ОбъектXDTO, КоллекцияСвойств)
	
	ОбъектЗаполнен = Ложь;
	Если Не ТипЗнч(ОбъектXDTO) = Тип("ОбъектXDTO") Тогда
		Возврат ОбъектЗаполнен;
	КонецЕсли;
	
	Для каждого СвойствоОбъектаXDTO Из КоллекцияСвойств Цикл
		
		Если ТипЗнч(ОбъектXDTO[СвойствоОбъектаXDTO.Имя]) = Тип("ОбъектXDTO") Тогда
			ОбъектЗаполнен = ОбъектЗаполнен ИЛИ ОбъектXDTOЗаполнен(ОбъектXDTO[СвойствоОбъектаXDTO.Имя], ОбъектXDTO[СвойствоОбъектаXDTO.Имя].Свойства());
		Иначе
			ОбъектЗаполнен = ОбъектЗаполнен ИЛИ ЗначениеЗаполнено(ОбъектXDTO[СвойствоОбъектаXDTO.Имя]);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОбъектЗаполнен;
	
КонецФункции

// Преобразует значение коллекции, в соответствии с полем преобразования.
//
// Параметры:
//		ЗначениеДляПреобразования - значение для преобразования.
//		СоответствиеПреобразованияЗначений - соответствие для преобразования значений.
//
Функция ПреобразованноеЗначениеПоСоответствию(Знач ЗначениеДляПреобразования, СоответствиеПреобразованияЗначений)
	
	ЗначениеДляПоиска = ?(ТипЗнч(ЗначениеДляПреобразования) = Тип("Строка"), ВРег(ЗначениеДляПреобразования), ЗначениеДляПреобразования);
	
	НайденноеЗначение = СоответствиеПреобразованияЗначений.Получить(ЗначениеДляПоиска);
	Если Не НайденноеЗначение = Неопределено Тогда
		ПреобразованноеЗначение = НайденноеЗначение;
	Иначе
		ПреобразованноеЗначение = ЗначениеДляПреобразования;
	КонецЕсли;
	
	ОбменСБанкамиПоЗарплатнымПроектамПереопределяемый.ПреобразоватьЗначениеПоСоответствию(ПреобразованноеЗначение, СоответствиеПреобразованияЗначений);
	
	Возврат ПреобразованноеЗначение;
	
КонецФункции

// Получает корневые свойства XDTO-пакета формируемого файла.
//
// Параметры:
//		ВидОперации - Вид операции обмена с банками, для которого получаются корневые свойства XDTO-пакета.
//
//	Возвращаемое значение:
//		КорневыеСвойства - Соответствие - корневые свойства XDTO-пакета, выгружаемые для этого вида документов.
//			Ключ элемента соответствия идентифицирует название корневого свойства.
//			В значении элемента указывается массив свойств, которые будут выгружены.
//
Функция КорневыеСвойства(ВидОперации) Экспорт
	
	Если ВидОперации = "ВедомостьНаВыплатуЗарплатыВБанк"
		Или ВидОперации = "ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников"
		Или ВидОперации = "ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудников" Тогда
		
		КорневыеСвойства = Новый Массив;
		КорневыеСвойства.Добавить("СчетаПК");
		
	ИначеЕсли ВидОперации = "ПодтверждениеЗачисленияЗарплаты" Тогда
		
		СчетаПК = Новый Массив;
		СчетаПК.Добавить("РезультатЗачисленияЗарплаты");
		СчетаПК.Добавить("ИдПервичногоДокумента");
		СчетаПК.Добавить("КонтрольныеСуммы");
		
		КорневыеСвойства = Новый Соответствие;
		КорневыеСвойства.Вставить("СчетаПК", СчетаПК);
		
	ИначеЕсли ВидОперации = "ПодтверждениеОткрытияЛицевыхСчетовСотрудников" Тогда
		СчетаПК = Новый Массив;
		СчетаПК.Добавить("РезультатОткрытияСчетов");
		СчетаПК.Добавить("КонтрольныеСуммы");
		СчетаПК.Добавить("ИдПервичногоДокумента");
		
		КорневыеСвойства = Новый Соответствие;
		КорневыеСвойства.Вставить("СчетаПК", СчетаПК);
		
	КонецЕсли;
	
	ОбменСБанкамиПоЗарплатнымПроектамПереопределяемый.ОпределитьКорневыеСвойства(КорневыеСвойства, ВидОперации);
	
	Возврат КорневыеСвойства;
	
КонецФункции

// Получает соответствие между полями файла и полями выгружаемой коллекции.
//
// Параметры:
//		ВидОперации - Вид операции обмена с банками, для которого получается соответствие полей.
//		ФорматФайла - Версия формата файла для обмена с банками, указанная в зарплатном проекте.
//
// Возвращаемое значение:
//		СоответствиеПолей - Соответствие полей файла.
//			Ключ элемента соответствия идентифицирует название поля в файле.
//			Значение элемента соответствия идентифицирует название поля в коллекции.
//
Функция СоответствиеПолейФайла(ВидОперации, ФорматФайла = Неопределено) Экспорт
	
	СоответствиеПолей = Новый Соответствие;
	
	Если ВидОперации = "ВедомостьНаВыплатуЗарплатыВБанк" Тогда
		СоответствиеПолей.Вставить("ДатаФормирования", "ДатаФормирования");
		СоответствиеПолей.Вставить("НомерДоговора", "НомерДоговора");
		СоответствиеПолей.Вставить("ДатаДоговора", "ДатаДоговора");
		СоответствиеПолей.Вставить("НаименованиеОрганизации", "ПолноеНаименованиеОрганизации");
		СоответствиеПолей.Вставить("ИНН", "ИННОрганизации");
		СоответствиеПолей.Вставить("РасчетныйСчетОрганизации", "НомерРасчетногоСчетаОрганизации");
		СоответствиеПолей.Вставить("БИК", "БИКБанка");
		СоответствиеПолей.Вставить("ИдПервичногоДокумента", "ИдПервичногоДокумента");
		СоответствиеПолей.Вставить("НомерРеестра", "НомерРеестра");
		СоответствиеПолей.Вставить("ДатаРеестра", "ДатаДокумента");
		
		СоответствиеЗачислениеЗарплаты = Новый Соответствие;
		СоответствиеЗачислениеЗарплаты.Вставить("Нпп", "НомерСтроки");
		СоответствиеЗачислениеЗарплаты.Вставить("Фамилия", "Фамилия");
		СоответствиеЗачислениеЗарплаты.Вставить("Имя", "Имя");
		СоответствиеЗачислениеЗарплаты.Вставить("Отчество", "Отчество");
		СоответствиеЗачислениеЗарплаты.Вставить("ОтделениеБанка", "ОтделениеБанка");
		СоответствиеЗачислениеЗарплаты.Вставить("ФилиалОтделенияБанка", "ФилиалОтделенияБанка");
		СоответствиеЗачислениеЗарплаты.Вставить("ЛицевойСчет", "НомерЛицевогоСчета");
		СоответствиеЗачислениеЗарплаты.Вставить("Сумма", "СуммаКВыплате");
		СоответствиеЗачислениеЗарплаты.Вставить("КодВалюты", "КодВалюты");
		СоответствиеПолей.Вставить("ЗачислениеЗарплаты", СоответствиеЗачислениеЗарплаты);
		
		СоответствиеПолей.Вставить("ВидЗачисления", "ВидЗачисления");
		СоответствиеПолей.Вставить("ПлатежноеПоручение", "НомерПлатежногоПоручения");
		СоответствиеПолей.Вставить("ДатаПлатежногоПоручения", "ДатаПлатежногоПоручения");
		
		СоответствиеКонтрольныеСуммы = Новый Соответствие;
		СоответствиеКонтрольныеСуммы.Вставить("КоличествоЗаписей", "КоличествоЗаписей");
		СоответствиеКонтрольныеСуммы.Вставить("СуммаИтого", "СуммаИтого");
		СоответствиеПолей.Вставить("КонтрольныеСуммы", СоответствиеКонтрольныеСуммы);
		
	ИначеЕсли ВидОперации = "ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников" Тогда
		
		СоответствиеФорматаФайла = СоответствиеФорматовФайла();
		Если НЕ ЗначениеЗаполнено(ФорматФайла) Тогда
			ФорматФайла = ПредопределенноеЗначение("Перечисление.ФорматыФайловОбменаПоЗарплатномуПроекту.Версия35");
		КонецЕсли;
		
		СоответствиеПолей.Вставить("ДатаФормирования", "ДатаФормирования");
		СоответствиеПолей.Вставить("НомерДоговора", "НомерДоговора");
		СоответствиеПолей.Вставить("ДатаДоговора", "ДатаДоговора");
		СоответствиеПолей.Вставить("НаименованиеОрганизации", "ПолноеНаименованиеОрганизации");
		СоответствиеПолей.Вставить("ИНН", "ИННОрганизации");
		СоответствиеПолей.Вставить("РасчетныйСчетОрганизации", "НомерРасчетногоСчетаОрганизации");
		СоответствиеПолей.Вставить("БИК", "БИКБанка");
		СоответствиеПолей.Вставить("ИдПервичногоДокумента", "ИдПервичногоДокумента");
		СоответствиеПолей.Вставить("НомерРеестра", "НомерРеестра");
		СоответствиеПолей.Вставить("ДатаРеестра", "ДатаДокумента");
		
		СоответствиеОткрытиеСчетов = Новый Соответствие;
		СоответствиеОткрытиеСчетов.Вставить("Нпп", "НомерСтроки");
		СоответствиеОткрытиеСчетов.Вставить("Фамилия", "Фамилия");
		СоответствиеОткрытиеСчетов.Вставить("Имя", "Имя");
		СоответствиеОткрытиеСчетов.Вставить("Отчество", "Отчество");
		СоответствиеОткрытиеСчетов.Вставить("ОтделениеБанка", "ОтделениеБанка");
		СоответствиеОткрытиеСчетов.Вставить("ФилиалОтделенияБанка", "ФилиалОтделенияБанка");
		
		СоответствиеЭмбоссированныйТекст = Новый Соответствие;
		СоответствиеЭмбоссированныйТекст.Вставить("Поле1", "ЭмбоссированныйТекст1");
		СоответствиеЭмбоссированныйТекст.Вставить("Поле2", "ЭмбоссированныйТекст2");
		СоответствиеЭмбоссированныйТекст.Вставить("Поле3", "ЭмбоссированныйТекст3");
		СоответствиеОткрытиеСчетов.Вставить("ЭмбоссированныйТекст", СоответствиеЭмбоссированныйТекст);
		
		СоответствиеВидВклада = Новый Соответствие;
		СоответствиеВидВклада.Вставить("__content", "СистемаРасчетовПоБанковскимКартам");
		СоответствиеВидВклада.Вставить("КодВидаВклада", "КодВидаВклада");
		СоответствиеВидВклада.Вставить("КодПодвидаВклада", "КодПодвидаВклада");
		СоответствиеВидВклада.Вставить("КодВалюты", "КодВалютыВклада");
		СоответствиеОткрытиеСчетов.Вставить("ВидВклада", СоответствиеВидВклада);
		
		СоответствиеУдостоверениеЛичности = Новый Соответствие;
		СоответствиеУдостоверениеЛичности.Вставить("ВидДокумента", "ДокументВид");
		СоответствиеУдостоверениеЛичности.Вставить("Серия", "ДокументСерия");
		СоответствиеУдостоверениеЛичности.Вставить("Номер", "ДокументНомер");
		СоответствиеУдостоверениеЛичности.Вставить("ДатаВыдачи", "ДокументДатаВыдачи");
		СоответствиеУдостоверениеЛичности.Вставить("КемВыдан", "ДокументКемВыдан");
		СоответствиеУдостоверениеЛичности.Вставить("КодПодразделения", "ДокументКодПодразделения");
		СоответствиеУдостоверениеЛичности.Вставить("КодВидаДокумента", "КодВидаДокумента");
		СоответствиеОткрытиеСчетов.Вставить("УдостоверениеЛичности", СоответствиеУдостоверениеЛичности);
		
		УстановитьСоответствиеМестаРождения(СоответствиеОткрытиеСчетов, "МестоРождения", "МестоРождения");
		УстановитьСоответствиеАдреса(СоответствиеОткрытиеСчетов, "АдресМестаРаботы", "АдресМестаРаботы");
		УстановитьСоответствиеАдреса(СоответствиеОткрытиеСчетов, "АдресПрописки", "АдресПоПрописке");
		УстановитьСоответствиеАдреса(СоответствиеОткрытиеСчетов, "АдресПроживания", "АдресМестаПроживания");
		
		СоответствиеОткрытиеСчетов.Вставить("ДатаРождения", "ДатаРождения");
		СоответствиеОткрытиеСчетов.Вставить("Пол", "Пол");
		СоответствиеОткрытиеСчетов.Вставить("РабочийТелефон", "ТелефонРабочийПредставление");
		СоответствиеОткрытиеСчетов.Вставить("ДомашнийТелефон", "ТелефонДомашнийПредставление");
		СоответствиеОткрытиеСчетов.Вставить("Сумма", "СуммаПервоначальногоПополнения");
		
		Если СоответствиеФорматаФайла.Получить(ФорматФайла) > 1 Тогда
			// версии старше 1 
			СоответствиеОткрытиеСчетов.Вставить("КодВалюты", "КодВалюты");
			СоответствиеОткрытиеСчетов.Вставить("Должность", "Должность");
			СоответствиеОткрытиеСчетов.Вставить("ПризнакЗарплатный", "ОплатаЗарплатнойКарты");
			СоответствиеОткрытиеСчетов.Вставить("КатегорияНаселения", "КатегорияНаселения");
			СоответствиеОткрытиеСчетов.Вставить("БонусПрограмма", "БонуснаяПрограмма");
			СоответствиеОткрытиеСчетов.Вставить("БонусУчастника", "НомерУчастникаБонуснойПрограммы");
			СоответствиеОткрытиеСчетов.Вставить("ТарифСледующийГод", "ТарифСледующийГод");
			СоответствиеОткрытиеСчетов.Вставить("ТарифТекущийГод", "ТарифТекущийГод");
			СоответствиеОткрытиеСчетов.Вставить("ПризнакРассылки", "СпособРассылкиОтчета");
			СоответствиеОткрытиеСчетов.Вставить("ИнтернетАдрес", "АдресЭлектроннойПочты");
			СоответствиеОткрытиеСчетов.Вставить("СчетДебета", "СчетДебета");
			СоответствиеОткрытиеСчетов.Вставить("ОператорСвязи", "ОператорСвязи");
			СоответствиеОткрытиеСчетов.Вставить("МобильныйТелефон", "НомерМобильногоТелефона");
			СоответствиеОткрытиеСчетов.Вставить("МобильныйБанк", "ТарифМобильногоБанка");
			СоответствиеОткрытиеСчетов.Вставить("ПередачаБКИ", "РазрешитьПередачуИнформацииВБКИ");
			СоответствиеОткрытиеСчетов.Вставить("КонтрольнаяИнформация", "КонтрольнаяИнформация");
			СоответствиеОткрытиеСчетов.Вставить("Резидент", "ЯвляетсяРезидентом");
			СоответствиеОткрытиеСчетов.Вставить("Гражданство", "Гражданство");
		КонецЕсли;
		
		Если СоответствиеФорматаФайла.Получить(ФорматФайла) >= 3.2 Тогда
			
			СоответствиеМиграционнаяКарта = Новый Соответствие;
			СоответствиеМиграционнаяКарта.Вставить("Номер", "НомерМиграционнойКарты");
			СоответствиеМиграционнаяКарта.Вставить("ДатаНачалаПребывания", "ДатаНачалаПребыванияМиграционнойКарты");
			СоответствиеМиграционнаяКарта.Вставить("ДатаОкончанияПребывания", "ДатаОкончанияПребыванияМиграционнойКарты");
			СоответствиеМиграционныйДокумент = Новый Соответствие;
			СоответствиеМиграционныйДокумент.Вставить("КодДокумента", "КодМиграционногоДокумента");
			СоответствиеМиграционныйДокумент.Вставить("НомерДокумента", "НомерМиграционногоДокумента");
			СоответствиеМиграционныйДокумент.Вставить("ДатаНачалаПребывания", "ДатаНачалаПребыванияМиграционногоДокумента");
			СоответствиеМиграционныйДокумент.Вставить("ДатаОкончанияПребывания", "ДатаОкончанияПребыванияМиграционногоДокумента");
			СоответствиеНерезидент = Новый Соответствие;
			СоответствиеНерезидент.Вставить("МиграционнаяКарта", СоответствиеМиграционнаяКарта);
			СоответствиеНерезидент.Вставить("МиграционныйДокумент", СоответствиеМиграционныйДокумент);
			СоответствиеОткрытиеСчетов.Вставить("Нерезидент", СоответствиеНерезидент);
			
			СоответствиеОткрытиеСчетов.Вставить("СуммаЗаработнойПлаты", "ПрогнозируемыйМесячныйДоход");
			СоответствиеОткрытиеСчетов.Вставить("ТабельныйНомер", "ТабельныйНомер");
			СоответствиеОткрытиеСчетов.Вставить("ДатаОформления", "ДатаПриема");
			СоответствиеОткрытиеСчетов.Вставить("ДатаВыплаты", "ДатаВыплатыЗарплаты");
			УстановитьСоответствиеАдреса(СоответствиеОткрытиеСчетов, "АдресИнформирования", "АдресДляИнформирования");
			
		КонецЕсли;
		
		Если СоответствиеФорматаФайла.Получить(ФорматФайла) >= 3.4 Тогда
			
			СоответствиеОткрытиеСчетов.Вставить("ИдентификаторДизайна", "ИдентификаторДизайна");
			СоответствиеОткрытиеСчетов.Вставить("ПВК", "ПВК");
			СоответствиеПриложениеКарта = Новый Соответствие;
			СоответствиеПриложениеКарта.Вставить("Код", "ПриложениеКартаКод");
			СоответствиеПриложениеКарта.Вставить("Параметр", "ПриложениеКартаПараметры");
			СоответствиеОткрытиеСчетов.Вставить("ПриложениеКарта", СоответствиеПриложениеКарта);
			СоответствиеОткрытиеСчетов.Вставить("КонтактныйМобильныйТелефон", "НомерМобильногоТелефонаПредставление");
			
		КонецЕсли;
		
		Если СоответствиеФорматаФайла.Получить(ФорматФайла) >= 3.5 Тогда
			СоответствиеОткрытиеСчетов.Вставить("СНИЛС", "СНИЛС");
		КонецЕсли;
		
		СоответствиеПолей.Вставить("ОткрытиеСчетов", СоответствиеОткрытиеСчетов);
		
		СоответствиеКонтрольныеСуммы = Новый Соответствие;
		СоответствиеКонтрольныеСуммы.Вставить("КоличествоЗаписей", "КоличествоЗаписей");
		СоответствиеКонтрольныеСуммы.Вставить("СуммаИтого", "СуммаИтого");
		СоответствиеПолей.Вставить("КонтрольныеСуммы", СоответствиеКонтрольныеСуммы);
		
	ИначеЕсли ВидОперации = "ПодтверждениеЗачисленияЗарплаты" Тогда
		
		СоответствиеПолей.Вставить("Нпп", "НомерСтроки");
		СоответствиеПолей.Вставить("ДатаФормирования", "ДатаФормирования");
		СоответствиеПолей.Вставить("Фамилия", "Фамилия");
		СоответствиеПолей.Вставить("Имя", "Имя");
		СоответствиеПолей.Вставить("Отчество", "Отчество");
		СоответствиеПолей.Вставить("ЛицевойСчет", "НомерЛицевогоСчета");
		СоответствиеПолей.Вставить("Результат", "РезультатЗачисленияЗарплаты");
		СоответствиеПолей.Вставить("Сумма", "Сумма");
		СоответствиеПолей.Вставить("РасшифровкаРезультата", "КомментарийРезультата");
		
	ИначеЕсли ВидОперации = "ПодтверждениеОткрытияЛицевыхСчетовСотрудников" Тогда
		
		СоответствиеПолей.Вставить("Нпп", "НомерСтроки");
		СоответствиеПолей.Вставить("ДатаФормирования", "ДатаФормирования");
		СоответствиеПолей.Вставить("Фамилия", "Фамилия");
		СоответствиеПолей.Вставить("Имя", "Имя");
		СоответствиеПолей.Вставить("Отчество", "Отчество");
		СоответствиеПолей.Вставить("ЛицевойСчет", "НомерЛицевогоСчета");
		СоответствиеПолей.Вставить("Результат", "РезультатОткрытияСчета");
		СоответствиеПолей.Вставить("Сумма", "Сумма");
		СоответствиеПолей.Вставить("РасшифровкаРезультата", "КомментарийРезультата");
		
		СоответствиеУдостоверениеЛичности = Новый Соответствие;
		СоответствиеУдостоверениеЛичности.Вставить("ВидДокумента", "ДокументВид");
		СоответствиеУдостоверениеЛичности.Вставить("КодВидаДокумента", "КодВидаДокумента");
		СоответствиеУдостоверениеЛичности.Вставить("Серия", "ДокументСерия");
		СоответствиеУдостоверениеЛичности.Вставить("Номер", "ДокументНомер");
		СоответствиеУдостоверениеЛичности.Вставить("ДатаВыдачи", "ДокументДатаВыдачи");
		СоответствиеУдостоверениеЛичности.Вставить("КемВыдан", "ДокументКемВыдан");
		СоответствиеУдостоверениеЛичности.Вставить("КодПодразделения", "ДокументКодПодразделения");
		СоответствиеПолей.Вставить("УдостоверениеЛичности", СоответствиеУдостоверениеЛичности);
		
	ИначеЕсли ВидОперации = "ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудников" Тогда
		СоответствиеПолей.Вставить("ДатаФормирования", "ДатаФормирования");
		СоответствиеПолей.Вставить("НомерДоговора", "НомерДоговора");
		СоответствиеПолей.Вставить("ДатаДоговора", "ДатаДоговора");
		СоответствиеПолей.Вставить("НаименованиеОрганизации", "ПолноеНаименованиеОрганизации");
		СоответствиеПолей.Вставить("ИНН", "ИННОрганизации");
		СоответствиеПолей.Вставить("РасчетныйСчетОрганизации", "НомерРасчетногоСчетаОрганизации");
		СоответствиеПолей.Вставить("БИК", "БИКБанка");
		СоответствиеПолей.Вставить("ИдПервичногоДокумента", "ИдПервичногоДокумента");
		СоответствиеПолей.Вставить("НомерРеестра", "НомерРеестра");
		СоответствиеПолей.Вставить("ДатаРеестра", "ДатаДокумента");
		
		СоответствиеСпискаУвольнений = Новый Соответствие;
		СоответствиеСпискаУвольнений.Вставить("Нпп", "НомерСтроки");
		СоответствиеСпискаУвольнений.Вставить("Фамилия", "Фамилия");
		СоответствиеСпискаУвольнений.Вставить("Имя", "Имя");
		СоответствиеСпискаУвольнений.Вставить("Отчество", "Отчество");
		СоответствиеСпискаУвольнений.Вставить("ОтделениеБанка", "ОтделениеБанка");
		СоответствиеСпискаУвольнений.Вставить("ФилиалОтделенияБанка", "ФилиалОтделенияБанка");
		
		СоответствиеСпискаУвольнений.Вставить("ДатаУвольнения", "ДатаЗакрытия");
		СоответствиеСпискаУвольнений.Вставить("НомерСчета", "ЛицевойСчет");
		СоответствиеПолей.Вставить("СписокУвольнений", СоответствиеСпискаУвольнений);
		
		СоответствиеКонтрольныеСуммы = Новый Соответствие;
		СоответствиеКонтрольныеСуммы.Вставить("КоличествоЗаписей", "КоличествоЗаписей");
		СоответствиеКонтрольныеСуммы.Вставить("СуммаИтого", "СуммаИтого");
		СоответствиеПолей.Вставить("КонтрольныеСуммы", СоответствиеКонтрольныеСуммы);
		
	КонецЕсли;
	
	ОбменСБанкамиПоЗарплатнымПроектамПереопределяемый.ОпределитьСоответствиеПолейФайла(СоответствиеПолей, ВидОперации, ФорматФайла);
	
	Возврат СоответствиеПолей;
	
КонецФункции

// Получает соответствие обязательных для заполнения полей файла.
//
// Параметры:
//		ВидОперации - Вид операции обмена с банками, для которого получаются обязательные для заполнения в файле поля.
//
// Возвращаемое значение:
//		ОбязательныеПоля - Соответствие полей файла.
//			Ключ элемента соответствия идентифицирует название поля, обязательного для заполнения.
//			В значении указывается Истина,если поле обязательно для заполнения, иначе Ложь.
//			По умолчанию, если поле не включено в соответствие, используется значение Ложь.
//
// Для группы используется соответствие обязательных полей, где также можно указать обязательность самой группы
// добавив элемент "ЗначениеОбязательноДляЗаполнения".
//
Функция ОбязательныеПоляФайла(ВидОперации) Экспорт
	
	ОбязательныеПоля = Новый Соответствие;
	
	Если ВидОперации = "ВедомостьНаВыплатуЗарплатыВБанк" Тогда
		ОбязательныеПоля.Вставить("ДатаФормирования", Истина);
		ОбязательныеПоля.Вставить("НомерДоговора", Истина);
		ОбязательныеПоля.Вставить("НаименованиеОрганизации", Истина);
		
		СоответствиеЗачислениеЗарплаты = Новый Соответствие;
		СоответствиеЗачислениеЗарплаты.Вставить("Фамилия", Истина);
		СоответствиеЗачислениеЗарплаты.Вставить("Имя", Истина);
		СоответствиеЗачислениеЗарплаты.Вставить("ЛицевойСчет", Истина);
		СоответствиеЗачислениеЗарплаты.Вставить("Сумма", Истина);
		ОбязательныеПоля.Вставить("ЗачислениеЗарплаты", СоответствиеЗачислениеЗарплаты);
		
		СоответствиеКонтрольныеСуммы = Новый Соответствие;
		СоответствиеКонтрольныеСуммы.Вставить("КоличествоЗаписей", Истина);
		СоответствиеКонтрольныеСуммы.Вставить("СуммаИтого", Истина);
		ОбязательныеПоля.Вставить("КонтрольныеСуммы", СоответствиеКонтрольныеСуммы);
		
	ИначеЕсли ВидОперации = "ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников" Тогда
		
		ОбязательныеПоля.Вставить("ДатаФормирования", Истина);
		ОбязательныеПоля.Вставить("НомерДоговора", Истина);
		ОбязательныеПоля.Вставить("НаименованиеОрганизации", Истина);
		
		СоответствиеОткрытиеСчетов = Новый Соответствие;
		СоответствиеОткрытиеСчетов.Вставить("Фамилия", Истина);
		СоответствиеОткрытиеСчетов.Вставить("Имя", Истина);
		СоответствиеОткрытиеСчетов.Вставить("ДатаРождения", Истина);
		СоответствиеОткрытиеСчетов.Вставить("Пол", Истина);
		СоответствиеОткрытиеСчетов.Вставить("ЯвляетсяРезидентом", Истина);
		
		СоответствиеВидВклада = Новый Соответствие;
		СоответствиеВидВклада.Вставить("Название", Истина);
		СоответствиеВидВклада.Вставить("ЗначениеОбязательноДляЗаполнения", Истина);
		СоответствиеОткрытиеСчетов.Вставить("ВидВклада", СоответствиеВидВклада);
		
		СоответствиеУдостоверениеЛичности = Новый Соответствие;
		СоответствиеУдостоверениеЛичности.Вставить("ВидДокумента", Истина);
		СоответствиеУдостоверениеЛичности.Вставить("Номер", Истина);
		СоответствиеУдостоверениеЛичности.Вставить("ДатаВыдачи", Истина);
		СоответствиеУдостоверениеЛичности.Вставить("КемВыдан", Истина);
		СоответствиеУдостоверениеЛичности.Вставить("ЗначениеОбязательноДляЗаполнения", Истина);
		СоответствиеОткрытиеСчетов.Вставить("УдостоверениеЛичности", СоответствиеУдостоверениеЛичности);
		
		СоответствиеЭмбоссированныйТекст = Новый Соответствие;
		СоответствиеЭмбоссированныйТекст.Вставить("Поле1", Истина);
		СоответствиеЭмбоссированныйТекст.Вставить("Поле2", Истина);
		СоответствиеЭмбоссированныйТекст.Вставить("ЗначениеОбязательноДляЗаполнения", Истина);
		СоответствиеОткрытиеСчетов.Вставить("ЭмбоссированныйТекст", СоответствиеЭмбоссированныйТекст);
		
		СоответствиеМиграционнаяКарта = Новый Соответствие;
		СоответствиеМиграционнаяКарта.Вставить("Номер", Истина);
		СоответствиеМиграционнаяКарта.Вставить("ДатаНачалаПребывания", Истина);
		СоответствиеМиграционнаяКарта.Вставить("ДатаОкончанияПребывания", Истина);
		
		СоответствиеМиграционныйДокумент = Новый Соответствие;
		СоответствиеМиграционныйДокумент.Вставить("КодДокумента", Истина);
		СоответствиеМиграционныйДокумент.Вставить("НомерДокумента", Истина);
		СоответствиеМиграционныйДокумент.Вставить("ДатаНачалаПребывания", Истина);
		СоответствиеМиграционныйДокумент.Вставить("ДатаОкончанияПребывания", Истина);
		
		СоответствиеНерезидент = Новый Соответствие;
		СоответствиеНерезидент.Вставить("МиграционнаяКарта", СоответствиеМиграционнаяКарта);
		СоответствиеНерезидент.Вставить("МиграционныйДокумент", СоответствиеМиграционныйДокумент);
		СоответствиеОткрытиеСчетов.Вставить("Нерезидент", СоответствиеНерезидент);
		
		СоответствиеКонтрольныеСуммы = Новый Соответствие;
		СоответствиеКонтрольныеСуммы.Вставить("КоличествоЗаписей", Истина);
		
		ОбязательныеПоля.Вставить("ОткрытиеСчетов", СоответствиеОткрытиеСчетов);
		ОбязательныеПоля.Вставить("КонтрольныеСуммы", СоответствиеКонтрольныеСуммы);
		
	ИначеЕсли ВидОперации = "ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудников" Тогда
		ОбязательныеПоля.Вставить("ДатаФормирования", Истина);
		ОбязательныеПоля.Вставить("НомерДоговора", Истина);
		ОбязательныеПоля.Вставить("НаименованиеОрганизации", Истина);
		
		СоответствиеСпискаУвольнений = Новый Соответствие;
		СоответствиеСпискаУвольнений.Вставить("ДатаУвольнения", Истина);
		СоответствиеСпискаУвольнений.Вставить("НомерСчета", Истина);
		ОбязательныеПоля.Вставить("СписокУвольнений", СоответствиеСпискаУвольнений);
		
		СоответствиеКонтрольныеСуммы = Новый Соответствие;
		СоответствиеКонтрольныеСуммы.Вставить("КоличествоЗаписей", Истина);
		СоответствиеКонтрольныеСуммы.Вставить("СуммаИтого", Истина);
		ОбязательныеПоля.Вставить("КонтрольныеСуммы", СоответствиеКонтрольныеСуммы);
		
	ИначеЕсли ВидОперации = "ПодтверждениеЗачисленияЗарплаты" Тогда
		ОбязательныеПоля.Вставить("ДатаФормирования", Истина);
		ОбязательныеПоля.Вставить("ИдПервичногоДокумента", Истина);
		
		СоответствиеРезультатЗачисленияЗарплаты = Новый Соответствие;
		СоответствиеРезультатЗачисленияЗарплаты.Вставить("Фамилия", Истина);
		СоответствиеРезультатЗачисленияЗарплаты.Вставить("Имя", Истина);
		СоответствиеРезультатЗачисленияЗарплаты.Вставить("ЛицевойСчет", Истина);
		
		СоответствиеКонтрольныеСуммы = Новый Соответствие;
		СоответствиеКонтрольныеСуммы.Вставить("КоличествоЗаписей", Истина);
		
		ОбязательныеПоля.Вставить("РезультатЗачисленияЗарплаты", СоответствиеРезультатЗачисленияЗарплаты);
		ОбязательныеПоля.Вставить("КонтрольныеСуммы", СоответствиеКонтрольныеСуммы);
		
	ИначеЕсли ВидОперации = "ПодтверждениеОткрытияЛицевыхСчетовСотрудников" Тогда
		ОбязательныеПоля.Вставить("ДатаФормирования", Истина);
		ОбязательныеПоля.Вставить("ИдПервичногоДокумента", Истина);
		
		СоответствиеРезультатОткрытияСчетов = Новый Соответствие;
		СоответствиеРезультатОткрытияСчетов.Вставить("Фамилия", Истина);
		СоответствиеРезультатОткрытияСчетов.Вставить("Имя", Истина);
		
		СоответствиеУдостоверениеЛичности = Новый Соответствие;
		СоответствиеУдостоверениеЛичности.Вставить("ВидДокумента", Истина);
		СоответствиеУдостоверениеЛичности.Вставить("Номер", Истина);
		СоответствиеУдостоверениеЛичности.Вставить("ДатаВыдачи", Истина);
		СоответствиеУдостоверениеЛичности.Вставить("КемВыдан", Истина);
		СоответствиеУдостоверениеЛичности.Вставить("ЗначениеОбязательноДляЗаполнения", Истина);
		СоответствиеРезультатОткрытияСчетов.Вставить("УдостоверениеЛичности", СоответствиеУдостоверениеЛичности);
		
		СоответствиеКонтрольныеСуммы = Новый Соответствие;
		СоответствиеКонтрольныеСуммы.Вставить("КоличествоЗаписей", Истина);
		
		ОбязательныеПоля.Вставить("РезультатОткрытияСчетов", СоответствиеРезультатОткрытияСчетов);
		ОбязательныеПоля.Вставить("КонтрольныеСуммы", СоответствиеКонтрольныеСуммы);
		
	КонецЕсли;
	
	ОбменСБанкамиПоЗарплатнымПроектамПереопределяемый.ОпределитьОбязательныеПоляФайла(ОбязательныеПоля, ВидОперации);
	
	Возврат ОбязательныеПоля;
	
КонецФункции

// Получает соответствие значений для преобразования.
//
// Параметры:
//		ВидОперации - Вид операции обмена с банками, для которого получается соответствие преобразования значений полей.
//
// Возвращаемое значение:
//		СоответствиеПреобразованияЗначений - соответствие значений для преобразования.
//			Ключ элемента соответствия идентифицирует преобразуемое значение.
//			Значение элемента соответствия идентифицирует преобразованное значение.
//
Функция СоответствиеПреобразованияЗначений(ВидОперации) Экспорт
	
	СоответствиеПреобразованияЗначений = Новый Соответствие;
	
	Если ВидОперации = "ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников" Тогда
		
		СоответствиеПреобразованияЗначений.Вставить(Истина, "true");
		СоответствиеПреобразованияЗначений.Вставить(Ложь, "false");
		СоответствиеПреобразованияЗначений.Вставить(Перечисления.ПолФизическогоЛица.Женский, "Женский");
		СоответствиеПреобразованияЗначений.Вставить(Перечисления.ПолФизическогоЛица.Мужской, "Мужской");
		СоответствиеПреобразованияЗначений.Вставить(Перечисления.ВидыОплатыЗарплатнойКарты.ПустаяСсылка(), "0");
		СоответствиеПреобразованияЗначений.Вставить(Перечисления.ВидыОплатыЗарплатнойКарты.ОплачиваетсяПредприятием, "1");
		СоответствиеПреобразованияЗначений.Вставить(Перечисления.ВидыОплатыЗарплатнойКарты.ОплачиваетсяДержателем, "2");
		СоответствиеПреобразованияЗначений.Вставить(Перечисления.ВидыОплатыЗарплатнойКарты.ОплачиваетсяПредприятиемИДержателемВДолях, "3");
		СоответствиеПреобразованияЗначений.Вставить(Перечисления.БонусныеПрограммыБанков.ПустаяСсылка(), Неопределено);
		СоответствиеПреобразованияЗначений.Вставить(Перечисления.БонусныеПрограммыБанков.AE, "AE");
		СоответствиеПреобразованияЗначений.Вставить(Перечисления.БонусныеПрограммыБанков.GM, "GM");
		СоответствиеПреобразованияЗначений.Вставить(Перечисления.БонусныеПрограммыБанков.PG, "PG");
		СоответствиеПреобразованияЗначений.Вставить(Перечисления.СпособыДоставкиКорреспонденцииБанка.ПустаяСсылка(), Неопределено);
		СоответствиеПреобразованияЗначений.Вставить(Перечисления.СпособыДоставкиКорреспонденцииБанка.НеРассылать, Неопределено);
		СоответствиеПреобразованияЗначений.Вставить(Перечисления.СпособыДоставкиКорреспонденцииБанка.Email, "1");
		СоответствиеПреобразованияЗначений.Вставить(Перечисления.СпособыДоставкиКорреспонденцииБанка.Почтой, "3");
		СоответствиеПреобразованияЗначений.Вставить(Перечисления.ТарифыМобильногоБанка.ПустаяСсылка(), "0");
		СоответствиеПреобразованияЗначений.Вставить(Перечисления.ТарифыМобильногоБанка.Экономный, "1");
		СоответствиеПреобразованияЗначений.Вставить(Перечисления.ТарифыМобильногоБанка.Полный, "2");
		
	ИначеЕсли ВидОперации = "ПодтверждениеЗачисленияЗарплаты" Тогда
		
		СоответствиеПреобразованияЗначений.Вставить(ВРег("Зачислено"), Перечисления.РезультатыЗачисленияЗарплаты.Зачислено);
		СоответствиеПреобразованияЗначений.Вставить(ВРег("ОшибкаФИО"), Перечисления.РезультатыЗачисленияЗарплаты.ОшибкаФИО);
		СоответствиеПреобразованияЗначений.Вставить(ВРег("ОшибкаВФИО"), Перечисления.РезультатыЗачисленияЗарплаты.ОшибкаФИО);
		СоответствиеПреобразованияЗначений.Вставить(ВРег("СчетЗакрыт"), Перечисления.РезультатыЗачисленияЗарплаты.СчетЗакрыт);
		СоответствиеПреобразованияЗначений.Вставить(ВРег("СчетОтсутствует"), Перечисления.РезультатыЗачисленияЗарплаты.СчетОтсутствует);
		СоответствиеПреобразованияЗначений.Вставить(ВРег("НеЗачислено"), Перечисления.РезультатыЗачисленияЗарплаты.НеЗачислено);
		
	ИначеЕсли ВидОперации = "ПодтверждениеОткрытияЛицевыхСчетовСотрудников" Тогда
		
		СоответствиеПреобразованияЗначений.Вставить(ВРег("СчетОткрыт"), Перечисления.РезультатыОткрытияЛицевыхСчетовСотрудников.СчетОткрыт);
		СоответствиеПреобразованияЗначений.Вставить(ВРег("ОшибкаЗаполненияДанных"), Перечисления.РезультатыОткрытияЛицевыхСчетовСотрудников.ОшибкаЗаполненияДанных);
		СоответствиеПреобразованияЗначений.Вставить(ВРег("СчетНеОткрыт"), Перечисления.РезультатыОткрытияЛицевыхСчетовСотрудников.СчетНеОткрыт);
		
	КонецЕсли;
	
	ОбменСБанкамиПоЗарплатнымПроектамПереопределяемый.ОпределитьСоответствиеПреобразованияЗначений(СоответствиеПреобразованияЗначений, ВидОперации);
	
	Возврат СоответствиеПреобразованияЗначений;
	
КонецФункции

// Получает соответствие типов между наименованиями полей файла и колонок коллекции.
//
// Параметры:
//		ВидОперации - Вид операции обмена с банками, для которого получается соответствие типов.
//
// Возвращаемое значение:
//		СоответствиеПолей - Соответствие типов полей документа.
//			Ключ элемента соответствия идентифицирует название поля в файле.
//			Значение элемента соответствия идентифицирует тип поля в коллекции.
//
Функция СоответствиеТипов(ВидОперации) Экспорт
	
	СоответствиеТипов = Новый Соответствие;
	
	Если ВидОперации = "ПодтверждениеЗачисленияЗарплаты" Тогда
		
		СоответствиеТипов.Вставить("Нпп", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
		СоответствиеТипов.Вставить("ДатаФормирования", Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.Дата)));
		СоответствиеТипов.Вставить("Фамилия", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(20)));
		СоответствиеТипов.Вставить("Имя", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(20)));
		СоответствиеТипов.Вставить("Отчество", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(20)));
		СоответствиеТипов.Вставить("ЛицевойСчет", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(20)));
		СоответствиеТипов.Вставить("Результат", Новый ОписаниеТипов("ПеречислениеСсылка.РезультатыЗачисленияЗарплаты"));
		СоответствиеТипов.Вставить("Сумма", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
		СоответствиеТипов.Вставить("РасшифровкаРезультата", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(500)));
		
	ИначеЕсли ВидОперации = "ПодтверждениеОткрытияЛицевыхСчетовСотрудников" Тогда
		
		СоответствиеТипов.Вставить("Нпп", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
		СоответствиеТипов.Вставить("ДатаФормирования", Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.Дата)));
		СоответствиеТипов.Вставить("Фамилия", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(20)));
		СоответствиеТипов.Вставить("Имя", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(20)));
		СоответствиеТипов.Вставить("Отчество", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(20)));
		СоответствиеТипов.Вставить("ЛицевойСчет", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(20)));
		СоответствиеТипов.Вставить("Результат", Новый ОписаниеТипов("ПеречислениеСсылка.РезультатыОткрытияЛицевыхСчетовСотрудников"));
		СоответствиеТипов.Вставить("Сумма", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
		СоответствиеТипов.Вставить("РасшифровкаРезультата", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(500)));
		
		СоответствиеУдостоверениеЛичности = Новый Соответствие;
		СоответствиеУдостоверениеЛичности.Вставить("ВидДокумента", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(75)));
		СоответствиеУдостоверениеЛичности.Вставить("КодВидаДокумента", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(2)));
		СоответствиеУдостоверениеЛичности.Вставить("Серия", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(14)));
		СоответствиеУдостоверениеЛичности.Вставить("Номер", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(14)));
		СоответствиеУдостоверениеЛичности.Вставить("ДатаВыдачи", Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.Дата)));
		СоответствиеУдостоверениеЛичности.Вставить("КемВыдан", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(250)));
		СоответствиеУдостоверениеЛичности.Вставить("КодПодразделения", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(10)));
		СоответствиеТипов.Вставить("УдостоверениеЛичности", СоответствиеУдостоверениеЛичности);
		
	КонецЕсли;
	
	ОбменСБанкамиПоЗарплатнымПроектамПереопределяемый.ОпределитьСоответствиеТипов(СоответствиеТипов, ВидОперации);
	
	Возврат СоответствиеТипов;
	
КонецФункции

// Устанавливает соответствие полей адреса в файле обмена и полей таблицы значений.
//
// Параметры:
//		СоответствиеПолей - соответствие полей формата.
//		НазваниеПоляПриемника - название поля в файле обмена.
//		НазваниеПоляИсточника - название поля в таблице значений.
//
Процедура УстановитьСоответствиеАдреса(СоответствиеПолей, НазваниеПоляПриемника, НазваниеПоляИсточника)
	
	Страна = Новый Соответствие;
	Страна.Вставить("СтранаНазвание", "СтранаНазвание" + НазваниеПоляИсточника);
	Страна.Вставить("СтранаКод", "СтранаКод" + НазваниеПоляИсточника);
	
	Регион = Новый Соответствие;
	Регион.Вставить("РегионНазвание", "РегионНазвание" + НазваниеПоляИсточника);
	Регион.Вставить("РегионСокращение", "РегионСокращение" + НазваниеПоляИсточника);
	
	Район = Новый Соответствие;
	Район.Вставить("РайонНазвание", "РайонНазвание" + НазваниеПоляИсточника);
	Район.Вставить("РайонСокращение", "РайонСокращение" + НазваниеПоляИсточника);
	
	Город = Новый Соответствие;
	Город.Вставить("ГородНазвание", "ГородНазвание" + НазваниеПоляИсточника);
	Город.Вставить("ГородСокращение", "ГородСокращение" + НазваниеПоляИсточника);
	
	НаселенныйПункт = Новый Соответствие;
	НаселенныйПункт.Вставить("НаселенныйПунктНазвание", "НаселенныйПунктНазвание" + НазваниеПоляИсточника);
	НаселенныйПункт.Вставить("НаселенныйПунктСокращение", "НаселенныйПунктСокращение" + НазваниеПоляИсточника);
	
	Улица = Новый Соответствие;
	Улица.Вставить("УлицаНазвание", "УлицаНазвание" + НазваниеПоляИсточника);
	Улица.Вставить("УлицаСокращение", "УлицаСокращение" + НазваниеПоляИсточника);
	
	Адрес = Новый Соответствие;
	Адрес.Вставить("Индекс", "Индекс" + НазваниеПоляИсточника);
	Адрес.Вставить("Страна", Страна);
	Адрес.Вставить("Регион", Регион);
	Адрес.Вставить("Район", Район);
	Адрес.Вставить("Город", Город);
	Адрес.Вставить("НаселенныйПункт", НаселенныйПункт);
	Адрес.Вставить("Улица", Улица);
	Адрес.Вставить("Дом", "Дом" + НазваниеПоляИсточника);
	Адрес.Вставить("Корпус", "Корпус" + НазваниеПоляИсточника);
	Адрес.Вставить("Квартира", "Квартира" + НазваниеПоляИсточника);
	
	СоответствиеПолей.Вставить(НазваниеПоляПриемника, Адрес);
	
КонецПроцедуры

// Устанавливает соответствие полей места рождения в файле обмена и полей таблицы значений.
//
// Параметры:
//		СоответствиеПолей - соответствие полей формата.
//		НазваниеПоляПриемника - название поля в файле обмена.
//		НазваниеПоляИсточника - название поля в таблице значений.
//
Процедура УстановитьСоответствиеМестаРождения(СоответствиеПолей, НазваниеПоляПриемника, НазваниеПоляИсточника)
	
	Страна = Новый Соответствие;
	Страна.Вставить("СтранаНазвание", "СтранаНазвание" + НазваниеПоляИсточника);
	
	Регион = Новый Соответствие;
	Регион.Вставить("РегионНазвание", "РегионНазвание" + НазваниеПоляИсточника);
	Регион.Вставить("РегионСокращение", "РегионСокращение" + НазваниеПоляИсточника);
	
	Район = Новый Соответствие;
	Район.Вставить("РайонНазвание", "РайонНазвание" + НазваниеПоляИсточника);
	Район.Вставить("РайонСокращение", "РайонСокращение" + НазваниеПоляИсточника);
	
	НаселенныйПункт = Новый Соответствие;
	НаселенныйПункт.Вставить("НаселенныйПунктНазвание", "НаселенныйПунктНазвание" + НазваниеПоляИсточника);
	НаселенныйПункт.Вставить("НаселенныйПунктСокращение", "НаселенныйПунктСокращение" + НазваниеПоляИсточника);
	
	Адрес = Новый Соответствие;
	Адрес.Вставить("Страна", Страна);
	Адрес.Вставить("Регион", Регион);
	Адрес.Вставить("Район", Район);
	Адрес.Вставить("НаселенныйПункт", НаселенныйПункт);
	
	СоответствиеПолей.Вставить(НазваниеПоляПриемника, Адрес);
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииДляПолученияФайлаПодтверждения

// Загружает подтверждение банка из файла.
//
// Параметры:
//		МассивОписанийФайлов - ОписаниеПереданногоФайла/Адрес во временном хранилище - массив описаний файлов/массив адресов во временном хранилище.
//
// Возвращаемое значение:
//		СозданныеДокументы - Соответствие созданных документов и первичных документов.
//
Функция ЗагрузитьПодтвержденияИзБанка(МассивОписанийФайлов, ДокументОбъектПодтверждение = Неопределено) Экспорт
	
	СозданныеДокументы = Новый Соответствие;
	
	Для каждого ЭлементМассива Из МассивОписанийФайлов Цикл
		
		Если ЭтоАдресВременногоХранилища(ЭлементМассива) Тогда
			АдресФайлаВоВременномХранилище = ЭлементМассива;
			РасширениеФайла = "xml";
		Иначе
			АдресФайлаВоВременномХранилище = ЭлементМассива.Хранение;
			РасширениеФайла = ЭлементМассива.Имя;
			Поз = СтрНайти(РасширениеФайла, ".");
			Пока Поз > 0 Цикл
				РасширениеФайла = Сред(РасширениеФайла, Поз + 1);
				Поз = СтрНайти(РасширениеФайла, ".");
			КонецЦикла;
		КонецЕсли;
		
		ИмяВременногоФайла  = ПолучитьИмяВременногоФайла(РасширениеФайла);
		ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(АдресФайлаВоВременномХранилище);
		ДвоичныеДанныеФайла.Записать(ИмяВременногоФайла);
		
		ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.MD5);
		ХешированиеДанных.Добавить(ДвоичныеДанныеФайла);
		ХешСумма = СтрЗаменить(ХешированиеДанных.ХешСумма, " ", "");
		
		ДокументПодтверждения = ЗагрузитьПодтверждениеИзБанка(ИмяВременногоФайла, ХешСумма, ДокументОбъектПодтверждение);
		
		// Удаляем временный файл
		Попытка
			УдалитьФайлы(ИмяВременногоФайла);
		Исключение
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Обмен с банками по зарплатным проектам.Загрузка подтверждения банка из файла'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), 
				УровеньЖурналаРегистрации.Ошибка, , , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
		СозданныеДокументы.Вставить(ДокументПодтверждения, ?(ДокументПодтверждения = Неопределено, Неопределено, ДокументПодтверждения.ПервичныйДокумент));
		Если ДокументПодтверждения <> Неопределено Тогда
			Если ЭтоАдресВременногоХранилища(ЭлементМассива) Тогда
				ИмяФайла = ИмяФайлаОбменаСБанкамиПоЗарплатнымПроектам(
					ДокументПодтверждения,
					ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументПодтверждения.ЗарплатныйПроект, "ОтделениеБанка"),
					ДокументПодтверждения.Номер,
					?(ТипЗнч(ДокументПодтверждения) = Тип("ДокументСсылка.ПодтверждениеЗачисленияЗарплаты"), "y", "n"));
			Иначе
				ИмяФайла = ЭлементМассива.Имя;
			КонецЕсли;
			Поз = СтрНайти(ИмяФайла, "\");
			Пока Поз > 0 Цикл
				ИмяФайла = Сред(ИмяФайла, Поз + 1);
				Поз = СтрНайти(ИмяФайла, "\");
			КонецЦикла;
			УстановитьПривилегированныйРежим(Истина);
			
			ПараметрыФайла = Новый Структура(
				"Автор,
				|ВладелецФайлов,
				|ИмяБезРасширения,
				|РасширениеБезТочки,
				|ВремяИзменения,
				|ВремяИзмененияУниверсальное");
				
			ПараметрыФайла.ВладелецФайлов = ДокументПодтверждения;
			ПараметрыФайла.ИмяБезРасширения =Лев(ИмяФайла, СтрДлина(ИмяФайла) - 4);
			ПараметрыФайла.РасширениеБезТочки = Прав(ИмяФайла, 3);
			
			ПрисоединенныйФайл = РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, АдресФайлаВоВременномХранилище);
			
			УстановитьПривилегированныйРежим(Ложь);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СозданныеДокументы;
	
КонецФункции

// Загружает подтверждение банка из файла.
//
// Параметры:
//		ПолноеИмяФайла - расположение файла.
//
// Возвращаемое значение:
//		Ссылка на документ или Неопределено, если документ не создан.
//
Функция ЗагрузитьПодтверждениеИзБанка(ПолноеИмяФайла, ХешСумма, ДокументОбъектПодтверждение = Неопределено)
	
	СтандартнаяОбработка = Истина;
	ЗагруженныйДокумент = Неопределено;
	ОбменСБанкамиПоЗарплатнымПроектамПереопределяемый.ОбработатьЗагрузку(ПолноеИмяФайла, ЗагруженныйДокумент, СтандартнаяОбработка, ДокументОбъектПодтверждение);
	
	Если Не СтандартнаяОбработка Тогда
		Возврат ЗагруженныйДокумент;
	КонецЕсли;
	
	Пакет = ФабрикаXDTO.Пакеты.Получить(Метаданные.ПакетыXDTO.ФорматОбменаСБанкамиПоЗарплатнымПроектам.ПространствоИмен);
	ТипОбъектаXDTO = Пакет.КорневыеСвойства[0].Тип;
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ПолноеИмяФайла);
	Попытка
		ОбъектXDTOВременный = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML);
		
		ОбъектXDTO = ФабрикаXDTO.Создать(ТипОбъектаXDTO);
		ЗаполнитьОбъектXDTO(ОбъектXDTO, ОбъектXDTOВременный);
		
	Исключение
		ТекстСообщенияОбОшибке = НСтр("ru = 'Неверный формат файла.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщенияОбОшибке);
		
		Возврат Неопределено;
	КонецПопытки;
	ЧтениеXML.Закрыть();
	
	Если ОбъектXDTO.Получить("РезультатЗачисленияЗарплаты") <> Неопределено
		И (ДокументОбъектПодтверждение = Неопределено Или ТипЗнч(ДокументОбъектПодтверждение) = Тип("ДокументОбъект.ПодтверждениеЗачисленияЗарплаты"))Тогда
		Возврат СоздатьДокументПодтверждения(ОбъектXDTO, ХешСумма, Метаданные.Документы.ПодтверждениеЗачисленияЗарплаты, ДокументОбъектПодтверждение);
	ИначеЕсли ОбъектXDTO.Получить("РезультатОткрытияСчетов") <> Неопределено
		И (ДокументОбъектПодтверждение = Неопределено Или ТипЗнч(ДокументОбъектПодтверждение) = Тип("ДокументОбъект.ПодтверждениеОткрытияЛицевыхСчетовСотрудников"))Тогда
		Возврат СоздатьДокументПодтверждения(ОбъектXDTO, ХешСумма, Метаданные.Документы.ПодтверждениеОткрытияЛицевыхСчетовСотрудников, ДокументОбъектПодтверждение);
	Иначе
		ТекстСообщенияОбОшибке = НСтр("ru = 'Нет сведений о подтверждении банка.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщенияОбОшибке);
		
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Создает структуру для заполнения документа подтверждения банка.
//
// Параметры:
//		ВидПодтверждения - Строка - Вид подтверждения, по которому будет производиться загрузка файла.
//		ОбъектXDTO - Заполненный ОбъектXDTO, по которому будет заполнена для заполнения документа.
//		ПервичныйДокумент - Документ, по которому получено подтверждение из банка.
//		Отказ - Булево - Если Истина, то загрузка файла не будет выполнена.
//
// Возвращаемое значение:
//
//		Структура, по которой будет заполнен документ подтверждения.
//
Функция СтруктураДляЗаполненияДокументаПоПодтверждениюБанка(ВидПодтверждения, ОбъектXDTO, ХешСумма, ПервичныйДокумент, Отказ) Экспорт
	
	КорневыеСвойства = КорневыеСвойства(ВидПодтверждения);
	СоответствиеПолей = СоответствиеПолейФайла(ВидПодтверждения);
	СоответствиеПреобразованияЗначений = СоответствиеПреобразованияЗначений(ВидПодтверждения);
	СоответствиеТипов = СоответствиеТипов(ВидПодтверждения);
	ОбязательныеПоляФайла = ОбязательныеПоляФайла(ВидПодтверждения);
	
	СтруктураДанныхДляЗаполненияДокумента = Новый Структура("ПервичныйДокумент");
	СтруктураДанныхДляЗаполненияДокумента.Вставить("Дата", ОбъектXDTO.ДатаФормирования);
	СтруктураДанныхДляЗаполненияДокумента.Вставить("МесяцОткрытия", НачалоМесяца(ТекущаяДатаСеанса()));
	СтруктураДанныхДляЗаполненияДокумента.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	СтруктураДанныхДляЗаполненияДокумента.Вставить("ЗарплатныйПроект", Справочники.ЗарплатныеПроекты.ПустаяСсылка());
	СтруктураДанныхДляЗаполненияДокумента.Вставить("Подразделение", Справочники.ПодразделенияОрганизаций.ПустаяСсылка());
	СтруктураДанныхДляЗаполненияДокумента.Вставить("ХешФайла", ХешСумма);
	
	Пакет = ФабрикаXDTO.Пакеты.Получить(Метаданные.ПакетыXDTO.ФорматОбменаСБанкамиПоЗарплатнымПроектам.ПространствоИмен);
	
	Для каждого КорневоеСвойствоXDTO Из Пакет.КорневыеСвойства Цикл
		
		ЗагружаемыеСвойства = КорневыеСвойства.Получить(КорневоеСвойствоXDTO.Имя);
		Если ЗагружаемыеСвойства = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Для каждого ЗагружаемоеСвойство Из ОбъектXDTO.Свойства() Цикл
			
			Если ЗагружаемыеСвойства.Найти(ЗагружаемоеСвойство.Имя) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ЗначениеОбязательностиЗаполнения = ОбязательныеПоляФайла.Получить(ЗагружаемоеСвойство.Имя);
			Если ТипЗнч(ЗначениеОбязательностиЗаполнения) = Тип("Соответствие") Тогда
				ЗначениеОбязательноДляЗаполнения = ЗначениеОбязательностиЗаполнения.Получить("ЗначениеОбязательноДляЗаполнения") <> Неопределено;
			Иначе
				ЗначениеОбязательноДляЗаполнения = ЗначениеОбязательностиЗаполнения <> Неопределено;
			КонецЕсли;
			
			Если ЗагружаемоеСвойство.Имя = "ИдПервичногоДокумента" И ЗначениеЗаполнено(ПервичныйДокумент) Тогда
				ПолучаемыеРеквизиты = "Организация";
				
				МетаданныеПервичногоДокумента = ПервичныйДокумент.Метаданные();
				ЕстьРеквизитЗарплатныйПроект = МетаданныеПервичногоДокумента.Реквизиты.Найти("ЗарплатныйПроект") <> Неопределено;
				Если ЕстьРеквизитЗарплатныйПроект Тогда
					ПолучаемыеРеквизиты = ПолучаемыеРеквизиты + ", ЗарплатныйПроект";
				КонецЕсли;
				ЕстьРеквизитПодразделения = МетаданныеПервичногоДокумента.Реквизиты.Найти("Подразделение") <> Неопределено;
				Если ЕстьРеквизитПодразделения Тогда
					ПолучаемыеРеквизиты = ПолучаемыеРеквизиты + ", Подразделение";
				КонецЕсли;
				
				СтруктураДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПервичныйДокумент, ПолучаемыеРеквизиты);
				СтруктураДанныхДляЗаполненияДокумента.Вставить("ПервичныйДокумент", ПервичныйДокумент);
				СтруктураДанныхДляЗаполненияДокумента.Вставить("Организация", СтруктураДокумента.Организация);
				Если ЕстьРеквизитЗарплатныйПроект Тогда
					СтруктураДанныхДляЗаполненияДокумента.Вставить("ЗарплатныйПроект", СтруктураДокумента.ЗарплатныйПроект);
				Иначе
					СведенияОПлатежномДокументе = СведенияОПлатежномДокументе(
						ВедомостиПлатежногоДокументаПеречисленияЗарплаты(ПервичныйДокумент),
						Новый Структура("ЗарплатныйПроект"));
					СтруктураДанныхДляЗаполненияДокумента.Вставить("ЗарплатныйПроект", СведенияОПлатежномДокументе.ЗарплатныйПроект);
				КонецЕсли;
				Если ЕстьРеквизитПодразделения Тогда
					СтруктураДанныхДляЗаполненияДокумента.Вставить("Подразделение", СтруктураДокумента.Подразделение);
				КонецЕсли;
			КонецЕсли;
			
			ЗагружаемоеСвойствоОбъектаXDTO = ОбъектXDTO[ЗагружаемоеСвойство.Имя];
			
			Если ЗначениеОбязательноДляЗаполнения И ЗагружаемоеСвойствоОбъектаXDTO = Неопределено Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'В файле подтверждения не указано обязательное значение поля %1.'"),
					ЗагружаемоеСвойство.Имя);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			КонецЕсли;
			
			Если Не ТипЗнч(ЗагружаемоеСвойствоОбъектаXDTO) = Тип("ОбъектXDTO") Тогда
				Продолжить;
			КонецЕсли;
			
			Для каждого СвойствоОбъектаXDTO Из ЗагружаемоеСвойствоОбъектаXDTO.Свойства() Цикл
				
				Если ТипЗнч(ЗагружаемоеСвойствоОбъектаXDTO[СвойствоОбъектаXDTO.Имя]) = Тип("СписокXDTO") Тогда
					
					ДанныеФизическихЛиц = Новый ТаблицаЗначений;
					СписокXDTO = ЗагружаемоеСвойствоОбъектаXDTO.ПолучитьСписок(СвойствоОбъектаXDTO.Имя);
					ОбъектXDTOпоТипуСвойстваXDTO = ФабрикаXDTO.Создать(СписокXDTO.ВладеющееСвойство.Тип);
					ДобавитьКолонкиВТаблицуЗначенийИзСвойствXDTO(ДанныеФизическихЛиц, 
							ОбъектXDTOпоТипуСвойстваXDTO.Свойства(), СоответствиеПолей, СоответствиеТипов);
					
					Если ЗначениеОбязательноДляЗаполнения И ОбъектXDTO[СвойствоОбъектаXDTO.Имя] = Неопределено Тогда
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'В файле подтверждения не указано обязательное значение поля %1.'"),
							СвойствоОбъектаXDTO.Имя);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
						Продолжить;
					Иначе
						ЗначениеОбязательностиЗаполнения = ?(ЗначениеОбязательностиЗаполнения = Неопределено, Новый Соответствие, ЗначениеОбязательностиЗаполнения);
					КонецЕсли;
					
					Для каждого СтрокаСпискаXDTO Из СписокXDTO Цикл
						ЗаполнитьКоллекциюИзСвойствXDTO(ДанныеФизическихЛиц.Добавить(), СтрокаСпискаXDTO, 
								ОбъектXDTOпоТипуСвойстваXDTO.Свойства(), СоответствиеПолей, СоответствиеПреобразованияЗначений, ЗначениеОбязательностиЗаполнения);
					КонецЦикла;
					СтруктураДанныхДляЗаполненияДокумента.Вставить("Сотрудники", ДанныеФизическихЛиц);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	ОбменСБанкамиПоЗарплатнымПроектамПереопределяемый.ОпределитьСтруктуруДляЗаполненияДокументаПоПодтверждениюБанка(
			СтруктураДанныхДляЗаполненияДокумента, ОбъектXDTO, 
			КорневыеСвойства, СоответствиеПолей, СоответствиеПреобразованияЗначений, СоответствиеТипов, ПервичныйДокумент, Отказ);
	
	Возврат СтруктураДанныхДляЗаполненияДокумента;
	
КонецФункции

// Добавляет колонки в таблицу значений из коллекции свойств по соответствиям типов.
//
// Параметры:
//		ДанныеФизическихЛиц - Таблица значений, в которую нужно добавить колонки.
//		СвойстваXDTO - Коллекция свойств.
//		СоответствиеПолей - соответствие между полями коллекции и полями файла.
//		СоответствиеТипов - соответствие типов между наименованиями полей файла и колонок коллекции.
//
Процедура ДобавитьКолонкиВТаблицуЗначенийИзСвойствXDTO(ДанныеФизическихЛиц, СвойстваXDTO, СоответствиеПолей, СоответствиеТипов)
	
	Для каждого СвойствоXDTO Из СвойстваXDTO Цикл
		
		НаименованиеКолонки = СоответствиеПолей.Получить(СвойствоXDTO.Имя);
		ТипКолонки = СоответствиеТипов.Получить(СвойствоXDTO.Имя);
		Если ТипЗнч(НаименованиеКолонки) = Тип("Соответствие") И ТипЗнч(ТипКолонки) = Тип("Соответствие") Тогда
			
			ОбъектXDTOпоТипуСвойстваXDTO = ФабрикаXDTO.Создать(СвойствоXDTO.Тип);
			
			ДобавитьКолонкиВТаблицуЗначенийИзСвойствXDTO(ДанныеФизическихЛиц, ОбъектXDTOпоТипуСвойстваXDTO.Свойства(), НаименованиеКолонки, ТипКолонки);
			
			Продолжить;
			
		ИначеЕсли НаименованиеКолонки = Неопределено ИЛИ ТипКолонки = Неопределено Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Если ДанныеФизическихЛиц.Колонки.Найти(НаименованиеКолонки) = Неопределено Тогда
			ДанныеФизическихЛиц.Колонки.Добавить(НаименованиеКолонки, ТипКолонки);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет коллекцию свойствами объекта XDTO по соответствию полей.
// Поля преобразуются по соответствию преобразования.
//
// Параметры:
//		Коллекция - коллекция значений, которые нужно заполнить.
//		ОбъектXDTO - заполненный ОбъектXDTO.
//		СвойстваXDTO - коллекция свойств объекта XDTO.
//		СоответствиеПолей - соответствие между полями коллекции и полями файла.
//		СоответствиеПреобразованияЗначений - соответствие значений для преобразования.
//
Процедура ЗаполнитьКоллекциюИзСвойствXDTO(Коллекция, ОбъектXDTO, СвойстваXDTO, СоответствиеПолей, СоответствиеПреобразованияЗначений, ОбязательныеПоля)
	
	Для каждого СвойствоXDTO Из СвойстваXDTO Цикл
		
		ЗначениеОбязательностиЗаполнения = ОбязательныеПоля.Получить(СвойствоXDTO.Имя);
		Если ТипЗнч(ЗначениеОбязательностиЗаполнения) = Тип("Соответствие") Тогда
			ЗначениеОбязательноДляЗаполнения = ЗначениеОбязательностиЗаполнения.Получить("ЗначениеОбязательноДляЗаполнения") <> Неопределено;
		Иначе
			ЗначениеОбязательноДляЗаполнения = ЗначениеОбязательностиЗаполнения <> Неопределено;
		КонецЕсли;
		
		НаименованиеКолонки = СоответствиеПолей.Получить(СвойствоXDTO.Имя);
		Если НаименованиеКолонки = Неопределено Тогда
			
			Продолжить;
			
		ИначеЕсли ТипЗнч(НаименованиеКолонки) = Тип("Соответствие") Тогда
			
			ОбъектXDTOпоТипуСвойстваXDTO = ФабрикаXDTO.Создать(СвойствоXDTO.Тип);
			
			Если ЗначениеОбязательноДляЗаполнения И ОбъектXDTO[СвойствоXDTO.Имя] = Неопределено Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'В файле подтверждения не указано обязательное значение поля %1.'"),
					СвойствоXDTO.Имя);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
				Продолжить;
			Иначе
				ЗначениеОбязательностиЗаполнения = ?(ЗначениеОбязательностиЗаполнения = Неопределено, Новый Соответствие, ЗначениеОбязательностиЗаполнения);
			КонецЕсли;
			
			ЗаполнитьКоллекциюИзСвойствXDTO(Коллекция, ОбъектXDTO[СвойствоXDTO.Имя], ОбъектXDTOпоТипуСвойстваXDTO.Свойства(), 
						НаименованиеКолонки, СоответствиеПреобразованияЗначений, ЗначениеОбязательностиЗаполнения);
			
			Продолжить;
			
		КонецЕсли;
		
		Если ЗначениеОбязательноДляЗаполнения И ОбъектXDTO[СвойствоXDTO.Имя] = Неопределено Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В файле подтверждения не указано обязательное значение поля %1.'"),
				СвойствоXDTO.Имя);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Продолжить;
		КонецЕсли;
		
		Коллекция[НаименованиеКолонки] = ПреобразованноеЗначениеПоСоответствию(ОбъектXDTO[СвойствоXDTO.Имя], СоответствиеПреобразованияЗначений);
		
	КонецЦикла;
	
КонецПроцедуры

// Создает документ "Подтверждение" и заполняет его свойствами объекта XDTO.
//
// Параметры:
//		ОбъектXDTO
//
// Возвращаемое значение:
//		Ссылка на созданный документ или Неопределено, если документ не создан.
//
Функция СоздатьДокументПодтверждения(ОбъектXDTO, ХешСумма, МетаданныеПодтверждения, ДокументОбъектПодтверждение = Неопределено)
	
	СвойстваОбъектаXDTO = ОбъектXDTO.Свойства();
	
	Если СвойстваОбъектаXDTO.Получить("ИдПервичногоДокумента") = Неопределено Или Не ЗначениеЗаполнено(ОбъектXDTO.ИдПервичногоДокумента) Тогда
		ТекстСообщенияОбОшибке = НСтр("ru = 'Не указан ИдПервичногоДокумента в загружаемом файле.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщенияОбОшибке);
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(ОбъектXDTO.ИдПервичногоДокумента) Или ОбъектXDTO.ИдПервичногоДокумента = СтроковыеФункцииБЗККлиентСервер.ПустойУникальныйИдентификатор() Тогда
		ТекстСообщенияОбОшибке = НСтр("ru = 'Неверный ИдПервичногоДокумента в загружаемом файле (ожидается не пустой GUID).'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщенияОбОшибке);
		Возврат Неопределено;
	КонецЕсли;
	
	ИдДокумента = Новый УникальныйИдентификатор(ОбъектXDTO.ИдПервичногоДокумента);
	
	ПервичныйДокументОбъект = Неопределено;
	
	Для каждого ТипПервичногоДокумента Из МетаданныеПодтверждения.Реквизиты.ПервичныйДокумент.Тип.Типы() Цикл
		МетаданныеПервичногоДокумента = Метаданные.НайтиПоТипу(ТипПервичногоДокумента);
		МенеджерПервичногоДокумента = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(МетаданныеПервичногоДокумента.ПолноеИмя());
		СсылкаНаПервичныйДокумент = МенеджерПервичногоДокумента.ПолучитьСсылку(ИдДокумента);
		ПервичныйДокументОбъект = СсылкаНаПервичныйДокумент.ПолучитьОбъект();
		Если ПервичныйДокументОбъект <> Неопределено Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ПервичныйДокументОбъект = Неопределено Тогда
		ТекстСообщенияОбОшибке = НСтр("ru = 'Отсутствует первичный документ для загружаемого файла.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщенияОбОшибке);
		Возврат Неопределено;
	КонецЕсли;
	
	Если ДокументОбъектПодтверждение = Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ПервичныйДокумент", СсылкаНаПервичныйДокумент);
		Запрос.УстановитьПараметр("ХешФайла", ХешСумма);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ДокументыПодтверждения.Ссылка КАК ДокументПодтверждения
		|ИЗ
		|	Документ." + МетаданныеПодтверждения.Имя + " КАК ДокументыПодтверждения
		|ГДЕ
		|	ДокументыПодтверждения.ПервичныйДокумент = &ПервичныйДокумент
		|		И ДокументыПодтверждения.ХешФайла = &ХешФайла";
		
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			ДокументОбъект = Выборка.ДокументПодтверждения.ПолучитьОбъект();
			
		Иначе
			
			ДокументОбъект = Документы[МетаданныеПодтверждения.Имя].СоздатьДокумент();
			
		КонецЕсли;
	Иначе
		ДокументОбъект = ДокументОбъектПодтверждение;
	КонецЕсли;
	
	Если ДокументОбъект.Проведен Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Для документа %1 уже зарегистрирован документ подтверждения'"),
			СсылкаНаПервичныйДокумент);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ДокументОбъект.Ссылка, "ПервичныйДокумент");
		Возврат Неопределено;
	КонецЕсли;
	
	Если ДокументОбъект.ПометкаУдаления Тогда
		ДокументОбъект.УстановитьПометкуУдаления(Ложь);
	КонецЕсли;
	
	Отказ = Ложь;
	
	ДокументОбъект.ЗаполнитьДокументИзОбъектаXDTO(ОбъектXDTO, ХешСумма, СсылкаНаПервичныйДокумент, Отказ);
	
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументОбъект.ПервичныйДокумент) Тогда
		
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		
		КонтрольныеСуммы = ОбъектXDTO.КонтрольныеСуммы;
		
		БылиОшибкиПриЗагрузке = Ложь;
		
		Если ДокументОбъект.Сотрудники.Количество() <> КонтрольныеСуммы.КоличествоЗаписей Тогда
			ТекстОшибки = НСтр("ru = 'Количество строк в документе не совпадает с контрольными данными.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ДокументОбъект, , , БылиОшибкиПриЗагрузке);
		КонецЕсли;
		
		Если КонтрольныеСуммы.СуммаИтого <> Неопределено И ДокументОбъект.Сотрудники.Итог("Сумма") <> КонтрольныеСуммы.СуммаИтого Тогда
			ТекстОшибки = НСтр("ru = 'Сумма, зачисленная по документу, не совпадает с контрольными данными.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ДокументОбъект, , , БылиОшибкиПриЗагрузке);
		КонецЕсли;
		
		Если Не БылиОшибкиПриЗагрузке И ДокументОбъект.ПроверитьЗаполнение() Тогда
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		КонецЕсли;
		
		Возврат ДокументОбъект.Ссылка;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Копирует значения свойств ОбъектXDTO(источник) в свойства ОбъектXDTO(приемник).
// Сопоставление производится по именам свойств.
//
Процедура ЗаполнитьОбъектXDTO(ОбъектXDTOПриемник, ОбъектXDTOИсточник)
	
	Для каждого СвойствоОбъектаXDTO Из ОбъектXDTOИсточник.Свойства() Цикл
		ТипСвойства = ОбъектXDTOПриемник.Свойства().Получить(СвойствоОбъектаXDTO.Имя).Тип;
		Если ТипЗнч(ТипСвойства) = Тип("ТипОбъектаXDTO") Тогда
			Если ТипЗнч(ОбъектXDTOПриемник[СвойствоОбъектаXDTO.Имя]) = Тип("СписокXDTO") Тогда
				СписокXDTOПриемник = ОбъектXDTOПриемник.ПолучитьСписок(СвойствоОбъектаXDTO.Имя);
				Если ТипЗнч(ОбъектXDTOИсточник[СвойствоОбъектаXDTO.Имя]) = Тип("СписокXDTO") Тогда
					СписокXDTOИсточник = ОбъектXDTOИсточник.ПолучитьСписок(СвойствоОбъектаXDTO.Имя);
				Иначе
					СписокXDTOИсточник = Новый Массив;
					СписокXDTOИсточник.Добавить(ОбъектXDTOИсточник[СвойствоОбъектаXDTO.Имя]);
				КонецЕсли;
				Для каждого ОбъектXDTOСпискаИсточника Из СписокXDTOИсточник Цикл
					ОбъектXDTOСпискаПриемника = ФабрикаXDTO.Создать(ОбъектXDTOПриемник.Свойства().Получить(СвойствоОбъектаXDTO.Имя).Тип);
					ЗаполнитьОбъектXDTO(ОбъектXDTOСпискаПриемника, ОбъектXDTOСпискаИсточника);
					СписокXDTOПриемник.Добавить(ОбъектXDTOСпискаПриемника);
				КонецЦикла;
				Продолжить;
			КонецЕсли;
			Если ОбъектXDTOИсточник.ПолучитьXDTO(СвойствоОбъектаXDTO.Имя) <> Неопределено Тогда
				ОбъектXDTOПриемник[СвойствоОбъектаXDTO.Имя] = ФабрикаXDTO.Создать(ОбъектXDTOПриемник.Свойства().Получить(СвойствоОбъектаXDTO.Имя).Тип);
				ЗаполнитьОбъектXDTO(ОбъектXDTOПриемник[СвойствоОбъектаXDTO.Имя], ОбъектXDTOИсточник.ПолучитьXDTO(СвойствоОбъектаXDTO.Имя));
			КонецЕсли;
		Иначе
			Если ТипСвойства.Фасеты <> Неопределено Тогда
				// Значение может быть указано в различных вариантах заглавных и прописных символов.
				// Нам нужно получить точное соответствие значения со значениями перечисления из формата обмена.
				ЗначениеСвойстваXDTO = ОбъектXDTOИсточник.ПолучитьXDTO(СвойствоОбъектаXDTO.Имя);
				Для Каждого Фасет Из ТипСвойства.Фасеты Цикл
					Если Фасет.Вид <> ВидФасетаXDTO.Перечисление Тогда
						Продолжить;
					КонецЕсли;
					Если ВРег(Фасет.Значение) = ВРег(ЗначениеСвойстваXDTO.Значение) Тогда
						ОбъектXDTOИсточник[СвойствоОбъектаXDTO.Имя] = Фасет.Значение;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			ЗначениеИсточника = ОбъектXDTOИсточник.ПолучитьXDTO(СвойствоОбъектаXDTO.Имя);
			// Значение может быть пустым, проверим
			Если ТипЗнч(ЗначениеИсточника) <> Тип("ОбъектXDTO") И ЗначениеИсточника <> Неопределено Тогда
				ОбъектXDTOПриемник[СвойствоОбъектаXDTO.Имя] = ЗначениеИсточника;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция СостояниеОткрытияЛицевыхСчетов(ПодтверждениеОткрытияЛицевыхСчетов) Экспорт
	
	ПервичныйДокумент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПодтверждениеОткрытияЛицевыхСчетов, "ПервичныйДокумент");
	
	// Получим физических лиц по заявке на открытие л/с и сравним их с физическими лицами в подтверждениях
	// Состояние заявки "Выгружен в банк" пока не получим подтверждения по всем физическим лицам.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПервичныйДокумент", ПервичныйДокумент);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПодтверждениеОткрытияЛицевыхСчетовСотрудников.Ссылка КАК Ссылка,
	|	ПодтверждениеОткрытияЛицевыхСчетовСотрудников.ПервичныйДокумент КАК ПервичныйДокумент
	|ПОМЕСТИТЬ ВТПодтвержденияОткрытияЛицевыхСчетов
	|ИЗ
	|	Документ.ПодтверждениеОткрытияЛицевыхСчетовСотрудников КАК ПодтверждениеОткрытияЛицевыхСчетовСотрудников
	|ГДЕ
	|	ПодтверждениеОткрытияЛицевыхСчетовСотрудников.ПервичныйДокумент = &ПервичныйДокумент
	|	И ПодтверждениеОткрытияЛицевыхСчетовСотрудников.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ФизическоеЛицо
	|ПОМЕСТИТЬ ВТДанныеЗаявкиНаОткрытиеЛицевыхСчетов
	|ИЗ
	|	Документ.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.Сотрудники КАК ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПодтвержденияОткрытияЛицевыхСчетов КАК ПодтвержденияОткрытияЛицевыхСчетов
	|		ПО ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.Ссылка = ПодтвержденияОткрытияЛицевыхСчетов.ПервичныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодтвержденияОткрытияЛицевыхСчетов.Ссылка КАК Ссылка,
	|	ПодтвержденияОткрытияЛицевыхСчетов.ПервичныйДокумент КАК ПервичныйДокумент,
	|	ПодтверждениеОткрытияЛицевыхСчетовСотрудниковСотрудники.ФизическоеЛицо,
	|	ПодтверждениеОткрытияЛицевыхСчетовСотрудниковСотрудники.РезультатОткрытияСчета
	|ПОМЕСТИТЬ ВТПодтвержденияОткрытияЛицевыхСчетовСотрудники
	|ИЗ
	|	ВТПодтвержденияОткрытияЛицевыхСчетов КАК ПодтвержденияОткрытияЛицевыхСчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПодтверждениеОткрытияЛицевыхСчетовСотрудников.Сотрудники КАК ПодтверждениеОткрытияЛицевыхСчетовСотрудниковСотрудники
	|		ПО ПодтвержденияОткрытияЛицевыхСчетов.Ссылка = ПодтверждениеОткрытияЛицевыхСчетовСотрудниковСотрудники.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК ПодтвержденияЗагруженыНеПоВсемСотрудникам
	|ПОМЕСТИТЬ ВТДанныеЗаполненности
	|ИЗ
	|	ВТДанныеЗаявкиНаОткрытиеЛицевыхСчетов КАК ДанныеЗаявкиНаОткрытиеЛицевыхСчетов
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТПодтвержденияОткрытияЛицевыхСчетовСотрудники КАК ПодтвержденияОткрытияЛицевыхСчетовСотрудники
	|		ПО ДанныеЗаявкиНаОткрытиеЛицевыхСчетов.ФизическоеЛицо = ПодтвержденияОткрытияЛицевыхСчетовСотрудники.ФизическоеЛицо
	|ГДЕ
	|	(ДанныеЗаявкиНаОткрытиеЛицевыхСчетов.ФизическоеЛицо ЕСТЬ NULL 
	|			ИЛИ ПодтвержденияОткрытияЛицевыхСчетовСотрудники.ФизическоеЛицо ЕСТЬ NULL )
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодтверждениеОткрытияЛицевыхСчетовСотрудниковСотрудники.ПервичныйДокумент КАК ПервичныйДокумент,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ДанныеЗаполненности.ПодтвержденияЗагруженыНеПоВсемСотрудникам = ИСТИНА
	|				ТОГДА ЛОЖЬ
	|			КОГДА ПодтверждениеОткрытияЛицевыхСчетовСотрудниковСотрудники.РезультатОткрытияСчета = ЗНАЧЕНИЕ(Перечисление.РезультатыОткрытияЛицевыхСчетовСотрудников.СчетОткрыт)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК СуществуетРезультатОткрытияСчетаСчетОткрыт,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ДанныеЗаполненности.ПодтвержденияЗагруженыНеПоВсемСотрудникам = ИСТИНА
	|				ТОГДА ЛОЖЬ
	|			КОГДА ПодтверждениеОткрытияЛицевыхСчетовСотрудниковСотрудники.РезультатОткрытияСчета <> ЗНАЧЕНИЕ(Перечисление.РезультатыОткрытияЛицевыхСчетовСотрудников.СчетОткрыт)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК СуществуетРезультатыОткрытияСчетаНеОткрыт
	|ПОМЕСТИТЬ ВТРезультатыОткрытияСчета
	|ИЗ
	|	ВТПодтвержденияОткрытияЛицевыхСчетовСотрудники КАК ПодтверждениеОткрытияЛицевыхСчетовСотрудниковСотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеЗаполненности КАК ДанныеЗаполненности
	|		ПО (ИСТИНА)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПодтверждениеОткрытияЛицевыхСчетовСотрудниковСотрудники.ПервичныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА НЕ ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.Проведен
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеЗаявкиНаОткрытиеЛицевогоСчетаСотрудника.ПустаяСсылка)
	|		КОГДА РезультатыОткрытияСчета.СуществуетРезультатОткрытияСчетаСчетОткрыт
	|				И РезультатыОткрытияСчета.СуществуетРезультатыОткрытияСчетаНеОткрыт
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеЗаявкиНаОткрытиеЛицевогоСчетаСотрудника.ЛицевыеСчетаОткрытыСОшибками)
	|		КОГДА РезультатыОткрытияСчета.СуществуетРезультатыОткрытияСчетаНеОткрыт
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеЗаявкиНаОткрытиеЛицевогоСчетаСотрудника.ЛицевыеСчетаНеОткрыты)
	|		КОГДА РезультатыОткрытияСчета.СуществуетРезультатОткрытияСчетаСчетОткрыт
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеЗаявкиНаОткрытиеЛицевогоСчетаСотрудника.ЛицевыеСчетаОткрыты)
	|		КОГДА ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.Проведен
	|			ТОГДА ВЫБОР
	|					КОГДА НаличиеФайлов.ЕстьФайлы
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеЗаявкиНаОткрытиеЛицевогоСчетаСотрудника.ОжидаетПодтверждения)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостояниеЗаявкиНаОткрытиеЛицевогоСчетаСотрудника.ГотовКВыгрузке)
	|				КОНЕЦ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостояниеЗаявкиНаОткрытиеЛицевогоСчетаСотрудника.ПустаяСсылка)
	|	КОНЕЦ КАК Состояние
	|ИЗ
	|	Документ.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников КАК ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРезультатыОткрытияСчета КАК РезультатыОткрытияСчета
	|		ПО ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.Ссылка = РезультатыОткрытияСчета.ПервичныйДокумент
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НаличиеФайлов КАК НаличиеФайлов
	|		ПО ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.Ссылка = НаличиеФайлов.ОбъектСФайлами
	|ГДЕ
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.Ссылка = &ПервичныйДокумент";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Состояние;
	КонецЕсли;
	
	Возврат Перечисления.СостояниеЗаявкиНаОткрытиеЛицевогоСчетаСотрудника.ПустаяСсылка();
	
КонецФункции

Функция СостояниеЗачисленияЗарплатыДляПодтверждения(ПодтверждениеЗачисленияЗарплаты) Экспорт
	
	ПервичныйДокумент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПодтверждениеЗачисленияЗарплаты, "ПервичныйДокумент");
	МетаданныеПервичногоДокумента = ПервичныйДокумент.Метаданные();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПервичныйДокумент", ПервичныйДокумент);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПодтверждениеЗачисленияЗарплаты.Ссылка КАК Ссылка,
	|	ПодтверждениеЗачисленияЗарплаты.ПервичныйДокумент КАК ПервичныйДокумент
	|ПОМЕСТИТЬ ВТПодтвержденияЗачисленияЗарплаты
	|ИЗ
	|	Документ.ПодтверждениеЗачисленияЗарплаты КАК ПодтверждениеЗачисленияЗарплаты
	|ГДЕ
	|	ПодтверждениеЗачисленияЗарплаты.ПервичныйДокумент = &ПервичныйДокумент
	|	И ПодтверждениеЗачисленияЗарплаты.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодтвержденияЗачисленияЗарплаты.Ссылка,
	|	ПодтвержденияЗачисленияЗарплаты.ПервичныйДокумент
	|ИЗ
	|	ВТПодтвержденияЗачисленияЗарплаты КАК ПодтвержденияЗачисленияЗарплаты";
	
	ПодтвержденияЗачисленияЗарплаты = Запрос.Выполнить().Выгрузить();
	
	ДокументыПодтверждений = ПодтвержденияЗачисленияЗарплаты.ВыгрузитьКолонку("Ссылка");
	ОбщегоНазначенияКлиентСервер.СвернутьМассив(ДокументыПодтверждений);
	
	ОплаченныеВедомости = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПервичныйДокумент);
	МетаданныеПлатежногоДокумента = МетаданныеПлатежногоДокументаПеречисленияЗарплаты();
	Если МетаданныеПлатежногоДокумента <> Неопределено И МетаданныеПлатежногоДокумента.ПолноеИмя() = МетаданныеПервичногоДокумента.ПолноеИмя() Тогда
		ОплаченныеВедомости = ВедомостиПлатежногоДокументаПеречисленияЗарплаты(ПервичныйДокумент);
	КонецЕсли;
	
	// Получим физических лиц по ведомостям и сравним их с физическими лицами в подтверждениях
	// Если подтверждения получены не по всем физическим лицам, то считаем, что результат "ЗачисленоПолностью" по
	// неполученным физическим лицам.
	ВзаиморасчетыССотрудниками.СоздатьВТДанныеВедомостейДляОплатыДокументами(
		Запрос.МенеджерВременныхТаблиц, Истина, ДокументыПодтверждений, ОплаченныеВедомости, , Неопределено);
		
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПодтверждениеЗачисленияЗарплаты.Ссылка КАК Ссылка,
	|	ПодтверждениеЗачисленияЗарплаты.ПервичныйДокумент КАК ПервичныйДокумент,
	|	ПодтверждениеЗачисленияЗарплатыСотрудники.ФизическоеЛицо,
	|	ПодтверждениеЗачисленияЗарплатыСотрудники.РезультатЗачисленияЗарплаты
	|ПОМЕСТИТЬ ВТПодтвержденияЗачисленияЗарплатыСотрудники
	|ИЗ
	|	ВТПодтвержденияЗачисленияЗарплаты КАК ПодтверждениеЗачисленияЗарплаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПодтверждениеЗачисленияЗарплаты.Сотрудники КАК ПодтверждениеЗачисленияЗарплатыСотрудники
	|		ПО ПодтверждениеЗачисленияЗарплаты.Ссылка = ПодтверждениеЗачисленияЗарплатыСотрудники.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК ПодтвержденияЗагруженыНеПоВсемСотрудникам
	|ПОМЕСТИТЬ ВТДанныеЗаполненности
	|ИЗ
	|	ВТДанныеВедомостейДляОплатыДокументами КАК ДанныеВедомостейДляОплатыДокументами
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТПодтвержденияЗачисленияЗарплатыСотрудники КАК ПодтвержденияЗачисленияЗарплаты
	|		ПО ДанныеВедомостейДляОплатыДокументами.ФизическоеЛицо = ПодтвержденияЗачисленияЗарплаты.ФизическоеЛицо
	|ГДЕ
	|	(ДанныеВедомостейДляОплатыДокументами.ФизическоеЛицо ЕСТЬ NULL 
	|			ИЛИ ПодтвержденияЗачисленияЗарплаты.ФизическоеЛицо ЕСТЬ NULL )
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодтверждениеЗачисленияЗарплатыСотрудники.ПервичныйДокумент КАК ПервичныйДокумент,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ДанныеЗаполненности.ПодтвержденияЗагруженыНеПоВсемСотрудникам = ИСТИНА
	|				ТОГДА ИСТИНА
	|			КОГДА ПодтверждениеЗачисленияЗарплатыСотрудники.РезультатЗачисленияЗарплаты = ЗНАЧЕНИЕ(Перечисление.РезультатыЗачисленияЗарплаты.Зачислено)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК СуществуетРезультатЗачисленияЗарплатыЗачислено,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ПодтверждениеЗачисленияЗарплатыСотрудники.РезультатЗачисленияЗарплаты <> ЗНАЧЕНИЕ(Перечисление.РезультатыЗачисленияЗарплаты.Зачислено)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК СуществуетРезультатЗачисленияЗарплатыНеЗачислено
	|ПОМЕСТИТЬ ВТРезультатыЗачисленияЗарплаты
	|ИЗ
	|	ВТПодтвержденияЗачисленияЗарплатыСотрудники КАК ПодтверждениеЗачисленияЗарплатыСотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеЗаполненности КАК ДанныеЗаполненности
	|		ПО (ИСТИНА)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПодтверждениеЗачисленияЗарплатыСотрудники.ПервичныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА НЕ ДокументЗачисленияЗарплаты.Проведен
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеЗачисленияЗарплаты.ПустаяСсылка)
	|		КОГДА РезультатыЗачисленияЗарплаты.СуществуетРезультатЗачисленияЗарплатыЗачислено
	|				И РезультатыЗачисленияЗарплаты.СуществуетРезультатЗачисленияЗарплатыНеЗачислено
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеЗачисленияЗарплаты.ЗачисленоСОшибками)
	|		КОГДА РезультатыЗачисленияЗарплаты.СуществуетРезультатЗачисленияЗарплатыНеЗачислено
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеЗачисленияЗарплаты.НеЗачислено)
	|		КОГДА РезультатыЗачисленияЗарплаты.СуществуетРезультатЗачисленияЗарплатыЗачислено
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеЗачисленияЗарплаты.ЗачисленоПолностью)
	|		КОГДА ДокументЗачисленияЗарплаты.Проведен
	|			ТОГДА ВЫБОР
	|					КОГДА НаличиеФайлов.ЕстьФайлы
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеЗачисленияЗарплаты.ОжидаетПодтверждения)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостояниеЗачисленияЗарплаты.ГотовКВыгрузке)
	|				КОНЕЦ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостояниеЗачисленияЗарплаты.ПустаяСсылка)
	|	КОНЕЦ КАК Состояние
	|ИЗ
	|	#ДокументЗачисленияЗарплаты КАК ДокументЗачисленияЗарплаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРезультатыЗачисленияЗарплаты КАК РезультатыЗачисленияЗарплаты
	|		ПО ДокументЗачисленияЗарплаты.Ссылка = РезультатыЗачисленияЗарплаты.ПервичныйДокумент
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НаличиеФайлов КАК НаличиеФайлов
	|		ПО ДокументЗачисленияЗарплаты.Ссылка = НаличиеФайлов.ОбъектСФайлами
	|ГДЕ
	|	ДокументЗачисленияЗарплаты.Ссылка = &ПервичныйДокумент";
	
	ИмяОбъекта = МетаданныеПервичногоДокумента.ПолноеИмя();
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ДокументЗачисленияЗарплаты", ИмяОбъекта);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Состояние;
	КонецЕсли;
	
	Возврат Перечисления.СостояниеЗачисленияЗарплаты.ПустаяСсылка();
	
КонецФункции

#КонецОбласти

#Область ПроцедурыИФункцииЗаполненияЗаявкиНаОткрытиеСчетов

// Формирует временную таблицу ВТСостоянияОткрытияЛицевыхСчетовФизическихЛиц, 
// список физических лиц, с состояниями открытия лицевых счетов.
//
// Параметры:
//		МенеджерВременныхТаблиц
//		Организация
//		ЗарплатныйПроект
//		ДатаПолученияДанных - дата получения кадровой информации по физическим лицам.
//		Подразделение - не обязательно для заполнения.
//		СписокФизическихЛиц - список физических лиц, по которым нужно получить данные,
//				если не указан - данные получаются по всем физическим лицам организации и подразделения.
//		ТолькоРазрешенные - Булево
//
// Создает в МенеджерВременныхТаблиц временную таблицу с именем ВТСостоянияОткрытияЛицевыхСчетовФизическихЛиц.
// Поля временной таблицы
//		ФизическоеЛицо,
//		Организация,
//		ЗарплатныйПроект,
//		ОткрытиеЛицевогоСчетаОтложено - булево, Истина, если по физическому лицу отложено открытие лицевого счета, иначе
//		                                Ложь.
//		ОжидаетПодтверждения - булево, Истина, если по физическому лицу ожидается подтверждение открытия лицевого счета,
//		                       иначе Ложь.
//		ЛицевойСчетНеОткрыт - булево, Истина, если по физическому лицу получено подтверждение открытия лицевого счета
//								и результат открытия отрицательный, иначе Ложь.
//		ЗаявкаНеОформлялась - булево, Истина, если по физическому лицу еще не создавались документы и не откладывалось
//		                      открытие лицевого счета, иначе Ложь.
//		Документ - ссылка на Документ.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников, если документа нет - NULL.
//		НомерСтрокиДокумента - номер строки в Документ.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников, если документа нет - NULL.
//
Процедура СоздатьВТСостоянияОткрытияЛицевыхСчетовФизическихЛиц(МенеджерВременныхТаблиц, Организация, ЗарплатныйПроект, ДатаПолученияДанных, Подразделение = Неопределено, СписокФизическихЛиц = Неопределено, ТолькоРазрешенные = Истина, ЗаявкаНаОткрытиеЛицевыхСчетов = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ЗарплатныйПроект", ЗарплатныйПроект);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("ДатаПолученияДанных", ДатаПолученияДанных);
	Запрос.УстановитьПараметр("СписокФизическихЛиц", СписокФизическихЛиц);
	Запрос.УстановитьПараметр("ПоВсемФизическимЛицам", ?(СписокФизическихЛиц = Неопределено, Истина, Ложь));
	Запрос.УстановитьПараметр("ЗаявкаНаОткрытиеЛицевыхСчетов", ЗаявкаНаОткрытиеЛицевыхСчетов);
	
	ПараметрыПолученияСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудников.Организация = Организация;
	Если Подразделение <> Неопределено Тогда
		ПараметрыПолученияСотрудников.Подразделение = Подразделение;
	КонецЕсли;
	ПараметрыПолученияСотрудников.ОкончаниеПериода = ДатаПолученияДанных;
	ПараметрыПолученияСотрудников.КадровыеДанные = "НомерЛицевогоСчета, ВидЗанятости";
	ОбменСБанкамиПоЗарплатнымПроектамВнутренний.ДополнитьПараметрыПолученияСотрудников(ПараметрыПолученияСотрудников);
	
	Если СписокФизическихЛиц <> Неопределено Тогда
		ПараметрыПолученияСотрудников.СписокФизическихЛиц = СписокФизическихЛиц;
	КонецЕсли;
	
	КадровыйУчет.СоздатьВТСотрудникиОрганизации(Запрос.МенеджерВременныхТаблиц, ТолькоРазрешенные, ПараметрыПолученияСотрудников, "ВТВсеСотрудникиОрганизации");
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.Организация,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ФизическоеЛицо
	|ПОМЕСТИТЬ ВТОтложенноеОткрытиеЛицевыхСчетовСотрудников
	|ИЗ
	|	РегистрСведений.ОтложенноеОткрытиеЛицевыхСчетовСотрудников КАК ОтложенноеОткрытиеЛицевыхСчетовСотрудников
	|ГДЕ
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.Организация = &Организация
	|	И (&ПоВсемФизическимЛицам
	|			ИЛИ ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ФизическоеЛицо В (&СписокФизическихЛиц))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудники.Ссылка КАК ДокументСсылка,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудники.Ссылка.Дата КАК ДатаДокумента
	|ПОМЕСТИТЬ ВТЗаявкиФизическихЛиц
	|ИЗ
	|	Документ.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.Сотрудники КАК ЗаявкаНаОткрытиеЛицевыхСчетовСотрудники
	|ГДЕ
	|	НЕ ЗаявкаНаОткрытиеЛицевыхСчетовСотрудники.Ссылка.ПометкаУдаления
	|	И (&ПоВсемФизическимЛицам
	|			ИЛИ ЗаявкаНаОткрытиеЛицевыхСчетовСотрудники.ФизическоеЛицо В (&СписокФизическихЛиц))
	|	И (ЗаявкаНаОткрытиеЛицевыхСчетовСотрудники.Ссылка.Организация = &Организация
	|			ИЛИ &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|	И (ЗаявкаНаОткрытиеЛицевыхСчетовСотрудники.Ссылка.ЗарплатныйПроект = &ЗарплатныйПроект
	|			ИЛИ &ЗарплатныйПроект = ЗНАЧЕНИЕ(Справочник.ЗарплатныеПроекты.ПустаяСсылка))
	|	И (ЗаявкаНаОткрытиеЛицевыхСчетовСотрудники.Ссылка.Подразделение = &Подразделение
	|			ИЛИ &Подразделение = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|			ИЛИ &Подразделение = НЕОПРЕДЕЛЕНО)
	|	И &ЗаявкаНаОткрытиеЛицевыхСчетов = НЕОПРЕДЕЛЕНО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаявкиФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
	|	МАКСИМУМ(ЗаявкиФизическихЛиц.ДатаДокумента) КАК ДатаДокумента
	|ПОМЕСТИТЬ ВТДатыПоследнихЗаявок
	|ИЗ
	|	ВТЗаявкиФизическихЛиц КАК ЗаявкиФизическихЛиц
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаявкиФизическихЛиц.ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаявкиФизическихЛиц.ФизическоеЛицо,
	|	МАКСИМУМ(ЗаявкиФизическихЛиц.ДокументСсылка) КАК Документ
	|ПОМЕСТИТЬ ВТЗаявкиНаОткрытиеЛицевыхСчетов
	|ИЗ
	|	ВТЗаявкиФизическихЛиц КАК ЗаявкиФизическихЛиц
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДатыПоследнихЗаявок КАК ДатыПоследнихЗаявок
	|		ПО (ДатыПоследнихЗаявок.ФизическоеЛицо = ЗаявкиФизическихЛиц.ФизическоеЛицо)
	|			И (ДатыПоследнихЗаявок.ДатаДокумента = ЗаявкиФизическихЛиц.ДатаДокумента)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаявкиФизическихЛиц.ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ПодтверждениеОткрытияЛицевыхСчетовСотрудники.Ссылка КАК ДокументПодтверждение,
	|	ПодтверждениеОткрытияЛицевыхСчетовСотрудники.РезультатОткрытияСчета КАК РезультатОткрытияСчета,
	|	ЗаявкиНаОткрытиеЛицевыхСчетов.Документ КАК Документ,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудники.Ссылка.Проведен КАК Проведен,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудники.Ссылка.Организация КАК Организация,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудники.Ссылка.ЗарплатныйПроект КАК ЗарплатныйПроект,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудники.Ссылка.Подразделение КАК Подразделение,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудники.НомерСтроки КАК НомерСтрокиДокумента
	|ПОМЕСТИТЬ ВТЗаявкиНаОткрытиеЛицевыхСчетовПоФизическимЛицам
	|ИЗ
	|	ВТЗаявкиНаОткрытиеЛицевыхСчетов КАК ЗаявкиНаОткрытиеЛицевыхСчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.Сотрудники КАК ЗаявкаНаОткрытиеЛицевыхСчетовСотрудники
	|		ПО ЗаявкиНаОткрытиеЛицевыхСчетов.ФизическоеЛицо = ЗаявкаНаОткрытиеЛицевыхСчетовСотрудники.ФизическоеЛицо
	|			И ЗаявкиНаОткрытиеЛицевыхСчетов.Документ = ЗаявкаНаОткрытиеЛицевыхСчетовСотрудники.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПодтверждениеОткрытияЛицевыхСчетовСотрудников.Сотрудники КАК ПодтверждениеОткрытияЛицевыхСчетовСотрудники
	|		ПО ЗаявкиНаОткрытиеЛицевыхСчетов.ФизическоеЛицо = ПодтверждениеОткрытияЛицевыхСчетовСотрудники.ФизическоеЛицо
	|			И ЗаявкиНаОткрытиеЛицевыхСчетов.Документ = ПодтверждениеОткрытияЛицевыхСчетовСотрудники.Ссылка.ПервичныйДокумент
	|			И (ПодтверждениеОткрытияЛицевыхСчетовСотрудники.Ссылка.Проведен)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаявкиНаОткрытиеЛицевыхСчетовПоФизическимЛицам.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЗаявкиНаОткрытиеЛицевыхСчетовПоФизическимЛицам.Организация КАК Организация,
	|	ЛОЖЬ КАК ОткрытиеЛицевогоСчетаОтложено,
	|	ВЫБОР
	|		КОГДА СостоянияДокументовОткрытияЛицевыхСчетов.Состояние = ЗНАЧЕНИЕ(Перечисление.СостояниеЗаявкиНаОткрытиеЛицевогоСчетаСотрудника.ОжидаетПодтверждения)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОжидаетПодтверждения,
	|	ВЫБОР
	|		КОГДА ЗаявкиНаОткрытиеЛицевыхСчетовПоФизическимЛицам.РезультатОткрытияСчета ЕСТЬ NULL 
	|				ИЛИ ЗаявкиНаОткрытиеЛицевыхСчетовПоФизическимЛицам.РезультатОткрытияСчета = ЗНАЧЕНИЕ(Перечисление.РезультатыОткрытияЛицевыхСчетовСотрудников.СчетОткрыт)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЛицевойСчетНеОткрыт,
	|	ЛОЖЬ КАК ЗаявкаНеОформлялась,
	|	ЗаявкиНаОткрытиеЛицевыхСчетовПоФизическимЛицам.Документ КАК ДокументОткрытияЛицевыхСчетов,
	|	ЗаявкиНаОткрытиеЛицевыхСчетовПоФизическимЛицам.ЗарплатныйПроект КАК ЗарплатныйПроект,
	|	ЗаявкиНаОткрытиеЛицевыхСчетовПоФизическимЛицам.Подразделение КАК Подразделение,
	|	ЗаявкиНаОткрытиеЛицевыхСчетовПоФизическимЛицам.НомерСтрокиДокумента КАК НомерСтрокиДокумента
	|ПОМЕСТИТЬ ВТСостоянияОткрытияЛицевыхСчетовФизическихЛиц
	|ИЗ
	|	ВТЗаявкиНаОткрытиеЛицевыхСчетовПоФизическимЛицам КАК ЗаявкиНаОткрытиеЛицевыхСчетовПоФизическимЛицам
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.СрезПоследних(&ДатаПолученияДанных, Организация = &Организация) КАК ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам
	|		ПО ЗаявкиНаОткрытиеЛицевыхСчетовПоФизическимЛицам.ФизическоеЛицо = ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияДокументовОткрытияЛицевыхСчетов КАК СостоянияДокументовОткрытияЛицевыхСчетов
	|		ПО ЗаявкиНаОткрытиеЛицевыхСчетовПоФизическимЛицам.Документ = СостоянияДокументовОткрытияЛицевыхСчетов.ДокументОткрытияЛицевыхСчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОтложенноеОткрытиеЛицевыхСчетовСотрудников КАК ОтложенноеОткрытиеЛицевыхСчетовСотрудников
	|		ПО ЗаявкиНаОткрытиеЛицевыхСчетовПоФизическимЛицам.ФизическоеЛицо = ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ФизическоеЛицо
	|			И ЗаявкиНаОткрытиеЛицевыхСчетовПоФизическимЛицам.Организация = ОтложенноеОткрытиеЛицевыхСчетовСотрудников.Организация
	|ГДЕ
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ФизическоеЛицо ЕСТЬ NULL 
	|	И ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.НомерЛицевогоСчета ЕСТЬ NULL 
	|	И (ЗаявкиНаОткрытиеЛицевыхСчетовПоФизическимЛицам.ДокументПодтверждение ЕСТЬ NULL 
	|			ИЛИ ЗаявкиНаОткрытиеЛицевыхСчетовПоФизическимЛицам.ДокументПодтверждение ЕСТЬ НЕ NULL 
	|				И ЗаявкиНаОткрытиеЛицевыхСчетовПоФизическимЛицам.РезультатОткрытияСчета <> ЗНАЧЕНИЕ(Перечисление.РезультатыОткрытияЛицевыхСчетовСотрудников.СчетОткрыт))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ФизическоеЛицо,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.Организация,
	|	ИСТИНА,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL
	|ИЗ
	|	ВТОтложенноеОткрытиеЛицевыхСчетовСотрудников КАК ОтложенноеОткрытиеЛицевыхСчетовСотрудников
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СотрудникиОрганизации.ФизическоеЛицо,
	|	&Организация,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ИСТИНА,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL
	|ИЗ
	|	ВТВсеСотрудникиОрганизации КАК СотрудникиОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаявкиНаОткрытиеЛицевыхСчетовПоФизическимЛицам КАК ЗаявкиНаОткрытиеЛицевыхСчетовПоФизическимЛицам
	|		ПО СотрудникиОрганизации.ФизическоеЛицо = ЗаявкиНаОткрытиеЛицевыхСчетовПоФизическимЛицам.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОтложенноеОткрытиеЛицевыхСчетовСотрудников КАК ОтложенноеОткрытиеЛицевыхСчетовСотрудников
	|		ПО СотрудникиОрганизации.ФизическоеЛицо = ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ФизическоеЛицо
	|ГДЕ
	|	СотрудникиОрганизации.НомерЛицевогоСчета ЕСТЬ NULL 
	|	И ЗаявкиНаОткрытиеЛицевыхСчетовПоФизическимЛицам.ФизическоеЛицо ЕСТЬ NULL 
	|	И ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ФизическоеЛицо ЕСТЬ NULL 
	|	И (&ПоВсемФизическимЛицам
	|			ИЛИ СотрудникиОрганизации.ФизическоеЛицо В (&СписокФизическихЛиц))
	|
	|СГРУППИРОВАТЬ ПО
	|	СотрудникиОрганизации.ФизическоеЛицо";
	
	Если Не ТолькоРазрешенные Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "РАЗРЕШЕННЫЕ", "");
	КонецЕсли;
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Получает данные сотрудников в порядке приоритета.
// 1. Данные из документов "Заявка на открытие лицевых счетов", по которым нет подтверждения открытия лицевых счетов.
// 2. Данные из регистра сведений "Отложенное открытие лицевых счетов сотрудников".
// 3. Кадровые данные сотрудников.
//
// Параметры:
//		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц, подготовленный методом
//		                          СоздатьВТСостоянияОткрытияЛицевыхСчетовФизическихЛиц.
//		ДатаПолученияДанных - дата, на которую будут получены периодические значения.
//		Организация - Организация
//		ЗарплатныйПроект - зарплатный проект.
//		СписокФизическихЛиц - список физических лиц, по которым нужно получить данные,
//				если не указан - кадровые данные получаются по всем физическим лицам организации и подразделения.
//
// Возвращаемое значение:
//		Таблица значений с данными сотрудников.
//
Функция ДанныеСотрудниковДляОткрытияЛицевыхСчетов(МенеджерВременныхТаблиц, ДатаПолученияДанных, Организация, ЗарплатныйПроект, СписокФизическихЛиц = Неопределено, ЗаявкаНаОткрытиеЛицевыхСчетов = Неопределено, ПолучитьТолькоЛицевойСчетНеОткрыт = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.Ссылка КАК ДокументЗаявка,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.НомерСтроки КАК НомерСтрокиДокумента,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.Фамилия КАК Фамилия,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.Имя КАК Имя,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.Отчество КАК Отчество,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.Должность КАК Должность,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ЭмбоссированныйТекст1 КАК ЭмбоссированныйТекст1,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ЭмбоссированныйТекст2 КАК ЭмбоссированныйТекст2,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ЭмбоссированныйТекст3 КАК ЭмбоссированныйТекст3,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.СистемаРасчетовПоБанковскимКартам КАК СистемаРасчетовПоБанковскимКартам,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.СуммаПервоначальногоПополнения КАК СуммаПервоначальногоПополнения,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ЯвляетсяЗарплатнойКартой КАК ЯвляетсяЗарплатнойКартой,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ОплатаЗарплатнойКарты КАК ОплатаЗарплатнойКарты,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ЯвляетсяСотрудникомБанка КАК ЯвляетсяСотрудникомБанка,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ИспользуетсяОвердрафт КАК ИспользуетсяОвердрафт,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.КатегорияСотрудника КАК КатегорияСотрудника,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ЯвляетсяУчастникомБонуснойПрограммы КАК ЯвляетсяУчастникомБонуснойПрограммы,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.БонуснаяПрограмма КАК БонуснаяПрограмма,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.НомерУчастникаБонуснойПрограммы КАК НомерУчастникаБонуснойПрограммы,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ТарифСледующийГод КАК ТарифСледующийГод,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ТарифТекущийГод КАК ТарифТекущийГод,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.СпособРассылкиОтчета КАК СпособРассылкиОтчета,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.АдресЭлектроннойПочты КАК АдресЭлектроннойПочты,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.СчетДебета КАК СчетДебета,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.НомерМобильногоТелефона КАК НомерМобильногоТелефона,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.НомерМобильногоТелефонаПредставление КАК НомерМобильногоТелефонаПредставление,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ОператорСвязи КАК ОператорСвязи,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ИспользованиеМобильногоБанка КАК ИспользованиеМобильногоБанка,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ТарифМобильногоБанка КАК ТарифМобильногоБанка,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.РазрешитьПередачуИнформацииВБКИ КАК РазрешитьПередачуИнформацииВБКИ,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.КонтрольнаяИнформация КАК КонтрольнаяИнформация,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.НомерМиграционнойКарты КАК НомерМиграционнойКарты,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ДатаНачалаПребыванияМиграционнойКарты КАК ДатаНачалаПребыванияМиграционнойКарты,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ДатаОкончанияПребыванияМиграционнойКарты КАК ДатаОкончанияПребыванияМиграционнойКарты,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ВидМиграционногоДокумента КАК ВидМиграционногоДокумента,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.НомерМиграционногоДокумента КАК НомерМиграционногоДокумента,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ДатаНачалаПребыванияМиграционногоДокумента КАК ДатаНачалаПребыванияМиграционногоДокумента,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ДатаОкончанияПребыванияМиграционногоДокумента КАК ДатаОкончанияПребыванияМиграционногоДокумента,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ПрогнозируемыйМесячныйДоход КАК ПрогнозируемыйМесячныйДоход,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ТабельныйНомер КАК ТабельныйНомер,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ДатаПриема КАК ДатаПриема,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ДеньВыплатыЗарплаты КАК ДеньВыплатыЗарплаты,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.КодВидаВклада КАК КодВидаВклада,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.КодПодвидаВклада КАК КодПодвидаВклада,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ВалютаВклада КАК ВалютаВклада,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ДокументВид КАК ДокументВид,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ДокументСерия КАК ДокументСерия,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ДокументНомер КАК ДокументНомер,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ДокументДатаВыдачи КАК ДокументДатаВыдачи,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ДокументСрокДействия КАК ДокументСрокДействия,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ДокументКемВыдан КАК ДокументКемВыдан,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ДокументКодПодразделения КАК ДокументКодПодразделения,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ДокументПредставление КАК ДокументПредставление,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.Пол КАК Пол,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ДатаРождения КАК ДатаРождения,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.МестоРождения КАК МестоРождения,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.МестоРожденияПредставление КАК МестоРожденияПредставление,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.АдресДляИнформирования КАК АдресДляИнформирования,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.АдресДляИнформированияПредставление КАК АдресДляИнформированияПредставление,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.АдресМестаРаботы КАК АдресМестаРаботы,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.АдресМестаРаботыПредставление КАК АдресМестаРаботыПредставление,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.АдресПоПрописке КАК АдресПоПрописке,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.АдресПоПропискеПредставление КАК АдресПоПропискеПредставление,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.АдресМестаПроживания КАК АдресМестаПроживания,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.АдресМестаПроживанияПредставление КАК АдресМестаПроживанияПредставление,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ТелефонДомашний КАК ТелефонДомашний,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ТелефонДомашнийПредставление КАК ТелефонДомашнийПредставление,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ТелефонРабочий КАК ТелефонРабочий,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ТелефонРабочийПредставление КАК ТелефонРабочийПредставление,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ЯвляетсяРезидентом КАК ЯвляетсяРезидентом,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.Гражданство КАК Гражданство,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ИдентификаторДизайна КАК ИдентификаторДизайна,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ПВК КАК ПВК,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ПриложениеКартаКод КАК ПриложениеКартаКод,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ПриложениеКартаПараметры КАК ПриложениеКартаПараметры,
	|	ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.СтраховойНомерПФР КАК СтраховойНомерПФР
	|ИЗ
	|	ВТСостоянияОткрытияЛицевыхСчетовФизическихЛиц КАК СостоянияФизическихЛиц
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.Сотрудники КАК ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники
	|		ПО СостоянияФизическихЛиц.ДокументОткрытияЛицевыхСчетов = ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.Ссылка
	|			И СостоянияФизическихЛиц.ФизическоеЛицо = ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.ФизическоеЛицо
	|			И СостоянияФизическихЛиц.НомерСтрокиДокумента = ЗаявкаНаОткрытиеЛицевыхСчетовСотрудниковСотрудники.НомерСтроки";
	
	Если ЗаявкаНаОткрытиеЛицевыхСчетов = Неопределено Тогда
		ДанныеПоЛицевымСчетамСотрудников = Запрос.Выполнить().Выгрузить();
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.Организация,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ФизическоеЛицо,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.Должность,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ЭмбоссированныйТекст1,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ЭмбоссированныйТекст2,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ЭмбоссированныйТекст3,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.СистемаРасчетовПоБанковскимКартам,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.СуммаПервоначальногоПополнения,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ЯвляетсяЗарплатнойКартой,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ОплатаЗарплатнойКарты,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ЯвляетсяСотрудникомБанка,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ИспользуетсяОвердрафт,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.КатегорияСотрудника,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ЯвляетсяУчастникомБонуснойПрограммы,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.БонуснаяПрограмма,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.НомерУчастникаБонуснойПрограммы,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ТарифСледующийГод,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ТарифТекущийГод,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.СпособРассылкиОтчета,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.АдресЭлектроннойПочты,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.СчетДебета,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.НомерМобильногоТелефона,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.НомерМобильногоТелефонаПредставление,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ОператорСвязи,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ИспользованиеМобильногоБанка,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ТарифМобильногоБанка,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.РазрешитьПередачуИнформацииВБКИ,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.КонтрольнаяИнформация,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.НомерМиграционнойКарты,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ДатаНачалаПребыванияМиграционнойКарты,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ДатаОкончанияПребыванияМиграционнойКарты,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ВидМиграционногоДокумента,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.НомерМиграционногоДокумента,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ДатаНачалаПребыванияМиграционногоДокумента,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ДатаОкончанияПребыванияМиграционногоДокумента,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ПрогнозируемыйМесячныйДоход,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ДеньВыплатыЗарплаты,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.КодВидаВклада,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.КодПодвидаВклада,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ВалютаВклада,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.АдресДляИнформирования,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.АдресДляИнформированияПредставление,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.АдресМестаРаботы,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.АдресМестаРаботыПредставление,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ИдентификаторДизайна,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ПВК,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ПриложениеКартаКод,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ПриложениеКартаПараметры,
	|	ОтложенноеОткрытиеЛицевыхСчетовСотрудников.СтраховойНомерПФР
	|ИЗ
	|	ВТОтложенноеОткрытиеЛицевыхСчетовСотрудников КАК ОтложенноеОткрытиеЛицевыхСчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОтложенноеОткрытиеЛицевыхСчетовСотрудников КАК ОтложенноеОткрытиеЛицевыхСчетовСотрудников
	|		ПО ОтложенноеОткрытиеЛицевыхСчетов.Организация = ОтложенноеОткрытиеЛицевыхСчетовСотрудников.Организация
	|			И ОтложенноеОткрытиеЛицевыхСчетов.ФизическоеЛицо = ОтложенноеОткрытиеЛицевыхСчетовСотрудников.ФизическоеЛицо";
	
	Если ЗаявкаНаОткрытиеЛицевыхСчетов = Неопределено Тогда
		ДанныеПоОтложенномуОткрытиюЛицевыхСчетовСотрудников = Запрос.Выполнить().Выгрузить();
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СостоянияОткрытияЛицевыхСчетовФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СостоянияОткрытияЛицевыхСчетовФизическихЛиц.Организация КАК Организация,
	|	СостоянияОткрытияЛицевыхСчетовФизическихЛиц.ОткрытиеЛицевогоСчетаОтложено КАК ОткрытиеЛицевогоСчетаОтложено,
	|	СостоянияОткрытияЛицевыхСчетовФизическихЛиц.ОжидаетПодтверждения КАК ОжидаетПодтверждения,
	|	СостоянияОткрытияЛицевыхСчетовФизическихЛиц.ЛицевойСчетНеОткрыт КАК ЛицевойСчетНеОткрыт,
	|	СостоянияОткрытияЛицевыхСчетовФизическихЛиц.ЗаявкаНеОформлялась КАК ЗаявкаНеОформлялась,
	|	СостоянияОткрытияЛицевыхСчетовФизическихЛиц.ДокументОткрытияЛицевыхСчетов КАК ДокументОткрытияЛицевыхСчетов,
	|	СостоянияОткрытияЛицевыхСчетовФизическихЛиц.ЗарплатныйПроект КАК ЗарплатныйПроект,
	|	СостоянияОткрытияЛицевыхСчетовФизическихЛиц.Подразделение КАК Подразделение,
	|	СостоянияОткрытияЛицевыхСчетовФизическихЛиц.НомерСтрокиДокумента КАК НомерСтрокиДокумента
	|ИЗ
	|	ВТСостоянияОткрытияЛицевыхСчетовФизическихЛиц КАК СостоянияОткрытияЛицевыхСчетовФизическихЛиц";
	
	ДанныеПоСостояниямОткрытияЛицевыхСчетов = Запрос.Выполнить().Выгрузить();
	
	Если СписокФизическихЛиц = Неопределено Тогда
		СписокФизическихЛиц = ДанныеПоСостояниямОткрытияЛицевыхСчетов.ВыгрузитьКолонку("ФизическоеЛицо");
	КонецЕсли;
	
	КадровыйУчет.СоздатьВТОсновныеСотрудникиФизическихЛиц(Запрос.МенеджерВременныхТаблиц, Истина, СписокФизическихЛиц, Организация, ДатаПолученияДанных);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОсновныеСотрудникиФизическихЛиц.Сотрудник
	|ИЗ
	|	ВТОсновныеСотрудникиФизическихЛиц КАК ОсновныеСотрудникиФизическихЛиц
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.Сотрудник
	|ИЗ
	|	ВТВсеСотрудникиОрганизации КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОсновныеСотрудникиФизическихЛиц КАК ОсновныеСотрудникиФизическихЛиц
	|		ПО СотрудникиОрганизаций.ФизическоеЛицо = ОсновныеСотрудникиФизическихЛиц.ФизическоеЛицо
	|ГДЕ
	|	ОсновныеСотрудникиФизическихЛиц.Сотрудник ЕСТЬ NULL 
	|	И СотрудникиОрганизаций.ВидЗанятости ЕСТЬ NULL ";
	
	СписокСотрудников = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Сотрудник");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	КадровыеДанные = "Фамилия, Имя, Отчество, АдресДляИнформирования, АдресДляИнформированияПредставление,"
		+ "АдресМестаПроживания, АдресМестаПроживанияПредставление, АдресПоПрописке, АдресПоПропискеПредставление,"
		+ "ТелефонДомашний, ТелефонДомашнийПредставление, ТелефонРабочий, ТелефонРабочийПредставление, Страна,"
		+ "СтатусНалогоплательщика, Пол, ДокументПредставление, НомерЛицевогоСчета, EmailПредставление,"
		+ "ДокументВид, ДокументСерия, ДокументНомер, ДокументДатаВыдачи, ДокументСрокДействия, ДокументКемВыдан,"
		+ "ДокументКодПодразделения, МестоРождения, ДатаРождения, ТабельныйНомер, Должность, ДатаПриема, Подразделение, СтраховойНомерПФР";
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Сотрудники.Ссылка КАК Сотрудник,
	|	КадровыеДанныеСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
	|	КадровыеДанныеСотрудников.Фамилия КАК Фамилия,
	|	КадровыеДанныеСотрудников.Имя КАК Имя,
	|	КадровыеДанныеСотрудников.Отчество КАК Отчество,
	|	КадровыеДанныеСотрудников.АдресДляИнформирования КАК АдресДляИнформирования,
	|	КадровыеДанныеСотрудников.АдресДляИнформированияПредставление КАК АдресДляИнформированияПредставление,
	|	КадровыеДанныеСотрудников.АдресМестаПроживания КАК АдресМестаПроживания,
	|	КадровыеДанныеСотрудников.АдресМестаПроживанияПредставление КАК АдресМестаПроживанияПредставление,
	|	КадровыеДанныеСотрудников.АдресПоПрописке КАК АдресПоПрописке,
	|	КадровыеДанныеСотрудников.АдресПоПропискеПредставление КАК АдресПоПропискеПредставление,
	|	КадровыеДанныеСотрудников.ТелефонДомашний КАК ТелефонДомашний,
	|	КадровыеДанныеСотрудников.ТелефонДомашнийПредставление КАК ТелефонДомашнийПредставление,
	|	КадровыеДанныеСотрудников.ТелефонРабочий КАК ТелефонРабочий,
	|	КадровыеДанныеСотрудников.ТелефонРабочийПредставление КАК ТелефонРабочийПредставление,
	|	КадровыеДанныеСотрудников.EmailПредставление КАК АдресЭлектроннойПочты,
	|	КадровыеДанныеСотрудников.Страна КАК Страна,
	|	КадровыеДанныеСотрудников.СтатусНалогоплательщика КАК СтатусНалогоплательщика,
	|	КадровыеДанныеСотрудников.Пол КАК Пол,
	|	КадровыеДанныеСотрудников.ДокументПредставление КАК ДокументПредставление,
	|	КадровыеДанныеСотрудников.ДокументВид КАК ДокументВид,
	|	КадровыеДанныеСотрудников.ДокументСерия КАК ДокументСерия,
	|	КадровыеДанныеСотрудников.ДокументНомер КАК ДокументНомер,
	|	КадровыеДанныеСотрудников.ДокументДатаВыдачи КАК ДокументДатаВыдачи,
	|	КадровыеДанныеСотрудников.ДокументСрокДействия КАК ДокументСрокДействия,
	|	КадровыеДанныеСотрудников.ДокументКемВыдан КАК ДокументКемВыдан,
	|	КадровыеДанныеСотрудников.ДокументКодПодразделения КАК ДокументКодПодразделения,
	|	КадровыеДанныеСотрудников.МестоРождения КАК МестоРождения,
	|	КадровыеДанныеСотрудников.ДатаРождения КАК ДатаРождения,
	|	КадровыеДанныеСотрудников.ТабельныйНомер КАК ТабельныйНомер,
	|	КадровыеДанныеСотрудников.Должность КАК Должность,
	|	КадровыеДанныеСотрудников.ДатаПриема КАК ДатаПриема,
	|	КадровыеДанныеСотрудников.Подразделение КАК Подразделение,
	|	КадровыеДанныеСотрудников.НомерЛицевогоСчета КАК НомерЛицевогоСчета,
	|	КадровыеДанныеСотрудников.СтраховойНомерПФР КАК СтраховойНомерПФР
	|ИЗ
	|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|		ПО КадровыеДанныеСотрудников.Сотрудник = Сотрудники.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПодразделенияОрганизаций КАК Подразделения
	|		ПО КадровыеДанныеСотрудников.Подразделение = Подразделения.Ссылка
	|ГДЕ
	|	(НЕ Подразделения.Ссылка ЕСТЬ NULL
	|			ИЛИ КадровыеДанныеСотрудников.Подразделение ЕСТЬ NULL)";
	
	ОбменСБанкамиПоЗарплатнымПроектамВнутренний.ДополнитьКадровыеДанные(КадровыеДанные, Запрос.Текст);
	
	КадровыйУчет.СоздатьНаДатуВТКадровыеДанныеСотрудников(Запрос.МенеджерВременныхТаблиц, Истина, СписокСотрудников, КадровыеДанные, ДатаПолученияДанных);
	
	ПсевдонимыТаблиц = Новый Соответствие;
	ПсевдонимыТаблиц.Вставить("Справочник.ПодразделенияОрганизаций", "КадровыеДанныеСотрудников");
	ПсевдонимыТаблиц.Вставить("Справочник.Должности", "КадровыеДанныеСотрудников");
	ПсевдонимыТаблиц.Вставить("Справочник.Сотрудники", "Сотрудники");
	ЗарплатаКадры.ДополнитьТекстЗапросаУпорядочиваниемСотрудников(Запрос, ПсевдонимыТаблиц);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ДанныеСотрудников = РезультатЗапроса.Выгрузить();
	
	МассивОбъектов = Новый Массив;
	МассивОбъектов.Добавить(Организация);
	ФактАдресОрганизации = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(
			МассивОбъектов,
			Перечисления.ТипыКонтактнойИнформации.Адрес,
			Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации,
			ДатаПолученияДанных);
	
	АдресМестаРаботы = Новый Структура;
	АдресМестаРаботы.Вставить("Значение", ?(ФактАдресОрганизации.Количество() = 0, "", ФактАдресОрганизации[0].ЗначенияПолей));
	АдресМестаРаботы.Вставить("Представление", ?(ФактАдресОрганизации.Количество() = 0, "", ФактАдресОрганизации[0].Представление));
	
	СоответствиеСимволов = ОбменСБанкамиПоЗарплатнымПроектамКлиентСервер.СоответствиеСимволовЭмбоссированногоТекста();
	СистемаРасчетовПоБанковскимКартам = СистемаРасчетовПоБанковскимКартам(ЗарплатныйПроект);
	
	ОткрытиеЛицевыхСчетов = Новый ТаблицаЗначений;
	КолонкиДокумента = Метаданные.Документы.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников.ТабличныеЧасти.Сотрудники.Реквизиты;
	Для Каждого КолонкаДокумента Из КолонкиДокумента Цикл
		ОткрытиеЛицевыхСчетов.Колонки.Добавить(КолонкаДокумента.Имя, КолонкаДокумента.Тип);
	КонецЦикла;
	ОткрытиеЛицевыхСчетов.Колонки.Добавить("ДокументЗаявка", Новый ОписаниеТипов("ДокументСсылка.ЗаявкаНаОткрытиеЛицевыхСчетовСотрудников"));
	ОткрытиеЛицевыхСчетов.Колонки.Добавить("ОткрытиеЛицевогоСчетаОтложено", Новый ОписаниеТипов("Булево"));
	ОткрытиеЛицевыхСчетов.Колонки.Добавить("ОжидаетПодтверждения", Новый ОписаниеТипов("Булево"));
	ОткрытиеЛицевыхСчетов.Колонки.Добавить("ЛицевойСчетНеОткрыт", Новый ОписаниеТипов("Булево"));
	ОткрытиеЛицевыхСчетов.Колонки.Добавить("АнкетаЗаполнена", Новый ОписаниеТипов("Булево"));
	
	ДеньВыплатыЗарплатыВОрганизации = ДеньВыплатыЗарплатыВОрганизации(Организация);
	
	Для каждого ДанныеСотрудника Из ДанныеСотрудников Цикл
		
		ДанныеПоСостояниюОткрытияЛицевогоСчета = ДанныеПоСостояниямОткрытияЛицевыхСчетов.Найти(
				ДанныеСотрудника.ФизическоеЛицо, "ФизическоеЛицо");
		
		Если ЗаявкаНаОткрытиеЛицевыхСчетов <> Неопределено И ДанныеПоСостояниюОткрытияЛицевогоСчета <> Неопределено
					И ДанныеПоСостояниюОткрытияЛицевогоСчета.ОткрытиеЛицевогоСчетаОтложено Тогда
			// В документ "Заявка на открытие лицевых счетов сотрудников" не заполняем данные по отложенному открытию.
			Продолжить;
		КонецЕсли;
		
		// У сотрудника уже открыт номер лицевого счета
		Если ПолучитьТолькоЛицевойСчетНеОткрыт И ЗначениеЗаполнено(ДанныеСотрудника.НомерЛицевогоСчета) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаОткрытиеЛицевыхСчетов = ОткрытиеЛицевыхСчетов.Добавить();
		
		// Установим состояние открытия лицевого счета.
		Если ДанныеПоСостояниюОткрытияЛицевогоСчета <> Неопределено Тогда
			СтрокаОткрытиеЛицевыхСчетов.ОткрытиеЛицевогоСчетаОтложено = ДанныеПоСостояниюОткрытияЛицевогоСчета.ОткрытиеЛицевогоСчетаОтложено;
			СтрокаОткрытиеЛицевыхСчетов.ОжидаетПодтверждения = ДанныеПоСостояниюОткрытияЛицевогоСчета.ОжидаетПодтверждения;
			СтрокаОткрытиеЛицевыхСчетов.ЛицевойСчетНеОткрыт = ДанныеПоСостояниюОткрытияЛицевогоСчета.ЛицевойСчетНеОткрыт;
		КонецЕсли;
		
		// Заполнение по данным документа "Заявка на открытие лицевых счетов".
		Если ЗаявкаНаОткрытиеЛицевыхСчетов = Неопределено Тогда
			ДанныеПоЛицевымСчетамСотрудника = ДанныеПоЛицевымСчетамСотрудников.Найти(
					ДанныеСотрудника.ФизическоеЛицо, "ФизическоеЛицо");
			Если ДанныеПоЛицевымСчетамСотрудника <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(СтрокаОткрытиеЛицевыхСчетов, ДанныеПоЛицевымСчетамСотрудника);
				СтрокаОткрытиеЛицевыхСчетов.АнкетаЗаполнена = ОбменСБанкамиПоЗарплатнымПроектамКлиентСервер.АнкетаЗаполнена(СтрокаОткрытиеЛицевыхСчетов);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		// Заполнение по кадровым данным.
		ЗаполнитьЗначенияСвойств(СтрокаОткрытиеЛицевыхСчетов, ДанныеСотрудника);
		
		ИмяНаЛатинском = ОбменСБанкамиПоЗарплатнымПроектамКлиентСервер.СтрокаНаЛатинском(
			ДанныеСотрудника.Имя, СоответствиеСимволов);
		ФамилияНаЛатинском = ОбменСБанкамиПоЗарплатнымПроектамКлиентСервер.СтрокаНаЛатинском(
			ДанныеСотрудника.Фамилия, СоответствиеСимволов);
		
		СтрокаОткрытиеЛицевыхСчетов.ЭмбоссированныйТекст1 = ИмяНаЛатинском;
		СтрокаОткрытиеЛицевыхСчетов.ЭмбоссированныйТекст2 = ФамилияНаЛатинском;
		СтрокаОткрытиеЛицевыхСчетов.ЭмбоссированныйТекст3 = ?(ДанныеСотрудника.Пол = Перечисления.ПолФизическогоЛица.Женский, "MRS", "MR");
		
		СтрокаОткрытиеЛицевыхСчетов.КатегорияСотрудника = Перечисления.КатегорииСотрудниковОбменаСБанками.Прочие;
		СтрокаОткрытиеЛицевыхСчетов.СпособРассылкиОтчета = Перечисления.СпособыДоставкиКорреспонденцииБанка.НеРассылать;
		СтрокаОткрытиеЛицевыхСчетов.Гражданство = ДанныеСотрудника.Страна;
		СтрокаОткрытиеЛицевыхСчетов.ЯвляетсяРезидентом = ДанныеСотрудника.СтатусНалогоплательщика = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент");
		СтрокаОткрытиеЛицевыхСчетов.СистемаРасчетовПоБанковскимКартам = СистемаРасчетовПоБанковскимКартам;
		
		СтрокаОткрытиеЛицевыхСчетов.АдресМестаРаботыПредставление = АдресМестаРаботы.Представление;
		СтрокаОткрытиеЛицевыхСчетов.АдресМестаРаботы = АдресМестаРаботы.Значение;
		СтрокаОткрытиеЛицевыхСчетов.МестоРожденияПредставление = ПерсонифицированныйУчетКлиентСервер.ПредставлениеМестаРождения(ДанныеСотрудника.МестоРождения);
		
		СтрокаОткрытиеЛицевыхСчетов.ДеньВыплатыЗарплаты = ДеньВыплатыЗарплатыВОрганизации;
		
		Если ЗначениеЗаполнено(СтрокаОткрытиеЛицевыхСчетов.НомерМобильногоТелефона) Тогда
			СтруктураПолейТелефона = РаботаСАдресами.ПредыдущаяСтруктураКонтактнойИнформацииXML(СтрокаОткрытиеЛицевыхСчетов.НомерМобильногоТелефона);
			СтрокаОткрытиеЛицевыхСчетов.ОператорСвязи = СтруктураПолейТелефона.КодГорода;
		КонецЕсли;
		
		Если ЗаявкаНаОткрытиеЛицевыхСчетов = Неопределено Тогда
			ДанныеПоОтложенномуОткрытиюЛицевыхСчетовСотрудника = ДанныеПоОтложенномуОткрытиюЛицевыхСчетовСотрудников.Найти(
					ДанныеСотрудника.ФизическоеЛицо, "ФизическоеЛицо");
			// Заполнение по данным отложенного открытия лицевых счетов сотрудников.
			Если ДанныеПоОтложенномуОткрытиюЛицевыхСчетовСотрудника <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(СтрокаОткрытиеЛицевыхСчетов, ДанныеПоОтложенномуОткрытиюЛицевыхСчетовСотрудника);
			КонецЕсли;
		КонецЕсли;
		
		СтрокаОткрытиеЛицевыхСчетов.АнкетаЗаполнена = ОбменСБанкамиПоЗарплатнымПроектамКлиентСервер.АнкетаЗаполнена(СтрокаОткрытиеЛицевыхСчетов);
	КонецЦикла;
	
	ОбменСБанкамиПоЗарплатнымПроектамПереопределяемый.ОпределитьДанныеСотрудниковДляОткрытияЛицевыхСчетов(ОткрытиеЛицевыхСчетов,
			СписокФизическихЛиц, СписокСотрудников, ДатаПолученияДанных, Организация, ЗарплатныйПроект);
	
	Возврат ОткрытиеЛицевыхСчетов;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыИФункцииЗаполненияУведомленияОбУвольненииСотрудников

// Формирует временную таблицу ВТЗакрытиеЛицевыхСчетов, 
// список физических лиц, с состояниями закрытия лицевых счетов.
//
// Параметры:
//		МенеджерВременныхТаблиц
//		Ссылка - Ссылка на документ "Заявка на закрытие лицевых счетов".
//		Организация
//		ЗарплатныйПроект
//		Подразделение
//		ДатаПолученияДанных - дата получения кадровой информации по физическим лицам.
//		Сотрудник - Ссылка на сотрудника, для которого требуется получить кадровую информацию.
//		ТолькоРазрешенные - Булево
//
// Создает в МенеджерВременныхТаблиц временную таблицу с именем ВТЗакрытиеЛицевыхСчетов.
// Поля временной таблицы
//		Сотрудник,
//		ФизическоеЛицо,
//		ДатаЗакрытия - Дата, на которую требуется закрыть лицевой счет сотрудника.
//		ЛицевойСчет - Строка - Лицевой счет сотрудника.
//
Процедура СоздатьВТЗакрытиеЛицевыхСчетов(МенеджерВременныхТаблиц, Ссылка, Организация, ЗарплатныйПроект, Подразделение, ДатаПолученияДанных, Сотрудник = Неопределено, ТолькоРазрешенные = Истина) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДатаПолученияДанных", ДатаПолученияДанных);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ЗарплатныйПроект", ЗарплатныйПроект);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	
	Если Сотрудник = Неопределено Тогда
		ПараметрыПолученияСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
		ПараметрыПолученияСотрудников.Организация = Организация;
		ПараметрыПолученияСотрудников.Подразделение = Подразделение;
		ПараметрыПолученияСотрудников.ОкончаниеПериода = ДатаПолученияДанных;
		
		КадровыйУчет.СоздатьВТСотрудникиОрганизации(Запрос.МенеджерВременныхТаблиц, Истина, ПараметрыПолученияСотрудников);
	Иначе
		
		Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	&ДатаПолученияДанных КАК Период
		|ПОМЕСТИТЬ ВТСотрудникиОрганизации
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Ссылка = &Сотрудник";
		
		Если Не ТолькоРазрешенные Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "РАЗРЕШЕННЫЕ", "");
		КонецЕсли;
		
		Если ТипЗнч(Сотрудник) = Тип("Массив") Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "= &Сотрудник", "В (&Сотрудник)");
		КонецЕсли;
		
		Запрос.Выполнить();
		
	КонецЕсли;
	
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(Запрос.МенеджерВременныхТаблиц, "ВТСотрудникиОрганизации");
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, ТолькоРазрешенные, "ДатаУвольнения, ОсновноеРабочееМестоВОрганизации");
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Сотрудники.Сотрудник КАК Сотрудник,
	|	Сотрудники.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Сотрудники.ДатаУвольнения КАК ДатаЗакрытия,
	|	Сотрудники.ДатаУвольнения КАК Период
	|ПОМЕСТИТЬ ВТСотрудники
	|ИЗ
	|	ВТКадровыеДанныеСотрудников КАК Сотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудников.Сотрудники КАК ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудниковСотрудники
	|		ПО Сотрудники.Сотрудник = ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудниковСотрудники.Сотрудник
	|			И (ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудниковСотрудники.Ссылка.ЗарплатныйПроект = &ЗарплатныйПроект
	|				ИЛИ &ЗарплатныйПроект = ЗНАЧЕНИЕ(Справочник.ЗарплатныеПроекты.ПустаяСсылка))
	|			И (ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудниковСотрудники.Ссылка.Подразделение = &Подразделение
	|				ИЛИ &Подразделение = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
	|			И (ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудниковСотрудники.Ссылка.Проведен)
	|			И (ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудниковСотрудники.Ссылка <> &Ссылка)
	|ГДЕ
	|	Сотрудники.ОсновноеРабочееМестоВОрганизации
	|	И ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудниковСотрудники.Сотрудник ЕСТЬ NULL 
	|	И Сотрудники.ДатаУвольнения <> ДАТАВРЕМЯ(1, 1, 1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТКадровыеДанныеСотрудников";
	
	Если Не ТолькоРазрешенные Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "РАЗРЕШЕННЫЕ", "");
	КонецЕсли;
	
	Запрос.Выполнить();
	
	ОписаниеВременнойТаблицы = ОписаниеВременнойТаблицыДляСоздатьВТЛицевыеСчетаСотрудников(Запрос.МенеджерВременныхТаблиц, "ВТСотрудники", "ФизическоеЛицо", "ДатаЗакрытия");
	СоздатьПоВременнойТаблицеВТЛицевыеСчетаСотрудников(ОписаниеВременнойТаблицы, ТолькоРазрешенные, Организация, ЗарплатныйПроект);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Сотрудники.Сотрудник КАК Сотрудник,
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЕСТЬNULL(ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудниковСотрудники.ДатаЗакрытия, Сотрудники.ДатаЗакрытия) КАК ДатаЗакрытия,
	|	ЕСТЬNULL(ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудниковСотрудники.ЛицевойСчет, ЛицевыеСчетаСотрудников.НомерЛицевогоСчета) КАК ЛицевойСчет,
	|	ЕСТЬNULL(ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудниковСотрудники.Ссылка, ЗНАЧЕНИЕ(Документ.ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудников.ПустаяСсылка)) КАК ДокументЗаявка,
	|	ЕСТЬNULL(ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудниковСотрудники.НомерСтроки, НЕОПРЕДЕЛЕНО) КАК НомерСтрокиДокумента
	|ПОМЕСТИТЬ ВТЗакрытиеЛицевыхСчетов
	|ИЗ
	|	ВТСотрудники КАК Сотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудников.Сотрудники КАК ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудниковСотрудники
	|		ПО Сотрудники.Сотрудник = ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудниковСотрудники.Сотрудник
	|			И (ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудниковСотрудники.Ссылка.ЗарплатныйПроект = &ЗарплатныйПроект
	|				ИЛИ &ЗарплатныйПроект = ЗНАЧЕНИЕ(Справочник.ЗарплатныеПроекты.ПустаяСсылка))
	|			И (ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудниковСотрудники.Ссылка.Подразделение = &Подразделение
	|				ИЛИ &Подразделение = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
	|			И (Не ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудниковСотрудники.Ссылка.Проведен)
	|			И (Не ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудниковСотрудники.Ссылка.ПометкаУдаления)
	|			И (ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудниковСотрудники.Ссылка <> &Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЛицевыеСчетаСотрудников КАК ЛицевыеСчетаСотрудников
	|		ПО Сотрудники.Сотрудник = ЛицевыеСчетаСотрудников.Сотрудник
	|ГДЕ
	|	ЕСТЬNULL(ЗаявкаНаЗакрытиеЛицевыхСчетовСотрудниковСотрудники.ЛицевойСчет, ЛицевыеСчетаСотрудников.НомерЛицевогоСчета) ЕСТЬ НЕ NULL ";
	
	Если Не ТолькоРазрешенные Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "РАЗРЕШЕННЫЕ", "");
	КонецЕсли;
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Получает данные сотрудников, для которых требуется закрыть лицевой счет.
//
// Параметры:
//		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц, подготовленный методом СоздатьВТЗакрытиеЛицевыхСчетов.
//
// Возвращаемое значение:
//		Выборка с данными сотрудников.
//
Функция ДанныеСотрудниковДляЗакрытияЛицевыхСчетов(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВТЗакрытиеЛицевыхСчетов.Сотрудник,
	|	ВТЗакрытиеЛицевыхСчетов.ДатаЗакрытия КАК Период
	|ПОМЕСТИТЬ ВТСотрудникиПериоды
	|ИЗ
	|	ВТЗакрытиеЛицевыхСчетов КАК ВТЗакрытиеЛицевыхСчетов";
	Запрос.Выполнить();
	
	// Получаем кадровые данные сотрудников с полями для сортировки. 	
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(МенеджерВременныхТаблиц, "ВТСотрудникиПериоды");
	КадровыеДанные = "";
	ЗарплатаКадры.ДополнитьКадровымиДаннымиНастройкиПорядкаСписка(КадровыеДанные);
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, Истина, КадровыеДанные);	
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Сотрудники.Сотрудник КАК Сотрудник,
	|	ЗакрытиеЛицевыхСчетов.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЗакрытиеЛицевыхСчетов.ДатаЗакрытия КАК ДатаЗакрытия,
	|	ЗакрытиеЛицевыхСчетов.ДокументЗаявка КАК ДокументЗаявка,
	|	ЗакрытиеЛицевыхСчетов.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	ЗакрытиеЛицевыхСчетов.ЛицевойСчет КАК ЛицевойСчет
	|ИЗ
	|	ВТЗакрытиеЛицевыхСчетов КАК ЗакрытиеЛицевыхСчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК Сотрудники
	|		ПО ЗакрытиеЛицевыхСчетов.Сотрудник = Сотрудники.Сотрудник
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник";
	ЗарплатаКадры.ДополнитьТекстЗапросаУпорядочиваниемСотрудниковПоВТСДаннымиПорядка(Запрос, "Сотрудники");
	ЗакрытиеЛицевыхСчетов = Запрос.Выполнить();
	
	ОбменСБанкамиПоЗарплатнымПроектамПереопределяемый.ОпределитьДанныеСотрудниковДляЗакрытияЛицевыхСчетов(МенеджерВременныхТаблиц, ЗакрытиеЛицевыхСчетов);
	
	Возврат ЗакрытиеЛицевыхСчетов;
	
КонецФункции

#КонецОбласти

#Область ДанныеДляПечатиПоXML

Функция ПредставлениеАдресаИзЭДXML(Адрес, ВидАдреса)
	
	Если Адрес = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	СтруктураАдреса = РаботаСАдресамиКлиентСервер.СтруктураКонтактнойИнформацииПоТипу(
		Перечисления.ТипыКонтактнойИнформации.Адрес);
	СтруктураАдреса.Страна = Адрес.Страна.СтранаНазвание;
	СтруктураАдреса.НаименованиеСтраны = Адрес.Страна.СтранаНазвание;
	СтруктураАдреса.КодСтраны = Адрес.Страна.СтранаКод;
	СтруктураАдреса.Индекс = Адрес.Индекс;
	Если Адрес.Регион <> Неопределено Тогда
		СтруктураАдреса.Регион = Адрес.Регион.РегионНазвание + " " + Адрес.Регион.РегионСокращение;
		СтруктураАдреса.РегионСокращение = Адрес.Регион.РегионСокращение;
	КонецЕсли;
	Если Адрес.Район <> Неопределено Тогда
		СтруктураАдреса.Район = Адрес.Район.РайонНазвание + " " + Адрес.Район.РайонСокращение;
		СтруктураАдреса.РайонСокращение = Адрес.Район.РайонСокращение;
	КонецЕсли;
	Если Адрес.Город <> Неопределено Тогда
		СтруктураАдреса.Город = Адрес.Город.ГородНазвание + " " + Адрес.Город.ГородСокращение;
		СтруктураАдреса.ГородСокращение = Адрес.Город.ГородСокращение;
	КонецЕсли;
	Если Адрес.НаселенныйПункт <> Неопределено Тогда
		СтруктураАдреса.НаселенныйПункт = Адрес.НаселенныйПункт.НаселенныйПунктНазвание + " " + Адрес.НаселенныйПункт.НаселенныйПунктСокращение;
		СтруктураАдреса.НаселенныйПунктСокращение = Адрес.НаселенныйПункт.НаселенныйПунктСокращение;
	КонецЕсли;
	Если Адрес.Улица <> Неопределено Тогда
		СтруктураАдреса.Улица = Адрес.Улица.УлицаНазвание + " " + Адрес.Улица.УлицаСокращение;
		СтруктураАдреса.УлицаСокращение = Адрес.Улица.УлицаСокращение;
	КонецЕсли;
	СтруктураАдреса.Дом = Адрес.Дом;
	СтруктураАдреса.Корпус = Адрес.Корпус;
	СтруктураАдреса.Квартира = Адрес.Квартира;
	СтруктураАдреса.Вставить("ТипДома","дом");
	СтруктураАдреса.Вставить("ТипКорпуса","корпус");
	СтруктураАдреса.Вставить("ТипКвартиры","кв.");
	
	ПредставлениеАдреса = "";
	УправлениеКонтактнойИнформациейКлиентСервер.СформироватьПредставлениеАдреса(СтруктураАдреса, ПредставлениеАдреса);
	
	Возврат ПредставлениеАдреса;
	
КонецФункции

Функция ПредставлениеУдостоверенияЛичностиИзЭДXML(УдостоверениеЛичности)
	
	ТекстСерия				= НСтр("ru = ', серия: %1'");
	ТекстНомер				= НСтр("ru = ', № %1'");
	ТекстДатаВыдачи			= НСтр("ru = ', выдан: %1 года'");
	ТекстСрокДействия		= НСтр("ru = ', действует до: %1 года'");
	ТекстКодПодразделения	= НСтр("ru = ', № подр. %1'");
	
	Представление = ""
		+ УдостоверениеЛичности.ВидДокумента
		+ ?(ЗначениеЗаполнено(УдостоверениеЛичности.Серия), СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСерия, УдостоверениеЛичности.Серия), "")
		+ ?(ЗначениеЗаполнено(УдостоверениеЛичности.Номер), СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстНомер, УдостоверениеЛичности.Номер), "")
		+ ?(ЗначениеЗаполнено(УдостоверениеЛичности.ДатаВыдачи), СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстДатаВыдачи, Формат(УдостоверениеЛичности.ДатаВыдачи, "ДЛФ=ДД")), "")
		+ ?(ЗначениеЗаполнено(УдостоверениеЛичности.КемВыдан), ", " + УдостоверениеЛичности.КемВыдан, "")
		+ ?(ЗначениеЗаполнено(УдостоверениеЛичности.КодПодразделения) И УдостоверениеЛичности.КодВидаДокумента = Справочники.ВидыДокументовФизическихЛиц.ПаспортРФ, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстКодПодразделения, УдостоверениеЛичности.КодПодразделения), "");
	
	Возврат Представление;
	
КонецФункции

#КонецОбласти

#КонецОбласти
