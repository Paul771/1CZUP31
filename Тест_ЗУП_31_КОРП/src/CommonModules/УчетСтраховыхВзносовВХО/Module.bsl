
#Область СлужебныйПрограммныйИнтерфейс

// Добавляет в список Обработчики процедуры-обработчики обновления,
// необходимые данной подсистеме.
//
// Параметры:
//   Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//                                   общего модуля ОбновлениеИнформационнойБазы.
// 
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.25.77";
	Обработчик.Процедура = "УчетСтраховыхВзносовВХО.УточнитьУплатуСуммПревышения";
	
КонецПроцедуры	

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// Определяет объекты, в которых есть процедура ДобавитьКомандыПечати().
// Подробнее см. УправлениеПечатьюПереопределяемый.
//
// Параметры:
//  СписокОбъектов - Массив - список менеджеров объектов.
//
Процедура ПриОпределенииОбъектовСКомандамиПечати(СписокОбъектов) Экспорт
	
	СписокОбъектов.Добавить(Документы.УплатаСтраховыхВзносовВФонды);
	СписокОбъектов.Добавить(Документы.ВозмещениеРасходовФондомСоциальногоСтрахования);
	
КонецПроцедуры

Процедура УточнитьУплатуСуммПревышения() Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОтложенноеПроведениеДокументов") Тогда 
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОтражениеДокументовВУчетеСтраховыхВзносов");
		Модуль.ОтразитьДокументыВУчетеСтраховыхВзносов();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УплатаСтраховыхВзносовВФонды.Ссылка
	|ИЗ
	|	Документ.УплатаСтраховыхВзносовВФонды КАК УплатаСтраховыхВзносовВФонды
	|ГДЕ
	|	УплатаСтраховыхВзносовВФонды.ВидПлатежа = ЗНАЧЕНИЕ(Перечисление.ВидыОбязательногоСтрахованияСотрудников.ПФРСПревышения)";
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Документ = Выборка.Ссылка.ПолучитьОбъект();		
		Документ.ОбменДанными.Загрузка = Истина;
		Документ.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
		
		Документ.ВидПлатежа = Перечисления.ВидыОбязательногоСтрахованияСотрудников.ПФРПоСуммарномуТарифу;
		
		Документ.Записать();
		
	КонецЦикла;
	
	Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСФондамиПоСтраховымВзносам.Регистратор
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	РегистрНакопления.РасчетыСФондамиПоСтраховымВзносам КАК РасчетыСФондамиПоСтраховымВзносам
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УплатаСтраховыхВзносовВФонды КАК УплатаСтраховыхВзносовВФонды
	|		ПО РасчетыСФондамиПоСтраховымВзносам.Регистратор = УплатаСтраховыхВзносовВФонды.Ссылка
	|ГДЕ
	|	РасчетыСФондамиПоСтраховымВзносам.ВидОбязательногоСтрахованияСотрудников = ЗНАЧЕНИЕ(Перечисление.ВидыОбязательногоСтрахованияСотрудников.ПФРСПревышения)
	|	И УплатаСтраховыхВзносовВФонды.Ссылка ЕСТЬ НЕ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыСФондамиПоСтраховымВзносам.Период,
	|	РасчетыСФондамиПоСтраховымВзносам.Регистратор КАК Регистратор,
	|	РасчетыСФондамиПоСтраховымВзносам.НомерСтроки КАК НомерСтроки,
	|	РасчетыСФондамиПоСтраховымВзносам.Активность,
	|	РасчетыСФондамиПоСтраховымВзносам.ВидДвижения,
	|	РасчетыСФондамиПоСтраховымВзносам.Организация,
	|	РасчетыСФондамиПоСтраховымВзносам.Сумма,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОбязательногоСтрахованияСотрудников.ПФРПоСуммарномуТарифу) КАК ВидОбязательногоСтрахованияСотрудников,
	|	РасчетыСФондамиПоСтраховымВзносам.ЭтоСтраховыеВзносы,
	|	РасчетыСФондамиПоСтраховымВзносам.ЭтоРасчетыПоНачислениюУплатеВзносовВФСС,
	|	РасчетыСФондамиПоСтраховымВзносам.ГодЗадолженностиПоЕСН,
	|	РасчетыСФондамиПоСтраховымВзносам.РасчетыПоПособиям,
	|	РасчетыСФондамиПоСтраховымВзносам.ЭтоДоначислениеПоАктуПроверки,
	|	РасчетыСФондамиПоСтраховымВзносам.ПлатежноеПоручениеНомер,
	|	РасчетыСФондамиПоСтраховымВзносам.ПлатежноеПоручениеДата
	|ИЗ
	|	РегистрНакопления.РасчетыСФондамиПоСтраховымВзносам КАК РасчетыСФондамиПоСтраховымВзносам
	|ГДЕ
	|	РасчетыСФондамиПоСтраховымВзносам.Регистратор В
	|			(ВЫБРАТЬ
	|				Регистраторы.Регистратор
	|			ИЗ
	|				ВТРегистраторы КАК Регистраторы)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	НомерСтроки";
	
	УчетСтраховыхВзносов.ОбработатьНаборыЗаписейРегистраНакопления("РасчетыСФондамиПоСтраховымВзносам", Текст)

КонецПроцедуры

#КонецОбласти
