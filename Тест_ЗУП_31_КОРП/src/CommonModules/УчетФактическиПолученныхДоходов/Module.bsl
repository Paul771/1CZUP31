#Область СлужебныйПрограммныйИнтерфейс

// Возвращает Таблицу соответствия КодовДоходаНДФЛ, Категории начисления (для кода дохода 4800) и Категории дохода
// Если заполнен параметр КодДоходаНДФЛ тип СправочникСсылка.ВидыДоходовНДФЛ, то соответствие будет возвращено только
// для этого кода.
//
Функция СопоставлениеКодовИКатегорийДоходовНДФЛ(КодДоходаНДФЛ = Неопределено) Экспорт
	Возврат УчетФактическиПолученныхДоходовВнутренний.СопоставлениеКодовИКатегорийДоходовНДФЛ(КодДоходаНДФЛ);
КонецФункции

// Формирует временную таблицу "ВТНалогУдержанный" со сведениями об удержанном НДФЛ,
// с заполненой датой выплаты учтеных доходов.
//
// Параметры:
// МенеджерВременныхТаблиц - МенеджерВременныхТаблиц, в котором будет создана временная таблица
// ИсточникНДФЛ            - Запрос - источник данных о НДФЛ: запрос с текстом получения таблицы налога и установленными
//                                    параметрами
// ДатаПолученияДохода     - Дата   - уточненная дата получения доходов
// ИмяВТ                   - Строка - имя создаваемой временной таблицы.
//
Процедура СоздатьВТНалогУдержанный(МенеджерВременныхТаблиц, ИсточникНДФЛ, ДатаПолученияДохода, ИмяВТ = "ВТНалогУдержанный") Экспорт
	
	ДатаНачалаПереносаДатыПолученияДоходов = ДатаНачалаИспользованияПодсистемы();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если ДатаПолученияДохода < ДатаНачалаПереносаДатыПолученияДоходов Тогда
		Запрос.Текст =
		"ВЫБРАТЬ
		|	// ФизическоеЛицо, СтавкаНалогообложенияРезидента, МесяцНалоговогоПериода, Подразделение, КодДохода, РегистрацияВНалоговомОргане, ВключатьВДекларациюПоНалогуНаПрибыль, ДокументОснование и др. поля
		|	*
		|ПОМЕСТИТЬ ВТНалогУдержанный
		|ИЗ
		|	#ТаблицаНДФЛ КАК НДФЛВедомостей";
	Иначе
		Запрос.УстановитьПараметр("ДатаПолученияДоходовНДФЛ", НачалоДня(ДатаПолученияДохода));
		Запрос.УстановитьПараметр("ДатаНачалаПереносаДатыПолученияДоходов", ДатаНачалаПереносаДатыПолученияДоходов);
		Запрос.УстановитьПараметр("КатегорииДоходаНДФЛСФиксированнойДатойПолученияДохода", Перечисления.КатегорииДоходовНДФЛ.СФиксированнойДатойПолученияДохода());
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА НДФЛВедомостей.МесяцНалоговогоПериода < &ДатаНачалаПереносаДатыПолученияДоходов
		|			ТОГДА НДФЛВедомостей.МесяцНалоговогоПериода
		|		КОГДА НДФЛВедомостей.КатегорияДохода В (&КатегорииДоходаНДФЛСФиксированнойДатойПолученияДохода)
		|			ТОГДА НДФЛВедомостей.МесяцНалоговогоПериода
		|		ИНАЧЕ &ДатаПолученияДоходовНДФЛ
		|	КОНЕЦ КАК МесяцНалоговогоПериода,
		|	// ФизическоеЛицо, СтавкаНалогообложенияРезидента, МесяцНалоговогоПериода, Подразделение, КодДохода, РегистрацияВНалоговомОргане, ВключатьВДекларациюПоНалогуНаПрибыль, ДокументОснование и др. поля
		|	*
		|ПОМЕСТИТЬ ВТНалогУдержанный
		|ИЗ
		|	#ТаблицаНДФЛ КАК НДФЛВедомостей";
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПОМЕСТИТЬ ВТНалогУдержанный", "ПОМЕСТИТЬ " + ИмяВТ);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ТаблицаНДФЛ", "(" + ИсточникНДФЛ.Текст + ")");
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Запрос.Параметры, ИсточникНДФЛ.Параметры);
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Регистрирует полученный доход "Начислятелей" на новую дату получения дохода
// Параметры:
//		Регистратор - ДокументСсылка - документ выплаты
//		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - должен содержать временные таблицы 
//      	ВТСписокСотрудников, с данными о выплатах вида:
//				ФизическоеЛицо: должно быть непустым
//          	СуммаВыплаты.
//          	ДокументОснование, необязательная
//          	СтатьяФинансирования, необязательная
//          	СтатьяРасходов, необязательная
//          	СуммаНачисленная, необязательная
//          	СуммаВыплаченная, необязательная, 
//			Если колонки СуммаНачисленная, СуммаВыплаченная отсутствуют, возможная частичная выплата не будет учтена.
//		Движения - коллекция движений регистратора.
//		ДатаВыплаты - дата - новая дата получения дохода.
//		ДатаОперации - дата - дата, которой будет зарегистрировано движение.
//		Отказ - признак отказа от заполнения движений.
//
Процедура ЗарегистрироватьНовуюДатуПолученияДохода(Регистратор, Движения, МенеджерВременныхТаблиц, ДатаВыплаты, ДатаОперации, Отказ , Записывать = Ложь) Экспорт
	УчетФактическиПолученныхДоходовВнутренний.ЗарегистрироватьНовуюДатуПолученияДохода(Регистратор, Движения, МенеджерВременныхТаблиц, ДатаВыплаты, ДатаОперации, Отказ, Записывать);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура НовоеСоответствиеКодуДохода(ТаблицаСоответствий, КодДохода, ОсновнаяКатегория, ДополнительныеКатегории = Неопределено, КатегорияНачисления = Неопределено) Экспорт
	
	НовоеСоответствие = ТаблицаСоответствий.Добавить();
	НовоеСоответствие.КодДохода = КодДохода;
	НовоеСоответствие.КатегорияДохода = ОсновнаяКатегория;
	НовоеСоответствие.КатегорияНачисления = КатегорияНачисления;
	НовоеСоответствие.Порядок = 1;
	
	Если ДополнительныеКатегории <> Неопределено Тогда
		Порядок = 2;
		Если ТипЗнч(ДополнительныеКатегории) = Тип("Массив") Тогда
			Для Каждого Категория Из ДополнительныеКатегории Цикл
				НовоеСоответствие = ТаблицаСоответствий.Добавить();
				НовоеСоответствие.КодДохода = КодДохода;
				НовоеСоответствие.КатегорияДохода = Категория;
				НовоеСоответствие.КатегорияНачисления = КатегорияНачисления;
				НовоеСоответствие.Порядок = Порядок;
				
				Порядок = Порядок + 1;
			КонецЦикла;
		Иначе
			НовоеСоответствие = ТаблицаСоответствий.Добавить();
			НовоеСоответствие.КодДохода = КодДохода;
			НовоеСоответствие.КатегорияДохода = ДополнительныеКатегории;
			НовоеСоответствие.КатегорияНачисления = КатегорияНачисления;
			НовоеСоответствие.Порядок = Порядок;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ДатаНачалаИспользованияПодсистемы() Экспорт
	Возврат '20170101';
КонецФункции

Функция РегистраторыУвольнения() Экспорт
	Возврат УчетФактическиПолученныхДоходовВнутренний.РегистраторыУвольнения();
КонецФункции

Процедура УточнитьКрайнийСрокУплаты(ЗаписиРегистра) Экспорт
	
	Если ЗаписиРегистра.Количество() = 0 Тогда
		Возврат
	КонецЕсли;
	
	ДатыУдержанияНалога = Новый Массив;
	Для Каждого СтрокаНабора Из ЗаписиРегистра Цикл
		Если СтрокаНабора.ВариантУдержания = Перечисления.ВариантыУдержанияНДФЛ.Удержано Тогда	
			Если СтрокаНабора.ВключатьВДекларациюПоНалогуНаПрибыль Тогда // пп. 3 п. 9 ст. 226.1 
				СрокУплаты = ДобавитьМесяц(НачалоДня(СтрокаНабора.Период), 1)
			ИначеЕсли СтрокаНабора.СрокПеречисления = Перечисления.СрокиПеречисляемогоНалога.МежрасчетныеОтпускаИБольничные Тогда
				СрокУплаты = НачалоДня(КонецМесяца(СтрокаНабора.Период))
			Иначе
				СрокУплаты = КонецДня(СтрокаНабора.Период) + 1;
			КонецЕсли;
			Если ДатыУдержанияНалога.Найти(СрокУплаты) = Неопределено Тогда
				ДатыУдержанияНалога.Добавить(НачалоДня(СрокУплаты))
			КонецЕсли;
			СтрокаНабора.КрайнийСрокУплаты = СрокУплаты;
		КонецЕсли;
	КонецЦикла;
	
	Если ДатыУдержанияНалога.Количество() = 0 Тогда
		Возврат
	КонецЕсли;
	
	РабочиеДни = КалендарныеГрафики.ДатыБлижайшихРабочихДней(КалендарныеГрафики.ПроизводственныйКалендарьРоссийскойФедерации(), ДатыУдержанияНалога, Ложь, Ложь, Истина);
	Если РабочиеДни = Неопределено Тогда 
		Возврат
	КонецЕсли;
	
	Для Каждого СтрокаНабора Из ЗаписиРегистра Цикл
		Если СтрокаНабора.ВариантУдержания = Перечисления.ВариантыУдержанияНДФЛ.Удержано Тогда	
			БлижайшийРабочийДень = РабочиеДни[СтрокаНабора.КрайнийСрокУплаты];
			Если БлижайшийРабочийДень <> Неопределено И БлижайшийРабочийДень <> СтрокаНабора.КрайнийСрокУплаты Тогда
				СтрокаНабора.КрайнийСрокУплаты = БлижайшийРабочийДень;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#Область ПервоначальноеЗаполнениеИОбновлениеИнформационнойБазы

// Добавляет в список Обработчики процедуры-обработчики обновления,
// необходимые данной подсистеме.
//
// Параметры:
//   Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//                                   общего модуля ОбновлениеИнформационнойБазы.
// 
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	УчетФактическиПолученныхДоходовВнутренний.ЗарегистрироватьОбработчикиОбновления(Обработчики);
КонецПроцедуры

Процедура ЗаполнитьКатегориюДоходаНДФЛВНачислениях(ПараметрыОбновления = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.Ссылка КАК Ссылка,
	|	Начисления.КодДоходаНДФЛ КАК КодДоходаНДФЛ
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.КатегорияДохода = ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
	|	И Начисления.КодДоходаНДФЛ <> ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.ПустаяСсылка)";
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
	Иначе
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "ПланВидовРасчета.Начисления", "Ссылка", Выборка.Ссылка) Тогда
				Продолжить;
			КонецЕсли;
			Объект = Выборка.Ссылка.ПолучитьОбъект();
			Объект.КатегорияДохода = УчетНДФЛПовтИсп.КатегорияДоходаПоЕгоКоду(Выборка.КодДоходаНДФЛ);
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект);
			ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьКатегориюДоходаВДвиженияхРеестрДСВ_3ПоРегиструСведенияОДоходахНДФЛ(ПараметрыОбновления = Неопределено) Экспорт 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеДоходы) КАК КатегорияДохода,
	|	РеестрДСВ_3.Ссылка КАК ДокументОснование,
	|	ЗаписиРегистра.*
	|ИЗ
	|	РегистрНакопления.СведенияОДоходахНДФЛ КАК ЗаписиРегистра
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеестрДСВ_3 КАК РеестрДСВ_3
	|		ПО ЗаписиРегистра.Регистратор = РеестрДСВ_3.Ссылка
	|ГДЕ
	|	РеестрДСВ_3.Дата >= ДАТАВРЕМЯ(2017,1,1)
	|	И ЗаписиРегистра.КатегорияДохода = ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаписиРегистра.Регистратор,
	|	ЗаписиРегистра.НомерСтроки";
	УчетНДФЛ.ОбработатьНаборыЗаписейРегистраНакопления("СведенияОДоходахНДФЛ", ТекстЗапроса, , , ПараметрыОбновления);
	
КонецПроцедуры

Процедура ЗаполнитьКатегориюДоходаВДвиженияхРеестрДСВ_3ПоРегиструРасчетыНалогоплательщиковСБюджетомПоНДФЛ(ПараметрыОбновления = Неопределено) Экспорт 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеДоходы) КАК КатегорияДохода,
	|	РеестрДСВ_3.Ссылка КАК ДокументОснование,
	|	ЗаписиРегистра.*
	|ИЗ
	|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК ЗаписиРегистра
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеестрДСВ_3 КАК РеестрДСВ_3
	|		ПО ЗаписиРегистра.Регистратор = РеестрДСВ_3.Ссылка
	|ГДЕ
	|	РеестрДСВ_3.Дата >= ДАТАВРЕМЯ(2017,1,1)
	|	И ЗаписиРегистра.КатегорияДохода = ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаписиРегистра.Регистратор,
	|	ЗаписиРегистра.НомерСтроки";
	УчетНДФЛ.ОбработатьНаборыЗаписейРегистраНакопления("СведенияОДоходахНДФЛ", ТекстЗапроса, , , ПараметрыОбновления);
	
КонецПроцедуры

Процедура ЗаполнитьКатегориюДоходаВОперацияНалоговогоУчетаПоНДФЛ(ПараметрыОбновления = Неопределено) Экспорт
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	ВложенныйЗапрос.Ссылка КАК Ссылка,
	|	ВложенныйЗапрос.Ссылка.Проведен КАК Проведен
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.ОперацияНалоговогоУчетаПоНДФЛ.СведенияОДоходах КАК ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах
	|	ГДЕ
	|		ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ДатаПолученияДохода >= ДАТАВРЕМЯ(2017,1,1)
	|		И ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.КатегорияДохода = ЗНАЧЕНИЕ(перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.Ссылка
	|	ИЗ
	|		Документ.ОперацияНалоговогоУчетаПоНДФЛ.НДФЛИсчисленныйПоСтавке13 КАК ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13
	|	ГДЕ
	|		ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.МесяцНалоговогоПериода >= ДАТАВРЕМЯ(2017,1,1)
	|		И ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.КатегорияДохода = ЗНАЧЕНИЕ(перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ссылка
	|	ИЗ
	|		Документ.ОперацияНалоговогоУчетаПоНДФЛ.НДФЛУдержанный КАК ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный
	|	ГДЕ
	|		ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.МесяцНалоговогоПериода >= ДАТАВРЕМЯ(2017,1,1)
	|		И ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.КатегорияДохода = ЗНАЧЕНИЕ(перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)) КАК ВложенныйЗапрос";
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, " ПЕРВЫЕ 1000", "");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыДоходовНДФЛ.Ссылка КАК Ссылка,
	|	ВидыДоходовНДФЛ.СтавкаНалогообложенияРезидента КАК Ставка
	|ИЗ
	|	Справочник.ВидыДоходовНДФЛ КАК ВидыДоходовНДФЛ
	|ГДЕ
	|	ВидыДоходовНДФЛ.СтавкаНалогообложенияРезидента <> ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)";
	КодыДоходовНеПоСтавка13 = Новый Соответствие;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		КодыДоходовНеПоСтавка13.Вставить(Выборка.Ссылка, Выборка.Ставка);
	КонецЦикла;
	
	НДФЛИсчисленный = Новый ТаблицаЗначений;
	НДФЛИсчисленный.Колонки.Добавить("ФизическоеЛицо",Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	НДФЛИсчисленный.Колонки.Добавить("СтавкаНалогообложенияРезидента",Новый ОписаниеТипов("ПеречислениеСсылка.НДФЛСтавкиНалогообложенияРезидента"));
	НДФЛИсчисленный.Колонки.Добавить("КодДохода",Новый ОписаниеТипов("СправочникСсылка.ВидыДоходовНДФЛ"));
	НДФЛИсчисленный.Колонки.Добавить("КатегорияДохода", Новый ОписаниеТипов("ПеречислениеСсылка.КатегорииДоходовНДФЛ"));
	НДФЛИсчисленный.Колонки.Добавить("МесяцНалоговогоПериода",Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	НДФЛИсчисленный.Колонки.Добавить("Подразделение", Метаданные.ОпределяемыеТипы.ТерриторияВыполненияРаботВОрганизации.Тип);
	НДФЛИсчисленный.Колонки.Добавить("Сумма",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(13,0)));
	НДФЛИсчисленный.Колонки.Добавить("ВключатьВДекларациюПоНалогуНаПрибыль",Новый ОписаниеТипов("Булево"));
	НДФЛИсчисленный.Колонки.Добавить("ДокументОснование", Метаданные.ОпределяемыеТипы.ДокументыОснованияНДФЛ.Тип);
	
	НДФЛУдержанный = Новый ТаблицаЗначений;
	НДФЛУдержанный.Колонки.Добавить("ФизическоеЛицо",Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	НДФЛУдержанный.Колонки.Добавить("СтавкаНалогообложенияРезидента",Новый ОписаниеТипов("ПеречислениеСсылка.НДФЛСтавкиНалогообложенияРезидента"));
	НДФЛУдержанный.Колонки.Добавить("Ставка",Новый ОписаниеТипов("ПеречислениеСсылка.НДФЛСтавки"));
	НДФЛУдержанный.Колонки.Добавить("МесяцНалоговогоПериода",Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	НДФЛУдержанный.Колонки.Добавить("Подразделение",Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	НДФЛУдержанный.Колонки.Добавить("КодДохода",Новый ОписаниеТипов("СправочникСсылка.ВидыДоходовНДФЛ"));
	НДФЛУдержанный.Колонки.Добавить("КатегорияДохода", Новый ОписаниеТипов("ПеречислениеСсылка.КатегорииДоходовНДФЛ"));
	НДФЛУдержанный.Колонки.Добавить("ВключатьВДекларациюПоНалогуНаПрибыль",Новый ОписаниеТипов("Булево"));
	НДФЛУдержанный.Колонки.Добавить("СрокПеречисления", Новый ОписаниеТипов("ПеречислениеСсылка.СрокиПеречисляемогоНалога"));
	НДФЛУдержанный.Колонки.Добавить("СуммаВыплаченногоДохода", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,2)));
	НДФЛУдержанный.Колонки.Добавить("Сумма",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(13,0)));
	НДФЛУдержанный.Колонки.Добавить("ДокументОснование", Метаданные.ОпределяемыеТипы.ДокументыОснованияНДФЛ.Тип);
	
	КатегорииСФиксированнойДатойДохода = Перечисления.КатегорииДоходовНДФЛ.СФиксированнойДатойПолученияДохода();
	ДатаНачалаУчетаПоКатегориям = '20170101';
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Если ПараметрыОбновления <> НеОпределено Тогда
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("Документ.ОперацияНалоговогоУчетаПоНДФЛ");
				ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
				Если Выборка.Проведен Тогда
					ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.СведенияОДоходахНДФЛ.НаборЗаписей");
					ЭлементБлокировки.УстановитьЗначение("Регистратор", Выборка.Ссылка);
					ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.НаборЗаписей");
					ЭлементБлокировки.УстановитьЗначение("Регистратор", Выборка.Ссылка);
				КонецЕсли;
				Блокировка.Заблокировать();
			КонецЕсли;
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			НДФЛИсчисленный.Очистить();
			НДФЛУдержанный.Очистить();
			
			// Сведения о доходах
			СведенияОДоходах = ДокументОбъект.СведенияОДоходах.Выгрузить();
			ОбновлятьСведенияОДоходах = Ложь;
			Для Каждого СтрокаТабличнойЧасти Из СведенияОДоходах Цикл
				Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.КодДохода) И Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.КатегорияДохода) Тогда
					
					ОбновлятьСведенияОДоходах = Истина;
					
					Если СтрокаТабличнойЧасти.ДатаПолученияДохода >= ДатаНачалаУчетаПоКатегориям Тогда
						СтрокаТабличнойЧасти.КатегорияДохода = УчетНДФЛПовтИсп.КатегорияДоходаПоЕгоКоду(СтрокаТабличнойЧасти.КодДохода);
					КонецЕсли;
					
					Если КодыДоходовНеПоСтавка13[СтрокаТабличнойЧасти.КодДохода] <> Неопределено 
						И СтрокаТабличнойЧасти.СуммаНалогаИсчисленная <> 0 Тогда
						
						СтрокаИсчисленного = НДФЛИсчисленный.Добавить();
						СтрокаИсчисленного.ФизическоеЛицо = ДокументОбъект.Сотрудник;
						СтрокаИсчисленного.СтавкаНалогообложенияРезидента = КодыДоходовНеПоСтавка13[СтрокаТабличнойЧасти.КодДохода];
						СтрокаИсчисленного.КодДохода = СтрокаТабличнойЧасти.КодДохода;
						СтрокаИсчисленного.КатегорияДохода = СтрокаТабличнойЧасти.КатегорияДохода;
						СтрокаИсчисленного.МесяцНалоговогоПериода = СтрокаТабличнойЧасти.ДатаПолученияДохода;
						СтрокаИсчисленного.Подразделение = СтрокаТабличнойЧасти.ОбособленноеПодразделение;
						СтрокаИсчисленного.Сумма = СтрокаТабличнойЧасти.СуммаНалогаИсчисленная;
						СтрокаИсчисленного.ВключатьВДекларациюПоНалогуНаПрибыль = СтрокаТабличнойЧасти.ВключатьВДекларациюПоНалогуНаПрибыль;
						
					КонецЕсли;
					
				КонецЕсли;
			КонецЦикла;
			Если ОбновлятьСведенияОДоходах Тогда
				ДокументОбъект.СведенияОДоходах.Загрузить(СведенияОДоходах);
			КонецЕсли;
			
			// НДФЛ, исчисленный по ставке 13%
			КатегорияДоходаИсчисленногоНалога = Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходы;
			Если ДокументОбъект.НДФЛУдержанный.Количество() = 0 Тогда
				КатегорияДоходаИсчисленногоНалога = Перечисления.КатегорииДоходовНДФЛ.ПрочиеНатуральныеДоходы;
			КонецЕсли;
			
			Отбор = Новый Структура("ДатаПолученияДохода,ОбособленноеПодразделение,ВключатьВДекларациюПоНалогуНаПрибыль");
			НДФЛИсчисленныйПоСтавке13 = ДокументОбъект.НДФЛИсчисленныйПоСтавке13.ВыгрузитьКолонки();
			ПромежуточныйИсчисленный = НДФЛИсчисленныйПоСтавке13.СкопироватьКолонки();
			ОбновлятьНДФЛИсчисленныйПоСтавке13 = Ложь;
			
			Для Каждого СтрокаТабличнойЧасти Из ДокументОбъект.НДФЛИсчисленныйПоСтавке13 Цикл
				Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.КатегорияДохода) Тогда
					
					Если СтрокаТабличнойЧасти.МесяцНалоговогоПериода >= ДатаНачалаУчетаПоКатегориям Тогда
						СтрокаТабличнойЧасти.КатегорияДохода = КатегорияДоходаИсчисленногоНалога;
					КонецЕсли;
					
					СтрокаНДФЛИсчисленныйПоСтавке13 = НДФЛИсчисленныйПоСтавке13.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаНДФЛИсчисленныйПоСтавке13, СтрокаТабличнойЧасти);
					
					СтрокаИсчисленного = НДФЛИсчисленный.Добавить();
					СтрокаИсчисленного.ФизическоеЛицо = ДокументОбъект.Сотрудник;
					СтрокаИсчисленного.СтавкаНалогообложенияРезидента = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
					СтрокаИсчисленного.КатегорияДохода = СтрокаТабличнойЧасти.КатегорияДохода;
					СтрокаИсчисленного.МесяцНалоговогоПериода = СтрокаТабличнойЧасти.МесяцНалоговогоПериода;
					СтрокаИсчисленного.Подразделение = СтрокаТабличнойЧасти.ОбособленноеПодразделение;
					СтрокаИсчисленного.Сумма = СтрокаТабличнойЧасти.Сумма;
					СтрокаИсчисленного.ВключатьВДекларациюПоНалогуНаПрибыль = СтрокаТабличнойЧасти.ВключатьВДекларациюПоНалогуНаПрибыль;
					
					ЗаполнитьЗначенияСвойств(Отбор, СтрокаТабличнойЧасти);
					Отбор.ДатаПолученияДохода = СтрокаТабличнойЧасти.МесяцНалоговогоПериода;
					СтрокиДохода = СведенияОДоходах.НайтиСтроки(Отбор);
					ПромежуточныйИсчисленный.Очистить();
					Если СтрокиДохода.Количество() Тогда
						ИтогоСумма = 0;
						Для Каждого СтрокаДохода Из СтрокиДохода Цикл
							Если Не ЗначениеЗаполнено(СтрокаДохода.КатегорияДохода) Тогда
								Продолжить;
							КонецЕсли;
							Если КодыДоходовНеПоСтавка13[СтрокаДохода.КодДохода] <> Неопределено 
								И СтрокаДохода.СуммаНалогаИсчисленная <> 0 Тогда
								Продолжить;
							КонецЕсли;
							
							НоваяПромежуточная = ПромежуточныйИсчисленный.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяПромежуточная, СтрокаТабличнойЧасти);
							НоваяПромежуточная.КатегорияДохода = СтрокаДохода.КатегорияДохода;
							НоваяПромежуточная.Сумма = СтрокаДохода.СуммаДохода - СтрокаДохода.СуммаВычета;
							НоваяПромежуточная.ЗачтеноАвансовыхПлатежей = СтрокаДохода.СуммаДохода - СтрокаДохода.СуммаВычета;
							
							ИтогоСумма = ИтогоСумма + НоваяПромежуточная.Сумма;
						КонецЦикла;
						ПромежуточныйИсчисленный.Свернуть("МесяцНалоговогоПериода,КатегорияДохода,ОбособленноеПодразделение,ВключатьВДекларациюПоНалогуНаПрибыль","Сумма,ЗачтеноАвансовыхПлатежей");
						
						Если ПромежуточныйИсчисленный.Количество() = 1 Тогда
							КатегорияДохода = ПромежуточныйИсчисленный[0].КатегорияДохода;
							СтрокаТабличнойЧасти.КатегорияДохода = КатегорияДохода;
							СтрокаИсчисленного.КатегорияДохода = КатегорияДохода;
							СтрокаНДФЛИсчисленныйПоСтавке13.КатегорияДохода = КатегорияДохода;
							
						ИначеЕсли ПромежуточныйИсчисленный.Количество() > 1 Тогда
							
							Если ИтогоСумма = 0 Тогда
								Продолжить;
							КонецЕсли;
							
							ОбновлятьНДФЛИсчисленныйПоСтавке13 = Истина;
							
							ЗарплатаКадры.РазнестиСуммуПоБазе(СтрокаТабличнойЧасти.Сумма, ПромежуточныйИсчисленный, , 0);
							ЗарплатаКадры.РазнестиСуммуПоБазе(СтрокаТабличнойЧасти.ЗачтеноАвансовыхПлатежей, ПромежуточныйИсчисленный, "ЗачтеноАвансовыхПлатежей", 0);
							
							ПерваяСтрока = Истина;
							Для Каждого НоваяПромежуточная Из ПромежуточныйИсчисленный Цикл
								Если ПерваяСтрока Тогда 
									СтрокаНДФЛИсчисленныйПоСтавке13.КатегорияДохода = НоваяПромежуточная.КатегорияДохода;
									СтрокаНДФЛИсчисленныйПоСтавке13.Сумма = НоваяПромежуточная.Сумма;
									СтрокаНДФЛИсчисленныйПоСтавке13.ЗачтеноАвансовыхПлатежей = НоваяПромежуточная.ЗачтеноАвансовыхПлатежей;
									
									СтрокаИсчисленного.КатегорияДохода = НоваяПромежуточная.КатегорияДохода;
									СтрокаИсчисленного.Сумма = НоваяПромежуточная.Сумма;
									
									ПерваяСтрока = Ложь;
								Иначе
									НоваяСтрокаНДФЛИсчисленныйПоСтавке13 = НДФЛИсчисленныйПоСтавке13.Добавить();
									ЗаполнитьЗначенияСвойств(НоваяСтрокаНДФЛИсчисленныйПоСтавке13, НоваяПромежуточная);
									
									НоваяСтрокаИсчисленного = НДФЛИсчисленный.Добавить();
									ЗаполнитьЗначенияСвойств(НоваяСтрокаИсчисленного, НоваяПромежуточная);
									НоваяСтрокаИсчисленного.ФизическоеЛицо = ДокументОбъект.Сотрудник;
									НоваяСтрокаИсчисленного.СтавкаНалогообложенияРезидента = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
									НоваяСтрокаИсчисленного.Подразделение = НоваяПромежуточная.ОбособленноеПодразделение;
									
								КонецЕсли;
							КонецЦикла;
						КонецЕсли;
					КонецЕсли;
					
				КонецЕсли;
			КонецЦикла;
			Если ОбновлятьНДФЛИсчисленныйПоСтавке13 Тогда
				НДФЛИсчисленныйПоСтавке13.Свернуть("МесяцНалоговогоПериода,КатегорияДохода,ОбособленноеПодразделение,ВключатьВДекларациюПоНалогуНаПрибыль","Сумма,ЗачтеноАвансовыхПлатежей");
				ДокументОбъект.НДФЛИсчисленныйПоСтавке13.Загрузить(НДФЛИсчисленныйПоСтавке13);
			КонецЕсли;
			
			// НДФЛ удержанный
			Отбор = Новый Структура("МесяцНалоговогоПериода,ОбособленноеПодразделение,ВключатьВДекларациюПоНалогуНаПрибыль");
			НДФЛУдержанныйПоСтавке13 = ДокументОбъект.НДФЛУдержанный.ВыгрузитьКолонки();
			ПромежуточныйУдержанный = НДФЛУдержанныйПоСтавке13.СкопироватьКолонки();
			ОбновлятьНДФЛУдержанныйПоСтавке13 = Ложь;
			
			Для Каждого СтрокаТабличнойЧасти Из ДокументОбъект.НДФЛУдержанный Цикл
				Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.КатегорияДохода) Тогда
					
					Если СтрокаТабличнойЧасти.МесяцНалоговогоПериода < ДатаНачалаУчетаПоКатегориям Тогда
						// оставляем категорию дохода незаполненой
					ИначеЕсли СтрокаТабличнойЧасти.КодДохода = Справочники.ВидыДоходовНДФЛ.Код1010 Тогда
						СтрокаТабличнойЧасти.КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.Дивиденды;
					Иначе
						СтрокаТабличнойЧасти.КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходы;
					КонецЕсли;
					
					СтрокаНДФЛУдержанныйПоСтавке13 = НДФЛУдержанныйПоСтавке13.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаНДФЛУдержанныйПоСтавке13, СтрокаТабличнойЧасти);
					
					СтрокаУдеранный = НДФЛУдержанный.Добавить();
					СтрокаУдеранный.ФизическоеЛицо = ДокументОбъект.Сотрудник;
					СтрокаУдеранный.СтавкаНалогообложенияРезидента = СтрокаТабличнойЧасти.СтавкаНалогообложения;
					СтрокаУдеранный.Ставка = СтрокаТабличнойЧасти.Ставка;
					СтрокаУдеранный.МесяцНалоговогоПериода = СтрокаТабличнойЧасти.МесяцНалоговогоПериода;
					СтрокаУдеранный.Подразделение = СтрокаТабличнойЧасти.ОбособленноеПодразделение;
					СтрокаУдеранный.КодДохода = СтрокаТабличнойЧасти.КодДохода;
					СтрокаУдеранный.КатегорияДохода = СтрокаТабличнойЧасти.КатегорияДохода;
					СтрокаУдеранный.ВключатьВДекларациюПоНалогуНаПрибыль = СтрокаТабличнойЧасти.ВключатьВДекларациюПоНалогуНаПрибыль;
					СтрокаУдеранный.СрокПеречисления = СтрокаТабличнойЧасти.СрокПеречисления;
					СтрокаУдеранный.СуммаВыплаченногоДохода = СтрокаТабличнойЧасти.СуммаВыплаченногоДохода;
					СтрокаУдеранный.Сумма = СтрокаТабличнойЧасти.Сумма;
					СтрокаУдеранный.ДокументОснование = ?(ЗначениеЗаполнено(СтрокаТабличнойЧасти.ДокументОснование), СтрокаТабличнойЧасти.ДокументОснование, ДокументОбъект.Ссылка);
					
					Если СтрокаТабличнойЧасти.КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.Дивиденды
						Или СтрокаТабличнойЧасти.СтавкаНалогообложения <> Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13 Тогда
						Продолжить;
					КонецЕсли;
					
					ЗаполнитьЗначенияСвойств(Отбор, СтрокаТабличнойЧасти);
					СтрокиИсчисленногоПоСтавке13 = НДФЛИсчисленныйПоСтавке13.НайтиСтроки(Отбор);
					ПромежуточныйУдержанный.Очистить();
					Если СтрокиИсчисленногоПоСтавке13.Количество() Тогда
						ИтогоСумма = 0;
						Для Каждого СтрокаИсчисленногоПоСтавке13 Из СтрокиИсчисленногоПоСтавке13 Цикл
							Если Не ЗначениеЗаполнено(СтрокаИсчисленногоПоСтавке13.КатегорияДохода) Тогда
								Продолжить;
							КонецЕсли;
							
							НоваяПромежуточная = ПромежуточныйУдержанный.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяПромежуточная, СтрокаТабличнойЧасти);
							НоваяПромежуточная.КатегорияДохода = СтрокаИсчисленногоПоСтавке13.КатегорияДохода;
							НоваяПромежуточная.Сумма = СтрокаИсчисленногоПоСтавке13.Сумма;
							НоваяПромежуточная.СуммаВыплаченногоДохода = СтрокаИсчисленногоПоСтавке13.Сумма;
							
							ИтогоСумма = ИтогоСумма + НоваяПромежуточная.Сумма;
						КонецЦикла;
						ПромежуточныйУдержанный.Свернуть("МесяцНалоговогоПериода,СтавкаНалогообложения,Ставка,КатегорияДохода,ОбособленноеПодразделение,КодДохода,КатегорияДохода,ВключатьВДекларациюПоНалогуНаПрибыль,СрокПеречисления,ДокументОснование","Сумма,СуммаВыплаченногоДохода");
						
						Если ПромежуточныйУдержанный.Количество() = 1 Тогда
							КатегорияДохода = ПромежуточныйУдержанный[0].КатегорияДохода;
							СтрокаТабличнойЧасти.КатегорияДохода = КатегорияДохода;
							СтрокаУдеранный.КатегорияДохода = КатегорияДохода;
							СтрокаНДФЛУдержанныйПоСтавке13.КатегорияДохода = КатегорияДохода;
							
						ИначеЕсли ПромежуточныйУдержанный.Количество() > 1 Тогда
							
							Если ИтогоСумма = 0 Тогда
								Продолжить;
							КонецЕсли;
							
							ОбновлятьНДФЛУдержанныйПоСтавке13 = Истина;
							
							ЗарплатаКадры.РазнестиСуммуПоБазе(СтрокаТабличнойЧасти.Сумма, ПромежуточныйУдержанный, , 0);
							ЗарплатаКадры.РазнестиСуммуПоБазе(СтрокаТабличнойЧасти.СуммаВыплаченногоДохода, ПромежуточныйУдержанный, "СуммаВыплаченногоДохода", 2);
							
							ПерваяСтрока = Истина;
							Для Каждого НоваяПромежуточная Из ПромежуточныйУдержанный Цикл
								Если ПерваяСтрока Тогда 
									СтрокаНДФЛУдержанныйПоСтавке13.КатегорияДохода = НоваяПромежуточная.КатегорияДохода;
									СтрокаНДФЛУдержанныйПоСтавке13.Сумма = НоваяПромежуточная.Сумма;
									СтрокаНДФЛУдержанныйПоСтавке13.СуммаВыплаченногоДохода = НоваяПромежуточная.СуммаВыплаченногоДохода;
									
									СтрокаУдеранный.КатегорияДохода = НоваяПромежуточная.КатегорияДохода;
									СтрокаУдеранный.Сумма = НоваяПромежуточная.Сумма;
									СтрокаУдеранный.СуммаВыплаченногоДохода = НоваяПромежуточная.СуммаВыплаченногоДохода;
									ПерваяСтрока = Ложь;
								Иначе
									НоваяСтрокаНДФЛУдержанныйПоСтавке13 = НДФЛУдержанныйПоСтавке13.Добавить();
									ЗаполнитьЗначенияСвойств(НоваяСтрокаНДФЛУдержанныйПоСтавке13, НоваяПромежуточная);
									
									НоваяСтрокаУдеранный = НДФЛУдержанный.Добавить();
									ЗаполнитьЗначенияСвойств(НоваяСтрокаУдеранный, НоваяПромежуточная);
									НоваяСтрокаУдеранный.ФизическоеЛицо = ДокументОбъект.Сотрудник;
									НоваяСтрокаУдеранный.СтавкаНалогообложенияРезидента = СтрокаТабличнойЧасти.СтавкаНалогообложения;
									НоваяСтрокаУдеранный.Подразделение = СтрокаТабличнойЧасти.ОбособленноеПодразделение;
									
								КонецЕсли;
							КонецЦикла;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			Если ОбновлятьНДФЛУдержанныйПоСтавке13 Тогда
				НДФЛУдержанныйПоСтавке13.Свернуть("МесяцНалоговогоПериода,СтавкаНалогообложения,Ставка,КатегорияДохода,ОбособленноеПодразделение,КодДохода,КатегорияДохода,ВключатьВДекларациюПоНалогуНаПрибыль,СрокПеречисления,ДокументОснование","Сумма,СуммаВыплаченногоДохода");
				ДокументОбъект.НДФЛУдержанный.Загрузить(НДФЛУдержанныйПоСтавке13);
			КонецЕсли;
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			
			Если ДокументОбъект.Проведен Тогда
				
				Отказ = Ложь;
				Если ОбновлятьСведенияОДоходах Тогда
					
					НаборЗаписей = РегистрыНакопления.СведенияОДоходахНДФЛ.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.Регистратор.Установить(ДокументОбъект.Ссылка);
					НаборЗаписей.Прочитать();
					Для Каждого Запись Из НаборЗаписей Цикл
						Если ЗначениеЗаполнено(Запись.КодДохода) И Не ЗначениеЗаполнено(Запись.КатегорияДохода) Тогда
							Запись.КатегорияДохода = УчетНДФЛПовтИсп.КатегорияДоходаПоЕгоКоду(Запись.КодДохода);
						КонецЕсли;
						Если Не ЗначениеЗаполнено(Запись.ДокументОснование) Тогда
							Запись.ДокументОснование = ДокументОбъект.Ссылка;
						КонецЕсли;
						Если КатегорииСФиксированнойДатойДохода.Найти(Запись.КатегорияДохода) <> Неопределено Тогда
							Запись.ДатаПолученияДоходаФиксирована = Истина;
						КонецЕсли;
					КонецЦикла;
					ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
					
				КонецЕсли;
				
				ОбновлятьИсчисленныйНалог = НДФЛИсчисленный.Количество() > 0;
				ОбновлятьУдержанныйНалог =НДФЛУдержанный.Количество() > 0;
				Если ОбновлятьИсчисленныйНалог Или ОбновлятьУдержанныйНалог Тогда
					
					ДокументОбъект.Движения.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Очистить();
					
					Если ОбновлятьИсчисленныйНалог Тогда
						УчетНДФЛ.СформироватьИсчисленныйНалогПоТаблицеЗначений(ДокументОбъект.Движения, Отказ, ДокументОбъект.Организация, ДокументОбъект.ДатаОперации, НДФЛИсчисленный);
					КонецЕсли;
					
					Если ОбновлятьУдержанныйНалог Тогда
						УчетНДФЛ.СформироватьУдержанныйНалогПоТаблицеЗначений(ДокументОбъект.Движения, Отказ, ДокументОбъект.Организация, ДокументОбъект.ДатаОперации, НДФЛУдержанный);
					КонецЕсли;
					
					НаборЗаписей = РегистрыНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.Регистратор.Установить(ДокументОбъект.Ссылка);
					Для Каждого Движение Из ДокументОбъект.Движения.РасчетыНалогоплательщиковСБюджетомПоНДФЛ Цикл
						Запись = НаборЗаписей.Добавить();
						ЗаполнитьЗначенияСвойств(Запись, Движение);
						Если Запись.ВидДвижения = ВидДвиженияНакопления.Приход 
							И КатегорииСФиксированнойДатойДохода.Найти(Запись.КатегорияДохода) <> Неопределено Тогда
							Запись.ДатаПолученияДоходаФиксирована = Истина;
						КонецЕсли;
					КонецЦикла;
					УточнитьКрайнийСрокУплаты(НаборЗаписей);
					ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
					
				КонецЕсли;
			КонецЕсли;
			
			// Завершение транзакции записи документа
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Не удалось заполнить категорию дохода НДФЛ в Операция учета НДФЛ: '"), УровеньЖурналаРегистрации.Ошибка,, Выборка.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьКатегориюДоходаВСведенияОДоходахНДФЛ(ПараметрыОбновления = Неопределено) Экспорт
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ИсключаемыеРегистраторы", ЗарплатаКадры.РегистраторыПереносаДанных());
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	СведенияОДоходахНДФЛ.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛ
	|ГДЕ
	|	СведенияОДоходахНДФЛ.ДатаПолученияДохода >= ДАТАВРЕМЯ(2017,1,1)
	|	И СведенияОДоходахНДФЛ.КатегорияДохода = ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
	|	И НЕ ТИПЗНАЧЕНИЯ(СведенияОДоходахНДФЛ.Регистратор) В (&ИсключаемыеРегистраторы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Поле1
	|ИЗ
	|	ВТРегистраторы КАК ВТРегистраторы";
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, " ПЕРВЫЕ 1000", "");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СведенияОДоходахНДФЛ.КатегорияДохода <> ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
	|			ТОГДА СведенияОДоходахНДФЛ.КатегорияДохода
	|		ИНАЧЕ ВЫРАЗИТЬ(СведенияОДоходахНДФЛ.Начисление КАК ПланВидовРасчета.Начисления).КатегорияДохода
	|	КОНЕЦ КАК КатегорияДохода,
	|	ВЫБОР
	|		КОГДА СведенияОДоходахНДФЛ.ДокументОснование = НЕОПРЕДЕЛЕНО
	|			ТОГДА СведенияОДоходахНДФЛ.Регистратор
	|		ИНАЧЕ СведенияОДоходахНДФЛ.ДокументОснование
	|	КОНЕЦ КАК ДокументОснование,
	|	СведенияОДоходахНДФЛ.*
	|ИЗ
	|	РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛ
	|ГДЕ
	|	СведенияОДоходахНДФЛ.Регистратор В
	|			(ВЫБРАТЬ
	|				Регистраторы.Регистратор
	|			ИЗ
	|				ВТРегистраторы КАК Регистраторы)
	|
	|УПОРЯДОЧИТЬ ПО
	|	СведенияОДоходахНДФЛ.Регистратор,
	|	СведенияОДоходахНДФЛ.НомерСтроки";
	
	КатегорииСФиксированнойДатойДохода = Перечисления.КатегорииДоходовНДФЛ.СФиксированнойДатойПолученияДохода();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрНакопления.СведенияОДоходахНДФЛ.НаборЗаписей", "Регистратор", Выборка.Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		НаборЗаписей = РегистрыНакопления.СведенияОДоходахНДФЛ.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		Пока Выборка.Следующий() Цикл
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			Если Не ЗначениеЗаполнено(Запись.КатегорияДохода) И ЗначениеЗаполнено(Запись.КодДохода) Тогда
				Запись.КатегорияДохода = УчетНДФЛПовтИсп.КатегорияДоходаПоЕгоКоду(Запись.КодДохода);
			КонецЕсли;
			Если КатегорииСФиксированнойДатойДохода.Найти(Запись.КатегорияДохода) <> Неопределено 
				Или Запись.ДатаПолученияДохода < Запись.МесяцНалоговогоПериода Тогда
				Запись.ДатаПолученияДоходаФиксирована = Истина;
			КонецЕсли;
		КонецЦикла;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьКатегориюДоходаВИсчисленномНДФЛМежрасчетныхДокументах(ПараметрыОбновления = Неопределено) Экспорт
	УчетФактическиПолученныхДоходовВнутренний.ЗаполнитьКатегориюДоходаВИсчисленномНДФЛМежрасчетныхДокументах(ПараметрыОбновления);
КонецПроцедуры

Процедура ЗаполнитьКатегориюДоходаВИсчисленномНДФЛОкончательныйРасчет(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Период, МЕСЯЦ) КАК Месяц
	|ИЗ
	|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|ГДЕ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода >= ДАТАВРЕМЯ(2017,1,1)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.РасчетМежрасчетногоПериода = ЛОЖЬ
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Месяц";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьКатегориюДоходаВИсчисленномНДФЛОкончательныйРасчетЗаМесяц(Выборка.Месяц);
	КонецЦикла;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
	
КонецПроцедуры

Процедура ЗаполнитьКатегориюДоходаВИсчисленномНДФЛОкончательныйРасчетЗаМесяц(Месяц) Экспорт
	УчетФактическиПолученныхДоходовВнутренний.ЗаполнитьКатегориюДоходаВИсчисленномНДФЛОкончательныйРасчетЗаМесяц(Месяц);
КонецПроцедуры

Процедура ОбнулитьЛишниеСтрокиНДФЛ(ПараметрыОбновления = Неопределено) Экспорт
	УчетФактическиПолученныхДоходовВнутренний.ОбнулитьЛишниеСтрокиНДФЛ(ПараметрыОбновления);
КонецПроцедуры

Процедура ЗаполнитьКатегориюДоходаВУдержанномНДФЛ(ПараметрыОбновления = Неопределено) Экспорт
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ИсключаемыеРегистраторы", ЗарплатаКадры.РегистраторыПереносаДанных());
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	УдержаныйНДФЛ.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК УдержаныйНДФЛ
	|ГДЕ
	|	УдержаныйНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И УдержаныйНДФЛ.МесяцНалоговогоПериода >= ДАТАВРЕМЯ(2017,1,1)
	|	И УдержаныйНДФЛ.КатегорияДохода = ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
	|	И НЕ ТИПЗНАЧЕНИЯ(УдержаныйНДФЛ.Регистратор) В (&ИсключаемыеРегистраторы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Поле1
	|ИЗ
	|	ВТРегистраторы КАК ВТРегистраторы";
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, " ПЕРВЫЕ 1000", "");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УдержаныйНДФЛ.Регистратор КАК Регистратор,
	|	УдержаныйНДФЛ.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ИсчисленныйНДФЛ.КатегорияДохода, ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ОплатаТруда)
	|		ИНАЧЕ ИсчисленныйНДФЛ.КатегорияДохода
	|	КОНЕЦ КАК КатегорияДохода,
	|	СУММА(ЕСТЬNULL(ИсчисленныйНДФЛ.Сумма, 0)) КАК НДФЛИсчисленный
	|ПОМЕСТИТЬ ВТИсчисленныйНДФЛПоКатегориям
	|ИЗ
	|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК УдержаныйНДФЛ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК ИсчисленныйНДФЛ
	|		ПО УдержаныйНДФЛ.ДокументОснование = ИсчисленныйНДФЛ.ДокументОснование
	|			И (ИсчисленныйНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|			И УдержаныйНДФЛ.ФизическоеЛицо = ИсчисленныйНДФЛ.ФизическоеЛицо
	|			И УдержаныйНДФЛ.СтавкаНалогообложенияРезидента = ИсчисленныйНДФЛ.СтавкаНалогообложенияРезидента
	|			И УдержаныйНДФЛ.Подразделение = ИсчисленныйНДФЛ.Подразделение
	|			И УдержаныйНДФЛ.Организация = ИсчисленныйНДФЛ.Организация
	|ГДЕ
	|	УдержаныйНДФЛ.Регистратор В
	|			(ВЫБРАТЬ
	|				Регистраторы.Регистратор
	|			ИЗ
	|				ВТРегистраторы КАК Регистраторы)
	|	И УдержаныйНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|
	|СГРУППИРОВАТЬ ПО
	|	УдержаныйНДФЛ.Регистратор,
	|	УдержаныйНДФЛ.НомерСтроки,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ИсчисленныйНДФЛ.КатегорияДохода, ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ОплатаТруда)
	|		ИНАЧЕ ИсчисленныйНДФЛ.КатегорияДохода
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор КАК Регистратор,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода <> ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяССылка)
	|			ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода
	|		ИНАЧЕ ЕСТЬNULL(КатегорииНДФЛ.КатегорияДохода, ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ОплатаТруда))
	|	КОНЕЦ КАК КатегорияДохода,
	|	КатегорииНДФЛ.НДФЛИсчисленный КАК НДФЛИсчисленный,
	|	КатегорииНДФЛ.НДФЛИсчисленный КАК НДФЛИсчисленныйДоходы,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.*
	|ИЗ
	|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИсчисленныйНДФЛПоКатегориям КАК КатегорииНДФЛ
	|		ПО РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор = КатегорииНДФЛ.Регистратор
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.НомерСтроки = КатегорииНДФЛ.НомерСтроки
	|ГДЕ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ВТИсчисленныйНДФЛПоКатегориям.Регистратор КАК Регистратор
	|			ИЗ
	|				ВТИсчисленныйНДФЛПоКатегориям КАК ВТИсчисленныйНДФЛПоКатегориям)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	НомерСтроки";
	Выборка = Запрос.Выполнить().Выбрать();
	
	НаборЗаписей = РегистрыНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СоздатьНаборЗаписей();
	
	ВременныйНабор = НаборЗаписей.ВыгрузитьКолонки();
	ВременныйНабор.Колонки.Добавить("НДФЛИсчисленный", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,2)));
	ВременныйНабор.Колонки.Добавить("НДФЛИсчисленныйДоходы", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,2)));
	
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.НаборЗаписей", "Регистратор", Выборка.Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей.Очистить();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		
		Пока Выборка.СледующийПоЗначениюПоля("НомерСтроки") Цикл
			Если Выборка.ВидДвижения = ВидДвиженияНакопления.Приход Тогда
				ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
				Продолжить;
			КонецЕсли;
			
			ВременныйНабор.Очистить();
			ОбщаяСуммаКоэффициентов = 0;
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(ВременныйНабор.Добавить(), Выборка);
				ОбщаяСуммаКоэффициентов = ОбщаяСуммаКоэффициентов + Выборка.НДФЛИсчисленный;
			КонецЦикла;
			
			Если ВременныйНабор.Количество() = 1 Или ОбщаяСуммаКоэффициентов = 0 Тогда
				Запись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, ВременныйНабор[0]);
				Если Не ЗначениеЗаполнено(Запись.КатегорияДохода) Тогда
					Запись.КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.ОплатаТруда;
				КонецЕсли;
				Продолжить;
			КонецЕсли;
			
			ЗарплатаКадры.РазнестиСуммуПоБазе(ВременныйНабор[0].Сумма, ВременныйНабор, "НДФЛИсчисленный", 0);
			ЗарплатаКадры.РазнестиСуммуПоБазе(ВременныйНабор[0].СуммаВыплаченногоДохода, ВременныйНабор, "НДФЛИсчисленныйДоходы", 2);
			
			Для Каждого Строка Из ВременныйНабор Цикл
				Запись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, Строка);
				Запись.Сумма = Строка.НДФЛИсчисленный;
				Запись.СуммаВыплаченногоДохода = Строка.НДФЛИсчисленныйДоходы;
			КонецЦикла;
				
		КонецЦикла;
		
		УточнитьКрайнийСрокУплаты(НаборЗаписей);
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ИсправитьДвиженияОперацияНалоговогоУчетаПоНДФЛ(ПараметрыОбновления = Неопределено) Экспорт
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СведенияОДоходахНДФЛ.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТВсеРегистраторы
	|ИЗ
	|	РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛ
	|ГДЕ
	|	СведенияОДоходахНДФЛ.ДатаПолученияДохода >= ДАТАВРЕМЯ(2017,1,1)
	|	И СведенияОДоходахНДФЛ.Регистратор ССЫЛКА Документ.ОперацияНалоговогоУчетаПоНДФЛ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|ГДЕ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода >= ДАТАВРЕМЯ(2017,1,1)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор ССЫЛКА Документ.ОперацияНалоговогоУчетаПоНДФЛ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	Регистраторы.Ссылка КАК Ссылка,
	|	Регистраторы.Ссылка.Организация КАК Организация,
	|	Регистраторы.Ссылка.ДатаОперации КАК ДатаОперации
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ДоходыНДФЛ.Ссылка КАК Ссылка
	|	ИЗ
	|		(ВЫБРАТЬ
	|			Доходы.Ссылка КАК Ссылка,
	|			Доходы.Сотрудник КАК Сотрудник,
	|			Доходы.ДатаПолученияДохода КАК ДатаПолученияДохода,
	|			Доходы.ОбособленноеПодразделение КАК ОбособленноеПодразделение
	|		ИЗ
	|			(ВЫБРАТЬ
	|				ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.Ссылка КАК Ссылка,
	|				ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ДатаПолученияДохода КАК ДатаПолученияДохода,
	|				ВЫБОР
	|					КОГДА ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ОбособленноеПодразделение.Код ЕСТЬ NULL
	|						ТОГДА НЕОПРЕДЕЛЕНО
	|					ИНАЧЕ ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ОбособленноеПодразделение
	|				КОНЕЦ КАК ОбособленноеПодразделение,
	|				ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.Ссылка.Сотрудник КАК Сотрудник,
	|				ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.СуммаДохода КАК СуммаДохода
	|			ИЗ
	|				Документ.ОперацияНалоговогоУчетаПоНДФЛ.СведенияОДоходах КАК ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах
	|			ГДЕ
	|				ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.Ссылка В
	|						(ВЫБРАТЬ
	|							регистраторы.Регистратор
	|						ИЗ
	|							ВТВсеРегистраторы КАК регистраторы)
	|			
	|			ОБЪЕДИНИТЬ ВСЕ
	|			
	|			ВЫБРАТЬ
	|				СведенияОДоходахНДФЛ.Регистратор,
	|				СведенияОДоходахНДФЛ.ДатаПолученияДохода,
	|				ВЫБОР
	|					КОГДА СведенияОДоходахНДФЛ.Подразделение.Код ЕСТЬ NULL
	|						ТОГДА НЕОПРЕДЕЛЕНО
	|					ИНАЧЕ СведенияОДоходахНДФЛ.Подразделение
	|				КОНЕЦ,
	|				СведенияОДоходахНДФЛ.ФизическоеЛицо,
	|				-СведенияОДоходахНДФЛ.СуммаДохода
	|			ИЗ
	|				РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛ
	|			ГДЕ
	|				СведенияОДоходахНДФЛ.Регистратор В
	|						(ВЫБРАТЬ
	|							регистраторы.Регистратор
	|						ИЗ
	|							ВТВсеРегистраторы КАК регистраторы)) КАК Доходы
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Доходы.Ссылка,
	|			Доходы.Сотрудник,
	|			Доходы.ДатаПолученияДохода,
	|			Доходы.ОбособленноеПодразделение
	|		
	|		ИМЕЮЩИЕ
	|			СУММА(Доходы.СуммаДохода) <> 0) КАК ДоходыНДФЛ
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ИсчисленныйНДФЛ.Ссылка
	|	ИЗ
	|		(ВЫБРАТЬ
	|			Налог.Ссылка КАК Ссылка,
	|			Налог.Сотрудник КАК Сотрудник,
	|			Налог.ДатаПолученияДохода КАК ДатаПолученияДохода,
	|			Налог.ОбособленноеПодразделение КАК ОбособленноеПодразделение
	|		ИЗ
	|			(ВЫБРАТЬ
	|				ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.Ссылка КАК Ссылка,
	|				ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ДатаПолученияДохода КАК ДатаПолученияДохода,
	|				ВЫБОР
	|					КОГДА ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ОбособленноеПодразделение.Код ЕСТЬ NULL
	|						ТОГДА НЕОПРЕДЕЛЕНО
	|					ИНАЧЕ ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ОбособленноеПодразделение
	|				КОНЕЦ КАК ОбособленноеПодразделение,
	|				ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.Ссылка.Сотрудник КАК Сотрудник,
	|				ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.СуммаНалогаИсчисленная КАК Сумма
	|			ИЗ
	|				Документ.ОперацияНалоговогоУчетаПоНДФЛ.СведенияОДоходах КАК ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах
	|			ГДЕ
	|				ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.Ссылка В
	|						(ВЫБРАТЬ
	|							регистраторы.Регистратор
	|						ИЗ
	|							ВТВсеРегистраторы КАК регистраторы)
	|				И ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.СуммаНалогаИсчисленная <> 0
	|				И ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента <> ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|			
	|			ОБЪЕДИНИТЬ ВСЕ
	|			
	|			ВЫБРАТЬ
	|				ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.Ссылка,
	|				ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.МесяцНалоговогоПериода,
	|				ВЫБОР
	|					КОГДА ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.ОбособленноеПодразделение.Код ЕСТЬ NULL
	|						ТОГДА НЕОПРЕДЕЛЕНО
	|					ИНАЧЕ ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.ОбособленноеПодразделение
	|				КОНЕЦ,
	|				ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.Ссылка.Сотрудник,
	|				ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.Сумма
	|			ИЗ
	|				Документ.ОперацияНалоговогоУчетаПоНДФЛ.НДФЛИсчисленныйПоСтавке13 КАК ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13
	|			ГДЕ
	|				ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.Ссылка В
	|						(ВЫБРАТЬ
	|							регистраторы.Регистратор
	|						ИЗ
	|							ВТВсеРегистраторы КАК регистраторы)
	|			
	|			ОБЪЕДИНИТЬ ВСЕ
	|			
	|			ВЫБРАТЬ
	|				РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор,
	|				РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода,
	|				ВЫБОР
	|					КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение.Код ЕСТЬ NULL
	|						ТОГДА НЕОПРЕДЕЛЕНО
	|					ИНАЧЕ РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение
	|				КОНЕЦ,
	|				РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо,
	|				-РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма
	|			ИЗ
	|				РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|			ГДЕ
	|				РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор В
	|						(ВЫБРАТЬ
	|							регистраторы.Регистратор
	|						ИЗ
	|							ВТВсеРегистраторы КАК регистраторы)
	|				И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)) КАК Налог
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Налог.Ссылка,
	|			Налог.Сотрудник,
	|			Налог.ДатаПолученияДохода,
	|			Налог.ОбособленноеПодразделение
	|		
	|		ИМЕЮЩИЕ
	|			СУММА(Налог.Сумма) <> 0) КАК ИсчисленныйНДФЛ
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		УдержанныйНДФЛ.Ссылка
	|	ИЗ
	|		(ВЫБРАТЬ
	|			Расход.Ссылка КАК Ссылка,
	|			Расход.Сотрудник КАК Сотрудник,
	|			Расход.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|			Расход.ОбособленноеПодразделение КАК ОбособленноеПодразделение
	|		ИЗ
	|			(ВЫБРАТЬ
	|				ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ссылка КАК Ссылка,
	|				ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|				ВЫБОР
	|					КОГДА ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.ОбособленноеПодразделение.Код ЕСТЬ NULL
	|						ТОГДА НЕОПРЕДЕЛЕНО
	|					ИНАЧЕ ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.ОбособленноеПодразделение
	|				КОНЕЦ КАК ОбособленноеПодразделение,
	|				ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ссылка.Сотрудник КАК Сотрудник,
	|				ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Сумма КАК Сумма
	|			ИЗ
	|				Документ.ОперацияНалоговогоУчетаПоНДФЛ.НДФЛУдержанный КАК ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный
	|			ГДЕ
	|				ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ссылка В
	|						(ВЫБРАТЬ
	|							регистраторы.Регистратор
	|						ИЗ
	|							ВТВсеРегистраторы КАК регистраторы)
	|			
	|			ОБЪЕДИНИТЬ ВСЕ
	|			
	|			ВЫБРАТЬ
	|				РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор,
	|				РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода,
	|				ВЫБОР
	|					КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение.Код ЕСТЬ NULL
	|						ТОГДА НЕОПРЕДЕЛЕНО
	|					ИНАЧЕ РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение
	|				КОНЕЦ,
	|				РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо,
	|				-РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма
	|			ИЗ
	|				РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|			ГДЕ
	|				РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор В
	|						(ВЫБРАТЬ
	|							регистраторы.Регистратор
	|						ИЗ
	|							ВТВсеРегистраторы КАК регистраторы)
	|				И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)) КАК Расход
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Расход.Ссылка,
	|			Расход.Сотрудник,
	|			Расход.МесяцНалоговогоПериода,
	|			Расход.ОбособленноеПодразделение
	|		
	|		ИМЕЮЩИЕ
	|			СУММА(Расход.Сумма) <> 0) КАК УдержанныйНДФЛ) КАК Регистраторы";
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, " ПЕРВЫЕ 1000", "");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.Ссылка.Сотрудник КАК ФизическоеЛицо,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.ОбособленноеПодразделение КАК Подразделение,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.Сумма КАК Сумма,
	|	ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13) КАК СтавкаНалогообложенияРезидента,
	|	ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.ПустаяСсылка) КАК КодДохода,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.КатегорияДохода КАК КатегорияДохода
	|ПОМЕСТИТЬ ВТНДФЛИсчисленный
	|ИЗ
	|	Документ.ОперацияНалоговогоУчетаПоНДФЛ.НДФЛИсчисленныйПоСтавке13 КАК ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13
	|ГДЕ
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.Ссылка.Сотрудник,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ДатаПолученияДохода,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ОбособленноеПодразделение,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.СуммаНалогаИсчисленная,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.КодДохода,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ВключатьВДекларациюПоНалогуНаПрибыль,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.КатегорияДохода
	|ИЗ
	|	Документ.ОперацияНалоговогоУчетаПоНДФЛ.СведенияОДоходах КАК ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах
	|ГДЕ
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.Ссылка = &Ссылка
	|	И ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента <> ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|	И ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.СуммаНалогаИсчисленная <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ссылка.Сотрудник КАК ФизическоеЛицо,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.ОбособленноеПодразделение КАК Подразделение,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.СтавкаНалогообложения КАК СтавкаНалогообложенияРезидента,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ставка КАК Ставка,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Сумма КАК Сумма,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.КодДохода КАК КодДохода,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.КатегорияДохода КАК КатегорияДохода,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.СрокПеречисления КАК СрокПеречисления,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.СуммаВыплаченногоДохода КАК СуммаВыплаченногоДохода,
	|	ВЫБОР
	|		КОГДА ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.ДокументОснование = НЕОПРЕДЕЛЕНО
	|			ТОГДА ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ссылка
	|		ИНАЧЕ ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.ДокументОснование
	|	КОНЕЦ КАК ДокументОснование,
	|	ИСТИНА КАК УчитыватьВыплаченныйДоходВ6НДФЛ
	|ПОМЕСТИТЬ ВТНалогУдержанный
	|ИЗ
	|	Документ.ОперацияНалоговогоУчетаПоНДФЛ.НДФЛУдержанный КАК ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный
	|ГДЕ
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.Ссылка.Сотрудник КАК ФизическоеЛицо,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.ОбособленноеПодразделение КАК Подразделение,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.ЗачтеноАвансовыхПлатежей КАК Сумма,
	|	ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13) КАК СтавкаНалогообложенияРезидента,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль
	|ПОМЕСТИТЬ ВТДанныеОЗачтенныхПлатежах
	|ИЗ
	|	Документ.ОперацияНалоговогоУчетаПоНДФЛ.НДФЛИсчисленныйПоСтавке13 КАК ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13
	|ГДЕ
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.Ссылка = &Ссылка
	|	И ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.ЗачтеноАвансовыхПлатежей <> 0";
	Пока Выборка.Следующий() Цикл
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.НаборЗаписей", "Регистратор", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Ссылка", Выборка.Ссылка);
		Запрос.Выполнить();
		
		Движения = Новый Структура;
		
		РасчетыНалогоплательщиков = РегистрыНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СоздатьНаборЗаписей();
		РасчетыНалогоплательщиков.Отбор.Регистратор.Установить(Выборка.Ссылка);
		Движения.Вставить("РасчетыНалогоплательщиковСБюджетомПоНДФЛ", РасчетыНалогоплательщиков);
		
		АвансовыеПлатежи = РегистрыНакопления.АвансовыеПлатежиИностранцевПоНДФЛ.СоздатьНаборЗаписей();
		АвансовыеПлатежи.Отбор.Регистратор.Установить(Выборка.Ссылка);
		Движения.Вставить("АвансовыеПлатежиИностранцевПоНДФЛ", АвансовыеПлатежи);
		
		РасчетыНалоговыхАгентов = РегистрыНакопления.РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.СоздатьНаборЗаписей();
		РасчетыНалоговыхАгентов.Отбор.Регистратор.Установить(Выборка.Ссылка);
		Движения.Вставить("РасчетыНалоговыхАгентовСБюджетомПоНДФЛ", РасчетыНалоговыхАгентов);
		
		Отказ = Ложь;
		
		УчетНДФЛ.СформироватьИсчисленныйНалогПоВременнойТаблице(Движения, Отказ, Выборка.Организация, Выборка.ДатаОперации, Запрос.МенеджерВременныхТаблиц);
		УчетНДФЛ.СформироватьИспользованныеАвансовыеПлатежиПоВременнойТаблице(Движения, Отказ, Выборка.Организация, Выборка.ДатаОперации, Запрос.МенеджерВременныхТаблиц);
		УчетНДФЛ.СформироватьУдержанныйНалогПоВременнойТаблице(Движения, Отказ, Выборка.Организация, Выборка.ДатаОперации, Запрос.МенеджерВременныхТаблиц);
		
		Если Не Отказ Тогда
			УточнитьКрайнийСрокУплаты(РасчетыНалогоплательщиков);
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(РасчетыНалогоплательщиков);
		КонецЕсли;
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ИсправитьКатегорииДоходовНДФЛВОперацияНалоговогоУчетаПоНДФЛ(ПараметрыОбновления = Неопределено) Экспорт
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор КАК Ссылка
	|ИЗ
	|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛ
	|		ПО РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор = СведенияОДоходахНДФЛ.Регистратор
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо = СведенияОДоходахНДФЛ.ФизическоеЛицо
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента = СведенияОДоходахНДФЛ.КодДохода.СтавкаНалогообложенияРезидента
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение = СведенияОДоходахНДФЛ.Подразделение
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль = СведенияОДоходахНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода = СведенияОДоходахНДФЛ.ДатаПолученияДохода
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация = СведенияОДоходахНДФЛ.Организация
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода <> СведенияОДоходахНДФЛ.КатегорияДохода
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛ2
	|		ПО РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор = СведенияОДоходахНДФЛ2.Регистратор
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо = СведенияОДоходахНДФЛ2.ФизическоеЛицо
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента = СведенияОДоходахНДФЛ2.КодДохода.СтавкаНалогообложенияРезидента
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение = СведенияОДоходахНДФЛ2.Подразделение
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль = СведенияОДоходахНДФЛ2.ВключатьВДекларациюПоНалогуНаПрибыль
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода = СведенияОДоходахНДФЛ2.ДатаПолученияДохода
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация = СведенияОДоходахНДФЛ2.Организация
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода = СведенияОДоходахНДФЛ2.КатегорияДохода
	|ГДЕ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор ССЫЛКА Документ.ОперацияНалоговогоУчетаПоНДФЛ
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода >= ДАТАВРЕМЯ(2017,1,1)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|	И СведенияОДоходахНДФЛ2.Регистратор ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка
	|ИЗ
	|	Документ.ОперацияНалоговогоУчетаПоНДФЛ.НДФЛИсчисленныйПоСтавке13 КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛ
	|		ПО Операция.Ссылка = СведенияОДоходахНДФЛ.Регистратор
	|			И Операция.Ссылка.Сотрудник = СведенияОДоходахНДФЛ.ФизическоеЛицо
	|			И (СведенияОДоходахНДФЛ.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13))
	|			И Операция.ОбособленноеПодразделение = СведенияОДоходахНДФЛ.Подразделение
	|			И Операция.ВключатьВДекларациюПоНалогуНаПрибыль = СведенияОДоходахНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль
	|			И Операция.МесяцНалоговогоПериода = СведенияОДоходахНДФЛ.ДатаПолученияДохода
	|			И Операция.Ссылка.Организация = СведенияОДоходахНДФЛ.Организация
	|			И Операция.КатегорияДохода <> СведенияОДоходахНДФЛ.КатегорияДохода
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛ2
	|		ПО Операция.Ссылка = СведенияОДоходахНДФЛ2.Регистратор
	|			И Операция.Ссылка.Сотрудник = СведенияОДоходахНДФЛ2.ФизическоеЛицо
	|			И (СведенияОДоходахНДФЛ2.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13))
	|			И Операция.ОбособленноеПодразделение = СведенияОДоходахНДФЛ2.Подразделение
	|			И Операция.ВключатьВДекларациюПоНалогуНаПрибыль = СведенияОДоходахНДФЛ2.ВключатьВДекларациюПоНалогуНаПрибыль
	|			И Операция.МесяцНалоговогоПериода = СведенияОДоходахНДФЛ2.ДатаПолученияДохода
	|			И Операция.Ссылка.Организация = СведенияОДоходахНДФЛ2.Организация
	|			И Операция.КатегорияДохода = СведенияОДоходахНДФЛ2.КатегорияДохода
	|ГДЕ
	|	Операция.МесяцНалоговогоПериода >= ДАТАВРЕМЯ(2017,1,1)
	|	И СведенияОДоходахНДФЛ2.Регистратор ЕСТЬ NULL";
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, " ПЕРВЫЕ 1000", "");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыДоходовНДФЛ.Ссылка КАК Ссылка,
	|	ВидыДоходовНДФЛ.СтавкаНалогообложенияРезидента КАК Ставка
	|ИЗ
	|	Справочник.ВидыДоходовНДФЛ КАК ВидыДоходовНДФЛ
	|ГДЕ
	|	ВидыДоходовНДФЛ.СтавкаНалогообложенияРезидента <> ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)";
	КодыДоходовНеПоСтавка13 = Новый Соответствие;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		КодыДоходовНеПоСтавка13.Вставить(Выборка.Ссылка, Выборка.Ставка);
	КонецЦикла;
	
	НДФЛИсчисленный = Новый ТаблицаЗначений;
	НДФЛИсчисленный.Колонки.Добавить("ФизическоеЛицо",Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	НДФЛИсчисленный.Колонки.Добавить("СтавкаНалогообложенияРезидента",Новый ОписаниеТипов("ПеречислениеСсылка.НДФЛСтавкиНалогообложенияРезидента"));
	НДФЛИсчисленный.Колонки.Добавить("КодДохода",Новый ОписаниеТипов("СправочникСсылка.ВидыДоходовНДФЛ"));
	НДФЛИсчисленный.Колонки.Добавить("КатегорияДохода", Новый ОписаниеТипов("ПеречислениеСсылка.КатегорииДоходовНДФЛ"));
	НДФЛИсчисленный.Колонки.Добавить("МесяцНалоговогоПериода",Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	НДФЛИсчисленный.Колонки.Добавить("Подразделение", Метаданные.ОпределяемыеТипы.ТерриторияВыполненияРаботВОрганизации.Тип);
	НДФЛИсчисленный.Колонки.Добавить("Сумма",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(13,0)));
	НДФЛИсчисленный.Колонки.Добавить("ВключатьВДекларациюПоНалогуНаПрибыль",Новый ОписаниеТипов("Булево"));
	НДФЛИсчисленный.Колонки.Добавить("ДокументОснование", Метаданные.ОпределяемыеТипы.ДокументыОснованияНДФЛ.Тип);
	
	НДФЛУдержанный = Новый ТаблицаЗначений;
	НДФЛУдержанный.Колонки.Добавить("ФизическоеЛицо",Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	НДФЛУдержанный.Колонки.Добавить("СтавкаНалогообложенияРезидента",Новый ОписаниеТипов("ПеречислениеСсылка.НДФЛСтавкиНалогообложенияРезидента"));
	НДФЛУдержанный.Колонки.Добавить("Ставка",Новый ОписаниеТипов("ПеречислениеСсылка.НДФЛСтавки"));
	НДФЛУдержанный.Колонки.Добавить("МесяцНалоговогоПериода",Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	НДФЛУдержанный.Колонки.Добавить("Подразделение",Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	НДФЛУдержанный.Колонки.Добавить("КодДохода",Новый ОписаниеТипов("СправочникСсылка.ВидыДоходовНДФЛ"));
	НДФЛУдержанный.Колонки.Добавить("КатегорияДохода", Новый ОписаниеТипов("ПеречислениеСсылка.КатегорииДоходовНДФЛ"));
	НДФЛУдержанный.Колонки.Добавить("ВключатьВДекларациюПоНалогуНаПрибыль",Новый ОписаниеТипов("Булево"));
	НДФЛУдержанный.Колонки.Добавить("СрокПеречисления", Новый ОписаниеТипов("ПеречислениеСсылка.СрокиПеречисляемогоНалога"));
	НДФЛУдержанный.Колонки.Добавить("СуммаВыплаченногоДохода", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,2)));
	НДФЛУдержанный.Колонки.Добавить("Сумма",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(13,0)));
	НДФЛУдержанный.Колонки.Добавить("ДокументОснование", Метаданные.ОпределяемыеТипы.ДокументыОснованияНДФЛ.Тип);
	
	КатегорииСФиксированнойДатойДохода = Перечисления.КатегорииДоходовНДФЛ.СФиксированнойДатойПолученияДохода();
	ДатаНачалаУчетаПоКатегориям = '20170101';
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Если ПараметрыОбновления <> НеОпределено Тогда
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("Документ.ОперацияНалоговогоУчетаПоНДФЛ");
				ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
				ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.НаборЗаписей");
				ЭлементБлокировки.УстановитьЗначение("Регистратор", Выборка.Ссылка);
				Блокировка.Заблокировать();
			КонецЕсли;
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			НДФЛИсчисленный.Очистить();
			НДФЛУдержанный.Очистить();
			
			// Сведения о доходах
			СведенияОДоходах = ДокументОбъект.СведенияОДоходах.Выгрузить();
			ОбновлятьСведенияОДоходах = Ложь;
			Для Каждого СтрокаТабличнойЧасти Из СведенияОДоходах Цикл
				Если КодыДоходовНеПоСтавка13[СтрокаТабличнойЧасти.КодДохода] <> Неопределено 
					И СтрокаТабличнойЧасти.СуммаНалогаИсчисленная <> 0 Тогда
					
					СтрокаИсчисленного = НДФЛИсчисленный.Добавить();
					СтрокаИсчисленного.ФизическоеЛицо = ДокументОбъект.Сотрудник;
					СтрокаИсчисленного.СтавкаНалогообложенияРезидента = КодыДоходовНеПоСтавка13[СтрокаТабличнойЧасти.КодДохода];
					СтрокаИсчисленного.КодДохода = СтрокаТабличнойЧасти.КодДохода;
					СтрокаИсчисленного.КатегорияДохода = СтрокаТабличнойЧасти.КатегорияДохода;
					СтрокаИсчисленного.МесяцНалоговогоПериода = СтрокаТабличнойЧасти.ДатаПолученияДохода;
					СтрокаИсчисленного.Подразделение = СтрокаТабличнойЧасти.ОбособленноеПодразделение;
					СтрокаИсчисленного.Сумма = СтрокаТабличнойЧасти.СуммаНалогаИсчисленная;
					СтрокаИсчисленного.ВключатьВДекларациюПоНалогуНаПрибыль = СтрокаТабличнойЧасти.ВключатьВДекларациюПоНалогуНаПрибыль;
					
				КонецЕсли;
			КонецЦикла;
			
			// НДФЛ, исчисленный по ставке 13%
			КатегорияДоходаИсчисленногоНалога = Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходы;
			Если ДокументОбъект.НДФЛУдержанный.Количество() = 0 Тогда
				КатегорияДоходаИсчисленногоНалога = Перечисления.КатегорииДоходовНДФЛ.ПрочиеНатуральныеДоходы;
			КонецЕсли;
			
			Отбор = Новый Структура("ДатаПолученияДохода,ОбособленноеПодразделение,ВключатьВДекларациюПоНалогуНаПрибыль");
			НДФЛИсчисленныйПоСтавке13 = ДокументОбъект.НДФЛИсчисленныйПоСтавке13.ВыгрузитьКолонки();
			ПромежуточныйИсчисленный = НДФЛИсчисленныйПоСтавке13.СкопироватьКолонки();
			ОбновлятьНДФЛИсчисленныйПоСтавке13 = Ложь;
			
			Для Каждого СтрокаТабличнойЧасти Из ДокументОбъект.НДФЛИсчисленныйПоСтавке13 Цикл
				
				СтрокаНДФЛИсчисленныйПоСтавке13 = НДФЛИсчисленныйПоСтавке13.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаНДФЛИсчисленныйПоСтавке13, СтрокаТабличнойЧасти);
				
				СтрокаИсчисленного = НДФЛИсчисленный.Добавить();
				СтрокаИсчисленного.ФизическоеЛицо = ДокументОбъект.Сотрудник;
				СтрокаИсчисленного.СтавкаНалогообложенияРезидента = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
				СтрокаИсчисленного.КатегорияДохода = СтрокаТабличнойЧасти.КатегорияДохода;
				СтрокаИсчисленного.МесяцНалоговогоПериода = СтрокаТабличнойЧасти.МесяцНалоговогоПериода;
				СтрокаИсчисленного.Подразделение = СтрокаТабличнойЧасти.ОбособленноеПодразделение;
				СтрокаИсчисленного.Сумма = СтрокаТабличнойЧасти.Сумма;
				СтрокаИсчисленного.ВключатьВДекларациюПоНалогуНаПрибыль = СтрокаТабличнойЧасти.ВключатьВДекларациюПоНалогуНаПрибыль;
				
				ЗаполнитьЗначенияСвойств(Отбор, СтрокаТабличнойЧасти);
				Отбор.ДатаПолученияДохода = СтрокаТабличнойЧасти.МесяцНалоговогоПериода;
				СтрокиДохода = СведенияОДоходах.НайтиСтроки(Отбор);
				ПромежуточныйИсчисленный.Очистить();
				Если СтрокиДохода.Количество() Тогда
					ИтогоСумма = 0;
					Для Каждого СтрокаДохода Из СтрокиДохода Цикл
						Если Не ЗначениеЗаполнено(СтрокаДохода.КатегорияДохода) Тогда
							Продолжить;
						КонецЕсли;
						Если КодыДоходовНеПоСтавка13[СтрокаДохода.КодДохода] <> Неопределено 
							И СтрокаДохода.СуммаНалогаИсчисленная <> 0 Тогда
							Продолжить;
						КонецЕсли;
						
						НоваяПромежуточная = ПромежуточныйИсчисленный.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяПромежуточная, СтрокаТабличнойЧасти);
						НоваяПромежуточная.КатегорияДохода = СтрокаДохода.КатегорияДохода;
						НоваяПромежуточная.Сумма = СтрокаДохода.СуммаДохода - СтрокаДохода.СуммаВычета;
						НоваяПромежуточная.ЗачтеноАвансовыхПлатежей = СтрокаДохода.СуммаДохода - СтрокаДохода.СуммаВычета;
						
						ИтогоСумма = ИтогоСумма + НоваяПромежуточная.Сумма;
					КонецЦикла;
					ПромежуточныйИсчисленный.Свернуть("МесяцНалоговогоПериода,КатегорияДохода,ОбособленноеПодразделение,ВключатьВДекларациюПоНалогуНаПрибыль","Сумма,ЗачтеноАвансовыхПлатежей");
					
					Если ПромежуточныйИсчисленный.Количество() = 1 Тогда
						КатегорияДохода = ПромежуточныйИсчисленный[0].КатегорияДохода;
						СтрокаТабличнойЧасти.КатегорияДохода = КатегорияДохода;
						СтрокаИсчисленного.КатегорияДохода = КатегорияДохода;
						СтрокаНДФЛИсчисленныйПоСтавке13.КатегорияДохода = КатегорияДохода;
						
					ИначеЕсли ПромежуточныйИсчисленный.Количество() > 1 Тогда
						
						Если ИтогоСумма = 0 Тогда
							Продолжить;
						КонецЕсли;
						
						ОбновлятьНДФЛИсчисленныйПоСтавке13 = Истина;
						
						ЗарплатаКадры.РазнестиСуммуПоБазе(СтрокаТабличнойЧасти.Сумма, ПромежуточныйИсчисленный, , 0);
						ЗарплатаКадры.РазнестиСуммуПоБазе(СтрокаТабличнойЧасти.ЗачтеноАвансовыхПлатежей, ПромежуточныйИсчисленный, "ЗачтеноАвансовыхПлатежей", 0);
						
						ПерваяСтрока = Истина;
						Для Каждого НоваяПромежуточная Из ПромежуточныйИсчисленный Цикл
							Если ПерваяСтрока Тогда 
								СтрокаНДФЛИсчисленныйПоСтавке13.КатегорияДохода = НоваяПромежуточная.КатегорияДохода;
								СтрокаНДФЛИсчисленныйПоСтавке13.Сумма = НоваяПромежуточная.Сумма;
								СтрокаНДФЛИсчисленныйПоСтавке13.ЗачтеноАвансовыхПлатежей = НоваяПромежуточная.ЗачтеноАвансовыхПлатежей;
								
								СтрокаИсчисленного.КатегорияДохода = НоваяПромежуточная.КатегорияДохода;
								СтрокаИсчисленного.Сумма = НоваяПромежуточная.Сумма;
								
								ПерваяСтрока = Ложь;
							Иначе
								НоваяСтрокаНДФЛИсчисленныйПоСтавке13 = НДФЛИсчисленныйПоСтавке13.Добавить();
								ЗаполнитьЗначенияСвойств(НоваяСтрокаНДФЛИсчисленныйПоСтавке13, НоваяПромежуточная);
								
								НоваяСтрокаИсчисленного = НДФЛИсчисленный.Добавить();
								ЗаполнитьЗначенияСвойств(НоваяСтрокаИсчисленного, НоваяПромежуточная);
								НоваяСтрокаИсчисленного.ФизическоеЛицо = ДокументОбъект.Сотрудник;
								НоваяСтрокаИсчисленного.СтавкаНалогообложенияРезидента = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
								НоваяСтрокаИсчисленного.Подразделение = НоваяПромежуточная.ОбособленноеПодразделение;
								
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			Если ОбновлятьНДФЛИсчисленныйПоСтавке13 Тогда
				НДФЛИсчисленныйПоСтавке13.Свернуть("МесяцНалоговогоПериода,КатегорияДохода,ОбособленноеПодразделение,ВключатьВДекларациюПоНалогуНаПрибыль","Сумма,ЗачтеноАвансовыхПлатежей");
				ДокументОбъект.НДФЛИсчисленныйПоСтавке13.Загрузить(НДФЛИсчисленныйПоСтавке13);
			КонецЕсли;
			
			// НДФЛ удержанный
			Отбор = Новый Структура("МесяцНалоговогоПериода,ОбособленноеПодразделение,ВключатьВДекларациюПоНалогуНаПрибыль");
			НДФЛУдержанныйПоСтавке13 = ДокументОбъект.НДФЛУдержанный.ВыгрузитьКолонки();
			ПромежуточныйУдержанный = НДФЛУдержанныйПоСтавке13.СкопироватьКолонки();
			ОбновлятьНДФЛУдержанныйПоСтавке13 = Ложь;
			
			Для Каждого СтрокаТабличнойЧасти Из ДокументОбъект.НДФЛУдержанный Цикл
				
				Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.КатегорияДохода)
					Или СтрокаТабличнойЧасти.МесяцНалоговогоПериода < ДатаНачалаУчетаПоКатегориям Тогда
					// оставляем категорию дохода без изменений
				ИначеЕсли СтрокаТабличнойЧасти.КодДохода = Справочники.ВидыДоходовНДФЛ.Код1010 Тогда
					СтрокаТабличнойЧасти.КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.Дивиденды;
				Иначе
					СтрокаТабличнойЧасти.КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходы;
				КонецЕсли;
				
				СтрокаНДФЛУдержанныйПоСтавке13 = НДФЛУдержанныйПоСтавке13.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаНДФЛУдержанныйПоСтавке13, СтрокаТабличнойЧасти);
				
				СтрокаУдеранный = НДФЛУдержанный.Добавить();
				СтрокаУдеранный.ФизическоеЛицо = ДокументОбъект.Сотрудник;
				СтрокаУдеранный.СтавкаНалогообложенияРезидента = СтрокаТабличнойЧасти.СтавкаНалогообложения;
				СтрокаУдеранный.Ставка = СтрокаТабличнойЧасти.Ставка;
				СтрокаУдеранный.МесяцНалоговогоПериода = СтрокаТабличнойЧасти.МесяцНалоговогоПериода;
				СтрокаУдеранный.Подразделение = СтрокаТабличнойЧасти.ОбособленноеПодразделение;
				СтрокаУдеранный.КодДохода = СтрокаТабличнойЧасти.КодДохода;
				СтрокаУдеранный.КатегорияДохода = СтрокаТабличнойЧасти.КатегорияДохода;
				СтрокаУдеранный.ВключатьВДекларациюПоНалогуНаПрибыль = СтрокаТабличнойЧасти.ВключатьВДекларациюПоНалогуНаПрибыль;
				СтрокаУдеранный.СрокПеречисления = СтрокаТабличнойЧасти.СрокПеречисления;
				СтрокаУдеранный.СуммаВыплаченногоДохода = СтрокаТабличнойЧасти.СуммаВыплаченногоДохода;
				СтрокаУдеранный.Сумма = СтрокаТабличнойЧасти.Сумма;
				СтрокаУдеранный.ДокументОснование = ?(ЗначениеЗаполнено(СтрокаТабличнойЧасти.ДокументОснование), СтрокаТабличнойЧасти.ДокументОснование, ДокументОбъект.Ссылка);
				
				Если СтрокаТабличнойЧасти.КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.Дивиденды
					Или СтрокаТабличнойЧасти.СтавкаНалогообложения <> Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13 Тогда
					Продолжить;
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(Отбор, СтрокаТабличнойЧасти);
				СтрокиИсчисленногоПоСтавке13 = НДФЛИсчисленныйПоСтавке13.НайтиСтроки(Отбор);
				ПромежуточныйУдержанный.Очистить();
				Если СтрокиИсчисленногоПоСтавке13.Количество() Тогда
					ИтогоСумма = 0;
					Для Каждого СтрокаИсчисленногоПоСтавке13 Из СтрокиИсчисленногоПоСтавке13 Цикл
						Если Не ЗначениеЗаполнено(СтрокаИсчисленногоПоСтавке13.КатегорияДохода) Тогда
							Продолжить;
						КонецЕсли;
						
						НоваяПромежуточная = ПромежуточныйУдержанный.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяПромежуточная, СтрокаТабличнойЧасти);
						НоваяПромежуточная.КатегорияДохода = СтрокаИсчисленногоПоСтавке13.КатегорияДохода;
						НоваяПромежуточная.Сумма = СтрокаИсчисленногоПоСтавке13.Сумма;
						
						ИтогоСумма = ИтогоСумма + НоваяПромежуточная.Сумма;
					КонецЦикла;
					ПромежуточныйУдержанный.Свернуть("МесяцНалоговогоПериода,СтавкаНалогообложения,Ставка,КатегорияДохода,ОбособленноеПодразделение,КодДохода,КатегорияДохода,ВключатьВДекларациюПоНалогуНаПрибыль,СрокПеречисления,ДокументОснование","Сумма,СуммаВыплаченногоДохода");
					
					Если ПромежуточныйУдержанный.Количество() = 1 Тогда
						КатегорияДохода = ПромежуточныйУдержанный[0].КатегорияДохода;
						СтрокаТабличнойЧасти.КатегорияДохода = КатегорияДохода;
						СтрокаУдеранный.КатегорияДохода = КатегорияДохода;
						СтрокаНДФЛУдержанныйПоСтавке13.КатегорияДохода = КатегорияДохода;
						
					ИначеЕсли ПромежуточныйУдержанный.Количество() > 1 Тогда
						
						Если ИтогоСумма = 0 Тогда
							Продолжить;
						КонецЕсли;
						
						ОбновлятьНДФЛУдержанныйПоСтавке13 = Истина;
						
						ЗарплатаКадры.РазнестиСуммуПоБазе(СтрокаТабличнойЧасти.Сумма, ПромежуточныйУдержанный, , 0);
						ЗарплатаКадры.РазнестиСуммуПоБазе(СтрокаТабличнойЧасти.СуммаВыплаченногоДохода, ПромежуточныйУдержанный, "СуммаВыплаченногоДохода", 2);
						
						ПерваяСтрока = Истина;
						Для Каждого НоваяПромежуточная Из ПромежуточныйУдержанный Цикл
							Если ПерваяСтрока Тогда 
								СтрокаНДФЛУдержанныйПоСтавке13.КатегорияДохода = НоваяПромежуточная.КатегорияДохода;
								СтрокаНДФЛУдержанныйПоСтавке13.Сумма = НоваяПромежуточная.Сумма;
								
								СтрокаУдеранный.КатегорияДохода = НоваяПромежуточная.КатегорияДохода;
								СтрокаУдеранный.Сумма = НоваяПромежуточная.Сумма;
								ПерваяСтрока = Ложь;
							Иначе
								НоваяСтрокаНДФЛУдержанныйПоСтавке13 = НДФЛУдержанныйПоСтавке13.Добавить();
								ЗаполнитьЗначенияСвойств(НоваяСтрокаНДФЛУдержанныйПоСтавке13, НоваяПромежуточная);
								
								НоваяСтрокаУдеранный = НДФЛУдержанный.Добавить();
								ЗаполнитьЗначенияСвойств(НоваяСтрокаУдеранный, НоваяПромежуточная);
								НоваяСтрокаУдеранный.ФизическоеЛицо = ДокументОбъект.Сотрудник;
								НоваяСтрокаУдеранный.СтавкаНалогообложенияРезидента = СтрокаТабличнойЧасти.СтавкаНалогообложения;
								НоваяСтрокаУдеранный.Подразделение = СтрокаТабличнойЧасти.ОбособленноеПодразделение;
								
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			Если ОбновлятьНДФЛУдержанныйПоСтавке13 Тогда
				НДФЛУдержанныйПоСтавке13.Свернуть("МесяцНалоговогоПериода,СтавкаНалогообложения,Ставка,КатегорияДохода,ОбособленноеПодразделение,КодДохода,КатегорияДохода,ВключатьВДекларациюПоНалогуНаПрибыль,СрокПеречисления,ДокументОснование","Сумма,СуммаВыплаченногоДохода");
				ДокументОбъект.НДФЛУдержанный.Загрузить(НДФЛУдержанныйПоСтавке13);
			КонецЕсли;
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			
			Отказ = Ложь;
			
			ОбновлятьИсчисленныйНалог = НДФЛИсчисленный.Количество() > 0;
			ОбновлятьУдержанныйНалог =НДФЛУдержанный.Количество() > 0;
			Если ОбновлятьИсчисленныйНалог Или ОбновлятьУдержанныйНалог Тогда
				
				ДокументОбъект.Движения.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Очистить();
				
				Если ОбновлятьИсчисленныйНалог Тогда
					УчетНДФЛ.СформироватьИсчисленныйНалогПоТаблицеЗначений(ДокументОбъект.Движения, Отказ, ДокументОбъект.Организация, ДокументОбъект.ДатаОперации, НДФЛИсчисленный);
				КонецЕсли;
				
				Если ОбновлятьУдержанныйНалог Тогда
					УчетНДФЛ.СформироватьУдержанныйНалогПоТаблицеЗначений(ДокументОбъект.Движения, Отказ, ДокументОбъект.Организация, ДокументОбъект.ДатаОперации, НДФЛУдержанный);
				КонецЕсли;
				
				НаборЗаписей = РегистрыНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Регистратор.Установить(ДокументОбъект.Ссылка);
				Для Каждого Движение Из ДокументОбъект.Движения.РасчетыНалогоплательщиковСБюджетомПоНДФЛ Цикл
					Запись = НаборЗаписей.Добавить();
					ЗаполнитьЗначенияСвойств(Запись, Движение);
					Если Запись.ВидДвижения = ВидДвиженияНакопления.Приход 
						И КатегорииСФиксированнойДатойДохода.Найти(Запись.КатегорияДохода) <> Неопределено Тогда
						Запись.ДатаПолученияДоходаФиксирована = Истина;
					КонецЕсли;
				КонецЦикла;
				УточнитьКрайнийСрокУплаты(НаборЗаписей);
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
				
			КонецЕсли;
			
			// Завершение транзакции записи документа
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Не удалось заполнить категорию дохода НДФЛ в Операция учета НДФЛ: '"), УровеньЖурналаРегистрации.Ошибка,, Выборка.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВычеркнутьДвиженияНалоговПрошлыхЛет(ПараметрыОбновления = Неопределено) Экспорт
	
	Текст = 
	"ВЫБРАТЬ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|ГДЕ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода = ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Период >= ДАТАВРЕМЯ(2017, 1, 1)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор
	|
	|ИМЕЮЩИЕ
	|	СУММА(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма) = 0 И
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|ГДЕ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода = ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВариантУдержания = ЗНАЧЕНИЕ(Перечисление.ВариантыУдержанияНДФЛ.Удержано)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Период >= ДАТАВРЕМЯ(2017, 1, 1)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор
	|
	|ИМЕЮЩИЕ
	|	СУММА(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма) = 0 И
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода < ДАТАВРЕМЯ(2017, 1, 1)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор КАК Регистратор,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.НомерСтроки КАК НомерСтроки,
	|	*
	|ИЗ
	|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|ГДЕ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор В
	|			(ВЫБРАТЬ
	|				Регистраторы.Регистратор
	|			ИЗ
	|				ВТРегистраторы КАК Регистраторы)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода <> ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	НомерСтроки";
	УчетНДФЛ.ОбработатьНаборыЗаписейРегистраНакопления("РасчетыНалогоплательщиковСБюджетомПоНДФЛ", Текст, , , ПараметрыОбновления)

КонецПроцедуры

Процедура ВычеркнутьДвиженияДоходовПрошлыхЛет(ПараметрыОбновления = Неопределено) Экспорт
	
	Текст = 
	"ВЫБРАТЬ
	|	СведенияОДоходахНДФЛ.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛ
	|ГДЕ
	|	СведенияОДоходахНДФЛ.КатегорияДохода = ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
	|	И СведенияОДоходахНДФЛ.Период >= ДАТАВРЕМЯ(2017, 1, 1)
	|
	|СГРУППИРОВАТЬ ПО
	|	СведенияОДоходахНДФЛ.Регистратор
	|
	|ИМЕЮЩИЕ
	|	СУММА(СведенияОДоходахНДФЛ.СуммаДохода) = 0 И
	|	СУММА(СведенияОДоходахНДФЛ.СуммаВычета) = 0 И
	|	СУММА(ВЫБОР
	|			КОГДА СведенияОДоходахНДФЛ.УстаревшаяДатаПолученияДохода <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияОДоходахНДФЛ.Регистратор КАК Регистратор,
	|	СведенияОДоходахНДФЛ.НомерСтроки КАК НомерСтроки,
	|	*
	|ИЗ
	|	РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛ
	|ГДЕ
	|	СведенияОДоходахНДФЛ.Регистратор В
	|			(ВЫБРАТЬ
	|				Регистраторы.Регистратор
	|			ИЗ
	|				ВТРегистраторы КАК Регистраторы)
	|	И СведенияОДоходахНДФЛ.КатегорияДохода <> ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	НомерСтроки";
	УчетНДФЛ.ОбработатьНаборыЗаписейРегистраНакопления("СведенияОДоходахНДФЛ", Текст, , , ПараметрыОбновления)

КонецПроцедуры

Процедура УточнитьКатегориюДоходаВУдержанномНДФЛ(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ИсключаемыеРегистраторы", ЗарплатаКадры.РегистраторыПереносаДанных());
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор КАК Регистратор,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода КАК КатегорияДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация КАК Организация,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КодДохода КАК КодДохода,
	|	СУММА(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма) КАК Сумма,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение КАК Подразделение,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование КАК ДокументОснование,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль
	|ПОМЕСТИТЬ ВТУдержанный
	|ИЗ
	|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|ГДЕ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода >= ДАТАВРЕМЯ(2017,1,1)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И НЕ ТИПЗНАЧЕНИЯ(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор) = ТИП(Документ.ОперацияНалоговогоУчетаПоНДФЛ)
	|	И НЕ ТИПЗНАЧЕНИЯ(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор) В (&ИсключаемыеРегистраторы)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ГоловнаяОрганизация,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.РегистрацияВНалоговомОргане,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КодДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТУдержанный.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ВТУдержанный.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТУдержанный.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	ВТУдержанный.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	ВТУдержанный.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ВТУдержанный.Организация КАК Организация,
	|	СУММА(ВТУдержанный.Сумма) КАК Сумма,
	|	ВТУдержанный.Подразделение КАК Подразделение,
	|	ВТУдержанный.ДокументОснование КАК ДокументОснование,
	|	ВТУдержанный.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль
	|ПОМЕСТИТЬ ВТИтогиУдержанного
	|ИЗ
	|	ВТУдержанный КАК ВТУдержанный
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТУдержанный.СтавкаНалогообложенияРезидента,
	|	ВТУдержанный.ГоловнаяОрганизация,
	|	ВТУдержанный.ФизическоеЛицо,
	|	ВТУдержанный.ВключатьВДекларациюПоНалогуНаПрибыль,
	|	ВТУдержанный.ДокументОснование,
	|	ВТУдержанный.РегистрацияВНалоговомОргане,
	|	ВТУдержанный.Подразделение,
	|	ВТУдержанный.МесяцНалоговогоПериода,
	|	ВТУдержанный.Организация
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВТУдержанный.Сумма) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	ВЫБОР
	|		КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода
	|		ИНАЧЕ РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода
	|	КОНЕЦ КАК МесяцНалоговогоПериода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода КАК КатегорияДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация КАК Организация,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КодДохода КАК КодДохода,
	|	СУММА(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма) КАК Сумма,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение КАК Подразделение,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование КАК ДокументОснование,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль
	|ПОМЕСТИТЬ ВТИсчисленный
	|ИЗ
	|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИтогиУдержанного КАК ВТИтогиУдержанного
	|		ПО РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ГоловнаяОрганизация = ВТИтогиУдержанного.ГоловнаяОрганизация
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо = ВТИтогиУдержанного.ФизическоеЛицо
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента = ВТИтогиУдержанного.СтавкаНалогообложенияРезидента
	|			И (ВЫБОР
	|				КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода
	|				ИНАЧЕ РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода
	|			КОНЕЦ = ВТИтогиУдержанного.МесяцНалоговогоПериода)
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.РегистрацияВНалоговомОргане = ВТИтогиУдержанного.РегистрацияВНалоговомОргане
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация = ВТИтогиУдержанного.Организация
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение = ВТИтогиУдержанного.Подразделение
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование = ВТИтогиУдержанного.ДокументОснование
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль = ВТИтогиУдержанного.ВключатьВДекларациюПоНалогуНаПрибыль
	|ГДЕ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И НЕ ТИПЗНАЧЕНИЯ(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор) В (&ИсключаемыеРегистраторы)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ГоловнаяОрганизация,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента,
	|	ВЫБОР
	|		КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода
	|		ИНАЧЕ РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода
	|	КОНЕЦ,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.РегистрацияВНалоговомОргане,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КодДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТИсчисленный.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ВТИсчисленный.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТИсчисленный.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	ВТИсчисленный.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	ВТИсчисленный.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ВТИсчисленный.Организация КАК Организация,
	|	СУММА(ВТИсчисленный.Сумма) КАК Сумма,
	|	ВТИсчисленный.Подразделение КАК Подразделение,
	|	ВТИсчисленный.ДокументОснование КАК ДокументОснование,
	|	ВТИсчисленный.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль
	|ПОМЕСТИТЬ ВТИтогиИсчисленного
	|ИЗ
	|	ВТИсчисленный КАК ВТИсчисленный
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТИсчисленный.ВключатьВДекларациюПоНалогуНаПрибыль,
	|	ВТИсчисленный.Подразделение,
	|	ВТИсчисленный.РегистрацияВНалоговомОргане,
	|	ВТИсчисленный.Организация,
	|	ВТИсчисленный.ДокументОснование,
	|	ВТИсчисленный.ГоловнаяОрганизация,
	|	ВТИсчисленный.ФизическоеЛицо,
	|	ВТИсчисленный.СтавкаНалогообложенияРезидента,
	|	ВТИсчисленный.МесяцНалоговогоПериода
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВТИсчисленный.Сумма) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Коэффициенты.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	Коэффициенты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Коэффициенты.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	Коэффициенты.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	Коэффициенты.КатегорияДохода КАК КатегорияДохода,
	|	Коэффициенты.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	Коэффициенты.Организация КАК Организация,
	|	Коэффициенты.КодДохода КАК КодДохода,
	|	СУММА(Коэффициенты.Коэфициент) КАК Коэфициент,
	|	Коэффициенты.Подразделение КАК Подразделение,
	|	Коэффициенты.ДокументОснование КАК ДокументОснование,
	|	Коэффициенты.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль
	|ПОМЕСТИТЬ ВТКоэфициенты
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТУдержанный.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|		ВТУдержанный.ФизическоеЛицо КАК ФизическоеЛицо,
	|		ВТУдержанный.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|		ВТУдержанный.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|		ВТУдержанный.КатегорияДохода КАК КатегорияДохода,
	|		ВТУдержанный.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|		ВТУдержанный.Организация КАК Организация,
	|		ВТУдержанный.КодДохода КАК КодДохода,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(ВТИтогиУдержанного.Сумма, 0) = 0
	|				ТОГДА 1
	|			ИНАЧЕ ВТУдержанный.Сумма / ВТИтогиУдержанного.Сумма
	|		КОНЕЦ КАК Коэфициент,
	|		ВТУдержанный.Подразделение КАК Подразделение,
	|		ВТУдержанный.ДокументОснование КАК ДокументОснование,
	|		ВТУдержанный.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль
	|	ИЗ
	|		ВТУдержанный КАК ВТУдержанный
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТИтогиУдержанного КАК ВТИтогиУдержанного
	|			ПО ВТУдержанный.ГоловнаяОрганизация = ВТИтогиУдержанного.ГоловнаяОрганизация
	|				И ВТУдержанный.ФизическоеЛицо = ВТИтогиУдержанного.ФизическоеЛицо
	|				И ВТУдержанный.СтавкаНалогообложенияРезидента = ВТИтогиУдержанного.СтавкаНалогообложенияРезидента
	|				И ВТУдержанный.МесяцНалоговогоПериода = ВТИтогиУдержанного.МесяцНалоговогоПериода
	|				И ВТУдержанный.РегистрацияВНалоговомОргане = ВТИтогиУдержанного.РегистрацияВНалоговомОргане
	|				И ВТУдержанный.Организация = ВТИтогиУдержанного.Организация
	|				И ВТУдержанный.Подразделение = ВТИтогиУдержанного.Подразделение
	|				И ВТУдержанный.ДокументОснование = ВТИтогиУдержанного.ДокументОснование
	|				И ВТУдержанный.ВключатьВДекларациюПоНалогуНаПрибыль = ВТИтогиУдержанного.ВключатьВДекларациюПоНалогуНаПрибыль
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТИсчисленный.ГоловнаяОрганизация,
	|		ВТИсчисленный.ФизическоеЛицо,
	|		ВТИсчисленный.СтавкаНалогообложенияРезидента,
	|		ВТИсчисленный.МесяцНалоговогоПериода,
	|		ВТИсчисленный.КатегорияДохода,
	|		ВТИсчисленный.РегистрацияВНалоговомОргане,
	|		ВТИсчисленный.Организация,
	|		ВТИсчисленный.КодДохода,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(ВТИтогиИсчисленного.Сумма, 0) = 0
	|				ТОГДА -1
	|			ИНАЧЕ -ВТИсчисленный.Сумма / ВТИтогиИсчисленного.Сумма
	|		КОНЕЦ,
	|		ВТИсчисленный.Подразделение,
	|		ВТИсчисленный.ДокументОснование,
	|		ВТИсчисленный.ВключатьВДекларациюПоНалогуНаПрибыль
	|	ИЗ
	|		ВТИсчисленный КАК ВТИсчисленный
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТИтогиИсчисленного КАК ВТИтогиИсчисленного
	|			ПО ВТИсчисленный.ГоловнаяОрганизация = ВТИтогиИсчисленного.ГоловнаяОрганизация
	|				И ВТИсчисленный.ФизическоеЛицо = ВТИтогиИсчисленного.ФизическоеЛицо
	|				И ВТИсчисленный.СтавкаНалогообложенияРезидента = ВТИтогиИсчисленного.СтавкаНалогообложенияРезидента
	|				И ВТИсчисленный.МесяцНалоговогоПериода = ВТИтогиИсчисленного.МесяцНалоговогоПериода
	|				И ВТИсчисленный.РегистрацияВНалоговомОргане = ВТИтогиИсчисленного.РегистрацияВНалоговомОргане
	|				И ВТИсчисленный.Организация = ВТИтогиИсчисленного.Организация
	|				И ВТИсчисленный.Подразделение = ВТИтогиИсчисленного.Подразделение
	|				И ВТИсчисленный.ДокументОснование = ВТИтогиИсчисленного.ДокументОснование
	|				И ВТИсчисленный.ВключатьВДекларациюПоНалогуНаПрибыль = ВТИтогиИсчисленного.ВключатьВДекларациюПоНалогуНаПрибыль) КАК Коэффициенты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИтогиИсчисленного КАК ВТИтогиИсчисленного
	|		ПО Коэффициенты.ГоловнаяОрганизация = ВТИтогиИсчисленного.ГоловнаяОрганизация
	|			И Коэффициенты.ФизическоеЛицо = ВТИтогиИсчисленного.ФизическоеЛицо
	|			И Коэффициенты.СтавкаНалогообложенияРезидента = ВТИтогиИсчисленного.СтавкаНалогообложенияРезидента
	|			И Коэффициенты.МесяцНалоговогоПериода = ВТИтогиИсчисленного.МесяцНалоговогоПериода
	|			И Коэффициенты.РегистрацияВНалоговомОргане = ВТИтогиИсчисленного.РегистрацияВНалоговомОргане
	|			И Коэффициенты.Организация = ВТИтогиИсчисленного.Организация
	|			И Коэффициенты.Подразделение = ВТИтогиИсчисленного.Подразделение
	|			И Коэффициенты.ДокументОснование = ВТИтогиИсчисленного.ДокументОснование
	|			И Коэффициенты.ВключатьВДекларациюПоНалогуНаПрибыль = ВТИтогиИсчисленного.ВключатьВДекларациюПоНалогуНаПрибыль
	|ГДЕ
	|	ВТИтогиИсчисленного.ФизическоеЛицо ЕСТЬ НЕ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	Коэффициенты.КодДохода,
	|	Коэффициенты.Организация,
	|	Коэффициенты.Подразделение,
	|	Коэффициенты.ДокументОснование,
	|	Коэффициенты.ВключатьВДекларациюПоНалогуНаПрибыль,
	|	Коэффициенты.ГоловнаяОрганизация,
	|	Коэффициенты.ФизическоеЛицо,
	|	Коэффициенты.СтавкаНалогообложенияРезидента,
	|	Коэффициенты.МесяцНалоговогоПериода,
	|	Коэффициенты.КатегорияДохода,
	|	Коэффициенты.РегистрацияВНалоговомОргане
	|
	|ИМЕЮЩИЕ
	|	(СУММА(Коэффициенты.Коэфициент) < -0.1
	|		ИЛИ СУММА(Коэффициенты.Коэфициент) > 0.1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	ВТУдержанный.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	ВТУдержанный КАК ВТУдержанный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКоэфициенты КАК ВложенныйЗапрос
	|		ПО ВТУдержанный.ГоловнаяОрганизация = ВложенныйЗапрос.ГоловнаяОрганизация
	|			И ВТУдержанный.ФизическоеЛицо = ВложенныйЗапрос.ФизическоеЛицо
	|			И ВТУдержанный.СтавкаНалогообложенияРезидента = ВложенныйЗапрос.СтавкаНалогообложенияРезидента
	|			И ВТУдержанный.МесяцНалоговогоПериода = ВложенныйЗапрос.МесяцНалоговогоПериода
	|			И ВТУдержанный.РегистрацияВНалоговомОргане = ВложенныйЗапрос.РегистрацияВНалоговомОргане
	|			И ВТУдержанный.Организация = ВложенныйЗапрос.Организация
	|			И ВТУдержанный.Подразделение = ВложенныйЗапрос.Подразделение
	|			И ВТУдержанный.ДокументОснование = ВложенныйЗапрос.ДокументОснование
	|			И ВТУдержанный.ВключатьВДекларациюПоНалогуНаПрибыль = ВложенныйЗапрос.ВключатьВДекларациюПоНалогуНаПрибыль
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВТИсчисленный.КатегорияДохода, ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ОплатаТруда)) КАК КатегорияДохода,
	|	ЕСТЬNULL(ВТИсчисленный.Сумма, 0) КАК НДФЛИсчисленный,
	|	ЕСТЬNULL(ВТИсчисленный.Сумма, 0) КАК НДФЛИсчисленныйДоходы,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.*
	|ИЗ
	|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИсчисленный КАК ВТИсчисленный
	|		ПО РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ГоловнаяОрганизация = ВТИсчисленный.ГоловнаяОрганизация
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо = ВТИсчисленный.ФизическоеЛицо
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента = ВТИсчисленный.СтавкаНалогообложенияРезидента
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода = ВТИсчисленный.МесяцНалоговогоПериода
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.РегистрацияВНалоговомОргане = ВТИсчисленный.РегистрацияВНалоговомОргане
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация = ВТИсчисленный.Организация
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение = ВТИсчисленный.Подразделение
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование = ВТИсчисленный.ДокументОснование
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль = ВТИсчисленный.ВключатьВДекларациюПоНалогуНаПрибыль
	|ГДЕ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор В
	|			(ВЫБРАТЬ
	|				ВТРегистраторы.Регистратор КАК Регистратор
	|			ИЗ
	|				ВТРегистраторы КАК ВТРегистраторы)
	|
	|УПОРЯДОЧИТЬ ПО
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.НомерСтроки";
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, " ПЕРВЫЕ 1000", "");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СоздатьНаборЗаписей();
	
	ВременныйНабор = НаборЗаписей.ВыгрузитьКолонки();
	ВременныйНабор.Колонки.Добавить("НДФЛИсчисленный", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,2)));
	ВременныйНабор.Колонки.Добавить("НДФЛИсчисленныйДоходы", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,2)));

	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.НаборЗаписей", "Регистратор", Выборка.Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей.Очистить();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		
		Пока Выборка.СледующийПоЗначениюПоля("НомерСтроки") Цикл
			Если Выборка.ВидДвижения = ВидДвиженияНакопления.Приход Тогда
				ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
				Продолжить;
			КонецЕсли;
			
			Если Выборка.Сумма = 0 И Выборка.СуммаВыплаченногоДохода = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ВременныйНабор.Очистить();
			ОбщаяСуммаКоэффициентов = 0;
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(ВременныйНабор.Добавить(), Выборка);
				ОбщаяСуммаКоэффициентов = ОбщаяСуммаКоэффициентов + Выборка.НДФЛИсчисленный;
			КонецЦикла;
			
			Если ВременныйНабор.Количество() = 1 Или ОбщаяСуммаКоэффициентов = 0 Тогда
				Запись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, ВременныйНабор[0]);
				Если Не ЗначениеЗаполнено(Запись.КатегорияДохода) Тогда
					Запись.КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.ОплатаТруда;
				КонецЕсли;
				Продолжить;
			КонецЕсли;
			
			ЗарплатаКадры.РазнестиСуммуПоБазе(ВременныйНабор[0].Сумма, ВременныйНабор, "НДФЛИсчисленный", 0);
			ЗарплатаКадры.РазнестиСуммуПоБазе(ВременныйНабор[0].СуммаВыплаченногоДохода, ВременныйНабор, "НДФЛИсчисленныйДоходы", 2);
			
			Для Каждого Строка Из ВременныйНабор Цикл
				Запись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, Строка);
				Запись.Сумма = Строка.НДФЛИсчисленный;
				Запись.СуммаВыплаченногоДохода = Строка.НДФЛИсчисленныйДоходы;
			КонецЦикла;
			
		КонецЦикла;
		
		УточнитьКрайнийСрокУплаты(НаборЗаписей);
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьКрайнийСрокУплатыУдержанногоНДФЛ(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|ГДЕ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода >= ДАТАВРЕМЯ(2017,1,1)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВариантУдержания = ЗНАЧЕНИЕ(Перечисление.ВариантыУдержанияНДФЛ.Удержано)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КрайнийСрокУплаты = ДАТАВРЕМЯ(1, 1, 1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.*
	|ИЗ
	|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|ГДЕ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор В
	|			(ВЫБРАТЬ
	|				ВТРегистраторы.Регистратор КАК Регистратор
	|			ИЗ
	|				ВТРегистраторы КАК ВТРегистраторы)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	НомерСтроки";
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, " ПЕРВЫЕ 1000", "");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СоздатьНаборЗаписей();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.НаборЗаписей", "Регистратор", Выборка.Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей.Очистить();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		
		Пока Выборка.СледующийПоЗначениюПоля("НомерСтроки") Цикл
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
			КонецЦикла;
		КонецЦикла;
		
		УточнитьКрайнийСрокУплаты(НаборЗаписей);
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УточнитьСуммаВыплаченногоДоходаУдержанногоНДФЛ(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ИсключаемыеРегистраторы", ЗарплатаКадры.РегистраторыПереносаДанных());
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор КАК Регистратор,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.НомерСтроки КАК НомерСтроки,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода КАК КатегорияДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация КАК Организация,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КодДохода КАК КодДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование КАК ДокументОснование,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма КАК Сумма,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаВыплаченногоДохода КАК СуммаВыплаченногоДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение КАК Подразделение
	|ПОМЕСТИТЬ ВТНеРаспределенныеСтроки
	|ИЗ
	|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛПроверка
	|		ПО РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор = РасчетыНалогоплательщиковСБюджетомПоНДФЛПроверка.Регистратор
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо = РасчетыНалогоплательщиковСБюджетомПоНДФЛПроверка.ФизическоеЛицо
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода = РасчетыНалогоплательщиковСБюджетомПоНДФЛПроверка.МесяцНалоговогоПериода
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента = РасчетыНалогоплательщиковСБюджетомПоНДФЛПроверка.СтавкаНалогообложенияРезидента
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.РегистрацияВНалоговомОргане = РасчетыНалогоплательщиковСБюджетомПоНДФЛПроверка.РегистрацияВНалоговомОргане
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация = РасчетыНалогоплательщиковСБюджетомПоНДФЛПроверка.Организация
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КодДохода = РасчетыНалогоплательщиковСБюджетомПоНДФЛПроверка.КодДохода
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода <> РасчетыНалогоплательщиковСБюджетомПоНДФЛПроверка.КатегорияДохода
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВариантУдержания = РасчетыНалогоплательщиковСБюджетомПоНДФЛПроверка.ВариантУдержания
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаВыплаченногоДохода = РасчетыНалогоплательщиковСБюджетомПоНДФЛПроверка.СуммаВыплаченногоДохода
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение = РасчетыНалогоплательщиковСБюджетомПоНДФЛПроверка.Подразделение
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование = РасчетыНалогоплательщиковСБюджетомПоНДФЛПроверка.ДокументОснование
	|ГДЕ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода >= ДАТАВРЕМЯ(2017,1,1)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВариантУдержания = ЗНАЧЕНИЕ(Перечисление.ВариантыУдержанияНДФЛ.Удержано)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаВыплаченногоДохода <> 0
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма <> 0
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛПроверка.Регистратор ЕСТЬ НЕ NULL 
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода <> ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛПроверка.КатегорияДохода <> ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
	|	И НЕ ТИПЗНАЧЕНИЯ(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор) В (&ИсключаемыеРегистраторы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	ЗаписиРегистра.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	ВТНеРаспределенныеСтроки КАК ЗаписиРегистра";
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, " ПЕРВЫЕ 1000", "");
	КонецЕсли;
	
	Запрос.Выполнить();
	Если Не ЗарплатаКадры.ВТСодержитСтроки(Запрос.МенеджерВременныхТаблиц, "ВТРегистраторы") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВТНеРаспределенныеСтроки.Регистратор КАК Регистратор,
	|	ВТНеРаспределенныеСтроки.НомерСтроки КАК НомерСтрокиЗаписи,
	|	ВТНеРаспределенныеСтроки.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТНеРаспределенныеСтроки.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	ВТНеРаспределенныеСтроки.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	ВТНеРаспределенныеСтроки.Подразделение КАК Подразделение,
	|	ВТНеРаспределенныеСтроки.КатегорияДохода КАК КатегорияДохода,
	|	ВТНеРаспределенныеСтроки.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ВТНеРаспределенныеСтроки.Организация КАК Организация,
	|	ВТНеРаспределенныеСтроки.КодДохода КАК КодДохода,
	|	ВТНеРаспределенныеСтроки.ДокументОснование КАК ДокументОснование,
	|	ВТНеРаспределенныеСтроки.Сумма КАК Сумма,
	|	ВТНеРаспределенныеСтроки.СуммаВыплаченногоДохода КАК СуммаВыплаченногоДохода
	|ИЗ
	|	ВТНеРаспределенныеСтроки КАК ВТНеРаспределенныеСтроки
	|ГДЕ
	|	ВТНеРаспределенныеСтроки.Регистратор В
	|			(ВЫБРАТЬ
	|				ВТРегистраторы.Регистратор КАК Регистратор
	|			ИЗ
	|				ВТРегистраторы КАК ВТРегистраторы)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТНеРаспределенныеСтроки.Регистратор,
	|	ВТНеРаспределенныеСтроки.ФизическоеЛицо,
	|	ВТНеРаспределенныеСтроки.СтавкаНалогообложенияРезидента,
	|	ВТНеРаспределенныеСтроки.МесяцНалоговогоПериода,
	|	ВТНеРаспределенныеСтроки.РегистрацияВНалоговомОргане,
	|	ВТНеРаспределенныеСтроки.Подразделение,
	|	ВТНеРаспределенныеСтроки.Организация,
	|	ВТНеРаспределенныеСтроки.КодДохода,
	|	ВТНеРаспределенныеСтроки.ДокументОснование,
	|	ВТНеРаспределенныеСтроки.СуммаВыплаченногоДохода,
	|	ВТНеРаспределенныеСтроки.НомерСтроки";
	РезультатЗапроса = Запрос.Выполнить();
	
	ВременныйНабор = Новый ТаблицаЗначений;
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		ВременныйНабор.Колонки.Добавить(Колонка.Имя, Колонка.ТипЗначения, Колонка.Ширина);
	КонецЦикла;
	
	Выборка = РезультатЗапроса.выбрать();
	
	НаборЗаписей = РегистрыНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СоздатьНаборЗаписей();
	
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.НаборЗаписей", "Регистратор", Выборка.Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей.Очистить();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		НаборЗаписей.Прочитать();
		
		Пока Выборка.СледующийПоЗначениюПоля("ФизическоеЛицо") Цикл
			Пока Выборка.СледующийПоЗначениюПоля("СтавкаНалогообложенияРезидента") Цикл
				Пока Выборка.СледующийПоЗначениюПоля("МесяцНалоговогоПериода") Цикл
					Пока Выборка.СледующийПоЗначениюПоля("РегистрацияВНалоговомОргане") Цикл
						Пока Выборка.СледующийПоЗначениюПоля("Подразделение") Цикл
							Пока Выборка.СледующийПоЗначениюПоля("Организация") Цикл
								Пока Выборка.СледующийПоЗначениюПоля("КодДохода") Цикл
									Пока Выборка.СледующийПоЗначениюПоля("ДокументОснование") Цикл
										Пока Выборка.СледующийПоЗначениюПоля("СуммаВыплаченногоДохода") Цикл
											ВременныйНабор.Очистить();
											ОбщаяСуммаКоэффициентов = 0;
											СуммаВыплаченногоДохода = Выборка.СуммаВыплаченногоДохода;
											Пока Выборка.Следующий() Цикл
												НовыяСтрока = ВременныйНабор.Добавить();
												ЗаполнитьЗначенияСвойств(НовыяСтрока, Выборка);
												НовыяСтрока.СуммаВыплаченногоДохода = Выборка.Сумма;
												ОбщаяСуммаКоэффициентов = ОбщаяСуммаКоэффициентов + Выборка.Сумма;
											КонецЦикла;
											
											Если ВременныйНабор.Количество() = 1 Или ОбщаяСуммаКоэффициентов = 0 Тогда
												Продолжить;
											КонецЕсли;
											
											ЗарплатаКадры.РазнестиСуммуПоБазе(СуммаВыплаченногоДохода, ВременныйНабор, "СуммаВыплаченногоДохода", 2);
											
											Для Каждого Строка Из ВременныйНабор Цикл
												Запись = НаборЗаписей[Строка.НомерСтрокиЗаписи -1];
												Запись.СуммаВыплаченногоДохода = Строка.СуммаВыплаченногоДохода;
											КонецЦикла;
											
										КонецЦикла;
									КонецЦикла;
								КонецЦикла;
							КонецЦикла;
						КонецЦикла;
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		
		УточнитьКрайнийСрокУплаты(НаборЗаписей);
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
