
#Область ПрограммныйИнтерфейс

// Заполняет текстовое представление значений характеристик
//
// Параметры:
//	- ТаблицаХарактеристик - таблица, содержащая характеристики.
//	- ТаблицаЗначений - таблица, содержащая значения характеристик.
// 
Процедура УстановитьЗначенияСтрокой(ТаблицаХарактеристик, ТаблицаЗначений) Экспорт 

	Для каждого СтрокаХарактеристики Из ТаблицаХарактеристик Цикл
		СтрокаХарактеристики.ЗначенияСтрокой = ЗначенияСтрокой(ТаблицаЗначений, СтрокаХарактеристики.Характеристика);
	КонецЦикла; 
	
КонецПроцедуры

// Формирует текстовое представление значений характеристик
//
// Параметры:
//	- ЗначенияХарактеристики - реквизит формы, содержащий таблицу со значениями характеристик.
//	- Характеристика - ПланВидовХарактеристикСсылка.ХарактеристикиПерсонала - ссылка на характеристику персонала, для которой нужно сформировать представление.
// Возвращаемое значение:
//	- Строка - строковое представление значений.
// 
Функция ЗначенияСтрокой(ЗначенияХарактеристики, Характеристика) Экспорт 

	СтрокиЗначений = ЗначенияХарактеристики.НайтиСтроки(Новый Структура("Характеристика", Характеристика));
	ЗначенияСтрокой = "";
	Для Каждого ТекущаяСтрока Из СтрокиЗначений Цикл
		Если Не ЗначениеЗаполнено(ТекущаяСтрока.Значение) Тогда
			Продолжить;
		КонецЕсли;
		ЗначенияСтрокой = СокрЛП(ЗначенияСтрокой) + ?(ЗначенияСтрокой = "", "", ", ") + СокрЛП(Строка(ТекущаяСтрока.Значение));
	КонецЦикла;

	Возврат ЗначенияСтрокой;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура УстановитьВидимостьГруппХарактеристикПерсонала(Форма, ЭлементКнопка, ИмяЭлементаТаблица = "ТаблицаХарактеристикГруппа", ИмяЭлементаДерево = "ДеревоХарактеристикГруппа") Экспорт

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		ИмяЭлементаТаблица,
		"Видимость",
		НЕ ЭлементКнопка.Пометка);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		ИмяЭлементаДерево,
		"Видимость",
		ЭлементКнопка.Пометка);

КонецПроцедуры

Процедура УстановитьВидимостьГруппКомандВКоманднойПанелиХарактеристик(Форма, ЭлементКнопка, ИмяГруппыТаблица = "КоманднаяПанельТаблицы", ИмяЭлементаДерево = "КоманднаяПанельДерева") Экспорт

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		ИмяГруппыТаблица,
		"Видимость",
		НЕ ЭлементКнопка.Пометка);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		ИмяЭлементаДерево,
		"Видимость",
		ЭлементКнопка.Пометка);

КонецПроцедуры

Процедура УстановитьДоступностьКомандПанелиХарактеристикПерсонала(Форма, ЭлементКнопка, ИмяГруппыКнопок = "ЗапрещаемыеКомандыТаблицыХарактеристик") Экспорт

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		ИмяГруппыКнопок,
		"Доступность",
		НЕ ЭлементКнопка.Пометка);
		
КонецПроцедуры

#Область ШтатноеРасписание

Процедура ШтатноеРасписаниеУстановитьДоступностьЭлементовДействийХарактеристик(Форма) Экспорт

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ДействияХарактеристикиГруппа",
		"ТолькоПросмотр",
		Форма.ДействияХарактеристикиСоответствуютПрофилюДолжностиЧисло = 1);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ХарактеристикиПерсоналаПодобратьХарактеристики",
		"Доступность",
		Форма.ДействияХарактеристикиСоответствуютПрофилюДолжностиЧисло <> 1);

КонецПроцедуры

#КонецОбласти

#КонецОбласти