
#Область СлужебныеПроцедурыИФункции

// Обработчик события "ОбработкаПолученияДанныхВыбора" модуля менеджера справочника
// СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.
//
// Параметры:
//  	ДанныеВыбора			- СписокЗначений
//		Параметры				- Структура
//		СтандартнаяОбработка	- Булево
//
Процедура СтрокиОтчетностиМониторингаРаботниковСоциальнойСферыОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка) Экспорт
	
	Запрос = Новый Запрос;
	
	ТекстЗапросаУсловий = "";
	СтандартнаяОбработка = Ложь;
	
	НастройкиСтатистики = СтатистикаПерсоналаРасширенный.НастройкиСтатистикиПерсонала();
	
	Если Не НастройкиСтатистики.ИспользоватьОтчетностьМониторингаРаботниковСоциальнойСферы Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("СтрокаПоиска") И НЕ ПустаяСтрока(Параметры.СтрокаПоиска) Тогда
		УсловияПодбора = "";
		МетаданныеОбъекта = Метаданные.Справочники.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы;
		Для каждого Поле Из МетаданныеОбъекта.ВводПоСтроке Цикл
			УсловияПодбора = УсловияПодбора + ?(ПустаяСтрока(УсловияПодбора), "", Символы.ПС + "ИЛИ ") + "(СправочникСтрокиОтчетностиМониторингаРаботниковСоциальнойСферы." + Поле.Имя + " ПОДОБНО &СтрокаПоиска)";
		КонецЦикла;
		Если НЕ ПустаяСтрока(УсловияПодбора) Тогда
			ТекстЗапросаУсловий = ?(ПустаяСтрока(ТекстЗапросаУсловий), "", ТекстЗапросаУсловий + "
				|	И ") + "(" + УсловияПодбора + ")";
		КонецЕсли; 
		Запрос.УстановитьПараметр("СтрокаПоиска", Параметры.СтрокаПоиска + "%");
	КонецЕсли; 
	
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ ПЕРВЫЕ 51
		|	СправочникСтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.Ссылка КАК Значение,
		|	СправочникСтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.ПометкаУдаления,
		|	"""" КАК Предупреждение,
		|	СправочникСтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.Наименование КАК Представление
		|ИЗ
		|	Справочник.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы КАК СправочникСтрокиОтчетностиМониторингаРаботниковСоциальнойСферы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСтатистикиПерсонала КАК НастройкиСтатистикиПерсонала
		|		ПО СправочникСтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.ФормаМониторинга = НастройкиСтатистикиПерсонала.ФормаОтчетностиМониторингаРаботниковСоциальнойСферы";
		
	Если НЕ ПустаяСтрока(ТекстЗапросаУсловий) Тогда
		ТекстЗапроса = ТекстЗапроса + "
			|ГДЕ
			|	" + ТекстЗапросаУсловий;
	КонецЕсли; 
		
	ТекстЗапроса = ТекстЗапроса + "
		|УПОРЯДОЧИТЬ ПО
		|	Представление";
	
	Запрос.Текст = ТекстЗапроса;
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Значение, Выборка.Представление);
	КонецЦикла;
	
КонецПроцедуры

Функция СоответствиеИдентификаторовСтрокОтчетностиРасходовИЧисленностиВидамДолжностей() Экспорт
	
	СоответствиеСтрокОтчетности = Новый Соответствие;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
		СоответствиеСтрокОтчетности = Модуль.СоответствиеИдентификаторовСтрокОтчетностиРасходовИЧисленностиВидамДолжностей();
	КонецЕсли;
	
	Возврат СоответствиеСтрокОтчетности;	
	
КонецФункции

Функция ПолучитьСтрокуОтчетностиРасходовИЧисленностиПоВидуДолжности(ВидДолжности) Экспорт
	
	СоответствиеСтрокОтчетности = СоответствиеИдентификаторовСтрокОтчетностиРасходовИЧисленностиВидамДолжностей();
	
	ИДСтроки = СоответствиеСтрокОтчетности[ВидДолжности];		
	
	Возврат Справочники.СтрокиОтчетностиРасходовИЧисленностиРаботниковГосударственныхОрганов.НайтиПоРеквизиту("КодКатегорииПерсонала",ИДСтроки);
	
КонецФункции

#КонецОбласти
