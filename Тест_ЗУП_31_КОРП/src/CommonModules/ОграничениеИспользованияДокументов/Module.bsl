////////////////////////////////////////////////////////////////////////////////
// Подсистема "Ограничение использования документов".
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Проверяет наличие хотя бы одного ограниченного документа.
//
// Параметры:
//  МассивДокументов  - Массив - массив ссылок на документы.
//  ПроверкаПриЗаписи - Булево - влияет на текст сообщения пользователю.
//  ВыводитьСообщения - Булево - определяет, выводить ли пользователю сообщения.
//
// Возвращаемое значение:
//  Булево
//
Функция ЕстьОграниченныеДокументы(Знач МассивДокументов, ПроверкаПриЗаписи = Ложь, ВыводитьСообщения = Истина) Экспорт
	
	Отказ = Ложь;
	ОграниченныеДокументы = Новый Массив();
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(МассивДокументов)) Тогда
		МассивДокументов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(МассивДокументов);
	КонецЕсли;
		
	Если МассивДокументов.Количество() > 0 Тогда
		
		МассивТипов = ОграничениеИспользованияДокументовПовтИсп.ТипыОграничиваемыхДокументов();
		
		Для каждого ДокументСсылка Из МассивДокументов Цикл
			
			Если МассивТипов.Найти(ТипЗнч(ДокументСсылка)) <> Неопределено Тогда
				ОграниченныеДокументы.Добавить(ДокументСсылка);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ОграниченныеДокументы.Количество() > 0 Тогда
		
		Запрос = Новый Запрос();
		
		Запрос.УстановитьПараметр("МассивДокументов", ОграниченныеДокументы);
		
		Запрос.Текст = "ВЫБРАТЬ
		|	ОграниченныеДокументы.Документ КАК ДокументСсылка
		|ИЗ
		|	РегистрСведений.ОграниченныеДокументы КАК ОграниченныеДокументы
		|ГДЕ
		|	ОграниченныеДокументы.Документ В(&МассивДокументов)";
				
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если ВыводитьСообщения Тогда
				
				ТекстДействия = ?(ПроверкаПриЗаписи, НСтр("ru='редактирования'"), НСтр("ru='использования'"));
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Документ %1 недоступен для %2, т.к. он был ранее %3.'"), 
				Выборка.ДокументСсылка, ТекстДействия, НРег(ПредставлениеПометкиОграниченияДокумента(Выборка.ДокументСсылка)));
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Выборка.ДокументСсылка,,, Отказ);
				
			Иначе
				 Отказ = Истина;
			КонецЕсли;
		
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Отказ;
	
КонецФункции

// Ограничивает для редактирования и выполнения прочих команд документы, для которых
// формирование файла выгрузки является операцией ограничения.
//
// Параметры:
//  МассивДокументов - Массив - массив ссылок на ограничиваемые документы.
//
// Возвращаемое значение:
//  Массив - массив ограниченных документов.
//
Функция ОграничитьДокументыПослеВыгрузки(МассивДокументов) Экспорт
	
	Возврат ОграничитьДокументы(МассивДокументов, ОперацияВыгрузки());
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Обработчики подписок на события.

// Обработчик подписки на событие ПередЗаписью для проверки ограничения использования документа.
//
// Параметры:
//  Источник        - ДокументОбъект - объект данных, передаваемый в подписку на событие ПередЗаписью.
//  Отказ           - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
//  РежимЗаписи     - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
//  РежимПроведения - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
//
Процедура ПроверитьОграничениеИспользованияДокумента(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Источник.ЭтоНовый()
		И ЕстьОграниченныеДокументы(Источник.Ссылка, Истина) Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПредставлениеПометкиОграниченияДокумента(ДокументСсылка) Экспорт
	
	Менеджер = ОбщегоНазначения.МенеджерОбъектаПоСсылке(ДокументСсылка);
	Возврат Менеджер.ПредставлениеПометкиОграничения();
	
КонецФункции

// Ограничивает документы для редактирования и выполнения прочих команд.
//
// Параметры:
//  МассивДокументов - Массив - массив ссылок на ограничиваемые документы.
//  ОперацияОграничения - строка - либо "Печать", либо "Выгрузка".
//
// Возвращаемое значение:
//  Массив - массив ограниченных документов.
//
Функция ОграничитьДокументы(Знач МассивДокументов, ОперацияОграничения) Экспорт
	
	ОграниченныеДокументы = Новый Массив;
		
	ОперацииОграниченияДокументов = ОграничениеИспользованияДокументовПовтИсп.ОперацииОграниченияДокументов();
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(МассивДокументов)) Тогда
		МассивДокументов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(МассивДокументов);
	КонецЕсли;
	
	Для каждого ДокументСсылка Из МассивДокументов Цикл
		
		ОперацияОграниченияДокумента = ОперацииОграниченияДокументов.Получить(ТипЗнч(ДокументСсылка));
		
		Если ОперацияОграниченияДокумента = ОперацияОграничения Тогда 
			РегистрыСведений.ОграниченныеДокументы.ОграничитьДокумент(ДокументСсылка);
			ОграниченныеДокументы.Добавить(ДокументСсылка);
		КонецЕсли;
		
	КонецЦикла;
		
	Возврат ОграниченныеДокументы;

КонецФункции

Функция ОперацияВыгрузки() Экспорт
	
	Возврат "Выгрузка";
	
КонецФункции

Функция ОперацияОтсутствует() Экспорт
	
	Возврат "Отсутствует";
	
КонецФункции

#КонецОбласти
