////////////////////////////////////////////////////////////////////////////////
// ФизическиеЛицаЗарплатаКадрыКлиентСервер
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Функция ФамилияИнициалы(Знач ФамилияИмяОтчество) Экспорт
	
	Если ТипЗнч(ФамилияИмяОтчество) = Тип("Структура")
		Или ТипЗнч(ФамилияИмяОтчество) = Тип("ФиксированнаяСтруктура") Тогда
		
		Фамилия = ФамилияИмяОтчество.Фамилия;
		Если Не ПустаяСтрока(Фамилия) Тогда
			
			Имя = ФамилияИмяОтчество.Имя;
			
			Имена = СоставныеЧастиИмени(Имя);
			Если Имена.Количество() > 1 Тогда
				
				ФамилияИО = Фамилия + " "
					+ Лев(Имена[0], 1) + ".-"
					+ Лев(Имена[1], 1) + ".";
				
				Отчество = ФамилияИмяОтчество.Отчество;
				Если Не ПустаяСтрока(Отчество) Тогда
					ФамилияИО = ФамилияИО + " " + Лев(Отчество, 1) + ".";
				КонецЕсли; 
				
				Возврат ФамилияИО;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ФамилияИмяОтчество);
	
КонецФункции

Функция ИнициалыИмени(Знач Имя) Экспорт
	
	Инициалы = "";
	
	Если Не ПустаяСтрока(Имя) Тогда
		
		Имена = СоставныеЧастиИмени(Имя);
		Если Имена.Количество() > 1 Тогда
			
			Инициалы = Лев(Имена[0], 1) + ".-"
				+ Лев(Имена[1], 1) + ".";
			
		Иначе
			Инициалы = Лев(Имя, 1) + ".";
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Инициалы;
	
КонецФункции

Функция СоставныеЧастиИмени(Знач Имя)
	
	Имена = Новый Массив;
	
	Если Не ПустаяСтрока(Имя) Тогда
		
		РазделительИмен = "-";
		ПозицияРазделителя = СтрНайти(Имя, РазделительИмен);
		Если ПозицияРазделителя = 0 Тогда
			РазделительИмен = " ";
			ПозицияРазделителя = СтрНайти(Имя, РазделительИмен);
		КонецЕсли;
		
		Имена = СтрРазделить(Имя, РазделительИмен, Ложь);
		
	КонецЕсли;
	
	Возврат Имена;
	
КонецФункции

#КонецОбласти
