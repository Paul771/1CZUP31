#Область СлужебныеПроцедурыИФункции

Процедура ВедомостьУстановитьПредставлениеОснований(Форма) Экспорт
	
	Если ЗначениеЗаполнено(Форма.Объект.СпособВыплаты) Тогда
		Если Форма.Объект.Основания.Количество() = 0 Тогда
			Форма.ОснованияПредставление = НСтр("ru = 'Не выбраны, заполнение по всем документам'");
		Иначе	
			Форма.ОснованияПредставление = ЧислоПрописью(Форма.Объект.Основания.Количество(),,НСтр("ru = 'документ, документа, документов, м, ,,,,0'"));
		КонецЕсли;	
	Иначе
		Форма.ОснованияПредставление = "";
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьОтображениеВХО(Форма) Экспорт
	
	Ведомость = Форма.Объект.Ссылка;
	
	Если Форма.Объект.ПеречислениеНДФЛВыполнено Тогда
		Форма.Элементы.ПеречислениеНДФЛСтраницы.ТекущаяСтраница = Форма.Элементы.ПеречислениеНДФЛСтраницаПолностью
	Иначе
		Форма.Элементы.ПеречислениеНДФЛСтраницы.ТекущаяСтраница = Форма.Элементы.ПеречислениеНДФЛСтраницаЧастично
	КонецЕсли;
	
	УстановитьОтображениеВХОПВыплатыДоходов(Форма);
	
КонецПроцедуры

Процедура УстановитьОтображениеВХОПВыплатыДоходов(Форма)
	
	ВзаиморасчетыССотрудникамиКлиентСерверРасширенный.УстановитьОтображениеВХОПВыплатыДоходов(Форма);
	
КонецПроцедуры

Функция ФормаОплатыВедомости(Ведомость) Экспорт
	
	Если ТипЗнч(Ведомость) = Тип("ДокументСсылка.ВедомостьПрочихДоходовВБанк") Или ТипЗнч(Ведомость) = Тип("ДокументСсылка.ВедомостьПрочихДоходовПеречислением") Тогда
		ФормаОплатыВедомости = ПредопределенноеЗначение("Перечисление.ФормыОплаты.Безналичная")
	ИначеЕсли ТипЗнч(Ведомость) = Тип("ДокументСсылка.ВедомостьПрочихДоходовВКассу") Тогда
		ФормаОплатыВедомости = ПредопределенноеЗначение("Перечисление.ФормыОплаты.Наличная")
	Иначе
		ФормаОплатыВедомости = Неопределено
	КонецЕсли;	
	
	Возврат ФормаОплатыВедомости
	
КонецФункции

Функция ВедомостьДатаВыплаты(Ведомость) Экспорт
	Возврат ?(ЗначениеЗаполнено(Ведомость.ДатаВыплаты), Ведомость.ДатаВыплаты, НачалоДня(Ведомость.Дата));
КонецФункции

#КонецОбласти

