////////////////////////////////////////////////////////////////////////////////
// Подсистема "Синхронизация данных"
// Серверные процедуры, обслуживающие правила регистрации объектов.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Регистрирует изменения для связанных с объектом присоединенных файлов и регистров сведений.
//
// Параметры:
//  ИмяПланаОбмена - Строка - Имя метаданных плана обмена
//  Отказ - Булево - флаг отказа от выполнения правил регистрации.
//      Отказ от выполнения правил означает, что объект и присоединенные файлы не будет зарегистрированы на узлах плана обмена,
//      для которого создано это правило.
//  Объект - Объект, который может содержать присоединенные файлы.
//  ОбъектМетаданных - объект метаданных, соответствующий параметру Объект.
//  Выгрузка - (только чтение) - Булево - параметр определяет контекст выполнения правила регистрации.
//      Истина - правило регистрации выполняется в контексте выгрузки объекта.
//      Ложь - правило регистрации выполняется в контексте перед записью объекта
//  Получатели - Массив - список узлов-получателей, на которых будут зарегистрированы изменения для присоединенных файлов.
//
Процедура ЗарегистрироватьСвязанныеПрисоединенныеФайлыИРегистрыСведенийОбъекта(ИмяПланаОбмена, Отказ, Объект, ОбъектМетаданных, Выгрузка, Получатели) Экспорт
	
	Если Выгрузка Или Получатели.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МассивФайлов = Новый Массив;
	РаботаСФайлами.ЗаполнитьПрисоединенныеФайлыКОбъекту(Объект.Ссылка, МассивФайлов);
	
	Для Каждого Элемент Из МассивФайлов Цикл
		ПланыОбмена.ЗарегистрироватьИзменения(Получатели, Элемент.ПолучитьОбъект());
	КонецЦикла;
	
	ЗарегистрироватьСвязанныеРегистрыСведенийОбъекта(
		ИмяПланаОбмена, Отказ, Объект, ОбъектМетаданных, Выгрузка, Получатели);
		
КонецПроцедуры

// Регистрирует изменения для регистров сведений, в которых объект присутствует в ведущем измерении.
//
// Параметры:
//  ИмяПланаОбмена - Строка - Имя метаданных плана обмена
//  Отказ - Булево - флаг отказа от выполнения правил регистрации.
//      Отказ от выполнения правил означает, что объект и присоединенные файлы не будет зарегистрированы на узлах плана обмена,
//      для которого создано это правило.
//  Объект - Объект, который может содержать присоединенные файлы.
//  ОбъектМетаданных - объект метаданных, соответствующий параметру Объект.
//  Выгрузка - (только чтение) - Булево - параметр определяет контекст выполнения правила регистрации.
//      Истина - правило регистрации выполняется в контексте выгрузки объекта.
//      Ложь - правило регистрации выполняется в контексте перед записью объекта
//  Получатели - Массив - список узлов-получателей, на которых будут зарегистрированы изменения для присоединенных файлов.
//
Процедура ЗарегистрироватьСвязанныеРегистрыСведенийОбъекта(ИмяПланаОбмена, Отказ, Объект, ОбъектМетаданных, Выгрузка, Получатели) Экспорт
	
	Если Выгрузка Или Получатели.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МассивСвязанныхДанных = Новый Структура;
	
	МассивСлужебныхРегистров = Новый Массив;
	МассивСлужебныхРегистров.Добавить("СоответствияОбъектовИнформационныхБаз");
	
	Для Каждого Состав Из Метаданные.ПланыОбмена[ИмяПланаОбмена].Состав Цикл
		МДСостава = Состав.Метаданные;
		Если Не ОбщегоНазначения.ЭтоРегистрСведений(МДСостава) Тогда
			Продолжить;
		КонецЕсли;
		Если МассивСлужебныхРегистров.Найти(МДСостава.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если МДСостава.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.ПодчинениеРегистратору Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого Измерение Из Состав.Метаданные.Измерения Цикл
			Если Не Измерение.Ведущее Тогда
				Продолжить;
			КонецЕсли;
			
			Для Каждого ТипИзмерения Из Измерение.Тип.Типы() Цикл
				Если Метаданные.НайтиПоТипу(ТипИзмерения) = ОбъектМетаданных Тогда
					МассивСвязанныхДанных.Вставить(Состав.Метаданные.Имя, Измерение.Имя);
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого СвязанныеДанные Из МассивСвязанныхДанных Цикл
		
		МетаданныеРС = Метаданные.РегистрыСведений[СвязанныеДанные.Ключ];
		ИзмеренияРС = МетаданныеРС.Измерения;
		
		Запрос = Новый Запрос;
		
		Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
		
		Запрос.Текст = "
		| ВЫБРАТЬ РАЗЛИЧНЫЕ ";
		
		Для каждого ИзмерениеРС Из ИзмеренияРС Цикл
			Если ИзмерениеРС.ОсновнойОтбор Тогда
				Запрос.Текст = Запрос.Текст + "СвязанныеДанные." + ИзмерениеРС.Имя + ", ";
			КонецЕсли;
		КонецЦикла;
		Если МетаданныеРС.ПериодичностьРегистраСведений <> Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
			Запрос.Текст = Запрос.Текст + "СвязанныеДанные.Период, ";
		КонецЕсли;
		
		СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(Запрос.Текст, 2);
		
		Запрос.Текст = Запрос.Текст + "
		| Из
		| РегистрСведений." + СвязанныеДанные.Ключ + " КАК СвязанныеДанные
		| ГДЕ
		| СвязанныеДанные." + СвязанныеДанные.Значение + " = &Ссылка";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		НаборЗаписей = РегистрыСведений[СвязанныеДанные.Ключ].СоздатьНаборЗаписей();
		
		Пока Выборка.Следующий() Цикл
			Для каждого ИзмерениеРС Из ИзмеренияРС Цикл
				Если ИзмерениеРС.ОсновнойОтбор Тогда
					НаборЗаписей.Отбор[ИзмерениеРС.Имя].Установить(Выборка[ИзмерениеРС.Имя]);
				КонецЕсли;
			КонецЦикла;
			Если МетаданныеРС.ПериодичностьРегистраСведений <> Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
				НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
			КонецЕсли;
			НаборЗаписей.Прочитать();
			
			ПланыОбмена.ЗарегистрироватьИзменения(Получатели, НаборЗаписей);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Заменяет текст запроса регистрации изменений подчиненного объекта для получения списка узлов-получателей, как у
// объекта владельца.
//
// Параметры:
//  ИмяПланаОбмена - Строка - Имя метаданных плана обмена
//  Отказ - Булево - флаг отказа от выполнения правил регистрации.
//      Отказ от выполнения правил означает, что объект и присоединенные файлы не будет зарегистрированы на узлах плана обмена,
//      для которого создано это правило.
//  ТекстЗапроса - Строка - текст запроса, который будет использован для определения узлов-получателей.
//  ПараметрыЗапроса - Структура - содержит значения свойств текущей версии объекта,
//      которые используются в качестве параметров в запросе для определения узлов-получателей.
//  ИспользоватьКэш - Булево - параметр определяет включение платформенного механизма повторно используемых значений
//      при определении узлов-получателей. Если передаваемые запросу значения в структуре ПараметрыЗапроса содержат
//      недопустимые типы данных для платформенного механизма кэширования, то флаг следует сбросить. Значение по
//      умолчанию - Истина.
//  Выгрузка - (только чтение) - Булево - параметр определяет контекст выполнения правила регистрации.
//      Истина - правило регистрации выполняется в контексте выгрузки объекта.
//      Ложь - правило регистрации выполняется в контексте перед записью объекта
//  ОбъектВладелец - Объект, узлы-получатели которого будут использоваться для регистрации изменений подчиненного объекта.
//
Процедура ОграничитьРегистрациюПодчиненногоОбъектаПоРегистрацииОбъектаВладельца(ИмяПланаОбмена, Отказ, ТекстЗапроса, ПараметрыЗапроса, ИспользоватьКэш, Выгрузка, ОбъектВладелец) Экспорт
	
	ИспользоватьКэш = Ложь;
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ОбъектВладелец)) Тогда
		ОбъектВладельца = ОбъектВладелец.ПолучитьОбъект();
	Иначе
		ОбъектВладельца = ОбъектВладелец;
	КонецЕсли;
	ПолучателиОбъектаВладельца = ОбменДаннымиСобытия.ОпределитьПолучателей(ОбъектВладельца, ИмяПланаОбмена);
	ПараметрыЗапроса.Вставить("ПолучателиОбъектаВладельца", ПолучателиОбъектаВладельца);
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПланОбменаОсновнаяТаблица.Ссылка КАК Ссылка
	|ИЗ
	|	#ПланОбмена КАК ПланОбменаОсновнаяТаблица
	|ГДЕ
	|	ПланОбменаОсновнаяТаблица.Ссылка В(&СвойствоОбъекта_ПолучателиОбъектаВладельца)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ПланОбмена",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ПланОбмена.%1", ИмяПланаОбмена));
	
КонецПроцедуры

// Заменяет текст запроса регистрации изменений физических лиц для получения списка узлов-получателей по организациям,
// в которых установлены трудовые отношения по этим физическим лицам.
//
// Параметры:
//  ИмяПланаОбмена - Строка - Имя метаданных плана обмена
//  Отказ - Булево - флаг отказа от выполнения правил регистрации.
//      Отказ от выполнения правил означает, что объект и присоединенные файлы не будет зарегистрированы на узлах плана обмена,
//      для которого создано это правило.
//  ТекстЗапроса - Строка - текст запроса, который будет использован для определения узлов-получателей.
//  ПараметрыЗапроса - Структура - содержит значения свойств текущей версии объекта,
//      которые используются в качестве параметров в запросе для определения узлов-получателей.
//  ИспользоватьКэш - Булево - параметр определяет включение платформенного механизма повторно используемых значений
//      при определении узлов-получателей. Если передаваемые запросу значения в структуре ПараметрыЗапроса содержат
//      недопустимые типы данных для платформенного механизма кэширования, то флаг следует сбросить. Значение по
//      умолчанию - Истина.
//  Выгрузка - (только чтение) - Булево - параметр определяет контекст выполнения правила регистрации.
//      Истина - правило регистрации выполняется в контексте выгрузки объекта.
//      Ложь - правило регистрации выполняется в контексте перед записью объекта
//  ФизическиеЛица - Ссылка или массив ссылок физических лиц, по которым нужно получить список узлов-получателей
//  ДополнительныеПараметрыПолученияСотрудников - Структура параметров, которые будут использоваться для получения
//                                                списка сотрудников физического лица.
//
Процедура ОграничитьРегистрациюОбъектаОтборомПоФизическимЛицамСТрудовымиОтношениями(ИмяПланаОбмена, Отказ, ТекстЗапроса, ПараметрыЗапроса, ИспользоватьКэш, Выгрузка, ФизическиеЛица, ДополнительныеПараметрыПолученияСотрудников = Неопределено) Экспорт
	
	ПараметрыПолученияСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудников.Вставить("КадровыеДанные",				"Организация");
	ПараметрыПолученияСотрудников.Вставить("СписокФизическихЛиц",			ФизическиеЛица);
	Если ТипЗнч(ДополнительныеПараметрыПолученияСотрудников) = Тип("Структура") Тогда
		Для Каждого Параметр Из ДополнительныеПараметрыПолученияСотрудников Цикл
			ПараметрыПолученияСотрудников.Вставить(Параметр.Ключ, Параметр.Значение)
		КонецЦикла;
	КонецЕсли;
	
	ПрисутствующиеОрганизации = КадровыйУчет.СотрудникиОрганизации(Ложь, ПараметрыПолученияСотрудников).ВыгрузитьКолонку("Организация");
	
	// Дополнение массива организаций индивидуальными предпринимателями
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("ФизическиеЛица", ФизическиеЛица);
	Запрос.Параметры.Вставить("ПрисутствующиеОрганизации", ПрисутствующиеОрганизации);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|	И Организации.ИндивидуальныйПредприниматель В(&ФизическиеЛица)
	|	И НЕ Организации.Ссылка В (&ПрисутствующиеОрганизации)";
	
	Организации = Запрос.Выполнить().Выгрузить();
	
	Если Организации.Количество() > 0 Тогда
		ДополнительныеОрганизации = Организации.ВыгрузитьКолонку("Организация");
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПрисутствующиеОрганизации, ДополнительныеОрганизации);
	КонецЕсли;
	
	Если ПрисутствующиеОрганизации.Количество() = 0 
		И ДополнительныеПараметрыПолученияСотрудников.Свойство("ФизическиеЛицаТолькоСТрудовымиОтношениями")
		И ДополнительныеПараметрыПолученияСотрудников.ФизическиеЛицаТолькоСТрудовымиОтношениями Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ОграничитьРегистрациюОбъектаОтборомПоОрганизациямИПодразделениям(ИмяПланаОбмена, Отказ, ТекстЗапроса, ПараметрыЗапроса, ИспользоватьКэш, Выгрузка, ПрисутствующиеОрганизации);
	
КонецПроцедуры

// Заменяет текст запроса регистрации изменений сотрудников для получения списка узлов-получателей по организациям,
// в которых установлены трудовые отношения по этим сотрудникам.
//
// Параметры:
//  ИмяПланаОбмена - Строка - Имя метаданных плана обмена
//  Отказ - Булево - флаг отказа от выполнения правил регистрации.
//      Отказ от выполнения правил означает, что объект и присоединенные файлы не будет зарегистрированы на узлах плана обмена,
//      для которого создано это правило.
//  ТекстЗапроса - Строка - текст запроса, который будет использован для определения узлов-получателей.
//  ПараметрыЗапроса - Структура - содержит значения свойств текущей версии объекта,
//      которые используются в качестве параметров в запросе для определения узлов-получателей.
//  ИспользоватьКэш - Булево - параметр определяет включение платформенного механизма повторно используемых значений
//      при определении узлов-получателей. Если передаваемые запросу значения в структуре ПараметрыЗапроса содержат
//      недопустимые типы данных для платформенного механизма кэширования, то флаг следует сбросить. Значение по
//      умолчанию - Истина.
//  Выгрузка - (только чтение) - Булево - параметр определяет контекст выполнения правила регистрации.
//      Истина - правило регистрации выполняется в контексте выгрузки объекта.
//      Ложь - правило регистрации выполняется в контексте перед записью объекта
//  Сотрудники - Ссылка или массив ссылок сотрудников, по которым нужно получить список узлов-получателей
//  ДатаСведений - дата на которую необходимо получить данные сотрудников, применимо к данным, носящим периодический характер
//      Если дату не указывать, будут получены самые последние данные.
//  ДополнительныеОрганизации - массив организаций, которые будут использоваться для получения списка узлов-получателей.
//
Процедура ОграничитьРегистрациюОбъектаОтборомПоОрганизациямСотрудников(ИмяПланаОбмена, Отказ, ТекстЗапроса, ПараметрыЗапроса, ИспользоватьКэш, Выгрузка, Сотрудники, ДатаСведений = '00010101', ДополнительныеОрганизации = Неопределено) Экспорт
	
	ПрисутствующиеОрганизации = КадровыйУчет.КадровыеДанныеСотрудников(Ложь, Сотрудники, "Организация", ДатаСведений).ВыгрузитьКолонку("Организация");
	
	Если ТипЗнч(ДополнительныеОрганизации) = Тип("Массив") И ДополнительныеОрганизации.Количество() > 0 Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПрисутствующиеОрганизации, ДополнительныеОрганизации, Истина);
	КонецЕсли;
	
	ОграничитьРегистрациюОбъектаОтборомПоОрганизациямИПодразделениям(ИмяПланаОбмена, Отказ, ТекстЗапроса, ПараметрыЗапроса, ИспользоватьКэш, Выгрузка, ПрисутствующиеОрганизации);
	
КонецПроцедуры

// Ограничивает регистрацию изменений регистра сведений по типу измерения
// Используется для измерений регистра сведений с составным типом.
//
// Параметры:
//  ИмяПланаОбмена - Строка - Имя метаданных плана обмена
//  Отказ - Булево - флаг отказа от выполнения правил регистрации.
//      Отказ от выполнения правил означает, что объект и присоединенные файлы не будет зарегистрированы на узлах плана обмена,
//      для которого создано это правило.
//  Объект - Запись регистра сведений, для которой регистрируется изменение.
//  ОбъектМетаданных - объект метаданных, соответствующий параметру Объект.
//  Выгрузка - (только чтение) - Булево - параметр определяет контекст выполнения правила регистрации.
//      Истина - правило регистрации выполняется в контексте выгрузки объекта.
//      Ложь - правило регистрации выполняется в контексте перед записью объекта
//  ИмяИзмерения - Строка - Имя измерения, ограничивающееся по типу
//  ИмяТипа - Строка - Имя типа, ограничивающее изменение записи регистра сведений.
//
Процедура ОграничитьРегистрациюРегистраСведенийПоТипуИзмерения(ИмяПланаОбмена, Отказ, Объект, ОбъектМетаданных, Выгрузка, ИмяИзмерения, ИмяТипа) Экспорт
	
	Для Каждого Запись Из Объект Цикл
		Если ТипЗнч(Запись[ИмяИзмерения]) <> Тип(ИмяТипа) Тогда
			Отказ = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Обработчик регистрации изменений для начальной выгрузки данных.
// Используется для переопределения стандартной обработки регистрации изменений.
// При стандартной обработке будут зарегистрированы изменения всех данных из состава плана обмена.
// Если для плана обмена предусмотрены фильтры ограничения миграции данных,
// то использование этого обработчика позволит повысить производительность начальной выгрузки данных.
// В обработчике следует реализовать регистрацию изменений с учетом фильтров ограничения миграции данных.
// Если для плана обмена используются ограничения миграции по дате или по дате и организациям,
// то можно воспользоваться универсальной процедурой
// ОбменДаннымиСервер.ЗарегистрироватьДанныеПоДатеНачалаВыгрузкиИОрганизациям.
// Обработчик используется только для универсального обмена данными с использованием правил обмена
// и для универсального обмена данными без правил обмена и не используется для обменов в РИБ.
// Использование обработчика позволяет повысить производительность
// начальной выгрузки данных в среднем в 2-4 раза.
//
// Параметры:
//
// Получатель - ПланОбменаСсылка - Узел плана обмена, в который требуется выгрузить данные.
//
// СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки
//                                 события.
//  Если в теле процедуры-обработчика установить данному параметру значение Ложь, стандартная обработка события
//  производиться не будет.
//  Отказ от стандартной обработки не отменяет действие.
//  Значение по умолчанию - Истина.
//
Процедура ОбработкаРегистрацииНачальнойВыгрузкиДанных(Знач Получатель, СтандартнаяОбработка, Отбор) Экспорт
	ОбменДаннымиЗарплатаКадрыВнутренний.ОбработкаРегистрацииНачальнойВыгрузкиДанных(Получатель, СтандартнаяОбработка, Отбор);
КонецПроцедуры

// Процедура обновляет вторичные данные использования обмена данными
// Параметры
//	ИмяПланаОбмена - имя плана обмена для которого выполняется запись вторичных данных
//	КонстантаМенеджер - тип КонстантаМенеджер, в которой хранится настройка использования обмена по всем организациям
//	НаборЗаписей - набор записей регистра сведений, в котором хранятся настройки использования обмена в разрезе организаций
//	СсылкаНаУдаляемыйУзел - ссылка на узел плана обмена при обработке удаления которого вызвана эта процедура.
//
Процедура ОбновитьНастройкиИспользованияОбменаДанными(ИмяПланаОбмена, КонстантаМенеджер, НаборЗаписей, СсылкаНаУдаляемыйУзел = Неопределено) Экспорт

	ПолноеИмяПланаОбмена = "ПланОбмена."+ИмяПланаОбмена;
	
	ИсключаемыеУзлы = Новый Массив;
	ИсключаемыеУзлы.Добавить(ПланыОбмена[ИмяПланаОбмена].ЭтотУзел());
	Если СсылкаНаУдаляемыйУзел <> Неопределено Тогда
		ИсключаемыеУзлы.Добавить(СсылкаНаУдаляемыйУзел);	
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИсключаемыеУзлы", ИсключаемыеУзлы);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПланыОбменов.ИспользоватьОтборПоОрганизациям
	|ИЗ
	|	#ПолноеИмяПланаОбмена КАК ПланыОбменов
	|ГДЕ
	|	НЕ ПланыОбменов.Ссылка В (&ИсключаемыеУзлы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ПланыОбменов.ИспользоватьОтборПоОрганизациям) КАК ИспользоватьОтборПоОрганизациям
	|ИЗ
	|	#ПолноеИмяПланаОбмена КАК ПланыОбменов
	|ГДЕ
	|	НЕ ПланыОбменов.Ссылка В (&ИсключаемыеУзлы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Организации.Организация,
	|	ИСТИНА КАК Используется
	|ИЗ
	|	#ТаблицаОрганизации КАК Организации
	|ГДЕ
	|	НЕ Организации.Ссылка В (&ИсключаемыеУзлы)";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ПолноеИмяПланаОбмена", ПолноеИмяПланаОбмена);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ТаблицаОрганизации", ПолноеИмяПланаОбмена + ".Организации");
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат[1].Выбрать();
	Выборка.Следующий();
	
	ЗначениеОтборПоВсемОрганизациям = Ложь;
	
	Если Результат[0].Пустой() Тогда
		// нет настроенных узлов обмена, не меняем значения по умолчанию
	ИначеЕсли Не Выборка.ИспользоватьОтборПоОрганизациям Тогда
		ЗначениеОтборПоВсемОрганизациям = Истина;
	Иначе
		// включен отбор по организациям, заполним список выбранных организаций
		НаборЗаписей.Загрузить(Результат[2].Выгрузить());
	КонецЕсли;
	
	КонстантаМенеджер.Установить(ЗначениеОтборПоВсемОрганизациям);
	НаборЗаписей.Записать();

КонецПроцедуры

// Заменяет текст запроса регистрации изменений объекта для получения списка узлов-получателей по организации.
//
// Параметры:
//  ИмяПланаОбмена - Строка - Имя метаданных плана обмена
//  Отказ - Булево - флаг отказа от выполнения правил регистрации.
//      Отказ от выполнения правил означает, что объект и присоединенные файлы не будет зарегистрированы на узлах плана обмена,
//      для которого создано это правило.
//  ТекстЗапроса - Строка - текст запроса, который будет использован для определения узлов-получателей.
//  ПараметрыЗапроса - Структура - содержит значения свойств текущей версии объекта,
//      которые используются в качестве параметров в запросе для определения узлов-получателей.
//  ИспользоватьКэш - Булево - параметр определяет включение платформенного механизма повторно используемых значений
//      при определении узлов-получателей. Если передаваемые запросу значения в структуре ПараметрыЗапроса содержат
//      недопустимые типы данных для платформенного механизма кэширования, то флаг следует сбросить. Значение по
//      умолчанию - Истина.
//  Выгрузка - (только чтение) - Булево - параметр определяет контекст выполнения правила регистрации.
//      Истина - правило регистрации выполняется в контексте выгрузки объекта.
//      Ложь - правило регистрации выполняется в контексте перед записью объекта
//  Организации - Ссылка или массив ссылок на организации, по которым нужно получить список узлов-получателей.
//
Процедура ОграничитьРегистрациюОбъектаОтборомПоОрганизациямИПодразделениям(ИмяПланаОбмена, Отказ, ТекстЗапроса, ПараметрыЗапроса, ИспользоватьКэш, Выгрузка, Организации = Неопределено, Подразделения = Неопределено) Экспорт
	ОбменДаннымиЗарплатаКадрыВнутренний.ОграничитьРегистрациюОбъектаОтборомПоОрганизациямИПодразделениям(ИмяПланаОбмена, Отказ, ТекстЗапроса, ПараметрыЗапроса, ИспользоватьКэш, Выгрузка, Организации, Подразделения);
КонецПроцедуры

// Заменяет текст запроса регистрации изменений объекта для получения списка узлов-получателей по организации.
//
// Параметры:
//  ИмяПланаОбмена - Строка - Имя метаданных плана обмена
//  Отказ - Булево - флаг отказа от выполнения правил регистрации.
//      Отказ от выполнения правил означает, что объект и присоединенные файлы не будет зарегистрированы на узлах плана обмена,
//      для которого создано это правило.
//  ТекстЗапроса - Строка - текст запроса, который будет использован для определения узлов-получателей.
//  ПараметрыЗапроса - Структура - содержит значения свойств текущей версии объекта,
//      которые используются в качестве параметров в запросе для определения узлов-получателей.
//  ИспользоватьКэш - Булево - параметр определяет включение платформенного механизма повторно используемых значений
//      при определении узлов-получателей. Если передаваемые запросу значения в структуре ПараметрыЗапроса содержат
//      недопустимые типы данных для платформенного механизма кэширования, то флаг следует сбросить. Значение по
//      умолчанию - Истина.
//  Выгрузка - (только чтение) - Булево - параметр определяет контекст выполнения правила регистрации.
//      Истина - правило регистрации выполняется в контексте выгрузки объекта.
//      Ложь - правило регистрации выполняется в контексте перед записью объекта
//  Организации - Ссылка или массив ссылок на организации, по которым нужно получить список узлов-получателей.
//
Процедура ОграничитьРегистрациюОбъектаОтборомПоГоловнымОрганизациям(ИмяПланаОбмена, Отказ, ТекстЗапроса, ПараметрыЗапроса, ИспользоватьКэш, Выгрузка, ГоловныеОрганизации = Неопределено) Экспорт
	
	ИспользоватьКэш = Ложь;
	
	ПараметрыЗапроса.Вставить("ГоловныеОрганизации", ГоловныеОрганизации);
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПланОбменаОсновнаяТаблица.Ссылка КАК Ссылка
	|ИЗ
	|	#ПланОбмена КАК ПланОбменаОсновнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ #ПланОбменаОрганизации КАК ПланОбменаОрганизации
	|		ПО (ПланОбменаОрганизации.Ссылка = ПланОбменаОсновнаяТаблица.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ГоловныеОрганизации
	|		ПО (ГоловныеОрганизации.Ссылка В (&СвойствоОбъекта_ГоловныеОрганизации))
	|			И (ПланОбменаОрганизации.Организация.ГоловнаяОрганизация = ГоловныеОрганизации.Ссылка)
	|ГДЕ
	|	(ПланОбменаОрганизации.Организация В (&СвойствоОбъекта_ГоловныеОрганизации)
	|			ИЛИ ГоловныеОрганизации.Ссылка ЕСТЬ НЕ NULL )
	|	И &ОбязательныеУсловия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПланОбменаОсновнаяТаблица.Ссылка
	|ИЗ
	|	#ПланОбмена КАК ПланОбменаОсновнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ #ПланОбменаОрганизации КАК ПланОбменаОрганизации
	|		ПО (ПланОбменаОрганизации.Ссылка = ПланОбменаОсновнаяТаблица.Ссылка)
	|ГДЕ
	|	ИСТИНА
	|	И &ОбязательныеУсловия
	|
	|СГРУППИРОВАТЬ ПО
	|	ПланОбменаОсновнаяТаблица.Ссылка
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(ПланОбменаОрганизации.Организация) = 0";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ПланОбменаОрганизации",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ПланОбмена.%1.Организации", ИмяПланаОбмена));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ПланОбмена",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ПланОбмена.%1", ИмяПланаОбмена));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПланаОбменаЭтотУзел",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("&%1ЭтотУзел", ИмяПланаОбмена));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ОбязательныеУсловия", "[ОбязательныеУсловия]");
	
КонецПроцедуры

#Область ОграниченияРегистрации

// Возвращает структуру параметров ограничения регистрации объекта
// 
// Возвращаемое значение:
//		Структура
//			* МенеджерВременныхТаблиц - Менеджер ВТ, в котором создаются временные таблицы.
//			* Организации - Массив - Организации для получения списка узлов-получателей
//			* Подразделения - Массив - Структурные единицы для получения списка узлов-получателей
//			* СообщениеПользователю - Строка - Сообщение, которое выводится пользователю при несоответствии настройкам узла РИБ
//			* ОграничитьИзменениеОбъекта - Булево - Флаг ограничения объекта в подчиненном узле
//			* ДополнительныеОрганизации - Массив - Организации для получения списка узлов-получателей
//				Неопределено, если организации не заданы
//			* ДополнительныеПодразделения - Массив - Структурные единицы для получения списка узлов-получателей
//				Неопределено, если структурные единицы не заданы
//			* Сотрудники - Массив - Сотрудники для определения организаций и структурных единиц
//			* ФизическиеЛица - Массив - Физические лица для определения организаций и структурных единиц
//			* ФизическиеЛицаТолькоСТрудовымиОтношениями - Булево - Истина, если физические лица должны учитываться только с трудовыми отношениями
//			* ДатаПолученияДанных - Дата - Дата получения кадровых данных
//			* ОграничиватьПоФизическимЛицамКонтрагентам - Булево - Истина, если физические лица должны учитываться как контрагенты
//			* ИмяТаблицыСотрудники - Строка - Имя таблицы, где выбраны сотрудники для определения организаций и структурных единиц
//			* ИмяПоляСотрудник - Строка - Имя поля временной таблицы, содержащего ссылку на сотрудника
//			* ИмяТаблицыФизическиеЛица - Строка - Имя таблицы, где выбраны физические лица для определения организаций и структурных единиц
//			* ИмяПоляФизическоеЛицо - Строка - Имя поля временной таблицы, содержащего ссылку на физическое лицо.
//
Функция ОграниченияРегистрации() Экспорт
	
	ОграниченияРегистрации = Новый Структура;
	ОграниченияРегистрации.Вставить("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц);
	ОграниченияРегистрации.Вставить("Организации", Новый Массив);
	ОграниченияРегистрации.Вставить("Подразделения", Новый Массив);
	ОграниченияРегистрации.Вставить("СообщениеПользователю", "");
	ОграниченияРегистрации.Вставить("ОграничитьИзменениеОбъекта", Ложь);
	ОграниченияРегистрации.Вставить("ДополнительныеОрганизации", Неопределено);
	ОграниченияРегистрации.Вставить("ДополнительныеПодразделения", Неопределено);
	ОграниченияРегистрации.Вставить("Сотрудники", Новый Массив);
	ОграниченияРегистрации.Вставить("ФизическиеЛица", Новый Массив);
	ОграниченияРегистрации.Вставить("ФизическиеЛицаТолькоСТрудовымиОтношениями", Ложь);
	ОграниченияРегистрации.Вставить("ДатаПолученияДанных", '00010101');
	ОграниченияРегистрации.Вставить("ОграничиватьПоФизическимЛицамКонтрагентам", Ложь);
	ОграниченияРегистрации.Вставить("ИмяТаблицыСотрудники", "ВТСотрудники");
	ОграниченияРегистрации.Вставить("ИмяПоляСотрудник", "Сотрудник");
	ОграниченияРегистрации.Вставить("ИмяТаблицыФизическиеЛица", "ВТФизическиеЛица");
	ОграниченияРегистрации.Вставить("ИмяПоляФизическоеЛицо", "ФизическоеЛицо");
	
	Возврат ОграниченияРегистрации;
	
КонецФункции

// Возвращает заполненную структуру ограничения регистрации по организации
//
// Параметры:
//		Объект - Объект, для которого получаем ограничения регистрации
//		Организация - Ссылка на организацию Объекта.
//
// Возвращаемое значение:
//		Структура - см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрацииПоОрганизации(Объект, Организация) Экспорт
	
	Если Объект.ДополнительныеСвойства.Свойство("ОграниченияРегистрации") Тогда
		// Ограничения регистрации уже были получены
		Возврат Объект.ДополнительныеСвойства.ОграниченияРегистрации;
	КонецЕсли;
	
	ОграниченияРегистрации = ОграниченияРегистрации();
	ОграниченияРегистрации.Организации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
	
	Возврат ОграниченияРегистрации;
	
КонецФункции

// Возвращает заполненную структуру ограничения регистрации по организации и физическому лицу.
//
// Параметры:
//		Объект - Объект, для которого получаем ограничения регистрации
//		Организация - Ссылка на организацию Объекта
//		ФизическоеЛицо - Ссылка на физическое лицо объекта
//		ДатаПолученияДанных - Дата, на которую будут получены кадровые сведения.
//
// Возвращаемое значение:
//		Структура - см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрацииПоОрганизацииИФизическомуЛицу(Объект, Организация, ФизическоеЛицо, ДатаПолученияДанных) Экспорт
	
	Возврат ОграниченияРегистрацииПоОрганизации(Объект, Организация);
	
	Если Объект.ДополнительныеСвойства.Свойство("ОграниченияРегистрации") Тогда
		// Ограничения регистрации уже были получены
		Возврат Объект.ДополнительныеСвойства.ОграниченияРегистрации;
	КонецЕсли;
	
	ОграниченияРегистрации = ОграниченияРегистрации();
	ОграниченияРегистрации.ФизическиеЛица = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо);
	ОграниченияРегистрации.ДатаПолученияДанных = ДатаПолученияДанных;
	ОграниченияРегистрации.Организации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
	
	Возврат ОграниченияРегистрации;
	
КонецФункции

// Возвращает заполненную структуру ограничения регистрации по организации и сотруднику.
//
// Параметры:
//		Объект - Объект, для которого получаем ограничения регистрации
//		Организация - Ссылка на организацию Объекта
//		Сотрудник - Ссылка на сотрудника объекта
//		ДатаПолученияДанных - Дата, на которую будут получены кадровые сведения.
//
// Возвращаемое значение:
//		Структура - см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрацииПоОрганизацииИСотруднику(Объект, Организация, Сотрудник, ДатаПолученияДанных) Экспорт
	
	Возврат ОграниченияРегистрацииПоОрганизации(Объект, Организация);
	
	Если Объект.ДополнительныеСвойства.Свойство("ОграниченияРегистрации") Тогда
		// Ограничения регистрации уже были получены
		Возврат Объект.ДополнительныеСвойства.ОграниченияРегистрации;
	КонецЕсли;
	
	ОграниченияРегистрации = ОграниченияРегистрации();
	ОграниченияРегистрации.Сотрудники = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник);
	ОграниченияРегистрации.ДатаПолученияДанных = ДатаПолученияДанных;
	ОграниченияРегистрации.Организации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
	
	Возврат ОграниченияРегистрации;
	
КонецФункции

// Возвращает заполненную структуру ограничения регистрации по организации и сотрудникам.
//
// Параметры:
//		Объект - Объект, для которого получаем ограничения регистрации
//		Организация - Ссылка на организацию Объекта
//		МассивПараметров - Массив структур
//			Ключ - Имя табличной части
//			Значение - Имя реквизита сотрудника
//		ДатаПолученияДанных - Дата, на которую будут получены кадровые сведения.
//
// Возвращаемое значение:
//		Структура - см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрацииПоОрганизацииИСотрудникам(Объект, Организация, МассивПараметров, ДатаПолученияДанных) Экспорт
	
	Возврат ОграниченияРегистрацииПоОрганизации(Объект, Организация);
	
	Если Объект.ДополнительныеСвойства.Свойство("ОграниченияРегистрации") Тогда
		// Ограничения регистрации уже были получены
		Возврат Объект.ДополнительныеСвойства.ОграниченияРегистрации;
	КонецЕсли;
	
	МассивСотрудников = Новый Массив;
	Для каждого ЭлементМассива Из МассивПараметров Цикл
		Для Каждого ЭлементСтруктуры Из ЭлементМассива Цикл
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСотрудников, Объект[ЭлементСтруктуры.Ключ].ВыгрузитьКолонку(ЭлементСтруктуры.Значение), Истина);
		КонецЦикла;
	КонецЦикла;
	
	ОграниченияРегистрации = ОграниченияРегистрации();
	ОграниченияРегистрации.Сотрудники = МассивСотрудников;
	ОграниченияРегистрации.ДатаПолученияДанных = ДатаПолученияДанных;
	ОграниченияРегистрации.Организации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
	
	Возврат ОграниченияРегистрации;
	
КонецФункции

// Возвращает заполненную структуру ограничения регистрации по организации и физическим лицам.
//
// Параметры:
//		Объект - Объект, для которого получаем ограничения регистрации
//		Организация - Ссылка на организацию Объекта
//		МассивПараметров - Массив структур
//			Ключ - Имя табличной части
//			Значение - Имя реквизита физического лица
//		ДатаПолученияДанных - Дата, на которую будут получены кадровые сведения.
//
// Возвращаемое значение:
//		Структура - см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрацииПоОрганизацииИФизическимЛицам(Объект, Организация, МассивПараметров, ДатаПолученияДанных) Экспорт
	
	Возврат ОграниченияРегистрацииПоОрганизации(Объект, Организация);
	
	Если Объект.ДополнительныеСвойства.Свойство("ОграниченияРегистрации") Тогда
		// Ограничения регистрации уже были получены
		Возврат Объект.ДополнительныеСвойства.ОграниченияРегистрации;
	КонецЕсли;
	
	МассивФизическихЛиц = Новый Массив;
	Для каждого ЭлементМассива Из МассивПараметров Цикл
		Для Каждого ЭлементСтруктуры Из ЭлементМассива Цикл
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивФизическихЛиц, Объект[ЭлементСтруктуры.Ключ].ВыгрузитьКолонку(ЭлементСтруктуры.Значение), Истина);
		КонецЦикла;
	КонецЦикла;
	
	ОграниченияРегистрации = ОграниченияРегистрации();
	ОграниченияРегистрации.ФизическиеЛица = МассивФизическихЛиц;
	ОграниченияРегистрации.ДатаПолученияДанных = ДатаПолученияДанных;
	ОграниченияРегистрации.Организации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
	
	Возврат ОграниченияРегистрации;
	
КонецФункции

// Возвращает заполненную структуру ограничения регистрации по сотруднику
//
// Параметры:
//		Объект - Объект, для которого получаем ограничения регистрации
//		Сотрудник - Ссылка на сотрудника
//		ДатаПолученияДанных - Дата, на которую будут получены кадровые сведения.
//
// Возвращаемое значение:
//		Структура - см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрацииПоСотруднику(Объект, Сотрудник, ДатаПолученияДанных) Экспорт
	
	Если Объект.ДополнительныеСвойства.Свойство("ОграниченияРегистрации") Тогда
		// Ограничения регистрации уже были получены
		Возврат Объект.ДополнительныеСвойства.ОграниченияРегистрации;
	КонецЕсли;
	
	ОграниченияРегистрации = ОграниченияРегистрации();
	ОграниченияРегистрации.Сотрудники = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник);
	ОграниченияРегистрации.ДатаПолученияДанных = ДатаПолученияДанных;
	
	Возврат ОграниченияРегистрации;
	
КонецФункции

// Возвращает заполненную структуру ограничения регистрации по физическому лицу
//
// Параметры:
//		Объект - Объект, для которого получаем ограничения регистрации
//		ФизическоеЛицо - Ссылка на физическое лицо объекта
//		ДатаПолученияДанных - Дата, на которую будут получены кадровые сведения.
//
// Возвращаемое значение:
//		Структура - см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрацииПоФизическомуЛицу(Объект, ФизическоеЛицо, ДатаПолученияДанных) Экспорт
	
	Если Объект.ДополнительныеСвойства.Свойство("ОграниченияРегистрации") Тогда
		// Ограничения регистрации уже были получены
		Возврат Объект.ДополнительныеСвойства.ОграниченияРегистрации;
	КонецЕсли;
	
	ОграниченияРегистрации = ОграниченияРегистрации();
	ОграниченияРегистрации.ФизическиеЛица = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо);
	ОграниченияРегистрации.ДатаПолученияДанных = ДатаПолученияДанных;
	
	Возврат ОграниченияРегистрации;
	
КонецФункции

// Возвращает структуру ограничения регистрации независимого регистра сведений
//
// Возвращаемое значение:
//		Структура
//			* Организации - Строка, Массив - имена полей организации в наборе сведений
//			* Подразделения - Строка, Массив - имена полей подразделения в наборе сведений
//			* ФизическиеЛица - Строка, Массив - имена полей физического лица в наборе сведений
//			* Сотрудники - Строка, Массив - имена полей сотрудника в наборе сведений
//			* ДатыПолученияДанных - Строка, Массив - имена полей даты получения кадровых сведений.
//
Функция ИменаПолейОграниченияРегистрацииРегистраСведений() Экспорт
	Возврат ОбменДаннымиЗарплатаКадрыВнутренний.ИменаПолейОграниченияРегистрацииРегистраСведений();
КонецФункции

// Возвращает заполненную структуру ограничения регистрации независимого регистра сведений.
//
// Параметры:
//		НаборСведений - Набор, для которого получаем ограничения регистрации
//		ИменаПолей - Структура - см. ОбменДаннымиЗарплатаКадры.ИменаПолейОграниченияРегистрацииРегистраСведений.
//
// Возвращаемое значение:
//		Структура - см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрацииРегистраСведений(НаборСведений, ИменаПолей) Экспорт
	
	Если НаборСведений.ДополнительныеСвойства.Свойство("ОграниченияРегистрации") Тогда
		// Ограничения регистрации уже были получены
		Возврат НаборСведений.ДополнительныеСвойства.ОграниченияРегистрации;
	КонецЕсли;
	
	ОграниченияРегистрации = ОграниченияРегистрации();
	
	ЗаполнитьОграниченияРегистрацииРегистраСведений(ОграниченияРегистрации, НаборСведений, ИменаПолей);
	
	Возврат ОграниченияРегистрации;
	
КонецФункции

#КонецОбласти

// Ограничивает изменение документов, если не все данные в документе доступны пользователю в подчиненном узле
// Вызывается в обработчике "ПриЧтенииНаСервере" всех документов, учавствующих в обмене.
//
// Параметры:
//		Форма - форма документа
//		Источник - Открываемый документ-объект.
//
Процедура ПриЧтенииНаСервереДокумента(Форма, Источник) Экспорт
	ОбменДаннымиЗарплатаКадрыВнутренний.ПриЧтенииНаСервереДокумента(Форма, Источник);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет массивы организаций, подразделений, физических лиц и сотрудников из переданного набора сведений.
//
// Параметры:
//		ОграниченияРегистрации - Структура - см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации
//		НаборСведений - Набор, для которого получаем ограничения регистрации
//		ИменаПолей - Структура - см. ОбменДаннымиЗарплатаКадры.ИменаПолейОграниченияРегистрацииРегистраСведений.
//
Процедура ЗаполнитьОграниченияРегистрацииРегистраСведений(ОграниченияРегистрации, НаборСведений, ИменаПолей) Экспорт
	ОбменДаннымиЗарплатаКадрыВнутренний.ЗаполнитьОграниченияРегистрацииРегистраСведений(ОграниченияРегистрации, НаборСведений, ИменаПолей);
КонецПроцедуры

#КонецОбласти
