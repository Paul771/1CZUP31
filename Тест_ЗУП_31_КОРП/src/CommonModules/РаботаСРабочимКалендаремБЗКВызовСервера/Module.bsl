////////////////////////////////////////////////////////////////////////////////
// Подсистема "Рабочий календарь".
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

// Получает данные об отсутствии пользователя.
//
// Параметры:
//	Пользователь - СправочникСсылка.Пользователи.
//	ДатаНачала - Дата - дата, с которой выполняется проверка.
//	ДатаОкончания - Дата - дата, до которой выполняется проверка.
//
// Возвращаемое значение:
//	Структура - структура со свойствами:
//		* ЕстьПредупреждения - Булево - признак наличия записей об отсутствии.
//		* Отсутствия - Массив - массив ссылок на регистраторы отсутствий.
//
Функция ДанныеОбОтсутствииПользователя(Пользователь, ДатаНачала, ДатаОкончания) Экспорт
	
	Если Не ЗначениеЗаполнено(ДатаНачала) Тогда
		ДатаНачала = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаОкончания) Тогда
		ДатаОкончания = ДатаНачала;
	КонецЕсли;
	
	ДанныеОбОтсутствии = Новый Структура;
	ДанныеОбОтсутствии.Вставить("ЕстьПредупреждения", Ложь);
	ДанныеОбОтсутствии.Вставить("Отсутствия", Новый Массив);
	
	ОтсутствияПользователя = КадровыйУчетКорпоративный.ОтсутствияПользователя(Пользователь, ДатаНачала, ДатаОкончания);
	
	Если ОтсутствияПользователя.Количество() = 0 Тогда
		Возврат ДанныеОбОтсутствии;
	КонецЕсли;
	
	ДанныеОбОтсутствии.ЕстьПредупреждения = Истина;
	ДанныеОбОтсутствии.Отсутствия = ОтсутствияПользователя;
	
	Возврат ДанныеОбОтсутствии;
	
КонецФункции

Функция ПараметрыРасширеннойФормыПодбора(СписокПользователей) Экспорт
	
	ВыбранныеПользователи = Новый ТаблицаЗначений;
	ВыбранныеПользователи.Колонки.Добавить("Пользователь");
	ВыбранныеПользователи.Колонки.Добавить("НомерКартинки");
	
	Для Каждого ЭлементСписка Из СписокПользователей Цикл
		
		СтрокаВыбранныеПользователи = ВыбранныеПользователи.Добавить();
		СтрокаВыбранныеПользователи.Пользователь = ЭлементСписка.Значение;
		
	КонецЦикла;
	
	ЗаголовокФормыПодбора = НСтр("ru = 'Выбор пользователей календаря'");
	ПараметрыРасширеннойФормыПодбора = 
		Новый Структура("ЗаголовокФормыПодбора, ВыбранныеПользователи, ПодборГруппНевозможен", 
							ЗаголовокФормыПодбора, ВыбранныеПользователи, Истина);
	
	АдресХранилища = ПоместитьВоВременноеХранилище(ПараметрыРасширеннойФормыПодбора);
	
	Возврат АдресХранилища;
	
КонецФункции

#КонецОбласти
