
#Область СлужебныйПрограммныйИнтерфейс

#Область МетодыОбслуживанияИсправленийВДокументе

Процедура Исправить(Ссылка, ИмяТаблицы, Форма = НеОпределено, ИмяПериодРегистрации = "ПериодРегистрации") Экспорт
	Основание = Новый Структура("Ссылка, Действие", Ссылка, "Исправить");
	ДополнитьПоИсправляемомуДокументу(Основание, Форма, ИмяПериодРегистрации, Ссылка);
	ПараметрыФормы = Новый Структура("Основание", Основание);
	ОткрытьФорму("Документ." + ИмяТаблицы + ".ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

Процедура Сторнировать(Ссылка, РеквизитНачислениеДокумента = "", Форма = НеОпределено, ИмяПериодРегистрации = "ПериодРегистрации") Экспорт
	Основание = Новый Структура("Ссылка, Действие, РеквизитНачислениеДокумента", Ссылка, "Сторнировать", РеквизитНачислениеДокумента);
	ДополнитьПоИсправляемомуДокументу(Основание, Форма, ИмяПериодРегистрации, Ссылка);
	ПараметрыФормы = Новый Структура("Основание", Основание);
	ОткрытьФорму("Документ.СторнированиеНачислений.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

Процедура ПерейтиКИсправлению(Ссылка, ИмяТаблицы) Экспорт 
	
	ПараметрыФормы = Новый Структура("Ключ", Ссылка);
	ОткрытьФорму("Документ." + ИмяТаблицы + ".ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

Процедура ПерейтиКИсправленному(ИсправленныйДокумент, ИмяТаблицы) Экспорт 
	
	ПараметрыФормы = Новый Структура("Ключ", ИсправленныйДокумент);
	ОткрытьФорму("Документ." + ИмяТаблицы + ".ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

Процедура ПерейтиКСторно(Ссылка) Экспорт 
	
	ПараметрыФормы = Новый Структура("Ключ", Ссылка);
	ОткрытьФорму("Документ.СторнированиеНачислений.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

Процедура ОбработкаОповещенияИсправленногоДокумента(Форма, Ссылка, ИмяСобытия, Параметр, Источник) Экспорт
	
	Если ИмяСобытия = "ИзмененоСторнированиеНачислений" И Источник = Ссылка Тогда
		Если Параметр.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			Форма.ДокументСторнирован = Истина;
			Форма.ДокументСторно = Параметр.Ссылка;
		Иначе
			Форма.ДокументСторнирован = Ложь;
			Форма.ДокументСторно = Неопределено;
		КонецЕсли;
		
		Если Параметр.Свойство("ПолеПериодРегистрации") И Параметр.ПолеПериодРегистрации <> Неопределено Тогда
			ИсправлениеДокументовЗарплатаКадрыКлиентСервер.УстановитьПоляИсправления(Форма,,,Параметр.ПолеПериодРегистрации);
		Иначе
			ИсправлениеДокументовЗарплатаКадрыКлиентСервер.УстановитьПоляИсправления(Форма);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ИсправленДокумент" И Источник = Ссылка Тогда
		Если Параметр.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			Форма.ДокументИсправлен = Истина;
			Форма.ДокументИсправление = Параметр.Ссылка;
		Иначе
			Форма.ДокументИсправлен = Ложь;
			Форма.ДокументИсправление = Неопределено;
		КонецЕсли;
		ИсправлениеДокументовЗарплатаКадрыКлиентСервер.УстановитьПоляИсправления(Форма, Параметр.РежимИсправления, Параметр.ИмяОсновногоРеквизита, Параметр.ПолеПериодРегистрации);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОповеститьОбИсправленииДокумента(Ссылка, ИсправленныйДокумент, РежимЗаписи, РежимИсправления = "РасчетЗарплаты", ИмяОсновногоРеквизита = "Объект", ПолеПериодРегистрации = "ПериодРегистрации") Экспорт
	
	Если ЗначениеЗаполнено(ИсправленныйДокумент) Тогда
		ПараметрыИсправления = Новый Структура;
		ПараметрыИсправления.Вставить("Ссылка", Ссылка);
		ПараметрыИсправления.Вставить("РежимЗаписи", РежимЗаписи);
		ПараметрыИсправления.Вставить("РежимИсправления", РежимИсправления);
		ПараметрыИсправления.Вставить("ИмяОсновногоРеквизита", ИмяОсновногоРеквизита);
		ПараметрыИсправления.Вставить("ПолеПериодРегистрации", ПолеПериодРегистрации);
		Оповестить("ИсправленДокумент", ПараметрыИсправления, ИсправленныйДокумент);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДополнитьПоИсправляемомуДокументу(Основание, Форма, ИмяПериодРегистрации, Ссылка)
	
	Если Форма = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Основание.Вставить("ИмяПериодРегистрации", ИмяПериодРегистрации);
	Основание.Вставить("ПериодРегистрацииИсправленногоДокумента", Форма.Объект[ИмяПериодРегистрации]);
	
	Если Не(Форма.ПроведенаВыплатаЗарплаты Или Форма.ПроизведеноОтражение) Тогда
		Основание.Вставить("ДопустимоИсправлениеВТекущемПериоде", Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
