////////////////////////////////////////////////////////////////////////////////
// ПОДСИСТЕМА ОСТАТКИ ОТПУСКОВ
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Процедура ПриАктивизацииСтрокиЕжегодногоОтпуска(Форма, ФиксирующиеРеквизиты = "ДействующийОтпуск") Экспорт
	
	ДанныеТекущейСтроки = Форма.Элементы.ЕжегодныеОтпуска.ТекущиеДанные;
	Если ДанныеТекущейСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеТекущейСтроки.Действие = ПредопределенноеЗначение("Перечисление.ДействияСЕжегоднымиОтпусками.Утвердить") Или (Не СтрокаЗафиксирована(ДанныеТекущейСтроки, ФиксирующиеРеквизиты)) Тогда
		Форма.Элементы.ЕжегодныеОтпускаВидЕжегодногоОтпуска.ТолькоПросмотр = Ложь;
	Иначе
		Форма.Элементы.ЕжегодныеОтпускаВидЕжегодногоОтпуска.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	УстановитьТекущуюСтраницуКоманднойПанели(Форма, ДанныеТекущейСтроки, ФиксирующиеРеквизиты);
	
КонецПроцедуры

Процедура ПередНачаломДобавленияЕжегодногоОтпуска(Форма, Отказ) Экспорт
	
	Если Форма.Элементы.Позиции.ТекущиеДанные = Неопределено Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриНачалеРедактированияЕжегодногоОтпуска(Форма, ТекущиеДанные, НоваяСтрока) Экспорт
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Объект") Тогда
		ТекущийОбъект = Форма.Объект;
	Иначе
		ТекущийОбъект = Форма;
	КонецЕсли;
	
	Если НоваяСтрока Тогда
		ТекущиеДанные.Действие = ПредопределенноеЗначение("Перечисление.ДействияСЕжегоднымиОтпусками.Утвердить");
		Если ТекущийОбъект.ЕжегодныеОтпуска.Количество() = 1 Тогда
			ТекущиеДанные.ВидЕжегодногоОтпуска = ПредопределенноеЗначение("Справочник.ВидыОтпусков.Основной");
			ПриИзмененииВидаЕжегодногоОтпуска(Форма);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОкончанииРедактированияЕжегодногоОтпуска(ТекущиеДанные, УстановитьУтвердить) Экспорт
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОстаткиОтпусковКлиентСервер.УстановитьКомментарииДействийСЕжегоднымОтпуском(ТекущиеДанные);
	
КонецПроцедуры
 
Процедура ПередУдалениемЕжегодногоОтпуска(Форма, Отказ, ФиксирующиеРеквизиты = "ДействующийОтпуск", ЕстьТабличнаяЧастьОстатков = Ложь) Экспорт
	
	Отказ = Истина;
	
	ДанныеТекущейСтроки	= Форма.Элементы.ЕжегодныеОтпуска.ТекущиеДанные;
	Если ДанныеТекущейСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Объект") Тогда
		ТекущийОбъект = Форма.Объект;
	Иначе
		ТекущийОбъект = Форма;
	КонецЕсли;
	
	Форма.Модифицированность = Истина;
	
	Если СтрокаЗафиксирована(ДанныеТекущейСтроки, ФиксирующиеРеквизиты) Тогда 
		Если ДанныеТекущейСтроки.Действие = ПредопределенноеЗначение("Перечисление.ДействияСЕжегоднымиОтпусками.Отменить") Тогда
			ВключитьПредоставлениеОтпуска(Форма, ДанныеТекущейСтроки, ФиксирующиеРеквизиты)
		Иначе
			ВыключитьПредоставлениеОтпуска(Форма, ДанныеТекущейСтроки, ФиксирующиеРеквизиты)
		КонецЕсли;
		ОстаткиОтпусковКлиентСервер.УстановитьКомментарииДействийСЕжегоднымОтпуском(ДанныеТекущейСтроки);
	Иначе	
		Если ЕстьТабличнаяЧастьОстатков Тогда
			ОстаткиОтпусковКлиент.ОчиститьОстаткиОтпусковПоВидуОтпуска(ТекущийОбъект.ОстаткиОтпусковПоРабочимГодам, ДанныеТекущейСтроки.ВидЕжегодногоОтпуска);
		КонецЕсли;
		ТекущийОбъект.ЕжегодныеОтпуска.Удалить(ТекущийОбъект.ЕжегодныеОтпуска.Индекс(ДанныеТекущейСтроки));
	КонецЕсли;
	
	
КонецПроцедуры

Процедура ПриИзмененииВидаЕжегодногоОтпуска(Форма) Экспорт
	
	ДанныеСтроки = Форма.Элементы.ЕжегодныеОтпуска.ТекущиеДанные;
	
	Если ДанныеСтроки = Неопределено Или ДанныеСтроки.ВидЕжегодногоОтпуска.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоДнейВГод = ОстаткиОтпусковВызовСервера.КоличествоДнейОтпускаВГодПоУмолчанию(ДанныеСтроки.ВидЕжегодногоОтпуска);
	Если КоличествоДнейВГод <> Неопределено Тогда
		ДанныеСтроки.КоличествоДнейВГод = КоличествоДнейВГод;
	КонецЕсли;
	
КонецПроцедуры

Функция ОписаниеКоманднойПанелиЕжегодныхОтпусков()
	
	ОписаниеКоманднойПанели = Новый Структура("СтраницыКоманднойПанели,СтраницаДобавитьОтменить,СтраницаДобавитьПродолжить,СтраницаДобавитьУдалить");
	ОписаниеКоманднойПанели.СтраницыКоманднойПанели		= "СтраницыКоманднойПанелиЕО";
	ОписаниеКоманднойПанели.СтраницаДобавитьОтменить	= "СтраницаДобавитьОтменитьЕО";
	ОписаниеКоманднойПанели.СтраницаДобавитьПродолжить	= "СтраницаДобавитьПродолжитьЕО";
	ОписаниеКоманднойПанели.СтраницаДобавитьУдалить		= "СтраницаДобавитьУдалитьЕО";
	
	Возврат ОписаниеКоманднойПанели;
	
КонецФункции

Процедура УстановитьТекущуюСтраницуКоманднойПанели(Форма, ДанныеТекущейСтроки, ФиксирующиеРеквизиты)
	
	ОписаниеКоманднойПанели = ОписаниеКоманднойПанелиЕжегодныхОтпусков();
	
	Если ДанныеТекущейСтроки.Действие = ПредопределенноеЗначение("Перечисление.ДействияСЕжегоднымиОтпусками.Отменить") Тогда
		Форма.Элементы[ОписаниеКоманднойПанели.СтраницыКоманднойПанели].ТекущаяСтраница = Форма.Элементы[ОписаниеКоманднойПанели.СтраницаДобавитьПродолжить];
		
	Иначе
		Если СтрокаЗафиксирована(ДанныеТекущейСтроки, ФиксирующиеРеквизиты) Тогда
			Форма.Элементы[ОписаниеКоманднойПанели.СтраницыКоманднойПанели].ТекущаяСтраница = Форма.Элементы[ОписаниеКоманднойПанели.СтраницаДобавитьОтменить];
			
		Иначе
			Форма.Элементы[ОписаниеКоманднойПанели.СтраницыКоманднойПанели].ТекущаяСтраница = Форма.Элементы[ОписаниеКоманднойПанели.СтраницаДобавитьУдалить];
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#Область РаботаСФормамиВводаОстатков

Функция СуществуетГодовойОстаток(ОстаткиОтпусковПоРабочимГодам, ВидЕжегодногоОтпуска) Экспорт

	Существует = Ложь;
	
	Для каждого Остаток Из ОстаткиОтпусковПоРабочимГодам Цикл
		Если Остаток.ВидЕжегодногоОтпуска = ВидЕжегодногоОтпуска Тогда
			Существует = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла; 

	Возврат Существует;
	
КонецФункции

Процедура ОчиститьОстаткиОтпусковПоВидуОтпуска(ОстаткиОтпусковПоРабочимГодам, ВидОтпуска) Экспорт

	МассивУдаляемыхСтрок = ОстаткиОтпусковПоРабочимГодам.НайтиСтроки(Новый Структура("ВидЕжегодногоОтпуска", ВидОтпуска));
	
	Для каждого УдаляемаяСтрока Из МассивУдаляемыхСтрок Цикл
		ОстаткиОтпусковПоРабочимГодам.Удалить(УдаляемаяСтрока);
	КонецЦикла; 

КонецПроцедуры

Процедура ПриИзмененииЕжегодныеОтпускаКоличество(Форма, ТекущиеДанные, ФиксирующиеРеквизиты) Экспорт

	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.КоличествоДнейВГод > 0
		И ТекущиеДанные.Действие = ПредопределенноеЗначение("Перечисление.ДействияСЕжегоднымиОтпусками.Отменить") Тогда
	
		ВключитьПредоставлениеОтпуска(Форма, ТекущиеДанные, ФиксирующиеРеквизиты)
	
	КонецЕсли;

	ОстаткиОтпусковКлиентСервер.УстановитьКомментарииДействийСЕжегоднымОтпуском(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

Функция СтрокаЗафиксирована(ТекущаяСтрока, ФиксирующиеРеквизиты)
	
	МассивФиксирующихРеквизитов = СтрРазделить(ФиксирующиеРеквизиты, ",", Ложь);
	
	Для каждого ИмяФиксирующегоРеквизита Из МассивФиксирующихРеквизитов Цикл
		ИмяРеквизита = СтрЗаменить(ИмяФиксирующегоРеквизита, " ", "");
		Если ТекущаяСтрока[ИмяРеквизита] Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла; 
	
	Возврат Ложь;
	
КонецФункции

Функция ЭтоДействующийОтпуск(ТекущиеДанные)

	ДействующийОтпуск = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущиеДанные, "ДействующийОтпуск");
	
	Если ДействующийОтпуск Тогда
		ДействующийОтпуск = ТекущиеДанные["ДействующийОтпуск"];
	КонецЕсли;
	
	Возврат ДействующийОтпуск;

КонецФункции

Процедура ВключитьПредоставлениеОтпуска(Форма, ДанныеТекущейСтроки, ФиксирующиеРеквизиты)

	Если ЭтоДействующийОтпуск(ДанныеТекущейСтроки) Тогда
		ДанныеТекущейСтроки.Действие = ПредопределенноеЗначение("Перечисление.ДействияСЕжегоднымиОтпусками.ПустаяСсылка");
	Иначе
		ДанныеТекущейСтроки.Действие = ПредопределенноеЗначение("Перечисление.ДействияСЕжегоднымиОтпусками.Утвердить");
	КонецЕсли;
	
	УстановитьТекущуюСтраницуКоманднойПанели(Форма, ДанныеТекущейСтроки, ФиксирующиеРеквизиты);

КонецПроцедуры

Процедура ВыключитьПредоставлениеОтпуска(Форма, ДанныеТекущейСтроки, ФиксирующиеРеквизиты)
	
	ДанныеТекущейСтроки.Действие = ПредопределенноеЗначение("Перечисление.ДействияСЕжегоднымиОтпусками.Отменить");
	Если НЕ ЭтоДействующийОтпуск(ДанныеТекущейСтроки) Тогда
		ДанныеТекущейСтроки.КоличествоДнейВГод = 0;
	КонецЕсли;
	
	УстановитьТекущуюСтраницуКоманднойПанели(Форма, ДанныеТекущейСтроки, ФиксирующиеРеквизиты);
	
КонецПроцедуры

Процедура ОткрытьФормуРедактированияСтажаОтпускаСотрудника(Форма, Сотрудник, ДатаСведений, ВидОтпуска) Экспорт
	
	ВидСтажа = ОстаткиОтпусковВызовСервера.ВидСтажаОтпуска(ВидОтпуска);
	
	КадровыйУчетРасширенныйКлиент.ОткрытьФормуРедактированияСтажейСотрудника(
		Форма, Сотрудник, ДатаСведений, Новый ФиксированныйМассив(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВидСтажа)));
	
КонецПроцедуры

#КонецОбласти
