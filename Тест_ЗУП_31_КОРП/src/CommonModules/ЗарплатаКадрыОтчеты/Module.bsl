////////////////////////////////////////////////////////////////////////////////
// ЗарплатаКадрыОтчеты: Методы, используемые для работы отчетов.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Содержит настройки размещения вариантов отчетов в панели отчетов.
// Описание см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.
//
Процедура НастроитьВариантыОтчетов(Настройки) Экспорт
	
	ЗарплатаКадрыОтчетыВнутренний.НастроитьВариантыОтчетов(Настройки);
	ЗарплатаКадрыОтчетыПереопределяемый.НастроитьВариантыОтчетов(Настройки);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныйПрограммныйИнтерфейс

// Добавляет элемент отбора, предварительно проверив доступность поля отбора.
//
// Параметры:
//		Отбор - ОтборКомпоновкиДанных
//		ИмяПоля - Строка
//		ВидСравнения - Системное перечисление ВидСравненияКомпоновкиДанных.
//		ПравоеЗначение - любое значение.
//
Процедура ДобавитьЭлементОтбора(Отбор, ИмяПоля, ВидСравнения, ПравоеЗначение) Экспорт
	
	Если Отбор.ДоступныеПоляОтбора.НайтиПоле(Новый ПолеКомпоновкиДанных(ИмяПоля)) <> Неопределено Тогда
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			Отбор, ИмяПоля, ВидСравнения, ПравоеЗначение);
			
	КонецЕсли; 
	
КонецПроцедуры

Функция КлючВарианта(КомпоновщикНастроекИлиНастройки) Экспорт
	Если ТипЗнч(КомпоновщикНастроекИлиНастройки) = Тип("НастройкиКомпоновкиДанных") Тогда
		НастройкиОтчета = КомпоновщикНастроекИлиНастройки;
	Иначе
		НастройкиОтчета = КомпоновщикНастроекИлиНастройки.ПолучитьНастройки();
	КонецЕсли;
	
	ЗначениеПараметра = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("КлючВарианта"));
	Если ЗначениеПараметра <> Неопределено
		И ЗначениеПараметра.Использование
		И ЗначениеЗаполнено(ЗначениеПараметра.Значение) Тогда
		Возврат ЗначениеПараметра.Значение;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

Функция ЭтоКлючВариантаОтчетаРасчетныйЛисток(КлючВарианта) Экспорт
	
	Возврат ЗарплатаКадрыОтчетыВнутренний.ЭтоКлючВариантаОтчетаРасчетныйЛисток(КлючВарианта);
	
КонецФункции

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция СоответствиеПользовательскихПолей(НастройкиОтчета) Экспорт
	
	ЭлементыПользовательскихПолей = НастройкиОтчета.ПользовательскиеПоля.Элементы;
	
	СоответствиеПользовательскихПолей = Новый Соответствие;
	
	Для каждого Элемент Из ЭлементыПользовательскихПолей Цикл
		СоответствиеПользовательскихПолей.Вставить(Элемент.Заголовок, СтрЗаменить(Элемент.ПутьКДанным,".",""));
	КонецЦикла;
	
	Возврат СоответствиеПользовательскихПолей;
	
КонецФункции

Процедура ЗаполнитьПараметрыПользовательскихПолей(Макет, Данные, СоответствиеПользовательскихПолей, ИменаЗаполняемыхПолей = "") Экспорт
	
	ЗаполняемыеПоля = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаЗаполняемыхПолей);
	
	СтруктураДанных = Новый Структура;
	Для каждого СоответствиеПользовательскогоПоля Из СоответствиеПользовательскихПолей Цикл
		
		Если ЗаполняемыеПоля.Количество() > 0
			И ЗаполняемыеПоля.Найти(СоответствиеПользовательскогоПоля.Ключ) = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		
		СтруктураДанных.Вставить(СоответствиеПользовательскогоПоля.Значение);
		
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(СтруктураДанных, Данные);
	
	СтруктураЗначенийПользовательскихПолей = Новый Структура;
	Для каждого СоответствиеПользовательскогоПоля Из СоответствиеПользовательскихПолей Цикл
		
		Если ЗаполняемыеПоля.Количество() > 0
			И ЗаполняемыеПоля.Найти(СоответствиеПользовательскогоПоля.Ключ) = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		
		СтруктураЗначенийПользовательскихПолей.Вставить(СоответствиеПользовательскогоПоля.Ключ, СтруктураДанных[СоответствиеПользовательскогоПоля.Значение]);
		
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(Макет.Параметры, СтруктураЗначенийПользовательскихПолей);
	
КонецПроцедуры

Процедура ЗаполнитьПользовательскиеПоляВариантаОтчета(КлючВарианта, НастройкиОтчета, НаАванс = Ложь) Экспорт
	
	ЗарплатаКадрыОтчетыВнутренний.ЗаполнитьПользовательскиеПоляВариантаОтчета(КлючВарианта, НастройкиОтчета, НаАванс);
	
КонецПроцедуры

Процедура НастроитьВариантОтчетаРасчетныйЛисток(НастройкиОтчета)
	
	ЗарплатаКадрыОтчетыВнутренний.НастроитьВариантОтчетаРасчетныйЛисток(НастройкиОтчета);
	
КонецПроцедуры

Процедура ОтчетАнализНачисленийИУдержанийПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	ЗарплатаКадрыОтчетыВнутренний.ОтчетАнализНачисленийИУдержанийПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД);
	
КонецПроцедуры

Функция ОтчетРасчетныйЛисток() Экспорт
	
	ОтчетАнализНачисленийИУдержаний = Отчеты.АнализНачисленийИУдержаний.Создать();
	ОтчетАнализНачисленийИУдержаний.ИнициализироватьОтчет();
	
	ОтчетАнализНачисленийИУдержаний.КомпоновщикНастроек.ЗагрузитьНастройки(
		ОтчетАнализНачисленийИУдержаний.СхемаКомпоновкиДанных.ВариантыНастроек.РасчетныйЛисток.Настройки);
	
	МакетКомпоновкиДанных = ЗарплатаКадрыОтчеты.МакетКомпоновкиДанныхПоМетаданнымОтчета(Метаданные.Отчеты.АнализНачисленийИУдержаний, "РасчетныйЛисток");
	ОтчетАнализНачисленийИУдержаний.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("МакетКомпоновкиДанных", МакетКомпоновкиДанных);
	
	Возврат ОтчетАнализНачисленийИУдержаний;
	
КонецФункции


#Область РаботаСМакетамиКомпоновкиДанных

Функция МакетКомпоновкиДанныхПоМетаданнымОтчета(МетаданныеОтчета, КлючВарианта) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МакетКомпоновки = Неопределено;
	
	Отчет = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(МетаданныеОтчета);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Отчет", Отчет);
	Запрос.УстановитьПараметр("КлючВарианта", КлючВарианта);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МакетыКомпоновкиДанных.МакетКомпоновкиДанных,
		|	МакетыКомпоновкиДанных.ВерсияКонфигурации
		|ИЗ
		|	РегистрСведений.МакетыКомпоновкиДанных КАК МакетыКомпоновкиДанных
		|ГДЕ
		|	МакетыКомпоновкиДанных.Отчет = &Отчет
		|	И МакетыКомпоновкиДанных.КлючВарианта = &КлючВарианта";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		Если Выборка.ВерсияКонфигурации = Метаданные.Версия Тогда
			МакетКомпоновки = Выборка.МакетКомпоновкиДанных.Получить();
		КонецЕсли;
		
	КонецЕсли;
	
	Если МакетКомпоновки = Неопределено Тогда
		МакетКомпоновки = ДобавитьМакетКомпоновкиДанныхОтчета(Отчет, КлючВарианта);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат МакетКомпоновки;
	
КонецФункции

Процедура ОчиститьМакетыКомпоновкиДанных() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.МакетыКомпоновкиДанных.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ДобавитьМакетКомпоновкиДанныхОтчета(Отчет, КлючВарианта)
	
	ОтчетОбъект = Отчеты.АнализНачисленийИУдержаний.Создать();
	ОтчетОбъект.ИнициализироватьОтчет(КлючВарианта);
	
	ОтчетОбъект.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ФормированиеМакетаКомпоновкиДанных", Истина);
	ОтчетОбъект.СкомпоноватьРезультат(Новый ТабличныйДокумент);
	
	МакетКомпоновкиДанных = ОтчетОбъект.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.СкомпонованныйМакетКомпоновкиДанных;
	ЗапомнитьМакетКомпоновкиДанных(Отчет, КлючВарианта, МакетКомпоновкиДанных);
	
	Возврат МакетКомпоновкиДанных;
	
КонецФункции

Процедура ЗапомнитьМакетКомпоновкиДанных(Отчет, КлючВарианта, МакетКомпоновкиДанных)
	
	НаборЗаписей = РегистрыСведений.МакетыКомпоновкиДанных.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Отчет.Установить(Отчет);
	НаборЗаписей.Отбор.КлючВарианта.Установить(КлючВарианта);
	
	Запись = НаборЗаписей.Добавить();
	Запись.Отчет = Отчет;
	Запись.КлючВарианта = КлючВарианта;
	Запись.МакетКомпоновкиДанных = Новый ХранилищеЗначения(МакетКомпоновкиДанных);
	Запись.ВерсияКонфигурации = Метаданные.Версия;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти


#Область МетодыПодсистемыВариантыОтчетов

// Возвращает список полей группировок всех группировок компоновщика настроек.
//
// Параметры: 
//		КомпоновщикНастроек - компоновщик настроек.
//		БезПользовательскихПолей - признак не включения пользовательских настроек СКД.
//
Функция ПолучитьПоляГруппировок(КомпоновщикНастроек, БезПользовательскихПолей = Ложь) Экспорт
	
	СписокПолей = Новый СписокЗначений;
	
	Структура = КомпоновщикНастроек.Настройки.Структура;
	ДобавитьПоляГруппировки(Структура, СписокПолей, БезПользовательскихПолей);
	Возврат СписокПолей;
	
КонецФункции

// Добавляет вложенные поля группировки.
Процедура ДобавитьПоляГруппировки(Структура, СписокПолей, БезПользовательскихПолей)
	
	Для каждого ЭлементСтруктуры Из Структура Цикл
		Если ТипЗнч(ЭлементСтруктуры) = Тип("ТаблицаКомпоновкиДанных") Тогда
			ДобавитьПоляГруппировки(ЭлементСтруктуры.Строки, СписокПолей, БезПользовательскихПолей);
			ДобавитьПоляГруппировки(ЭлементСтруктуры.Колонки, СписокПолей, БезПользовательскихПолей);
		ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("ДиаграммаКомпоновкиДанных") Тогда
			ДобавитьПоляГруппировки(ЭлементСтруктуры.Серии, СписокПолей, БезПользовательскихПолей);
			ДобавитьПоляГруппировки(ЭлементСтруктуры.Точки, СписокПолей, БезПользовательскихПолей);
		Иначе
			Для каждого ТекущееПолеГруппировки Из ЭлементСтруктуры.ПоляГруппировки.Элементы Цикл
				ДоступноеПоле = ЭлементСтруктуры.Выбор.ДоступныеПоляВыбора.НайтиПоле(ТекущееПолеГруппировки.Поле);
				Если ДоступноеПоле <> Неопределено 
				  И (ДоступноеПоле.Родитель = Неопределено ИЛИ Не БезПользовательскихПолей ИЛИ ДоступноеПоле.Родитель.Поле <> Новый ПолеКомпоновкиДанных("UserFields")) Тогда
					СписокПолей.Добавить(Строка(ДоступноеПоле.Поле), ДоступноеПоле.Заголовок);
				КонецЕсли;
			КонецЦикла;
			ДобавитьПоляГруппировки(ЭлементСтруктуры.Структура, СписокПолей, БезПользовательскихПолей);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает последний элемент структуры - группировку.
//
// Параметры:
//		ЭлементСтруктурыНастроек - элемент структуры компоновки данных.
//		Строки - признак для получения последний группировки строк (Серий) или колонок (точек).
//
Функция ПолучитьПоследнийЭлементСтруктуры(ЭлементСтруктурыНастроек, Строки = Истина) Экспорт
	
	Если ТипЗнч(ЭлементСтруктурыНастроек) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		Настройки = ЭлементСтруктурыНастроек.Настройки;
	ИначеЕсли ТипЗнч(ЭлементСтруктурыНастроек) = Тип("НастройкиКомпоновкиДанных") Тогда
		Настройки = ЭлементСтруктурыНастроек;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Структура = Настройки.Структура;
	Если Структура.Количество() = 0 Тогда
		Возврат Настройки;
	КонецЕсли;
	
	Если Строки Тогда
		ИмяСтруктурыТаблицы = "Строки";
		ИмяСтруктурыДиаграммы = "Серии";
	Иначе
		ИмяСтруктурыТаблицы = "Колонки";
		ИмяСтруктурыДиаграммы = "Точки";
	КонецЕсли;
	
	Пока Истина Цикл
		ЭлементСтруктуры = Структура[0];
		Если ТипЗнч(ЭлементСтруктуры) = Тип("ТаблицаКомпоновкиДанных") И ЭлементСтруктуры[ИмяСтруктурыТаблицы].Количество() > 0 Тогда
			Если ЭлементСтруктуры[ИмяСтруктурыТаблицы][0].Структура.Количество() = 0 Тогда
				Структура = ЭлементСтруктуры[ИмяСтруктурыТаблицы];
				Прервать;
			КонецЕсли;
			Структура = ЭлементСтруктуры[ИмяСтруктурыТаблицы][0].Структура;
		ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("ДиаграммаКомпоновкиДанных") И ЭлементСтруктуры[ИмяСтруктурыДиаграммы].Количество() > 0 Тогда
			Если ЭлементСтруктуры[ИмяСтруктурыДиаграммы][0].Структура.Количество() = 0 Тогда
				Структура = ЭлементСтруктуры[ИмяСтруктурыДиаграммы];
				Прервать;
			КонецЕсли;
			Структура = ЭлементСтруктуры[ИмяСтруктурыДиаграммы][0].Структура;
		ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаКомпоновкиДанных")
			  ИЛИ ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаТаблицыКомпоновкиДанных")
			  ИЛИ ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
			Если ЭлементСтруктуры.Структура.Количество() = 0 Тогда
				Прервать;
			КонецЕсли;
			Структура = ЭлементСтруктуры.Структура;
		ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("ТаблицаКомпоновкиДанных") Тогда
			Возврат ЭлементСтруктуры[ИмяСтруктурыТаблицы];
		ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("ДиаграммаКомпоновкиДанных")	Тогда
			Возврат ЭлементСтруктуры[ИмяСтруктурыДиаграммы];
		Иначе
			Возврат ЭлементСтруктуры;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Структура[0];
	
КонецФункции

#КонецОбласти


#Область Т49

Процедура ПриКомпоновкеРезультатаТ49(ОбъектОтчета, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка, НаАванс = Ложь) Экспорт
	
	Попытка
		
		КомпоновщикНастроек = ОбъектОтчета.КомпоновщикНастроек;
		СхемаКомпоновкиДанных = ОбъектОтчета.СхемаКомпоновкиДанных;
		
		КлючВарианта = КлючВарианта(КомпоновщикНастроек);
		
		// Параметры документа
		ДокументРезультат.ТолькоПросмотр = Истина;
		ДокументРезультат.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_УнифицированнаяФормаТ49";
		ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		ДокументРезультат.АвтоМасштаб = Истина;
		
		КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
		
		НастройкиОтчета = ОбъектОтчета.КомпоновщикНастроек.ПолучитьНастройки();
		
		ЗаполнитьПользовательскиеПоляВариантаОтчета(КлючВарианта, НастройкиОтчета, НаАванс);
		ДополнительныеНачисления = УчетНачисленнойЗарплаты.ДополнительныеНачисленияОтчетаАнализНачисленийИУдержанийТ49();
		УчетНачисленнойЗарплаты.ДобавитьПользовательскиеПоляДополнительныхНачисленийИУдержаний(ДополнительныеНачисления, НастройкиОтчета, 5);
		ДополнительныеУдержания = УчетНачисленнойЗарплаты.ДополнительныеУдержанияОтчетаАнализНачисленийИУдержанийТ49();
		УчетНачисленнойЗарплаты.ДобавитьПользовательскиеПоляДополнительныхНачисленийИУдержаний(ДополнительныеУдержания, НастройкиОтчета, 3, "Удержания");
		
		МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных,
			НастройкиОтчета, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
		
		СоответствиеПользовательскихПолей = СоответствиеПользовательскихПолей(НастройкиОтчета);
		
		Если КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("ДанныеДокумента") Тогда
			НаборыВнешнихДанных = КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.ДанныеДокумента;
		Иначе
			НаборыВнешнихДанных = НаборыВнешнихДанныхАнализНачисленийИУдержаний();
		КонецЕсли;
		
		ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
		ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных, НаборыВнешнихДанных, , Истина);
		
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
		ДанныеОтчета = Новый ДеревоЗначений;
		ПроцессорВывода.УстановитьОбъект(ДанныеОтчета);
		ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
		
		ПорядокДопНачислений = УчетНачисленнойЗарплаты.ПорядокДополнительныхНачислений(ДополнительныеНачисления, ДанныеОтчета, СоответствиеПользовательскихПолей, 8);
		ПорядокДопУдержаний = УчетНачисленнойЗарплаты.ПорядокДополнительныхУдержаний(ДополнительныеУдержания, ДанныеОтчета, СоответствиеПользовательскихПолей, 15);
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_Т49");
		
		Макеты = Новый Структура("ШапкаДокумента,Шапка,Строка,ПустаяСтрока,Подвал,ИтогоПоСтранице,ИтогоПоВедомости");
		
		Если НаАванс Тогда
			Макеты.ШапкаДокумента = Макет.ПолучитьОбласть("ШапкаДокументаПерваяПоловина");
		Иначе
			Макеты.ШапкаДокумента = Макет.ПолучитьОбласть("ШапкаДокумента");
		КонецЕсли;
		
		Макеты.Шапка = Макет.ПолучитьОбласть("Шапка");
		Макеты.Строка = Макет.ПолучитьОбласть("Строка");
		Макеты.ПустаяСтрока = Макет.ПолучитьОбласть("Строка");
		Макеты.Подвал = Макет.ПолучитьОбласть("Подвал");
		Макеты.ИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
		Макеты.ИтогоПоВедомости = Макет.ПолучитьОбласть("ИтогоПоВедомости");
		
		ДанныеОтчета.Колонки.Добавить("НомерСтроки", ОбщегоНазначения.ОписаниеТипаЧисло(18,0));
		Для каждого СтрокаМесяца Из ДанныеОтчета.Строки Цикл
			Для Каждого СтрокаОрганизации Из СтрокаМесяца.Строки Цикл
				ВывестиОрганизациюТ49(ОбъектОтчета, СтрокаОрганизации, ДокументРезультат, Макеты, СоответствиеПользовательскихПолей, ПорядокДопНачислений, ПорядокДопУдержаний);
			КонецЦикла;
		КонецЦикла;
		
		ДопСвойства = КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства;
		ДопСвойства.Вставить("ОтчетПустой", ДанныеОтчета.Строки.Количество() = 0);
		
		СтандартнаяОбработка = Ложь;
		
	Исключение
		
		Инфо = ИнформацияОбОшибке();
		ВызватьИсключение НСтр("ru = 'В настройку отчета Т-49 внесены критичные изменения. Отчет не будет сформирован.'")
			+ " " + КраткоеПредставлениеОшибки(Инфо);
		
	КонецПопытки;
	
КонецПроцедуры

Процедура ВывестиОрганизациюТ49(ОбъектОтчета, СтрокаОрганизации, ДокументРезультат, Макеты, СоответствиеПользовательскихПолей, ПорядокДопНачислений, ПорядокДопУдержаний)
	
	КомпоновщикНастроек = ОбъектОтчета.КомпоновщикНастроек;
	
	НастройкиПечатныхФорм = ЗарплатаКадрыПовтИсп.НастройкиПечатныхФорм();
	
	Если ДокументРезультат.ВысотаТаблицы > 0 Тогда
		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;
	
	КВыплатеПоВедомости = 0;
	
	Макеты.ШапкаДокумента.Параметры.Заполнить(СтрокаОрганизации);
	Макеты.ШапкаДокумента.Параметры.ДатаД = Формат(ТекущаяДатаСеанса(), "ДЛФ=D");
	Макеты.Шапка.Параметры.Заполнить(СтрокаОрганизации);
	
	Документ = Неопределено;
	КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("Документ", Документ);
	Если Документ <> Неопределено Тогда
		
		ДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Документ,
			"Номер, Дата, Руководитель, ДолжностьРуководителя, ГлавныйБухгалтер, Кассир, ДолжностьКассира, Бухгалтер");
		
	Иначе
		ДанныеДокумента = Новый Структура("Организация,Руководитель,ДолжностьРуководителя,ГлавныйБухгалтер,Кассир,ДолжностьКассира", СтрокаОрганизации.Организация);
		ЗарплатаКадры.ПолучитьЗначенияПоУмолчанию(ДанныеДокумента, КонецМесяца(СтрокаОрганизации.МесяцНачисления));
		ДанныеДокумента.Удалить("Организация");
		ДанныеДокумента.Вставить("Номер", "");
		ДанныеДокумента.Вставить("Дата", "");
		ДанныеДокумента.Вставить("Бухгалтер", Справочники.ФизическиеЛица.ПустаяСсылка());
		
	КонецЕсли;
	
	СписокОтветственных = Новый Массив;
	Если ЗначениеЗаполнено(ДанныеДокумента.Руководитель) Тогда
		СписокОтветственных.Добавить(ДанныеДокумента.Руководитель);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеДокумента.ГлавныйБухгалтер) Тогда
		СписокОтветственных.Добавить(ДанныеДокумента.ГлавныйБухгалтер);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеДокумента.Кассир) Тогда
		СписокОтветственных.Добавить(ДанныеДокумента.Кассир);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеДокумента.Бухгалтер) Тогда
		СписокОтветственных.Добавить(ДанныеДокумента.Бухгалтер);
	КонецЕсли;
	
	РасшифровкиПодписей = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, СписокОтветственных,"ИОФамилия", КонецМесяца(СтрокаОрганизации.МесяцНачисления));
	
	Для каждого СтрокаТаблицы Из ПорядокДопНачислений Цикл
		Макеты.Шапка.Параметры["Колонка" + СтрокаТаблицы.НомерКолонки] = СтрокаТаблицы.Заголовок;
	КонецЦикла;
	
	Для каждого СтрокаТаблицы Из ПорядокДопУдержаний Цикл
		Макеты.Шапка.Параметры["Колонка" + СтрокаТаблицы.НомерКолонки] = СтрокаТаблицы.Заголовок;
	КонецЦикла;
	
	Макеты.ШапкаДокумента.Параметры.НомерД = ДанныеДокумента.Номер;
	Макеты.ШапкаДокумента.Параметры.ДатаД = ДанныеДокумента.Дата;
	
	Макеты.ШапкаДокумента.Параметры.ДолжностьРуководителя = ДанныеДокумента.ДолжностьРуководителя;
	ДанныеРуководителя = РасшифровкиПодписей.Найти(ДанныеДокумента.Руководитель, "ФизическоеЛицо");
	Если ДанныеРуководителя <> Неопределено Тогда
		Макеты.ШапкаДокумента.Параметры.РуководительРасшифровкаПодписи = ДанныеРуководителя.ИОФамилия;
	КонецЕсли;
	
	ДанныеГлавногоБухгалтера = РасшифровкиПодписей.Найти(ДанныеДокумента.ГлавныйБухгалтер, "ФизическоеЛицо");
	Если ДанныеГлавногоБухгалтера <> Неопределено Тогда
		Макеты.ШапкаДокумента.Параметры.ГлавныйБухгалтерРасшифровкаПодписи = ДанныеГлавногоБухгалтера.ИОФамилия;
	КонецЕсли;
	
	ЗаполнитьПараметрыПользовательскихПолей(Макеты.ШапкаДокумента, СтрокаОрганизации, СоответствиеПользовательскихПолей);
	
	ДанныеВедомости = Неопределено;
	КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("ДанныеВедомости", ДанныеВедомости);
	Если ДанныеВедомости = Неопределено Тогда
		КВыплате = СтрокаОрганизации[СоответствиеПользовательскихПолей.Получить("КВыплате")];
	Иначе
		
		КВыплате = ДанныеВедомости.Итог("КВыплате");
		СтрокаОрганизации[СоответствиеПользовательскихПолей.Получить("КВыплате")] = КВыплате;
		
		// Упорядочим строки как в документе
		Для Каждого СтрокаГоловногоСотрудника Из СтрокаОрганизации.Строки Цикл
			
			Для каждого СтрокаСотрудника Из СтрокаГоловногоСотрудника.Строки Цикл
				
				СтрокаДанныхВедомости = ДанныеВедомости.Найти(СтрокаСотрудника.Сотрудник, "Сотрудник");
				Если СтрокаДанныхВедомости <> Неопределено Тогда
					
					СтрокаСотрудника.НомерСтроки = СтрокаДанныхВедомости.НомерСтроки;
					Если СтрокаГоловногоСотрудника.НомерСтроки < СтрокаСотрудника.НомерСтроки Тогда
						СтрокаГоловногоСотрудника.НомерСтроки = СтрокаСотрудника.НомерСтроки;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
		СтрокаОрганизации.Строки.Сортировать("НомерСтроки", Истина);
		
	КонецЕсли;
	
	Если КВыплате > 0 Тогда
		
		ВалютаУчета = ЗарплатаКадры.ВалютаУчетаЗаработнойПлаты();
		Макеты.ШапкаДокумента.Параметры.СуммаВсегоПрописью = РаботаСКурсамиВалют.СформироватьСуммуПрописью(КВыплате, ВалютаУчета);
		Макеты.ШапкаДокумента.Параметры.СуммаДокРублей = Цел(КВыплате);
		Макеты.ШапкаДокумента.Параметры.СуммаДокКопеек = (КВыплате - Цел(КВыплате)) * 100;
		
	КонецЕсли;
	
	ПодразделениеВШапке = Неопределено;
	КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("ПодразделениеВШапке", ПодразделениеВШапке);
	Если ПодразделениеВШапке <> Неопределено Тогда
		Макеты.ШапкаДокумента.Параметры.ПодразделениеНаКонецПериода = ПодразделениеВШапке;
	КонецЕсли;
	
	Если НастройкиПечатныхФорм.ВыводитьПолнуюИерархиюПодразделений И ЗначениеЗаполнено(Макеты.ШапкаДокумента.Параметры.ПодразделениеНаКонецПериода) Тогда
		Макеты.ШапкаДокумента.Параметры.ПодразделениеНаКонецПериода = Макеты.ШапкаДокумента.Параметры.ПодразделениеНаКонецПериода.ПолноеНаименование();
	КонецЕсли;
	
	ДокументРезультат.Вывести(Макеты.ШапкаДокумента);
	
	ЗаполнитьПараметрыПользовательскихПолей(Макеты.Шапка, СтрокаОрганизации, СоответствиеПользовательскихПолей);
	ДокументРезультат.Вывести(Макеты.Шапка);
	
	НомерСтроки = 0;
	ПромежуточныеИтоги = ПромежуточныеИтогиПоСтраницеТ49(СоответствиеПользовательскихПолей, ПорядокДопНачислений, ПорядокДопУдержаний);
	
	Для Каждого СтрокаСотрудника Из СтрокаОрганизации.Строки Цикл
		ВывестиСотрудникаТ49(НомерСтроки, СтрокаСотрудника, ДокументРезультат, Макеты, СоответствиеПользовательскихПолей, ПромежуточныеИтоги, ДанныеВедомости, КВыплатеПоВедомости, ПорядокДопНачислений, ПорядокДопУдержаний);
	КонецЦикла;
	
	ДополнитьСтраницуТ49(НомерСтроки, Макеты, ДокументРезультат, ПромежуточныеИтоги);
	Макеты.ИтогоПоВедомости.Параметры.Заполнить(СтрокаОрганизации);
	
	Для каждого СтрокаТаблицы Из ПорядокДопНачислений Цикл
		СуммаЯчейки = СтрокаОрганизации[СоответствиеПользовательскихПолей.Получить(СтрокаТаблицы.Имя)];
		Макеты.ИтогоПоВедомости.Параметры["Колонка" + СтрокаТаблицы.НомерКолонки] = СуммаЯчейки;
	КонецЦикла;
	
	Для каждого СтрокаТаблицы Из ПорядокДопУдержаний Цикл
		СуммаЯчейки = СтрокаОрганизации[СоответствиеПользовательскихПолей.Получить(СтрокаТаблицы.Имя)];
		Макеты.ИтогоПоВедомости.Параметры["Колонка" + СтрокаТаблицы.НомерКолонки] = СуммаЯчейки;
	КонецЦикла;
	
	ЗаполнитьПараметрыПользовательскихПолей(Макеты.ИтогоПоВедомости, СтрокаОрганизации, СоответствиеПользовательскихПолей);
	Макеты.Подвал.Параметры.Заполнить(СтрокаОрганизации);
	
	Если Документ <> Неопределено Тогда
		
		Макеты.Подвал.Параметры.ДолжностьКассира = ДанныеДокумента.ДолжностьКассира;
		ДанныеКассира = РасшифровкиПодписей.Найти(ДанныеДокумента.Кассир, "ФизическоеЛицо");
		Если ДанныеКассира <> Неопределено Тогда
			Макеты.Подвал.Параметры.ФИОКассира = ДанныеКассира.ИОФамилия;
		КонецЕсли;
		
		ДанныеБухгалтера = РасшифровкиПодписей.Найти(ДанныеДокумента.Бухгалтер, "ФизическоеЛицо");
		Если ДанныеБухгалтера <> Неопределено Тогда
			Макеты.Подвал.Параметры.ФИОБухгалтера = ДанныеБухгалтера.ИОФамилия;
		КонецЕсли;
		
	КонецЕсли;
	
	Макеты.ИтогоПоВедомости.Параметры.КВыплате = КВыплатеПоВедомости;
	ДокументРезультат.Вывести(Макеты.ИтогоПоВедомости);
	ДокументРезультат.Вывести(Макеты.Подвал);
	
КонецПроцедуры

Процедура ВывестиСотрудникаТ49(НомерСтроки, СтрокаГоловногоСотрудника, ДокументРезультат, Макеты, СоответствиеПользовательскихПолей, ПромежуточныеИтоги, ДанныеВедомости, КВыплатеПоВедомости, ПорядокДопНачислений, ПорядокДопУдержаний)
	
	НастройкиПечатныхФорм = ЗарплатаКадрыПовтИсп.НастройкиПечатныхФорм();
	НомерСтроки = НомерСтроки + 1;
	
	МакетСотрудника = Новый ТабличныйДокумент;
	ПромежуточныеИтогиПоГоловномуСотруднику = ПромежуточныеИтогиПоСтраницеТ49(СоответствиеПользовательскихПолей, ПорядокДопНачислений, ПорядокДопУдержаний);
	НесколькоРабочихМест = СтрокаГоловногоСотрудника.Строки.Количество() > 1;
	
	Для каждого СтрокаСотрудника Из СтрокаГоловногоСотрудника.Строки Цикл
		
		Если ДанныеВедомости <> Неопределено Тогда
			
			СтрокаДанныхВедомости = ДанныеВедомости.Найти(СтрокаСотрудника.Сотрудник, "Сотрудник");
			Если СтрокаДанныхВедомости <> Неопределено Тогда
				СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить("КВыплате")] = СтрокаДанныхВедомости.КВыплате;
			КонецЕсли;
			
		КонецЕсли;
		
		Макеты.Строка.Параметры.Заполнить(СтрокаСотрудника);
		
		Если НЕ НастройкиПечатныхФорм.ВыводитьПолныеФИОВСписочныхПечатныхФормах Тогда
			
			ФИО = Новый Структура("Фамилия, Имя, Отчество");
			ФИО.Фамилия = СтрокаСотрудника.Фамилия;
			ФИО.Имя = СтрокаСотрудника.Имя;
			ФИО.Отчество = СтрокаСотрудника.Отчество;
			Макеты.Строка.Параметры.ФизическоеЛицоФИО = ФизическиеЛицаЗарплатаКадрыКлиентСервер.ФамилияИнициалы(ФИО);
			
		КонецЕсли;
		
		ЗаполнитьПараметрыПользовательскихПолей(Макеты.Строка, СтрокаСотрудника, СоответствиеПользовательскихПолей);
		Для каждого СтрокаТаблицы Из ПорядокДопНачислений Цикл
			
			СуммаЯчейки = СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить(СтрокаТаблицы.Имя)];
			Макеты.Строка.Параметры["Колонка" + СтрокаТаблицы.НомерКолонки] = СуммаЯчейки;
			
		КонецЦикла;
		
		Для каждого СтрокаТаблицы Из ПорядокДопУдержаний Цикл
			
			СуммаЯчейки = СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить(СтрокаТаблицы.Имя)];
			Макеты.Строка.Параметры["Колонка" + СтрокаТаблицы.НомерКолонки] = СуммаЯчейки;
			
		КонецЦикла;
		
		ОтработаноРабочихДней = СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить("ОтработаноРабочихДней")];
		ОтработаноРабочихЧасов = СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить("ОтработаноРабочихЧасов")];
		ОтработаноПраздничныхДней = СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить("ОтработаноПраздничныхДней")];
		ОтработаноПраздничныхЧасов = СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить("ОтработаноПраздничныхЧасов")];
		ОтработаноВыходныхДней = СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить("ОтработаноВыходныхДней")];
		ОтработаноВыходныхЧасов = СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить("ОтработаноВыходныхЧасов")];
		
		Если ЗначениеЗаполнено(ОтработаноРабочихДней) И ОтработаноРабочихДней > 0 Тогда 
			Макеты.Строка.Параметры.ОтработаноРабочихДней = "" + ОтработаноРабочихДней + " (" + ОтработаноРабочихЧасов + ")";
		Иначе
			Макеты.Строка.Параметры.ОтработаноРабочихДней = "";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОтработаноПраздничныхДней) И ОтработаноПраздничныхДней > 0 Тогда 
			Макеты.Строка.Параметры.ОтработаноПраздничныхДней = "" + ОтработаноПраздничныхДней + " (" + ОтработаноПраздничныхЧасов + ")";
		Иначе
			Макеты.Строка.Параметры.ОтработаноПраздничныхДней = "";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОтработаноВыходныхДней) И ОтработаноВыходныхДней > 0 Тогда 
			Макеты.Строка.Параметры.ОтработаноВыходныхДней = "" + ОтработаноВыходныхДней + " (" + ОтработаноВыходныхЧасов + ")";
		Иначе
			Макеты.Строка.Параметры.ОтработаноВыходныхДней = "";
		КонецЕсли;
		
		Макеты.Строка.Параметры.НомерПП = НомерСтроки;
		
		Если Макеты.Строка.Параметры.КВыплате < 0 Тогда
			Макеты.Строка.Параметры.КВыплате = 0;
		КонецЕсли;
		
		ДобавитьВПромежуточныйИтог(ПромежуточныеИтогиПоГоловномуСотруднику, Макеты.Строка.Параметры);
		КВыплатеПоВедомости = КВыплатеПоВедомости + Макеты.Строка.Параметры.КВыплате;
		
		Если НесколькоРабочихМест Тогда
			
			ЗаполнитьПараметрыПользовательскихПолей(
				Макеты.Строка, СтрокаГоловногоСотрудника, СоответствиеПользовательскихПолей,
				"ВсегоНачислено,ДолгЗаОрганизацией,ДолгЗаСотрудником,КВыплате");
			
			Для каждого СтрокаТаблицы Из ПорядокДопУдержаний Цикл
				
				СуммаЯчейки = СтрокаГоловногоСотрудника[СоответствиеПользовательскихПолей.Получить(СтрокаТаблицы.Имя)];
				Макеты.Строка.Параметры["Колонка" + СтрокаТаблицы.НомерКолонки] = СуммаЯчейки;
				
			КонецЦикла;
			
		КонецЕсли;
			
		Если  СтрокаСотрудника.Сотрудник <> СтрокаСотрудника.ГоловнойСотрудник Тогда
			Макеты.Строка.Параметры.ДолжностьНаКонецПериодаНаименованиеКраткое = СтрокаСотрудника.СотрудникУточнениеНаименования;
		КонецЕсли; 
		
		МакетСотрудника.Вывести(Макеты.Строка);
		
	КонецЦикла;
	
	Если Не ЕстьИтогиПоГоловномуСотруднику(ПромежуточныеИтогиПоГоловномуСотруднику) Тогда
		
		НомерСтроки = НомерСтроки - 1;
		Возврат;
		
	КонецЕсли;
	
	Если МакетСотрудника.ВысотаТаблицы > 1 Тогда
		
		// Номер по порядку
		МакетСотрудника.Область(1, 1, МакетСотрудника.ВысотаТаблицы, 1).Объединить();
		// Табельный номер
		МакетСотрудника.Область(1, 2, МакетСотрудника.ВысотаТаблицы, 2).Объединить();
		// Всего начислено
		МакетСотрудника.Область(1, 19, МакетСотрудника.ВысотаТаблицы, 19).Объединить();
		// Удержания
		МакетСотрудника.Область(1, 20, МакетСотрудника.ВысотаТаблицы, 20).Объединить();
		МакетСотрудника.Область(1, 21, МакетСотрудника.ВысотаТаблицы, 21).Объединить();
		МакетСотрудника.Область(1, 22, МакетСотрудника.ВысотаТаблицы, 22).Объединить();
		МакетСотрудника.Область(1, 23, МакетСотрудника.ВысотаТаблицы, 23).Объединить();
		// Задолженность за организацией.
		МакетСотрудника.Область(1, 24, МакетСотрудника.ВысотаТаблицы, 24).Объединить();
		// Задолженность за работником.
		МакетСотрудника.Область(1, 25, МакетСотрудника.ВысотаТаблицы, 26).Объединить();
		// К выплате
		МакетСотрудника.Область(1, 27, МакетСотрудника.ВысотаТаблицы, 28).Объединить();
		// ФИО
		МакетСотрудника.Область(1, 29, МакетСотрудника.ВысотаТаблицы, 29).Объединить();
		// Подпись
		МакетСотрудника.Область(1, 30, МакетСотрудника.ВысотаТаблицы, 30).Объединить();
		
	КонецЕсли;
	
	МассивВыводимыхОбластей = Новый Массив;
	МассивВыводимыхОбластей.Добавить(МакетСотрудника);
	МассивВыводимыхОбластей.Добавить(Макеты.ИтогоПоСтранице);
	МассивВыводимыхОбластей.Добавить(Макеты.ИтогоПоВедомости);
	МассивВыводимыхОбластей.Добавить(Макеты.Подвал);
	
	Если НЕ ДокументРезультат.ПроверитьВывод(МассивВыводимыхОбластей) Тогда
		
		ЗаполнитьЗначенияСвойств(Макеты.ИтогоПоСтранице.Параметры, ПромежуточныеИтоги);
		ДокументРезультат.Вывести(Макеты.ИтогоПоСтранице);
		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		ЗаполнитьПараметрыПользовательскихПолей(Макеты.Шапка, СтрокаСотрудника, СоответствиеПользовательскихПолей);
		ДокументРезультат.Вывести(Макеты.Шапка);
		ПромежуточныеИтоги = ПромежуточныеИтогиПоСтраницеТ49(СоответствиеПользовательскихПолей, ПорядокДопНачислений, ПорядокДопУдержаний);
		
	КонецЕсли;
	
	ДобавитьВПромежуточныйИтог(ПромежуточныеИтоги, ПромежуточныеИтогиПоГоловномуСотруднику);
	
	ДокументРезультат.Вывести(МакетСотрудника);
	
КонецПроцедуры

Процедура ДополнитьСтраницуТ49(НомерСтроки, Макеты, ДокументРезультат, ПромежуточныеИтоги)
	
	МассивВыводимыхОбластей = Новый Массив;
	МассивВыводимыхОбластей.Добавить(Макеты.ПустаяСтрока);
	МассивВыводимыхОбластей.Добавить(Макеты.ИтогоПоСтранице);
	МассивВыводимыхОбластей.Добавить(Макеты.ИтогоПоВедомости);
	МассивВыводимыхОбластей.Добавить(Макеты.Подвал);
	
	Пока ДокументРезультат.ПроверитьВывод(МассивВыводимыхОбластей) Цикл
		ДокументРезультат.Вывести(Макеты.ПустаяСтрока);
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(Макеты.ИтогоПоСтранице.Параметры, ПромежуточныеИтоги);
	ДокументРезультат.Вывести(Макеты.ИтогоПоСтранице);
	
КонецПроцедуры

Функция ПромежуточныеИтогиПоСтраницеТ49(СоответствиеПользовательскихПолей, ПорядокДопНачислений, ПорядокДопУдержаний)
	
	ПромежуточныеИтоги = Новый Структура;
	
	Для каждого СтрокаТаблицы Из ПорядокДопНачислений Цикл
		ПромежуточныеИтоги.Вставить("Колонка" + СтрокаТаблицы.НомерКолонки, 0);
	КонецЦикла;
	
	Для каждого СтрокаТаблицы Из ПорядокДопУдержаний Цикл
		ПромежуточныеИтоги.Вставить("Колонка" + СтрокаТаблицы.НомерКолонки, 0);
	КонецЦикла;
	
	ПромежуточныеИтоги.Вставить("ВсегоНачислено", 0);
	ПромежуточныеИтоги.Вставить("ДолгЗаОрганизацией", 0);
	ПромежуточныеИтоги.Вставить("ДолгЗаСотрудником", 0);
	ПромежуточныеИтоги.Вставить("КВыплате", 0);
	
	Возврат ПромежуточныеИтоги;
	
КонецФункции

#КонецОбласти


#Область Т51

Процедура ПриКомпоновкеРезультатаТ51(ОбъектОтчета, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка, НаАванс = Ложь) Экспорт
	
	Попытка
		
		КомпоновщикНастроек = ОбъектОтчета.КомпоновщикНастроек;
		СхемаКомпоновкиДанных = ОбъектОтчета.СхемаКомпоновкиДанных;
		
		КлючВарианта = КлючВарианта(КомпоновщикНастроек);
		
		// Параметры документа
		ДокументРезультат.ТолькоПросмотр = Истина;
		ДокументРезультат.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_УнифицированнаяФормаТ51";
		ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		ДокументРезультат.АвтоМасштаб = Истина;
		
		КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
		
		НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
		
		// Нужно проверить включена ли группировка по подразделениям.
		ЕстьГруппировкаПоПодразделению = Ложь;
		ПараметрГруппировки = Новый ПараметрКомпоновкиДанных("РазбиватьПоПодразделениям");
		ЕстьГруппировкаПоПодразделению = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрГруппировки).Значение;
		
		Если Не ЕстьГруппировкаПоПодразделению Тогда
			
			Для каждого ЭлементСортировки Из НастройкиОтчета.Порядок.Элементы Цикл
				
				Если ЭлементСортировки.Поле = Новый ПолеКомпоновкиДанных("Подразделение.РеквизитДопУпорядочивания") Тогда
					ЭлементСортировки.Использование = Ложь;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		ИзменитьГруппировкиВариантаТ49Т51(НастройкиОтчета.Структура, ЕстьГруппировкаПоПодразделению);
		
		ЗаполнитьПользовательскиеПоляВариантаОтчета(КлючВарианта, НастройкиОтчета, НаАванс);
		
		МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных,
			НастройкиОтчета, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
		
		СоответствиеПользовательскихПолей = СоответствиеПользовательскихПолей(НастройкиОтчета);
		
		Если КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("ДанныеДокумента") Тогда
			НаборыВнешнихДанных = КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.ДанныеДокумента;
		Иначе
			НаборыВнешнихДанных = НаборыВнешнихДанныхАнализНачисленийИУдержаний();
		КонецЕсли;
		
		ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
		ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных, НаборыВнешнихДанных, , Истина);
		
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
		ДанныеОтчета = Новый ДеревоЗначений;
		ПроцессорВывода.УстановитьОбъект(ДанныеОтчета);
		ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
		
		Если КлючВарианта = "Форма0504402"
			Или КлючВарианта = "Форма0504402ПерваяПоловинаМесяца" Тогда
			ИмяМакета = "ОбщийМакет.ПФ_MXL_0504402";
		Иначе
			ИмяМакета = "ОбщийМакет.ПФ_MXL_Т51";
		КонецЕсли;
		Макет = УправлениеПечатью.МакетПечатнойФормы(ИмяМакета);
		
		Макеты = Новый Структура("ШапкаДокумента,Шапка,Строка,ПустаяСтрока,Подвал,ИтогоПоСтранице,ИтогоПоВедомости");
		
		Если НаАванс Тогда
			Макеты.ШапкаДокумента = Макет.ПолучитьОбласть("ШапкаДокументаПерваяПоловина");
		Иначе
			Макеты.ШапкаДокумента = Макет.ПолучитьОбласть("ШапкаДокумента");
		КонецЕсли;
		
		Макеты.Шапка = Макет.ПолучитьОбласть("Шапка");
		Макеты.Строка = Макет.ПолучитьОбласть("Строка");
		Макеты.ПустаяСтрока = Макет.ПолучитьОбласть("Строка");
		Макеты.Подвал = Макет.ПолучитьОбласть("Подвал");
		Макеты.ИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
		
		Если КлючВарианта <> "Форма0504402"
			И КлючВарианта <> "Форма0504402ПерваяПоловинаМесяца" Тогда
			Макеты.ИтогоПоВедомости = Макет.ПолучитьОбласть("ИтогоПоВедомости");
		КонецЕсли;
		
		Для каждого СтрокаМесяца Из ДанныеОтчета.Строки Цикл
			
			Для Каждого СтрокаОрганизации Из СтрокаМесяца.Строки Цикл
				
				ВывестиОрганизациюТ51(СтрокаОрганизации,
					ДокументРезультат,
					Макеты,
					ЕстьГруппировкаПоПодразделению,
					СоответствиеПользовательскихПолей,
					КлючВарианта);
				
			КонецЦикла;
			
		КонецЦикла;
		
		ДопСвойства = КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства;
		ДопСвойства.Вставить("ОтчетПустой", ДанныеОтчета.Строки.Количество() = 0);
		
		СтандартнаяОбработка = Ложь;
		
	Исключение
		
		Инфо = ИнформацияОбОшибке();
		ВызватьИсключение НСтр("ru = 'В настройку отчета Т-51 внесены критичные изменения. Отчет не будет сформирован.'")
			+ " " + КраткоеПредставлениеОшибки(Инфо);
		
	КонецПопытки;
	
КонецПроцедуры

Процедура ВывестиОрганизациюТ51(СтрокаОрганизации, ДокументРезультат, Макеты, ЕстьГруппировкаПоПодразделению, СоответствиеПользовательскихПолей, КлючВарианта)
	
	НастройкиПечатныхФорм = ЗарплатаКадрыПовтИсп.НастройкиПечатныхФорм();
	ЕстьОплатаВНатуральнойФорме = (СтрокаОрганизации[СоответствиеПользовательскихПолей["НачисленоВНатуральнойФорме"]] <> 0);
	
	Если ЕстьГруппировкаПоПодразделению Тогда
		
		Для Каждого СтрокаПодразделения Из СтрокаОрганизации.Строки Цикл
			
			ВывестиПодразделениеТ51(СтрокаПодразделения, ДокументРезультат, Макеты, СоответствиеПользовательскихПолей,
				ЕстьОплатаВНатуральнойФорме, КлючВарианта);
			
		КонецЦикла;
		
	Иначе
		
		Если ДокументРезультат.ВысотаТаблицы > 0 Тогда
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		КВыплатеПоВедомости = 0;
		
		Макеты.ШапкаДокумента.Параметры.Заполнить(СтрокаОрганизации);
		Если КлючВарианта <> "Форма0504402"
			И КлючВарианта <> "Форма0504402ПерваяПоловинаМесяца" Тогда
			
			Макеты.ШапкаДокумента.Параметры.ДатаД = Формат(ТекущаяДатаСеанса(), "ДЛФ=D");
			
			Макеты.ШапкаДокумента.Параметры.СообщениеОНеприменимостиПечатнойФормы = 
				ЗарплатаКадры.СообщениеОНеприменимостиПечатнойФормы(
					СтроковыеФункцииКлиентСервер.СтрокаВДату(Макеты.ШапкаДокумента.Параметры.ДатаД),
					'20150619',
					НСтр("ru='Приказа Минфина РФ'"),
					'20150330',
					"52н");
			
		КонецЕсли;
		
		Макеты.Шапка.Параметры.Заполнить(СтрокаОрганизации);
		ЗаполнитьПараметрыПользовательскихПолей(Макеты.ШапкаДокумента, СтрокаОрганизации, СоответствиеПользовательскихПолей);
		
		Если НастройкиПечатныхФорм.ВыводитьПолнуюИерархиюПодразделений И ЗначениеЗаполнено(Макеты.ШапкаДокумента.Параметры.ПодразделениеНаКонецПериода) Тогда
			Макеты.ШапкаДокумента.Параметры.ПодразделениеНаКонецПериода = Макеты.ШапкаДокумента.Параметры.ПодразделениеНаКонецПериода.ПолноеНаименование();
		КонецЕсли;
		
		ДокументРезультат.Вывести(Макеты.ШапкаДокумента);
		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		ЗаполнитьПараметрыПользовательскихПолей(Макеты.Шапка, СтрокаОрганизации, СоответствиеПользовательскихПолей);
		
		Если ЕстьОплатаВНатуральнойФорме Тогда
			Макеты.Шапка.Параметры.ГруппаНачислений2 = НСтр("ru='Оплата в натуральной форме'");
		КонецЕсли;
		
		ДокументРезультат.Вывести(Макеты.Шапка);
		
		НомерСтроки = 0;
		ПромежуточныеИтоги = ПромежуточныеИтогиПоСтраницеТ51();
		
		Для Каждого СтрокаСотрудника Из СтрокаОрганизации.Строки Цикл
			
			ВывестиСотрудникаТ51(НомерСтроки,
				СтрокаСотрудника,
				ДокументРезультат,
				Макеты,
				СоответствиеПользовательскихПолей,
				ПромежуточныеИтоги,
				КВыплатеПоВедомости,
				ЕстьОплатаВНатуральнойФорме,
				КлючВарианта);
			
		КонецЦикла;
		
		ДополнитьСтраницуТ51(НомерСтроки, Макеты, ДокументРезультат, ПромежуточныеИтоги);
		
		Макеты.ИтогоПоВедомости.Параметры.Заполнить(СтрокаОрганизации);
		
		ЗаполнитьПараметрыПользовательскихПолей(Макеты.ИтогоПоВедомости, СтрокаОрганизации, СоответствиеПользовательскихПолей);
		
		Макеты.Подвал.Параметры.Заполнить(СтрокаОрганизации);
		Макеты.Подвал.Параметры.ПараметрыДанныхОтветственный = ИОФамилияФизическогоЛица(Макеты.Подвал.Параметры.ПараметрыДанныхОтветственный);
		Макеты.ИтогоПоВедомости.Параметры.КВыплате = КВыплатеПоВедомости;
	
		Если ЕстьОплатаВНатуральнойФорме Тогда
			
			Макеты.ИтогоПоВедомости.Параметры.НачисленоСдельно =
				СтрокаОрганизации[СоответствиеПользовательскихПолей["НачисленоВНатуральнойФорме"]];
			
			Макеты.ИтогоПоВедомости.Параметры.ПрочиеДоходы =
				СтрокаОрганизации[СоответствиеПользовательскихПолей["ПрочиеДоходы"]] + СтрокаОрганизации[СоответствиеПользовательскихПолей["НачисленоСдельно"]];
			
		КонецЕсли;
		
		ДокументРезультат.Вывести(Макеты.ИтогоПоВедомости);
		ДокументРезультат.Вывести(Макеты.Подвал);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиПодразделениеТ51(СтрокаПодразделения, ДокументРезультат, Макеты, СоответствиеПользовательскихПолей, 
	
	ЕстьОплатаВНатуральнойФорме, КлючВарианта)
	
	НастройкиПечатныхФорм = ЗарплатаКадрыПовтИсп.НастройкиПечатныхФорм();
	
	КВыплатеПоВедомости = 0;
	
	Макеты.ШапкаДокумента.Параметры.Заполнить(СтрокаПодразделения);
	Макеты.ШапкаДокумента.Параметры.ДатаД = Формат(ТекущаяДатаСеанса(), "ДЛФ=D");
	Макеты.Шапка.Параметры.Заполнить(СтрокаПодразделения);
	
	ЗаполнитьПараметрыПользовательскихПолей(Макеты.ШапкаДокумента, СтрокаПодразделения, СоответствиеПользовательскихПолей);
	
	Если НастройкиПечатныхФорм.ВыводитьПолнуюИерархиюПодразделений И ЗначениеЗаполнено(Макеты.ШапкаДокумента.Параметры.ПодразделениеНаКонецПериода) Тогда
		Макеты.ШапкаДокумента.Параметры.ПодразделениеНаКонецПериода = Макеты.ШапкаДокумента.Параметры.ПодразделениеНаКонецПериода.ПолноеНаименование();
	КонецЕсли;
	
	ДокументРезультат.Вывести(Макеты.ШапкаДокумента);
	ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	ЗаполнитьПараметрыПользовательскихПолей(Макеты.Шапка, СтрокаПодразделения, СоответствиеПользовательскихПолей);
	ДокументРезультат.Вывести(Макеты.Шапка);
	
	НомерСтроки = 0;
	ПромежуточныеИтоги = ПромежуточныеИтогиПоСтраницеТ51();
	Для Каждого СтрокаСотрудника Из СтрокаПодразделения.Строки Цикл
		
		ВывестиСотрудникаТ51(НомерСтроки, СтрокаСотрудника, ДокументРезультат, Макеты, СоответствиеПользовательскихПолей, 
			ПромежуточныеИтоги, КВыплатеПоВедомости, ЕстьОплатаВНатуральнойФорме, КлючВарианта);
		
	КонецЦикла;
	
	ДополнитьСтраницуТ51(НомерСтроки, Макеты, ДокументРезультат, ПромежуточныеИтоги);
	
	Макеты.ИтогоПоВедомости.Параметры.Заполнить(СтрокаПодразделения);
	
	ЗаполнитьПараметрыПользовательскихПолей(Макеты.ИтогоПоВедомости, СтрокаПодразделения, СоответствиеПользовательскихПолей);
	
	Макеты.Подвал.Параметры.Заполнить(СтрокаПодразделения);
	Макеты.Подвал.Параметры.ПараметрыДанныхОтветственный = ИОФамилияФизическогоЛица(Макеты.Подвал.Параметры.ПараметрыДанныхОтветственный);
	Макеты.ИтогоПоВедомости.Параметры.КВыплате = КВыплатеПоВедомости;
	
	Если ЕстьОплатаВНатуральнойФорме Тогда
		
		Макеты.ИтогоПоВедомости.Параметры.НачисленоСдельно =
			СтрокаПодразделения[СоответствиеПользовательскихПолей["НачисленоВНатуральнойФорме"]];
		
		Макеты.ИтогоПоВедомости.Параметры.ПрочиеДоходы =
			СтрокаПодразделения[СоответствиеПользовательскихПолей["ПрочиеДоходы"]] + СтрокаПодразделения[СоответствиеПользовательскихПолей["НачисленоСдельно"]];
		
	КонецЕсли;
	
	ДокументРезультат.Вывести(Макеты.ИтогоПоВедомости);
	ДокументРезультат.Вывести(Макеты.Подвал);
	
КонецПроцедуры

Процедура ВывестиСотрудникаТ51(НомерСтроки,	СтрокаГоловногоСотрудника, ДокументРезультат, Макеты, СоответствиеПользовательскихПолей, ПромежуточныеИтоги, КВыплатеПоВедомости, ЕстьОплатаВНатуральнойФорме, КлючВарианта)
	
	НастройкиПечатныхФорм = ЗарплатаКадрыПовтИсп.НастройкиПечатныхФорм();
	
	НомерСтроки = НомерСтроки + 1;
	
	МакетСотрудника = Новый ТабличныйДокумент;
	ПромежуточныеИтогиПоГоловномуСотруднику = ПромежуточныеИтогиПоСтраницеТ51();
	НесколькоРабочихМест = СтрокаГоловногоСотрудника.Строки.Количество() > 1;
	
	Для каждого СтрокаСотрудника Из СтрокаГоловногоСотрудника.Строки Цикл
		
		Макеты.Строка.Параметры.Заполнить(СтрокаСотрудника);
		ЗаполнитьПараметрыПользовательскихПолей(Макеты.Строка, СтрокаСотрудника, СоответствиеПользовательскихПолей);
		
		Если НЕ НастройкиПечатныхФорм.ВыводитьПолныеФИОВСписочныхПечатныхФормах Тогда
			
			ФИО = Новый Структура("Фамилия, Имя, Отчество");
			ФИО.Фамилия = СтрокаСотрудника.Фамилия;
			ФИО.Имя = СтрокаСотрудника.Имя;
			ФИО.Отчество = СтрокаСотрудника.Отчество;
			Макеты.Строка.Параметры.ФизическоеЛицоФИО = ФизическиеЛицаЗарплатаКадрыКлиентСервер.ФамилияИнициалы(ФИО);
			
		КонецЕсли;
		
		ОтработаноРабочихДней = СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить("ОтработаноРабочихДней")];
		ОтработаноРабочихЧасов = СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить("ОтработаноРабочихЧасов")];
		ОтработаноПразднВыходныхДней = СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить("ОтработаноПразднВыходныхДней")];
		ОтработаноПразднВыходныхЧасов = СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить("ОтработаноПразднВыходныхЧасов")];
		
		Если КлючВарианта <> "Форма0504402"
			И КлючВарианта <> "Форма0504402ПерваяПоловинаМесяца" Тогда
			
			Если ЗначениеЗаполнено(ОтработаноРабочихДней) И ОтработаноРабочихДней > 0 Тогда 
				Макеты.Строка.Параметры.ОтработаноРабочихДней = "" + ОтработаноРабочихДней + " (" + ОтработаноРабочихЧасов + ")";
			Иначе
				Макеты.Строка.Параметры.ОтработаноРабочихДней = "";
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ОтработаноПразднВыходныхДней) И ОтработаноПразднВыходныхДней > 0 Тогда 
				Макеты.Строка.Параметры.ОтработаноПразднВыходныхДней = "" + ОтработаноПразднВыходныхДней + " (" + ОтработаноПразднВыходныхЧасов + ")";
			Иначе
				Макеты.Строка.Параметры.ОтработаноПразднВыходныхДней = "";
			КонецЕсли;
			
		КонецЕсли;
		
		Макеты.Строка.Параметры.НомерПП = НомерСтроки;
		Если Макеты.Строка.Параметры.КВыплате < 0 Тогда
			Макеты.Строка.Параметры.КВыплате = 0;
		КонецЕсли;
		
		Если ЕстьОплатаВНатуральнойФорме Тогда
			
			Макеты.Строка.Параметры.НачисленоСдельно =
				СтрокаСотрудника[СоответствиеПользовательскихПолей["НачисленоВНатуральнойФорме"]];
			
			Макеты.Строка.Параметры.ПрочиеДоходы =
				СтрокаСотрудника[СоответствиеПользовательскихПолей["ПрочиеДоходы"]] + СтрокаСотрудника[СоответствиеПользовательскихПолей["НачисленоСдельно"]];
			
		КонецЕсли;
		
		ДобавитьВПромежуточныйИтог(ПромежуточныеИтогиПоГоловномуСотруднику, Макеты.Строка.Параметры);
		КВыплатеПоВедомости = КВыплатеПоВедомости + Макеты.Строка.Параметры.КВыплате;
		
		Если НесколькоРабочихМест Тогда
			
			ЗаполнитьПараметрыПользовательскихПолей(
				Макеты.Строка, СтрокаГоловногоСотрудника, СоответствиеПользовательскихПолей, "ДолгЗаОрганизацией,ДолгЗаСотрудником,ВсегоНачислено,НДФЛ,ПрочиеУдержания,ВсегоУдержано,КВыплате");
			
		КонецЕсли;
		
		Если СтрокаСотрудника.Сотрудник <> СтрокаСотрудника.ГоловнойСотрудник Тогда
			Макеты.Строка.Параметры.ДолжностьНаКонецПериодаНаименованиеКраткое = СтрокаСотрудника.СотрудникУточнениеНаименования;
		КонецЕсли;
		
		МакетСотрудника.Вывести(Макеты.Строка);
	
	КонецЦикла;
	
	Если Не ЕстьИтогиПоГоловномуСотруднику(ПромежуточныеИтогиПоГоловномуСотруднику) Тогда
		
		НомерСтроки = НомерСтроки - 1;
		Возврат;
		
	КонецЕсли;
	
	Если МакетСотрудника.ВысотаТаблицы > 1 Тогда
		
		// Номер по порядку
		МакетСотрудника.Область(1, 1, МакетСотрудника.ВысотаТаблицы, 1).Объединить();
		// Табельный номер
		МакетСотрудника.Область(1, 2, МакетСотрудника.ВысотаТаблицы, 2).Объединить();
		// ФИО
		МакетСотрудника.Область(1, 3, МакетСотрудника.ВысотаТаблицы, 3).Объединить();
		// Всего начислено
		МакетСотрудника.Область(1, 17, МакетСотрудника.ВысотаТаблицы, 17).Объединить();
		// НДФЛ
		МакетСотрудника.Область(1, 18, МакетСотрудника.ВысотаТаблицы, 19).Объединить();
		// Прочие удержания
		МакетСотрудника.Область(1, 20, МакетСотрудника.ВысотаТаблицы, 20).Объединить();
		// Всего удержано
		МакетСотрудника.Область(1, 21, МакетСотрудника.ВысотаТаблицы, 22).Объединить();
		// Задолженность за организацией.
		МакетСотрудника.Область(1, 23, МакетСотрудника.ВысотаТаблицы, 23).Объединить();
		// Задолженность за работников.
		МакетСотрудника.Область(1, 24, МакетСотрудника.ВысотаТаблицы, 24).Объединить();
		// К выплате
		МакетСотрудника.Область(1, 25, МакетСотрудника.ВысотаТаблицы, 25).Объединить();
		
	КонецЕсли;
	
	МассивВыводимыхОбластей = Новый Массив;
	МассивВыводимыхОбластей.Добавить(МакетСотрудника);
	МассивВыводимыхОбластей.Добавить(Макеты.ИтогоПоСтранице);
	МассивВыводимыхОбластей.Добавить(Макеты.ИтогоПоВедомости);
	МассивВыводимыхОбластей.Добавить(Макеты.Подвал);
	
	Если НЕ ДокументРезультат.ПроверитьВывод(МассивВыводимыхОбластей) Тогда
		
		ЗаполнитьЗначенияСвойств(Макеты.ИтогоПоСтранице.Параметры, ПромежуточныеИтоги);
		ДокументРезультат.Вывести(Макеты.ИтогоПоСтранице);
		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		ЗаполнитьПараметрыПользовательскихПолей(Макеты.Шапка, СтрокаСотрудника, СоответствиеПользовательскихПолей);
		ДокументРезультат.Вывести(Макеты.Шапка);
		ПромежуточныеИтоги = ПромежуточныеИтогиПоСтраницеТ51();
		
	КонецЕсли;
	
	ДобавитьВПромежуточныйИтог(ПромежуточныеИтоги, ПромежуточныеИтогиПоГоловномуСотруднику);
	
	ДокументРезультат.Вывести(МакетСотрудника);
	
КонецПроцедуры

Процедура ДополнитьСтраницуТ51(НомерСтроки, Макеты, ДокументРезультат, ПромежуточныеИтоги)
	
	МассивВыводимыхОбластей = Новый Массив;
	МассивВыводимыхОбластей.Добавить(Макеты.ПустаяСтрока);
	МассивВыводимыхОбластей.Добавить(Макеты.ИтогоПоСтранице);
	МассивВыводимыхОбластей.Добавить(Макеты.ИтогоПоВедомости);
	МассивВыводимыхОбластей.Добавить(Макеты.Подвал);
	
	Пока ДокументРезультат.ПроверитьВывод(МассивВыводимыхОбластей) Цикл
		ДокументРезультат.Вывести(Макеты.ПустаяСтрока);
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(Макеты.ИтогоПоСтранице.Параметры, ПромежуточныеИтоги);
	ДокументРезультат.Вывести(Макеты.ИтогоПоСтранице);
	
КонецПроцедуры

Функция ПромежуточныеИтогиПоСтраницеТ51()
	
	ПромежуточныеИтоги = Новый Структура;
	
	ПромежуточныеИтоги.Вставить("НачисленоПовременно", 0);
	ПромежуточныеИтоги.Вставить("НачисленоСдельно", 0);
	ПромежуточныеИтоги.Вставить("ПрочиеДоходы", 0);
	ПромежуточныеИтоги.Вставить("ДругиеДоходы", 0);
	ПромежуточныеИтоги.Вставить("ВсегоНачислено", 0);
	ПромежуточныеИтоги.Вставить("НДФЛ", 0);
	ПромежуточныеИтоги.Вставить("ПрочиеУдержания", 0);
	ПромежуточныеИтоги.Вставить("ВсегоУдержано", 0);
	ПромежуточныеИтоги.Вставить("ДолгЗаОрганизацией", 0);
	ПромежуточныеИтоги.Вставить("ДолгЗаСотрудником", 0);
	ПромежуточныеИтоги.Вставить("КВыплате", 0);
	
	Возврат ПромежуточныеИтоги;
	
КонецФункции

#КонецОбласти


#Область РасчетныйЛисток

Процедура ПриКомпоновкеРезультатаРасчетныйЛисток(ОбъектОтчета, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка, НаАванс = Ложь) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Попытка
		
		КомпоновщикНастроек = ОбъектОтчета.КомпоновщикНастроек;
		СхемаКомпоновкиДанных = ОбъектОтчета.СхемаКомпоновкиДанных;
		
		КлючВарианта = КлючВарианта(КомпоновщикНастроек);
		
		ВыводитьПоРабочимМестам = (КлючВарианта = "РасчетныйЛистокПоРабочимМестам")
			Или (КлючВарианта = "РасчетныйЛистокПоРабочимМестамИСРазбивкойПоИсточникамФинансирования");
		
		ГруппироватьПоИсточникамФинансирования = (КлючВарианта = "РасчетныйЛистокСРазбивкойПоИсточникамФинансирования")
			Или (КлючВарианта = "РасчетныйЛистокСРазбивкойПоИсточникамФинансированияПерваяПоловинаМесяца")
			Или (КлючВарианта = "РасчетныйЛистокПоРабочимМестамИСРазбивкойПоИсточникамФинансирования");
		
		УправленческийРасчетныйЛисток = (КлючВарианта = "РасчетныйЛистокУправленческий");
		
		// Параметры документа
		ДокументРезультат.ТолькоПросмотр = Истина;
		ДокументРезультат.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РасчетныйЛисток";
		ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		ДокументРезультат.АвтоМасштаб = Истина;
		
		КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
		
		НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
			Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплата");
			Модуль.ДополнитьНастройкиОтчетаРасчетныйЛисток(НастройкиОтчета, КлючВарианта);
		КонецЕсли;
		
		ЗаполнитьПользовательскиеПоляВариантаОтчета(КлючВарианта, НастройкиОтчета, НаАванс);
		НастроитьВариантОтчетаРасчетныйЛисток(НастройкиОтчета);
		
		// Нужно проверить включена ли группировка по подразделениям.
		ЕстьГруппировкаПоПодразделению = Ложь;
		ПараметрГруппировки = Новый ПараметрКомпоновкиДанных("РазбиватьПоПодразделениям");
		Если НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрГруппировки).Использование Тогда
			ЕстьГруппировкаПоПодразделению = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрГруппировки).Значение;
		КонецЕсли;
		
		ПредварительныйПросмотр = КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("ПредварительныйПросмотр");
		
		Если ПредварительныйПросмотр Тогда
			
			НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(
				Новый ПараметрКомпоновкиДанных("ВыводитьОсобенностиРасчетаНДФЛ")).Значение = Ложь;
			
		КонецЕсли;
		
		НастройкиВывода = НастройкиВыводаРасчетногоЛистка(НастройкиОтчета, ПредварительныйПросмотр);
		
		Если НастройкиВывода.ВыводитьПериодыНачислений = Ложь Тогда
			ОтключитьИспользованиеПолей(НастройкиОтчета, "ПериодДействия,ДатаНачал,ДатаОкончания");
		КонецЕсли;
		
		Если НастройкиВывода.ВыводитьОтработанноеОплаченноеВремя = Ложь Тогда
			ОтключитьИспользованиеПолей(НастройкиОтчета, "ОтработаноДней,ОтработаноЧасов,ОплаченоДней,ОплаченоЧасов,ВремяВЧасах");
		КонецЕсли;
		
		Если НастройкиВывода.ВыводитьОснованияНачисленийИУдержаний = Ложь Тогда
			ОтключитьИспользованиеПолей(НастройкиОтчета, "ДокументОснование");
		КонецЕсли;
		
		Если НастройкиВывода.ВыводитьПоказателиНачисленийИУдержаний = Ложь Тогда
			ОтключитьИспользованиеПолей(НастройкиОтчета, "Показатель,Значение");
		КонецЕсли;
		
		Если КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("МакетКомпоновкиДанных")
			И КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.МакетКомпоновкиДанных <> Неопределено Тогда
			
			МакетКомпоновкиДанных = КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.МакетКомпоновкиДанных;
			КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Удалить("МакетКомпоновкиДанных");
			
		Иначе
			
			МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных,
				НастройкиОтчета, ДанныеРасшифровки, , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
			
			Если КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("ФормированиеМакетаКомпоновкиДанных") Тогда
				
				КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("СкомпонованныйМакетКомпоновкиДанных", МакетКомпоновкиДанных);
				Возврат;
				
			КонецЕсли;
			
		КонецЕсли;
		
		СоответствиеПользовательскихПолей = СоответствиеПользовательскихПолей(НастройкиОтчета);
		
		Если КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("ДанныеДокумента") Тогда
			
			НаборыВнешнихДанных = КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.ДанныеДокумента;
			КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Удалить("ДанныеДокумента");
			
		Иначе
			НаборыВнешнихДанных = НаборыВнешнихДанныхАнализНачисленийИУдержаний();
		КонецЕсли;
		
		ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
		ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных, НаборыВнешнихДанных, , Истина);
		
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
		ДанныеОтчета = Новый ДеревоЗначений;
		ПроцессорВывода.УстановитьОбъект(ДанныеОтчета);
		ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
		
		Если ВыводитьПоРабочимМестам Тогда
			
			Макет = УправлениеПечатью.МакетПечатнойФормы("Отчет.АнализНачисленийИУдержаний.ПФ_MXL_РасчетныйЛисток");
			
			Макеты = Новый Структура("Шапка1,АвансовыеПлатежи,Шапка2,Подработка,НачисленоУдержано,ПрочиеВыплачено,Льготы,СтрокаДвижений,Итог12,Итог34,Итог56,Сальдо,Группировка,СтатьяФинансирования,ИтогСтатьяФинансирования,Итог12ВНатуральнойФорме");
			
			Макеты.Шапка1 = Макет.ПолучитьОбласть("Шапка1");
			Макеты.АвансовыеПлатежи = Макет.ПолучитьОбласть("АвансовыеПлатежи");
			Макеты.Шапка2 = Макет.ПолучитьОбласть("Шапка2");
			Макеты.Подработка = Макет.ПолучитьОбласть("Подработка");
			Макеты.НачисленоУдержано = Макет.ПолучитьОбласть("НачисленоУдержано");
			Макеты.ПрочиеВыплачено = Макет.ПолучитьОбласть("ПрочиеВыплачено");
			Макеты.Льготы = Макет.ПолучитьОбласть("Льготы");
			Макеты.СтрокаДвижений = Макет.ПолучитьОбласть("СтрокаДвижений");
			Макеты.Итог12 = Макет.ПолучитьОбласть("Итог12");
			Макеты.Итог12ВНатуральнойФорме = Макет.ПолучитьОбласть("Итог12ВНатуральнойФорме");
			Макеты.Итог34 = Макет.ПолучитьОбласть("Итог34");
			Макеты.Итог56 = Макет.ПолучитьОбласть("Итог56");
			Макеты.Сальдо = Макет.ПолучитьОбласть("Сальдо");
			Макеты.Группировка = Макет.ПолучитьОбласть("Группировка");
			Макеты.СтатьяФинансирования = Макет.ПолучитьОбласть("СтатьяФинансирования");
			Макеты.ИтогСтатьяФинансирования = Макет.ПолучитьОбласть("ИтогСтатьяФинансирования");
		
		Иначе
			
			Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_РасчетныйЛистокНастраиваемый");
			
			Если ПредварительныйПросмотр Тогда
				ДокументРезультат.Вывести(Макет.ПолучитьОбласть("ПредварительныйПросмотр"));
			КонецЕсли;
			
		КонецЕсли;
		
		Если ПредварительныйПросмотр Тогда
			ЕстьГруппировкаПоОрганизации = Ложь;
			ЕстьГруппировкаПоПодразделению = Ложь;
		Иначе
			
			ЕстьГруппировкаПоОрганизации = МожноГруппировать(НастройкиОтчета.Порядок, "Организация");
			
			Если ЕстьГруппировкаПоПодразделению И НЕ МожноГруппировать(НастройкиОтчета.Порядок, "ПодразделениеСортировки.РеквизитДопУпорядочиванияИерархического", СоответствиеПользовательскихПолей) Тогда
				ЕстьГруппировкаПоПодразделению = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
		Организация = Неопределено;
		Подразделение = Неопределено;
		
		ДокументРезультатСтраница = Новый ТабличныйДокумент;
		Для каждого СтрокаМесяца Из ДанныеОтчета.Строки Цикл
			
			Если ВыводитьПоРабочимМестам = Истина Тогда
				
				ДанныеПоНДФЛИВзносам = ДанныеПоНДФЛИВзносам(СтрокаМесяца.Строки, СтрокаМесяца.МесяцНачисления, Истина, Ложь);
				
			ИначеЕсли НастройкиВывода.ВыводитьОсобенностиРасчетаНДФЛ = Истина
				Или НастройкиВывода.ВыводитьИнформациюОНачисленныхВзносахВПФР = Истина Тогда
				
				ДанныеПоНДФЛИВзносам = ДанныеПоНДФЛИВзносам(СтрокаМесяца.Строки, СтрокаМесяца.МесяцНачисления, НастройкиВывода.ВыводитьОсобенностиРасчетаНДФЛ, НастройкиВывода.ВыводитьИнформациюОНачисленныхВзносахВПФР);
				
			Иначе
				ДанныеПоНДФЛИВзносам = Неопределено;
			КонецЕсли;
			
			Если Не ВыводитьПоРабочимМестам Тогда
				
				ДанныеПоФизическимЛицам = Новый Массив;
				ДанныеПоФизическомуЛицу = Неопределено;
				
				ТекущееФизическоеЛицо = Неопределено;
				ТекущаяОрганизация = Неопределено;
				Для Каждого СтрокаГоловногоСотрудника Из СтрокаМесяца.Строки Цикл
					
					Если ТекущееФизическоеЛицо <> СтрокаГоловногоСотрудника.ФизическоеЛицо
						Или Не УправленческийРасчетныйЛисток И ТекущаяОрганизация <> СтрокаГоловногоСотрудника.Организация Тогда
						
						Если ТекущееФизическоеЛицо <> Неопределено Тогда
							ДанныеПоФизическимЛицам.Добавить(ДанныеПоФизическомуЛицу);
						КонецЕсли;
						
						ТекущееФизическоеЛицо = СтрокаГоловногоСотрудника.ФизическоеЛицо;
						ТекущаяОрганизация = Неопределено;
						
						ДанныеПоФизическомуЛицу = Новый Структура;
						ДанныеПоФизическомуЛицу.Вставить("ФизическоеЛицо", СтрокаГоловногоСотрудника.ФизическоеЛицо);
						
						Если Не УправленческийРасчетныйЛисток
							И СтрокаГоловногоСотрудника.Владелец().Колонки.Найти("Организация") <> Неопределено Тогда
							
							ДанныеПоФизическомуЛицу.Вставить("Организация", СтрокаГоловногоСотрудника.Организация);
							ТекущаяОрганизация = СтрокаГоловногоСотрудника.Организация;
							
						КонецЕсли;
						
						ДанныеПоФизическомуЛицу.Вставить("Подразделение", СтрокаГоловногоСотрудника.ПодразделениеГоловногоСотрудникаНаКонецПериода);
						ДанныеПоФизическомуЛицу.Вставить("ГоловныеСотрудники", Новый Массив);
						
					КонецЕсли;
					
					ДанныеПоФизическомуЛицу.ГоловныеСотрудники.Добавить(СтрокаГоловногоСотрудника);
					
				КонецЦикла;
				
				Если ТекущееФизическоеЛицо <> Неопределено Тогда
					ДанныеПоФизическимЛицам.Добавить(ДанныеПоФизическомуЛицу);
				КонецЕсли;
				
				КоллекцияДанных = ДанныеПоФизическимЛицам;
				
			Иначе
				КоллекцияДанных = СтрокаМесяца.Строки;
			КонецЕсли;
			
			ВыводимыеГруппировки = Новый Массив;
			
			Для каждого СтрокаКоллекции Из КоллекцияДанных Цикл
				
				Если ВыводитьПоРабочимМестам Тогда
					
					Если ЕстьГруппировкаПоОрганизации И Организация <> СтрокаКоллекции.Организация Тогда
						
						Макеты.Группировка.Параметры.НазваниеПараметра = НСтр("ru='Организация'");
						Макеты.Группировка.Параметры.Значение = СтрокаКоллекции.Организация;
						
						ДокументРезультат.Вывести(Макеты.Группировка);
						Организация = СтрокаКоллекции.Организация;
						
					КонецЕсли;
					
					Если ЕстьГруппировкаПоПодразделению И Подразделение <> СтрокаКоллекции.ПодразделениеГоловногоСотрудникаНаКонецПериода Тогда
						
						Макеты.Группировка.Параметры.НазваниеПараметра = НСтр("ru='Подразделение'");
						Макеты.Группировка.Параметры.Значение = СтрокаКоллекции.ПодразделениеГоловногоСотрудникаНаКонецПериода;
						
						ДокументРезультат.Вывести(Макеты.Группировка);
						Подразделение = СтрокаКоллекции.ПодразделениеГоловногоСотрудникаНаКонецПериода;
						
					КонецЕсли;
					
					ВыводимаяКоллекция = СтрокаКоллекции;
					ВывестиСотрудникаРасчетныйЛистокПоРабочимМестам(ВыводимаяКоллекция, ДокументРезультат, Макеты, СоответствиеПользовательскихПолей, ДанныеПоНДФЛИВзносам, ГруппироватьПоИсточникамФинансирования);
					
				Иначе
					
					Если ЕстьГруппировкаПоОрганизации И Организация <> СтрокаКоллекции.Организация Тогда
						
						ПараметрыГруппировки = Новый Структура("НазваниеПараметра, Значение", НСтр("ru='Организация'"));
						ПараметрыГруппировки.Значение = СтрокаКоллекции.Организация;
						
						Область = Макет.ПолучитьОбласть("Группировка");
						Область.Параметры.Заполнить(ПараметрыГруппировки);
						
						ВыводимыеГруппировки.Добавить(Область);
						Организация = СтрокаКоллекции.Организация;
						
					КонецЕсли;
					
					Если ЕстьГруппировкаПоПодразделению И Подразделение <> СтрокаКоллекции.Подразделение Тогда
						
						ПараметрыГруппировки = Новый Структура("НазваниеПараметра, Значение", НСтр("ru='Подразделение'"));
						ПараметрыГруппировки.Значение = СтрокаКоллекции.Подразделение;
						
						Область = Макет.ПолучитьОбласть("Группировка");
						Область.Параметры.Заполнить(ПараметрыГруппировки);
						
						ВыводимыеГруппировки.Добавить(Область);
						Подразделение = СтрокаКоллекции.Подразделение;
						
					КонецЕсли;
					
					ВыводимаяКоллекция = СтрокаКоллекции.ГоловныеСотрудники;
					
					ВывестиСотрудникаРасчетныйЛисток(ВыводимаяКоллекция, ДокументРезультат, ДокументРезультатСтраница, Макет,
						СоответствиеПользовательскихПолей, ДанныеПоНДФЛИВзносам, ГруппироватьПоИсточникамФинансирования,
						ВыводимыеГруппировки, НастройкиВывода, НаАванс);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
		Если ДокументРезультатСтраница.ВысотаТаблицы > 0 Тогда
			
			Если Не ПредварительныйПросмотр И ДокументРезультат.ВысотаТаблицы > 0 Тогда
				ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ДокументРезультат.Вывести(ДокументРезультатСтраница);
			
		КонецЕсли;
		
		ДопСвойства = КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства;
		ДопСвойства.Вставить("ОтчетПустой", ДанныеОтчета.Строки.Количество() = 0);
		
	Исключение
		
		Инфо = ИнформацияОбОшибке();
		ВызватьИсключение НСтр("ru = 'В настройку отчета ""Расчетный листок"" внесены критичные изменения. Отчет не будет сформирован.'")
			+ " " + КраткоеПредставлениеОшибки(Инфо);
		
	КонецПопытки;
	
КонецПроцедуры

Функция ДанныеПоНДФЛИВзносам(СтрокиСотрудников, МесяцНачисления, ВыводитьОсобенностиРасчетаНДФЛ, ВыводитьИнформациюОНачисленныхВзносахВПФР)
	
	ВозвращаемыеДанные = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Сотрудники", СтрокиСотрудников.ВыгрузитьКолонку("ГоловнойСотрудник"));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(МесяцНачисления));
	Запрос.УстановитьПараметр("НачалоГода", НачалоГода(МесяцНачисления));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(МесяцНачисления));
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Сотрудники.ГоловнаяОрганизация КАК ГоловнаяОрганизация
		|ПОМЕСТИТЬ ВТФизическиеЛица
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Ссылка В(&Сотрудники)";
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	// Подготовка сведений об НДФЛ
	Если ВыводитьОсобенностиРасчетаНДФЛ Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	СтандартныеВычетыФизическихЛицНДФЛСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(СтандартныеВычетыФизическихЛицНДФЛСрезПоследних.КодВычетаЛичный, ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.ПустаяСсылка)
			|			ТОГДА ЛОЖЬ
			|		ИНАЧЕ ИСТИНА
			|	КОНЕЦ КАК ПоложеныЛичныеВычеты
			|ПОМЕСТИТЬ ВТЛичныеВычетыФизическихЛиц
			|ИЗ
			|	РегистрСведений.СтандартныеВычетыФизическихЛицНДФЛ.СрезПоследних(
			|			&НачалоПериода,
			|			ФизическоеЛицо В
			|				(ВЫБРАТЬ
			|					ФизическиеЛица.ФизическоеЛицо
			|				ИЗ
			|					ВТФизическиеЛица КАК ФизическиеЛица)) КАК СтандартныеВычетыФизическихЛицНДФЛСрезПоследних
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	&НачалоПериода КАК МесяцНачисления,
			|	&КонецПериода КАК КонецМесяцаНачисления,
			|	ФизическиеЛица.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ФизическиеЛица.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	ЛичныеВычетыФизическихЛиц.ПоложеныЛичныеВычеты КАК ПоложеныЛичныеВычеты
			|ПОМЕСТИТЬ ВТСотрудникиПредварительно
			|ИЗ
			|	ВТФизическиеЛица КАК ФизическиеЛица
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЛичныеВычетыФизическихЛиц КАК ЛичныеВычетыФизическихЛиц
			|		ПО ФизическиеЛица.ФизическоеЛицо = ЛичныеВычетыФизическихЛиц.ФизическоеЛицо
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	МесяцНачисления,
			|	ГоловнаяОрганизация,
			|	ФизическоеЛицо
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
			|	СтандартныеВычетыНаДетейНДФЛ.КодВычета КАК КодВычета,
			|	МАКСИМУМ(СтандартныеВычетыНаДетейНДФЛ.ДатаДействия) КАК ДатаДействия
			|ПОМЕСТИТЬ ВТПоследниеДатыДействияВычетов
			|ИЗ
			|	ВТСотрудникиПредварительно КАК ИзмеренияДаты
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтандартныеВычетыНаДетейНДФЛ КАК СтандартныеВычетыНаДетейНДФЛ
			|		ПО ИзмеренияДаты.ФизическоеЛицо = СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо
			|			И ИзмеренияДаты.МесяцНачисления >= СтандартныеВычетыНаДетейНДФЛ.ДатаДействия
			|			И (ИзмеренияДаты.МесяцНачисления <= СтандартныеВычетыНаДетейНДФЛ.ДействуетДо
			|				ИЛИ СтандартныеВычетыНаДетейНДФЛ.ДействуетДо = ДАТАВРЕМЯ(1, 1, 1))
			|
			|СГРУППИРОВАТЬ ПО
			|	СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо,
			|	СтандартныеВычетыНаДетейНДФЛ.КодВычета
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо
			|ПОМЕСТИТЬ ВТСотрудникиСВычетамиНаДетей
			|ИЗ
			|	ВТПоследниеДатыДействияВычетов КАК ПоследниеДатыДействияВычетов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтандартныеВычетыНаДетейНДФЛ КАК СтандартныеВычетыНаДетейНДФЛ
			|		ПО ПоследниеДатыДействияВычетов.ФизическоеЛицо = СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо
			|			И ПоследниеДатыДействияВычетов.КодВычета = СтандартныеВычетыНаДетейНДФЛ.КодВычета
			|			И ПоследниеДатыДействияВычетов.ДатаДействия = СтандартныеВычетыНаДетейНДФЛ.ДатаДействия
			|			И (СтандартныеВычетыНаДетейНДФЛ.КоличествоДетей > 0)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Сотрудники.МесяцНачисления КАК МесяцНачисления,
			|	Сотрудники.КонецМесяцаНачисления КАК КонецМесяцаНачисления,
			|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
			|	Сотрудники.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	Сотрудники.ПоложеныЛичныеВычеты КАК ПоложеныЛичныеВычеты,
			|	ВЫБОР
			|		КОГДА ВычетыНаДетей.ФизическоеЛицо ЕСТЬ NULL
			|			ТОГДА ЛОЖЬ
			|		ИНАЧЕ ИСТИНА
			|	КОНЕЦ КАК ПоложеныВычетыНаДетей
			|ПОМЕСТИТЬ ВТСотрудники
			|ИЗ
			|	ВТСотрудникиПредварительно КАК Сотрудники
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудникиСВычетамиНаДетей КАК ВычетыНаДетей
			|		ПО Сотрудники.ФизическоеЛицо = ВычетыНаДетей.ФизическоеЛицо
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	НАЧАЛОПЕРИОДА(ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.Период, МЕСЯЦ) КАК МесяцНачисления,
			|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	СУММА(ВЫБОР
			|			КОГДА ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.КодВычета.ГруппаВычета = ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.Стандартные)
			|				ТОГДА ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.Сумма
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК ВычетНаФизлицо,
			|	СУММА(ВЫБОР
			|			КОГДА ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.КодВычета.ГруппаВычета = ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.СтандартныеНаДетей)
			|				ТОГДА ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.Сумма
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК ВычетНаДетей,
			|	СУММА(ВЫБОР
			|			КОГДА ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.КодВычета.ГруппаВычета = ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.Имущественные)
			|				ТОГДА ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.Сумма
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК ВычетИмущественный,
			|	СУММА(ВЫБОР
			|			КОГДА ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.КодВычета.ГруппаВычета В (ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.Социальные), ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.СоциальныеПоУведомлениюНО))
			|				ТОГДА ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.Сумма
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК ВычетСоциальный
			|ПОМЕСТИТЬ ВТПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ
			|ИЗ
			|	РегистрНакопления.ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ КАК ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ
			|ГДЕ
			|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.Период МЕЖДУ &НачалоПериода И &КонецПериода
			|	И ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ФизическоеЛицо В
			|			(ВЫБРАТЬ
			|				ФизическиеЛица.ФизическоеЛицо
			|			ИЗ
			|				ВТФизическиеЛица КАК ФизическиеЛица)
			|
			|СГРУППИРОВАТЬ ПО
			|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ФизическоеЛицо,
			|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ГоловнаяОрганизация,
			|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.Период
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.МесяцНачисления КАК МесяцНачисления,
			|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ВычетНаФизлицо КАК ВычетНаФизлицо,
			|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ВычетНаДетей КАК ВычетНаДетей,
			|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ВычетИмущественный КАК ВычетИмущественный,
			|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ВычетСоциальный КАК ВычетСоциальный
			|ПОМЕСТИТЬ ВТВычетыПредварительно
			|ИЗ
			|	ВТПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ КАК ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудники КАК Сотрудники
			|		ПО ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.МесяцНачисления = Сотрудники.МесяцНачисления
			|			И ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ФизическоеЛицо = Сотрудники.ФизическоеЛицо
			|			И ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ГоловнаяОрганизация = Сотрудники.ГоловнаяОрганизация
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	НАЧАЛОПЕРИОДА(ИмущественныеВычетыНДФЛ.Период, МЕСЯЦ),
			|	ИмущественныеВычетыНДФЛ.ФизическоеЛицо,
			|	ИмущественныеВычетыНДФЛ.ГоловнаяОрганизация,
			|	СУММА(ВЫБОР
			|			КОГДА ИмущественныеВычетыНДФЛ.КодВычета.ГруппаВычета = ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.Стандартные)
			|				ТОГДА ИмущественныеВычетыНДФЛ.Сумма
			|			ИНАЧЕ 0
			|		КОНЕЦ),
			|	СУММА(ВЫБОР
			|			КОГДА ИмущественныеВычетыНДФЛ.КодВычета.ГруппаВычета = ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.СтандартныеНаДетей)
			|				ТОГДА ИмущественныеВычетыНДФЛ.Сумма
			|			ИНАЧЕ 0
			|		КОНЕЦ),
			|	СУММА(ВЫБОР
			|			КОГДА ИмущественныеВычетыНДФЛ.КодВычета.ГруппаВычета = ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.Имущественные)
			|				ТОГДА ИмущественныеВычетыНДФЛ.Сумма
			|			ИНАЧЕ 0
			|		КОНЕЦ),
			|	СУММА(ВЫБОР
			|			КОГДА ИмущественныеВычетыНДФЛ.КодВычета.ГруппаВычета В (ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.Социальные), ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.СоциальныеПоУведомлениюНО))
			|				ТОГДА ИмущественныеВычетыНДФЛ.Сумма
			|			ИНАЧЕ 0
			|		КОНЕЦ)
			|ИЗ
			|	РегистрНакопления.ИмущественныеВычетыНДФЛ КАК ИмущественныеВычетыНДФЛ
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудники КАК Сотрудники
			|		ПО (ИмущественныеВычетыНДФЛ.Период МЕЖДУ Сотрудники.МесяцНачисления И Сотрудники.КонецМесяцаНачисления)
			|			И ИмущественныеВычетыНДФЛ.ФизическоеЛицо = Сотрудники.ФизическоеЛицо
			|			И ИмущественныеВычетыНДФЛ.ГоловнаяОрганизация = Сотрудники.ГоловнаяОрганизация
			|ГДЕ
			|	ИмущественныеВычетыНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
			|{ГДЕ
			|	ИмущественныеВычетыНДФЛ.Период КАК МесяцНачисления,
			|	ИмущественныеВычетыНДФЛ.ФизическоеЛицо.*,
			|	ИмущественныеВычетыНДФЛ.ГоловнаяОрганизация.*}
			|
			|СГРУППИРОВАТЬ ПО
			|	ИмущественныеВычетыНДФЛ.ФизическоеЛицо,
			|	ИмущественныеВычетыНДФЛ.ГоловнаяОрганизация,
			|	ИмущественныеВычетыНДФЛ.Период
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НАЧАЛОПЕРИОДА(ВычетыПредварительно.МесяцНачисления, МЕСЯЦ) КАК МесяцНачисления,
			|	ВычетыПредварительно.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ВычетыПредварительно.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	СУММА(ВычетыПредварительно.ВычетНаФизлицо) КАК ВычетНаФизлицо,
			|	СУММА(ВычетыПредварительно.ВычетНаДетей) КАК ВычетНаДетей,
			|	СУММА(ВычетыПредварительно.ВычетИмущественный) КАК ВычетИмущественный,
			|	СУММА(ВычетыПредварительно.ВычетСоциальный) КАК ВычетСоциальный
			|ПОМЕСТИТЬ ВТВычеты
			|ИЗ
			|	ВТВычетыПредварительно КАК ВычетыПредварительно
			|
			|СГРУППИРОВАТЬ ПО
			|	НАЧАЛОПЕРИОДА(ВычетыПредварительно.МесяцНачисления, МЕСЯЦ),
			|	ВычетыПредварительно.ФизическоеЛицо,
			|	ВычетыПредварительно.ГоловнаяОрганизация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СведенияОДоходахНДФЛОбороты.ФизическоеЛицо КАК ФизическоеЛицо,
			|	СведенияОДоходахНДФЛОбороты.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	СУММА(СведенияОДоходахНДФЛОбороты.СуммаДоходаОборот - ВЫБОР
			|			КОГДА СведенияОДоходахНДФЛОбороты.КодВычета = ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.ПустаяСсылка)
			|				ТОГДА 0
			|			ИНАЧЕ СведенияОДоходахНДФЛОбороты.СуммаВычетаОборот
			|		КОНЕЦ) КАК ОблагаемыйДоход,
			|	&НачалоПериода КАК МесяцНачисления
			|ПОМЕСТИТЬ ВТСведенияОДоходахНДФЛОбороты
			|{ВЫБРАТЬ
			|	ФизическоеЛицо.*,
			|	ГоловнаяОрганизация.*,
			|	ОблагаемыйДоход,
			|	МесяцНачисления}
			|ИЗ
			|	РегистрНакопления.СведенияОДоходахНДФЛ.Обороты(
			|			&НачалоГода,
			|			&КонецПериода,
			|			,
			|			ФизическоеЛицо В
			|				(ВЫБРАТЬ
			|					ФизическиеЛица.ФизическоеЛицо
			|				ИЗ
			|					ВТФизическиеЛица КАК ФизическиеЛица)) КАК СведенияОДоходахНДФЛОбороты
			|{ГДЕ
			|	СведенияОДоходахНДФЛОбороты.ФизическоеЛицо.*,
			|	СведенияОДоходахНДФЛОбороты.ГоловнаяОрганизация.*,
			|	СведенияОДоходахНДФЛОбороты.Период КАК МесяцНачисления}
			|
			|СГРУППИРОВАТЬ ПО
			|	СведенияОДоходахНДФЛОбороты.ГоловнаяОрганизация,
			|	СведенияОДоходахНДФЛОбороты.ФизическоеЛицо
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СведенияОДоходахНДФЛОбороты.ФизическоеЛицо КАК ФизическоеЛицо,
			|	СведенияОДоходахНДФЛОбороты.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	СведенияОДоходахНДФЛОбороты.ОблагаемыйДоход КАК ОблагаемыйДоход,
			|	&НачалоПериода КАК МесяцНачисления
			|ПОМЕСТИТЬ ВТОблагаемыйДоход
			|ИЗ
			|	ВТСведенияОДоходахНДФЛОбороты КАК СведенияОДоходахНДФЛОбороты
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудники КАК Сотрудники
			|		ПО СведенияОДоходахНДФЛОбороты.ФизическоеЛицо = Сотрудники.ФизическоеЛицо
			|			И СведенияОДоходахНДФЛОбороты.ГоловнаяОрганизация = Сотрудники.ГоловнаяОрганизация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	НАЧАЛОПЕРИОДА(АвансовыеПлатежиИностранцевПоНДФЛ.Период, МЕСЯЦ) КАК МесяцНачисления,
			|	АвансовыеПлатежиИностранцевПоНДФЛ.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	АвансовыеПлатежиИностранцевПоНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
			|	СУММА(АвансовыеПлатежиИностранцевПоНДФЛ.Сумма) КАК Сумма
			|ПОМЕСТИТЬ ВТАвансовыеПлатежи
			|ИЗ
			|	РегистрНакопления.АвансовыеПлатежиИностранцевПоНДФЛ КАК АвансовыеПлатежиИностранцевПоНДФЛ
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудники КАК Сотрудники
			|		ПО (АвансовыеПлатежиИностранцевПоНДФЛ.Период МЕЖДУ Сотрудники.МесяцНачисления И Сотрудники.КонецМесяцаНачисления)
			|			И АвансовыеПлатежиИностранцевПоНДФЛ.ФизическоеЛицо = Сотрудники.ФизическоеЛицо
			|			И АвансовыеПлатежиИностранцевПоНДФЛ.ГоловнаяОрганизация = Сотрудники.ГоловнаяОрганизация
			|ГДЕ
			|	АвансовыеПлатежиИностранцевПоНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
			|	И АвансовыеПлатежиИностранцевПоНДФЛ.Период МЕЖДУ &НачалоПериода И &КонецПериода
			|
			|СГРУППИРОВАТЬ ПО
			|	АвансовыеПлатежиИностранцевПоНДФЛ.ГоловнаяОрганизация,
			|	АвансовыеПлатежиИностранцевПоНДФЛ.ФизическоеЛицо,
			|	АвансовыеПлатежиИностранцевПоНДФЛ.Период
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	&НачалоПериода КАК Период,
			|	ЗарплатаКВыплатеОстатки.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	ЗарплатаКВыплатеОстатки.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ЗарплатаКВыплатеОстатки.СуммаКВыплатеОстаток КАК СуммаКВыплатеОстаток
			|ПОМЕСТИТЬ ВТОстаткиЗарплатыКВыплате
			|ИЗ
			|	РегистрНакопления.ЗарплатаКВыплате.Остатки(
			|			&НачалоПериода,
			|			ФизическоеЛицо В
			|				(ВЫБРАТЬ
			|					ФизическиеЛица.ФизическоеЛицо
			|				ИЗ
			|					ВТФизическиеЛица КАК ФизическиеЛица)) КАК ЗарплатаКВыплатеОстатки
			|
			|ОБЪЕДИНИТЬ
			|
			|ВЫБРАТЬ
			|	&КонецПериода,
			|	ЗарплатаКВыплатеОстатки.Организация.ГоловнаяОрганизация,
			|	ЗарплатаКВыплатеОстатки.ФизическоеЛицо,
			|	ЗарплатаКВыплатеОстатки.СуммаКВыплатеОстаток
			|ИЗ
			|	РегистрНакопления.ЗарплатаКВыплате.Остатки(
			|			&КонецПериода,
			|			ФизическоеЛицо В
			|				(ВЫБРАТЬ
			|					ФизическиеЛица.ФизическоеЛицо
			|				ИЗ
			|					ВТФизическиеЛица КАК ФизическиеЛица)) КАК ЗарплатаКВыплатеОстатки";
			
		Запрос.Выполнить();
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	&НачалоПериода КАК МесяцНачисления,
			|	ФизическиеЛица.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ФизическиеЛица.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	ЕСТЬNULL(Вычеты.ВычетНаФизлицо, 0) КАК ВычетНаФизлицо,
			|	ЕСТЬNULL(Вычеты.ВычетНаДетей, 0) КАК ВычетНаДетей,
			|	Вычеты.ВычетИмущественный КАК ВычетИмущественный,
			|	Вычеты.ВычетСоциальный КАК ВычетСоциальный,
			|	ОблагаемыйДоходПомесячно.ОблагаемыйДоход КАК ОблагаемыйДоход,
			|	АвансовыеПлатежи.Сумма КАК АвансовыеПлатежи,
			|	СотрудникиВычеты.ПоложеныЛичныеВычеты КАК ПоложеныЛичныеВычеты,
			|	СотрудникиВычеты.ПоложеныВычетыНаДетей КАК ПоложеныВычетыНаДетей,
			|	ЕСТЬNULL(ОстаткиЗарплатыКВыплатеНачалоПериода.СуммаКВыплатеОстаток, 0) КАК ЗарплатаКВыплатеНаНачалоПериода,
			|	ЕСТЬNULL(ОстаткиЗарплатыКВыплатеКонецПериода.СуммаКВыплатеОстаток, 0) КАК ЗарплатаКВыплатеНаКонецПериода
			|ИЗ
			|	ВТФизическиеЛица КАК ФизическиеЛица
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудники КАК СотрудникиВычеты
			|		ПО (СотрудникиВычеты.МесяцНачисления = &НачалоПериода)
			|			И ФизическиеЛица.ФизическоеЛицо = СотрудникиВычеты.ФизическоеЛицо
			|			И ФизическиеЛица.ГоловнаяОрганизация = СотрудникиВычеты.ГоловнаяОрганизация
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВычеты КАК Вычеты
			|		ПО (Вычеты.МесяцНачисления = &НачалоПериода)
			|			И ФизическиеЛица.ФизическоеЛицо = Вычеты.ФизическоеЛицо
			|			И ФизическиеЛица.ГоловнаяОрганизация = Вычеты.ГоловнаяОрганизация
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОблагаемыйДоход КАК ОблагаемыйДоходПомесячно
			|		ПО (ОблагаемыйДоходПомесячно.МесяцНачисления = &НачалоПериода)
			|			И ФизическиеЛица.ФизическоеЛицо = ОблагаемыйДоходПомесячно.ФизическоеЛицо
			|			И ФизическиеЛица.ГоловнаяОрганизация = ОблагаемыйДоходПомесячно.ГоловнаяОрганизация
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТАвансовыеПлатежи КАК АвансовыеПлатежи
			|		ПО (АвансовыеПлатежи.МесяцНачисления = &НачалоПериода)
			|			И ФизическиеЛица.ФизическоеЛицо = АвансовыеПлатежи.ФизическоеЛицо
			|			И ФизическиеЛица.ГоловнаяОрганизация = АвансовыеПлатежи.ГоловнаяОрганизация
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстаткиЗарплатыКВыплате КАК ОстаткиЗарплатыКВыплатеНачалоПериода
			|		ПО (ОстаткиЗарплатыКВыплатеНачалоПериода.Период = &НачалоПериода)
			|			И ФизическиеЛица.ФизическоеЛицо = ОстаткиЗарплатыКВыплатеНачалоПериода.ФизическоеЛицо
			|			И ФизическиеЛица.ГоловнаяОрганизация = ОстаткиЗарплатыКВыплатеНачалоПериода.ГоловнаяОрганизация
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстаткиЗарплатыКВыплате КАК ОстаткиЗарплатыКВыплатеКонецПериода
			|		ПО (ОстаткиЗарплатыКВыплатеКонецПериода.Период = &КонецПериода)
			|			И ФизическиеЛица.ФизическоеЛицо = ОстаткиЗарплатыКВыплатеКонецПериода.ФизическоеЛицо
			|			И ФизическиеЛица.ГоловнаяОрганизация = ОстаткиЗарплатыКВыплатеКонецПериода.ГоловнаяОрганизация";
		
		ВозвращаемыеДанные.Вставить("НДФЛ", Запрос.Выполнить().Выгрузить());
		
	КонецЕсли;
	
	// Подготовка сведений о взносах
	Если ВыводитьИнформациюОНачисленныхВзносахВПФР Тогда
		
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	&НачалоПериода КАК МесяцНачисления,
			|	ИсчисленныеСтраховыеВзносыОбороты.Организация,
			|	ИсчисленныеСтраховыеВзносыОбороты.ГоловнаяОрганизация,
			|	ИсчисленныеСтраховыеВзносыОбороты.ФизическоеЛицо,
			|	СУММА(&ПоляВзносов) КАК ПоляВзносов
			|ИЗ
			|	РегистрНакопления.ИсчисленныеСтраховыеВзносы.Обороты(
			|			&НачалоПериода,
			|			&КонецПериода,
			|			Месяц,
			|			ФизическоеЛицо В
			|				(ВЫБРАТЬ
			|					ФизическиеЛица.ФизическоеЛицо
			|				ИЗ
			|					ВТФизическиеЛица КАК ФизическиеЛица)) КАК ИсчисленныеСтраховыеВзносыОбороты
			|
			|СГРУППИРОВАТЬ ПО
			|	ИсчисленныеСтраховыеВзносыОбороты.ФизическоеЛицо,
			|	ИсчисленныеСтраховыеВзносыОбороты.ГоловнаяОрганизация,
			|	ИсчисленныеСтраховыеВзносыОбороты.Организация";
			
		ПоляВзносов = СтрРазделить(УчетСтраховыхВзносовКлиентСервер.РассчитываемыеВзносыВПФР(), ",");
		ТекстЗапросаСуммПолей = "";
		
		ДобавитьЗапятую = Ложь;
		Для каждого ПолеВзносов Из  ПоляВзносов Цикл
			
			Если ДобавитьЗапятую Тогда
				
				ТекстЗапросаСуммПолей = ТекстЗапросаСуммПолей + ",
					|	";
				
			Иначе
				ДобавитьЗапятую = Истина;
			КонецЕсли;
			
			ТекстЗапросаСуммПолей = ТекстЗапросаСуммПолей + "СУММА(ИсчисленныеСтраховыеВзносыОбороты." + ПолеВзносов + "Оборот) КАК " + ПолеВзносов;
			
		КонецЦикла;
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "СУММА(&ПоляВзносов) КАК ПоляВзносов", ТекстЗапросаСуммПолей);
		
		ВозвращаемыеДанные.Вставить("Взносы", Запрос.Выполнить().Выгрузить());
		
	КонецЕсли;
	
	Возврат ВозвращаемыеДанные;
	
КонецФункции

Процедура ВывестиСотрудникаРасчетныйЛисток(СтрокиГоловныхСотрудников, ДокументРезультатПромежуточный, ДокументРезультатСтраница, Макет, СоответствиеПользовательскихПолей, ДанныеПоНДФЛИВзносам, ГруппироватьПоИсточникамФинансирования, ВыводимыеГруппировки, НастройкиВывода, НаАванс)
	
	НастройкиПечатныхФорм = ЗарплатаКадрыПовтИсп.НастройкиПечатныхФорм();
	
	ДокументРезультат = Новый ТабличныйДокумент;
	
	СтрокаПервогоГоловногоСотрудника = СтрокиГоловныхСотрудников[0];
	
	Если ГруппироватьПоИсточникамФинансирования Тогда
		СтрокаШапки = СтрокаПервогоГоловногоСотрудника.Строки[0].Строки[0];
	Иначе
		СтрокаШапки = СтрокаПервогоГоловногоСотрудника.Строки[0];
	КонецЕсли;
	
	Если НастройкиВывода.ВыводитьОсобенностиРасчетаНДФЛ = Истина Тогда
		
		ГоловнаяОрганизация = ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(СтрокаШапки.Организация);
		СтрокиДанныхПоНДФЛ = ДанныеПоНДФЛИВзносам.НДФЛ.НайтиСтроки(Новый Структура("ФизическоеЛицо,ГоловнаяОрганизация", СтрокаШапки.ФизическоеЛицо, ГоловнаяОрганизация));
		Если СтрокиДанныхПоНДФЛ.Количество() > 0 Тогда
			СтрокаДанныхПоНДФЛ = СтрокиДанныхПоНДФЛ[0];
		КонецЕсли;
		
	Иначе
		СтрокаДанныхПоНДФЛ = Неопределено;
	КонецЕсли;
	
	ВсегоВзносов = 0;
	Если НастройкиВывода.ВыводитьИнформациюОНачисленныхВзносахВПФР = Истина Тогда
		
		Если ГруппироватьПоИсточникамФинансирования Тогда
			
			Организации = Новый Массив;
			Для каждого СтрокаСтатьиФинансирования Из СтрокиГоловныхСотрудников Цикл
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Организации, ОбщегоНазначения.ВыгрузитьКолонку(СтрокаСтатьиФинансирования.Строки, "Организация", Истина), Истина);
			КонецЦикла;
			
		Иначе
			Организации = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиГоловныхСотрудников, "Организация", Истина);
		КонецЕсли;
		
		ПоляВзносов = СтрРазделить(УчетСтраховыхВзносовКлиентСервер.РассчитываемыеВзносыВПФР(), ",");
		Для каждого Организация Из Организации Цикл
			
			ГоловнаяОрганизация = ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(СтрокаШапки.Организация);
			СтрокиДанныхПоВзносам = ДанныеПоНДФЛИВзносам.Взносы.НайтиСтроки(Новый Структура("ФизическоеЛицо,ГоловнаяОрганизация", СтрокаШапки.ФизическоеЛицо, ГоловнаяОрганизация));
			Если СтрокиДанныхПоВзносам.Количество() > 0 Тогда
				
				СтрокаДанныхПоВзносам = СтрокиДанныхПоВзносам[0];
				Для каждого ПолеВзносов Из  ПоляВзносов Цикл
					ВсегоВзносов = ВсегоВзносов + СтрокаДанныхПоВзносам[ПолеВзносов];
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	РазделительЛистков = Новый ТабличныйДокумент;
	ВывестиОбластиСтроки(РазделительЛистков, Макет, "РазделительЛистков", НастройкиВывода);
	
	#Область ВыводШапки
	
	ПараметрыШапки = Новый Структура("Месяц,ФизическоеЛицоФИО,СотрудникКод,КВыплате,Фамилия,Имя,Отчество");
	ЗаполнитьЗначенияСвойств(ПараметрыШапки, СтрокаШапки);
	
	ПараметрыШапки.Месяц = ВРег(Формат(СтрокаШапки.МесяцНачисления, "ДФ='ММММ гггг'"));
	Если НаАванс Тогда
		ВывестиОбластиСтрокиНачислено(ДокументРезультат, Макет, "ШапкаПерваяПоловина", НастройкиВывода, ПараметрыШапки);
	Иначе
		ВывестиОбластиСтрокиНачислено(ДокументРезультат, Макет, "Шапка", НастройкиВывода, ПараметрыШапки);
	КонецЕсли;
	
	Если НастройкиВывода.ВыводитьСуммуНачисленнойЗарплатыВРазделеКВыплате = Истина Тогда
		
		КВыплате = 0;
		Для каждого СтрокаСотрудника Из СтрокиГоловныхСотрудников Цикл
			КВыплате = КВыплате + СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить("КВыплате")];
		КонецЦикла;
		
		ПараметрыШапки.КВыплате = КВыплате;
		ПрисоединитьОбластиСтрокиУдержаноВыплачено(ДокументРезультат, Макет, "Шапка", НастройкиВывода, ПараметрыШапки);
		НастроитьОбъединение(ДокументРезультат, 1, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено));
		
	Иначе
		НастроитьОбъединение(ДокументРезультат, 1, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено) + ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейУдержаноВыплачено));
	КонецЕсли;
	
	#КонецОбласти
	
	
	#Область ВыводРабочегоМеста
	
	Если СтрокаШапки.Владелец().Колонки.Найти("ОрганизацияГоловногоСотрудникаНаКонецПериода") = Неопределено Тогда
		ПараметрыРабочегоМеста = Новый Структура("Подразделение,Должность,РазрядКатегория,Оклад");
	Иначе
		
		ПараметрыРабочегоМеста = Новый Структура("Организация,Подразделение,Должность,РазрядКатегория,Оклад");
		Если ЗначениеЗаполнено(СтрокаШапки.ОрганизацияГоловногоСотрудникаНаКонецПериода) Тогда
			ПараметрыРабочегоМеста.Организация = СтрокаШапки.ОрганизацияГоловногоСотрудникаНаКонецПериода;
		Иначе
			ПараметрыРабочегоМеста.Организация = СтрокаШапки.Организация;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаШапки.ПодразделениеГоловногоСотрудникаНаКонецПериода) Тогда
		
		Если НастройкиПечатныхФорм.ВыводитьПолнуюИерархиюПодразделений Тогда
			ПараметрыРабочегоМеста.Подразделение = СтрокаШапки.ПодразделениеГоловногоСотрудникаНаКонецПериода.ПолноеНаименование();
		Иначе
			ПараметрыРабочегоМеста.Подразделение = СтрокаШапки.ПодразделениеГоловногоСотрудникаНаКонецПериода;
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыРабочегоМеста.Должность = СтрокаШапки.ДолжностьГоловногоСотрудникаНаКонецПериода;
	
	Если СтрокаПервогоГоловногоСотрудника.Владелец().Колонки.Найти("РазрядКатегорияГоловногоСотрудникаНаКонецПериода") <> Неопределено
		И ЗначениеЗаполнено(СтрокаПервогоГоловногоСотрудника.РазрядКатегорияГоловногоСотрудникаНаКонецПериода) Тогда
		
		ПараметрыРабочегоМеста.РазрядКатегория = ", " + СтрокаПервогоГоловногоСотрудника.РазрядКатегорияГоловногоСотрудникаНаКонецПериода;
		
	КонецЕсли;
	
	ПараметрыРабочегоМеста.Оклад = СтрокаШапки.ТарифнаяСтавкаНаКонецПериода;
	
	ВывестиОбластиСтроки(ДокументРезультат, Макет, "РабочееМесто", НастройкиВывода, ПараметрыРабочегоМеста);
	
	// Объединение ячеек для поля Организация
	НастроитьОбъединение(ДокументРезультат, 5, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено) - 4, ДокументРезультат.ВысотаТаблицы - 1);
	
	// Объединение ячеек для поля Подразделение
	НастроитьОбъединение(ДокументРезультат, 5, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено) - 4);
	
	// Объединение ячеек для поля Должность
	НастроитьОбъединение(ДокументРезультат, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено) + 5,
		ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейУдержаноВыплачено) - 4, ДокументРезультат.ВысотаТаблицы - 1);
	
	#КонецОбласти
	
	
	Если ГруппироватьПоИсточникамФинансирования Тогда
		ИтогиПоГруппам = ВывестиГруппировкиПоСтатьямФинансирования(ДокументРезультат, Макет, НастройкиВывода, СтрокиГоловныхСотрудников, СоответствиеПользовательскихПолей);
	Иначе
		ИтогиПоГруппам = ВывестиТелоРасчетногоЛистка(ДокументРезультат, Макет, НастройкиВывода, СтрокиГоловныхСотрудников, СоответствиеПользовательскихПолей);
	КонецЕсли;
	
	// Взаимные задолженности взаиморасчетов
	ВывестиИтогиВзаиморасчетов(ДокументРезультат, Макет, НастройкиВывода, ИтогиПоГруппам);
	
	// Остатки взаиморасчетов по НДФЛ
	Если СтрокаДанныхПоНДФЛ <> Неопределено
		И (СтрокаДанныхПоНДФЛ.ЗарплатаКВыплатеНаНачалоПериода <> 0
			Или СтрокаДанныхПоНДФЛ.ЗарплатаКВыплатеНаКонецПериода <> 0) Тогда
		
		ДанныеЗадолженностиПоНДФЛ = Новый Структура;
		ДанныеЗадолженностиПоНДФЛ.Вставить("СуммаДолгНаНачалоПериодаНДФЛ", ИтогиПоГруппам.СуммаДолгаНаНачалоМесяцаРасчета - СтрокаДанныхПоНДФЛ.ЗарплатаКВыплатеНаНачалоПериода);
		ДанныеЗадолженностиПоНДФЛ.Вставить("СуммаДолгНаКонецПериодаНДФЛ", ИтогиПоГруппам.СуммаДолгаНаКонецМесяцаРасчета - СтрокаДанныхПоНДФЛ.ЗарплатаКВыплатеНаКонецПериода);
		
		Если ДанныеЗадолженностиПоНДФЛ.СуммаДолгНаНачалоПериодаНДФЛ < 0 Тогда
			ДанныеЗадолженностиПоНДФЛ.СуммаДолгНаНачалоПериодаНДФЛ = 0;
		КонецЕсли;
		
		Если ДанныеЗадолженностиПоНДФЛ.СуммаДолгНаКонецПериодаНДФЛ < 0 Тогда
			ДанныеЗадолженностиПоНДФЛ.СуммаДолгНаКонецПериодаНДФЛ = 0;
		КонецЕсли;
		
		Если ДанныеЗадолженностиПоНДФЛ.СуммаДолгНаНачалоПериодаНДФЛ > 0
			Или ДанныеЗадолженностиПоНДФЛ.СуммаДолгНаКонецПериодаНДФЛ > 0 Тогда
			
			ВывестиОбластиСтроки(ДокументРезультат, Макет, "ВТомЧислеНДФЛ", НастройкиВывода, ДанныеЗадолженностиПоНДФЛ);
			
			НастроитьОбъединение(ДокументРезультат, 1, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено, "СуммаНачислено"));
			НастроитьОбъединение(ДокументРезультат, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено) + 1,
				ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейУдержаноВыплачено, "СуммаУдержаноВыплачено"));
			
		КонецЕсли;
	
	КонецЕсли;
	
	// Вычеты НДФЛ, авансовые платежи
	Если НастройкиВывода.ВыводитьОсобенностиРасчетаНДФЛ = Истина И СтрокаДанныхПоНДФЛ <> Неопределено
		Или НастройкиВывода.ВыводитьИнформациюОНачисленныхВзносахВПФР = Истина И ВсегоВзносов <> 0 Тогда
		
		ТекстСекции = "";
		
		Если НастройкиВывода.ВыводитьИнформациюОНачисленныхВзносахВПФР = Истина Тогда
			
			Если ВсегоВзносов <> 0 Тогда
				
				ТекстСекции = ТекстСекции
					+ СтрокаСНепереносимымиПробелами(НСтр("ru='Страховые взносы в ПФР'") + ": " + Формат(ВсегоВзносов, "ЧДЦ=2; ЧН="));
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если НастройкиВывода.ВыводитьОсобенностиРасчетаНДФЛ = Истина Тогда
			
			Если Не ПустаяСтрока(ТекстСекции) Тогда
				ТекстСекции = ТекстСекции + Символы.ПС;
			КонецЕсли;
			
			ТекстСекции = ТекстСекции
				+ СтрокаСНепереносимымиПробелами(НСтр("ru='Общий облагаемый доход'") + ": " + Формат(СтрокаДанныхПоНДФЛ.ОблагаемыйДоход, "ЧДЦ=2; ЧН="));
			
			Если ЗначениеЗаполнено(СтрокаДанныхПоНДФЛ.АвансовыеПлатежи) Тогда
				
				Если Не ПустаяСтрока(ТекстСекции) Тогда
					ТекстСекции = ТекстСекции + "; ";
				КонецЕсли; 
				
				ТекстСекции = ТекстСекции
					+ СтрокаСНепереносимымиПробелами(НСтр("ru='Зачтено авансовых платежей'") + ": " + Формат(СтрокаДанныхПоНДФЛ.АвансовыеПлатежи, "ЧДЦ=2"));
				
			КонецЕсли;
			
			Если Не ПустаяСтрока(ТекстСекции) Тогда
				ТекстСекции = ТекстСекции + Символы.ПС;
			КонецЕсли;
			
			ДобавлятьТочкуСЗапятой = Ложь;
			Если СтрокаДанныхПоНДФЛ.ПоложеныЛичныеВычеты = Истина
				Или ЗначениеЗаполнено(СтрокаДанныхПоНДФЛ.ВычетНаФизлицо) Тогда
				
				ДобавлятьТочкуСЗапятой = Истина;
				ТекстСекции = ТекстСекции
					+ СтрокаСНепереносимымиПробелами(НСтр("ru='Вычетов личных'") + ": " + Формат(СтрокаДанныхПоНДФЛ.ВычетНаФизлицо, "ЧДЦ=2; ЧН="));
				
			КонецЕсли;
			
			Если СтрокаДанныхПоНДФЛ.ПоложеныВычетыНаДетей = Истина
				Или ЗначениеЗаполнено(СтрокаДанныхПоНДФЛ.ВычетНаДетей) Тогда
				
				Если ДобавлятьТочкуСЗапятой И Не ПустаяСтрока(ТекстСекции) Тогда
					ТекстСекции = ТекстСекции + "; ";
				Иначе
					ДобавлятьТочкуСЗапятой = Истина;
				КонецЕсли;
				
				ТекстСекции = ТекстСекции
					+ СтрокаСНепереносимымиПробелами(НСтр("ru='Вычетов на детей'") + ": " + Формат(СтрокаДанныхПоНДФЛ.ВычетНаДетей, "ЧДЦ=2; ЧН="));
				
			КонецЕсли; 
			
			Если ЗначениеЗаполнено(СтрокаДанныхПоНДФЛ.ВычетИмущественный) Тогда
				
				Если ДобавлятьТочкуСЗапятой И Не ПустаяСтрока(ТекстСекции) Тогда
					ТекстСекции = ТекстСекции + "; ";
				Иначе
					ДобавлятьТочкуСЗапятой = Истина;
				КонецЕсли;
				
				ТекстСекции = ТекстСекции
					+ СтрокаСНепереносимымиПробелами(НСтр("ru='Вычетов имущественных'") + ": " + Формат(СтрокаДанныхПоНДФЛ.ВычетИмущественный, "ЧДЦ=2; ЧН="));
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаДанныхПоНДФЛ.ВычетСоциальный) Тогда
				
				Если ДобавлятьТочкуСЗапятой И Не ПустаяСтрока(ТекстСекции) Тогда
					ТекстСекции = ТекстСекции + "; ";
				Иначе
					ДобавлятьТочкуСЗапятой = Истина;
				КонецЕсли; 
				
				ТекстСекции = ТекстСекции
					+ СтрокаСНепереносимымиПробелами(НСтр("ru='Вычетов социальных'") + ": " + Формат(СтрокаДанныхПоНДФЛ.ВычетСоциальный, "ЧДЦ=2; ЧН="));
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не ПустаяСтрока(ТекстСекции) Тогда
			ВывестиОбластиСтроки(ДокументРезультат, Макет, "ПодвалТаблицы", НастройкиВывода);
		КонецЕсли;
		
		ВывестиОбластиТекстовуюОбласть(ДокументРезультат, Макет, НастройкиВывода, СокрЛП(ТекстСекции));
		
	КонецЕсли;
	
	Ид = СтрокаШапки.ФизическоеЛицо.УникальныйИдентификатор();
	ДокументРезультат.Область(1, 1, ДокументРезультат.ВысотаТаблицы, ДокументРезультат.ШиринаТаблицы).Имя =
		"payslip_" + Формат(СтрокаШапки.МесяцНачисления, "ДФ=ММгггг") + "_0_" + СтрЗаменить(Ид, "-", "") + "_0";
	
	ВыведеныГруппировки = Ложь;
	Если ВыводимыеГруппировки.Количество() > 0 Тогда
		
		Если ДокументРезультатПромежуточный.ВысотаТаблицы > 0 Тогда
			ДокументРезультатПромежуточный.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		Если ДокументРезультатСтраница.ВысотаТаблицы > 0 Тогда
			ДокументРезультатПромежуточный.Вывести(ДокументРезультатСтраница);
			ДокументРезультатПромежуточный.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ДокументРезультатСтраница = Новый ТабличныйДокумент;
		
		Для каждого ВыводимаяОбласть Из ВыводимыеГруппировки Цикл
			ДокументРезультатСтраница.Вывести(ВыводимаяОбласть);
		КонецЦикла;
		
		ВыводимыеГруппировки.Очистить();
		
		ВыведеныГруппировки = Истина;
		
	КонецЕсли;
	
	ВывестиОбластиРасчетногоЛистка(ДокументРезультатПромежуточный, ДокументРезультатСтраница, РазделительЛистков, ДокументРезультат, ВыведеныГруппировки);
	
КонецПроцедуры

Процедура ВывестиОбластиРасчетногоЛистка(ДокументРезультатПромежуточный, ДокументРезультатСтраница, РазделительЛистков, ДокументРезультат, ВыведеныГруппировки)
	
	ВыводимыеОбласти = Новый Массив;
	ВыводимыеОбласти.Добавить(РазделительЛистков);
	ВыводимыеОбласти.Добавить(ДокументРезультат);
	
	Если ДокументРезультатСтраница.ВысотаТаблицы = 0 Или ВыведеныГруппировки Тогда
		ВыводимыеОбласти.Удалить(0);
	ИначеЕсли НЕ ДокументРезультатСтраница.ПроверитьВывод(ВыводимыеОбласти) Тогда
		
		ДокументРезультатПромежуточный.ВывестиГоризонтальныйРазделительСтраниц();
		ДокументРезультатПромежуточный.Вывести(ДокументРезультатСтраница);
		
		ДокументРезультатСтраница = Новый ТабличныйДокумент;
		ВыводимыеОбласти.Удалить(0);
		
	КонецЕсли;
	
	Для каждого ВыводимаяОбласть Из ВыводимыеОбласти Цикл
		ДокументРезультатСтраница.Вывести(ВыводимаяОбласть);
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиСотрудникаРасчетныйЛистокПоРабочимМестам(СтрокиГоловныхСотрудников, ДокументРезультатПромежуточный, Макеты, СоответствиеПользовательскихПолей, ДанныеПоНДФЛИВзносам, ГруппироватьПоИсточникамФинансирования)
	
	НастройкиПечатныхФорм = ЗарплатаКадрыПовтИсп.НастройкиПечатныхФорм();
	
	ДокументРезультат = Новый ТабличныйДокумент;
	
	СтрокаПервогоГоловногоСотрудника = СтрокиГоловныхСотрудников;
	
	Если ГруппироватьПоИсточникамФинансирования Тогда
		СтрокаШапки = СтрокаПервогоГоловногоСотрудника.Строки[0].Строки[0];
	Иначе
		СтрокаШапки = СтрокаПервогоГоловногоСотрудника.Строки[0];
	КонецЕсли;
	
	Макеты.Шапка1.Параметры.Заполнить(СтрокаШапки);
	Макеты.Шапка1.Параметры.ПериодПредставление = Формат(СтрокаШапки.МесяцНачисления, "ДФ='ММММ гггг'");
	
	ЗаполнитьПараметрыПользовательскихПолей(Макеты.Шапка1, СтрокаШапки, СоответствиеПользовательскихПолей);
	ЗаполнитьПараметрыПользовательскихПолей(Макеты.Шапка1, СтрокаПервогоГоловногоСотрудника, СоответствиеПользовательскихПолей, "КВыплате");

	Если СтрокаПервогоГоловногоСотрудника.Владелец().Колонки.Найти("РазрядКатегорияГоловногоСотрудникаНаКонецПериода") <> Неопределено
		И ЗначениеЗаполнено(СтрокаПервогоГоловногоСотрудника.РазрядКатегорияГоловногоСотрудникаНаКонецПериода) Тогда
		Макеты.Шапка1.Параметры.РазрядКатегорияСотрудника = ", " + СтрокаПервогоГоловногоСотрудника.РазрядКатегорияГоловногоСотрудникаНаКонецПериода;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Макеты.Шапка1.Параметры.КВыплате) ИЛИ Макеты.Шапка1.Параметры.КВыплате < 0 Тогда
		Макеты.Шапка1.Параметры.КВыплате = 0;
	КонецЕсли;
	
	СтрокаДанныхПоНДФЛ = ДанныеПоНДФЛИВзносам.НДФЛ.Найти(СтрокаШапки.ФизическоеЛицо, "ФизическоеЛицо");
	Если СтрокаДанныхПоНДФЛ <> Неопределено Тогда
		Макеты.Шапка1.Параметры.Заполнить(СтрокаДанныхПоНДФЛ);
	КонецЕсли;
	
	Если НастройкиПечатныхФорм.ВыводитьПолнуюИерархиюПодразделений И ЗначениеЗаполнено(Макеты.Шапка1.Параметры.ПодразделениеГоловногоСотрудникаНаКонецПериода) Тогда
		Макеты.Шапка1.Параметры.ПодразделениеГоловногоСотрудникаНаКонецПериода = Макеты.Шапка1.Параметры.ПодразделениеГоловногоСотрудникаНаКонецПериода.ПолноеНаименование();
	КонецЕсли;
	
	ДокументРезультат.Вывести(Макеты.Шапка1);
	
	Если СтрокаДанныхПоНДФЛ <> Неопределено
		И ЗначениеЗаполнено(СтрокаДанныхПоНДФЛ.АвансовыеПлатежи) Тогда
		
		Макеты.АвансовыеПлатежи.Параметры.АвансовыеПлатежи = СтрокаДанныхПоНДФЛ.АвансовыеПлатежи;
		ДокументРезультат.Вывести(Макеты.АвансовыеПлатежи);
		
	КонецЕсли;
	
	ДокументРезультат.Вывести(Макеты.Шапка2);
	
	Если ГруппироватьПоИсточникамФинансирования Тогда
		ИтогиПоГруппам = ВывестиГруппировкиПоСтатьямФинансированияПоРабочимМестам(ДокументРезультат, СтрокиГоловныхСотрудников,  Макеты, СоответствиеПользовательскихПолей);
	Иначе
		ИтогиПоГруппам = ВывестиТелоРасчетногоЛисткаПоРабочимМестам(ДокументРезультат, СтрокиГоловныхСотрудников, Макеты, СоответствиеПользовательскихПолей);
	КонецЕсли;
	
	Если СтрокаДанныхПоНДФЛ <> Неопределено Тогда
		Макеты.Сальдо.Параметры.Заполнить(СтрокаДанныхПоНДФЛ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИтогиПоГруппам.СуммаДолгаНаКонецМесяца) И ИтогиПоГруппам.СуммаДолгаНаКонецМесяца >= 0 Тогда
		Макеты.Сальдо.Параметры.ТекстДолгНаКонецПериода = НСтр("ru='Долг за предприятием на конец месяца'");
		Макеты.Сальдо.Параметры.СуммаДолгНаКонецПериода = ИтогиПоГруппам.СуммаДолгаНаКонецМесяца;
	ИначеЕсли ЗначениеЗаполнено(ИтогиПоГруппам.СуммаДолгаНаКонецМесяца) И ИтогиПоГруппам.СуммаДолгаНаКонецМесяца < 0 Тогда
		Макеты.Сальдо.Параметры.ТекстДолгНаКонецПериода = НСтр("ru='Долг за работником на конец месяца'");
		Макеты.Сальдо.Параметры.СуммаДолгНаКонецПериода = -ИтогиПоГруппам.СуммаДолгаНаКонецМесяца;
	Иначе
		Макеты.Сальдо.Параметры.ТекстДолгНаКонецПериода = НСтр("ru='Долг за предприятием на конец месяца'");
		Макеты.Сальдо.Параметры.СуммаДолгНаКонецПериода = "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца) И ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца >= 0 Тогда
		Макеты.Сальдо.Параметры.ТекстДолгНаНачалоПериода = НСтр("ru='Долг за предприятием на начало месяца'");
		Макеты.Сальдо.Параметры.СуммаДолгНаНачалоПериода = ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца;
	ИначеЕсли ЗначениеЗаполнено(ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца) И ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца < 0 Тогда
		Макеты.Сальдо.Параметры.ТекстДолгНаНачалоПериода = НСтр("ru='Долг за работником на начало месяца'");
		Макеты.Сальдо.Параметры.СуммаДолгНаНачалоПериода = -ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца;
	Иначе
		Макеты.Сальдо.Параметры.ТекстДолгНаНачалоПериода = НСтр("ru='Долг за предприятием на начало месяца'");
		Макеты.Сальдо.Параметры.СуммаДолгНаНачалоПериода = "";
	КонецЕсли;
	
	ДокументРезультат.Вывести(Макеты.Сальдо);
	
	Ид = СтрокаШапки.ФизическоеЛицо.УникальныйИдентификатор();
	ДокументРезультат.Область(1, 1, ДокументРезультат.ВысотаТаблицы, ДокументРезультат.ШиринаТаблицы).Имя =
		"payslip_" + Формат(СтрокаШапки.МесяцНачисления, "ДФ=ММгггг") + "_0_" + СтрЗаменить(Ид, "-", "") + "_0";
	
	Если НЕ ДокументРезультатПромежуточный.ПроверитьВывод(ДокументРезультат) Тогда
		ДокументРезультатПромежуточный.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;
	
	ДокументРезультатПромежуточный.Вывести(ДокументРезультат);
	
КонецПроцедуры

Функция ВывестиГруппировкиПоСтатьямФинансирования(ДокументРезультат, Макет, НастройкиВывода, СтрокиГоловныхСотрудников, СоответствиеПользовательскихПолей)
	
	ИтогиПоГруппам = ОписаниеИтоговПоГруппам();
	
	// Сбор сведений по всем рабочим местам
	СтатьиФинансирования = Новый Массив;
	СтрокиПоСтатьямФинансирования = Новый Соответствие;
	
	Для каждого СтрокаСотрудника Из СтрокиГоловныхСотрудников Цикл
		
		Для каждого СтрокаСтатьи Из СтрокаСотрудника.Строки Цикл
			
			СтрокиПоСтатьям = СтрокиПоСтатьямФинансирования.Получить(СтрокаСтатьи.СтатьяФинансирования);
			Если СтрокиПоСтатьям = Неопределено Тогда
				СтатьиФинансирования.Добавить(СтрокаСтатьи.СтатьяФинансирования);
				СтрокиПоСтатьям = Новый Массив;
			КонецЕсли;
			
			СтрокиПоСтатьям.Добавить(СтрокаСтатьи);
			СтрокиПоСтатьямФинансирования.Вставить(СтрокаСтатьи.СтатьяФинансирования, СтрокиПоСтатьям);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Для каждого СтатьяФинансирования Из СтатьиФинансирования Цикл
		
		СтрокиПоСтатьям = СтрокиПоСтатьямФинансирования.Получить(СтатьяФинансирования);
		
		Область = Макет.ПолучитьОбласть("СтатьяФинансирования");
		
		Если ЗначениеЗаполнено(СтатьяФинансирования) Тогда
			
			Область.Параметры.СтатьяФинансирования =
				?(ЗначениеЗаполнено(СтрокиПоСтатьям[0].СтатьяФинансированияКод), "(" + СтрокиПоСтатьям[0].СтатьяФинансированияКод + ")", "")
				+ ?(ЗначениеЗаполнено(СтрокиПоСтатьям[0].СтатьяФинансированияНаименование) , " " + СтрокиПоСтатьям[0].СтатьяФинансированияНаименование, "");
			
		КонецЕсли;
		
		ДокументРезультат.Вывести(Область);
		
		ИтогиПоСтатье = ВывестиТелоРасчетногоЛистка(ДокументРезультат, Макет, НастройкиВывода, СтрокиПоСтатьям, СоответствиеПользовательскихПолей);
		
		ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца = ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца + ИтогиПоСтатье.СуммаДолгаНаНачалоМесяца;
		ИтогиПоГруппам.СуммаДолгаНаКонецМесяца = ИтогиПоГруппам.СуммаДолгаНаКонецМесяца + ИтогиПоСтатье.СуммаДолгаНаКонецМесяца;
		
		ВывестиИтогиВзаиморасчетов(ДокументРезультат, Макет, НастройкиВывода, ИтогиПоСтатье);
		
	КонецЦикла;
	
	Область = Макет.ПолучитьОбласть("СтатьяФинансирования");
	Область.Параметры.СтатьяФинансирования = НСтр("ru='Итого по всем статьям финансирования'");
	ДокументРезультат.Вывести(Область);
	
	Возврат ИтогиПоГруппам;
	
КонецФункции

Функция ВывестиГруппировкиПоСтатьямФинансированияПоРабочимМестам(ДокументРезультат, СтрокиГоловныхСотрудников,  Макеты, СоответствиеПользовательскихПолей)
	
	ИтогиПоГруппам = ОписаниеИтоговПоГруппам();
	
	СтатьиФинансирования = Новый Массив;
	СтрокиПоСтатьямФинансирования = Новый Соответствие;
	
	Для каждого СтрокаСотрудника Из СтрокиГоловныхСотрудников.Строки Цикл
		
		СтрокиПоСтатьям = СтрокиПоСтатьямФинансирования.Получить(СтрокаСотрудника.СтатьяФинансирования);
		Если СтрокиПоСтатьям = Неопределено Тогда
			СтатьиФинансирования.Добавить(СтрокаСотрудника.СтатьяФинансирования);
			СтрокиПоСтатьям = Новый Массив;
		КонецЕсли;
		
		СтрокиПоСтатьям.Добавить(СтрокаСотрудника);
		СтрокиПоСтатьямФинансирования.Вставить(СтрокаСотрудника.СтатьяФинансирования, СтрокиПоСтатьям);
		
	КонецЦикла;
	
	Для каждого СтатьяФинансирования Из СтатьиФинансирования Цикл
		
		СтрокиПоСтатьям = СтрокиПоСтатьямФинансирования.Получить(СтатьяФинансирования);
		Для каждого СтрокаСтатьи Из СтрокиПоСтатьям Цикл
			
			Макеты.СтатьяФинансирования.Параметры.Заполнить(СтрокаСтатьи);
			
			Если ЗначениеЗаполнено(СтатьяФинансирования) Тогда
				
				Макеты.СтатьяФинансирования.Параметры.СтатьяФинансирования =
					?(ЗначениеЗаполнено(СтрокиПоСтатьям[0].СтатьяФинансированияКод), "(" + СтрокиПоСтатьям[0].СтатьяФинансированияКод + ")", "")
					+ ?(ЗначениеЗаполнено(СтрокиПоСтатьям[0].СтатьяФинансированияНаименование) , " " + СтрокиПоСтатьям[0].СтатьяФинансированияНаименование, "");
				
			КонецЕсли;
			
			ДокументРезультат.Вывести(Макеты.СтатьяФинансирования);
			
			ИтогиПоСтатье = ВывестиТелоРасчетногоЛисткаПоРабочимМестам(ДокументРезультат, СтрокаСтатьи, Макеты, СоответствиеПользовательскихПолей);
			
			ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца = ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца + ИтогиПоСтатье.СуммаДолгаНаНачалоМесяца;
			ИтогиПоГруппам.СуммаДолгаНаКонецМесяца = ИтогиПоГруппам.СуммаДолгаНаКонецМесяца + ИтогиПоСтатье.СуммаДолгаНаКонецМесяца;
			
			Если ЗначениеЗаполнено(ИтогиПоСтатье.СуммаДолгаНаКонецМесяца) И ИтогиПоСтатье.СуммаДолгаНаКонецМесяца >= 0 Тогда
				Макеты.ИтогСтатьяФинансирования.Параметры.ТекстДолгНаКонецПериода = НСтр("ru='Долг за предприятием на конец месяца'");
				Макеты.ИтогСтатьяФинансирования.Параметры.СуммаДолгНаКонецПериода = ИтогиПоСтатье.СуммаДолгаНаКонецМесяца;
			ИначеЕсли ЗначениеЗаполнено(ИтогиПоСтатье.СуммаДолгаНаКонецМесяца) И ИтогиПоСтатье.СуммаДолгаНаКонецМесяца < 0 Тогда
				Макеты.ИтогСтатьяФинансирования.Параметры.ТекстДолгНаКонецПериода = НСтр("ru='Долг за работником на конец месяца'");
				Макеты.ИтогСтатьяФинансирования.Параметры.СуммаДолгНаКонецПериода = -ИтогиПоСтатье.СуммаДолгаНаКонецМесяца;
			Иначе
				Макеты.ИтогСтатьяФинансирования.Параметры.ТекстДолгНаКонецПериода = НСтр("ru='Долг за предприятием на конец месяца'");
				Макеты.ИтогСтатьяФинансирования.Параметры.СуммаДолгНаКонецПериода = "";
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ИтогиПоСтатье.СуммаДолгаНаНачалоМесяца) И ИтогиПоСтатье.СуммаДолгаНаНачалоМесяца >= 0 Тогда
				Макеты.ИтогСтатьяФинансирования.Параметры.ТекстДолгНаНачалоПериода = НСтр("ru='Долг за предприятием на начало месяца'");
				Макеты.ИтогСтатьяФинансирования.Параметры.СуммаДолгНаНачалоПериода = ИтогиПоСтатье.СуммаДолгаНаНачалоМесяца;
			ИначеЕсли ЗначениеЗаполнено(ИтогиПоСтатье.СуммаДолгаНаНачалоМесяца) И ИтогиПоСтатье.СуммаДолгаНаНачалоМесяца < 0 Тогда
				Макеты.ИтогСтатьяФинансирования.Параметры.ТекстДолгНаНачалоПериода = НСтр("ru='Долг за работником на начало месяца'");
				Макеты.ИтогСтатьяФинансирования.Параметры.СуммаДолгНаНачалоПериода = -ИтогиПоСтатье.СуммаДолгаНаНачалоМесяца;
			Иначе
				Макеты.ИтогСтатьяФинансирования.Параметры.ТекстДолгНаНачалоПериода = НСтр("ru='Долг за предприятием на начало месяца'");
				Макеты.ИтогСтатьяФинансирования.Параметры.СуммаДолгНаНачалоПериода = "";
			КонецЕсли;
			
			ДокументРезультат.Вывести(Макеты.ИтогСтатьяФинансирования);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Макеты.СтатьяФинансирования.Параметры.СтатьяФинансирования =
		НСтр("ru='Итого по всем статьям финансирования'");
	ДокументРезультат.Вывести(Макеты.СтатьяФинансирования);
	
	Возврат ИтогиПоГруппам;
	
КонецФункции

Функция ВывестиТелоРасчетногоЛистка(ДокументРезультат, Макет, НастройкиВывода, СтрокиГоловныхСотрудников, СоответствиеПользовательскихПолей)
	
	ИтогиПоГруппам = ОписаниеИтоговПоГруппам();
	
	#Область СборСведений
	
	ИтогиРазделов = Новый Структура;
	ИтогиРазделов.Вставить("Начислено", 0);
	ИтогиРазделов.Вставить("Удержано", 0);
	ИтогиРазделов.Вставить("Выплачено", 0);
	ИтогиРазделов.Вставить("НачисленоВНатуральнойФорме", 0);
	
	ИмяПоляСуммаНачислено = СоответствиеПользовательскихПолей.Получить("СуммаНачислено");
	ИмяПоляСуммаНачисленоВНатуральнойФорме = СоответствиеПользовательскихПолей.Получить("СуммаНачисленоВНатуральнойФорме");
	ИмяПоляСуммаУдержано = СоответствиеПользовательскихПолей.Получить("СуммаУдержано");
	ИмяПоляСуммаСправочно = СоответствиеПользовательскихПолей.Получить("СуммаНачислено");
	ИмяПоляСуммаЛьготы = СоответствиеПользовательскихПолей.Получить("СуммаНачислено");
	ИмяПоляСуммаВыплачено = СоответствиеПользовательскихПолей.Получить("СуммаУдержано");
	
	КоллекцияНачислено = Новый ТаблицаЗначений;
	КоллекцияНачислено.Колонки.Добавить("ВидРасчета");
	КоллекцияНачислено.Колонки.Добавить("ПериодДействия");
	КоллекцияНачислено.Колонки.Добавить("ПериодДействияДатой");
	КоллекцияНачислено.Колонки.Добавить("МесяцНачисления");
	КоллекцияНачислено.Колонки.Добавить("ОтработаноДней");
	КоллекцияНачислено.Колонки.Добавить("ОтработаноЧасов");
	КоллекцияНачислено.Колонки.Добавить("ОплаченоДней");
	КоллекцияНачислено.Колонки.Добавить("ОплаченоЧасов");
	КоллекцияНачислено.Колонки.Добавить("Сумма");
	КоллекцияНачислено.Колонки.Добавить("ДокументОснование");
	КоллекцияНачислено.Колонки.Добавить("Показатель");
	КоллекцияНачислено.Колонки.Добавить("Значение");
	
	КоллекцияУдержано = Новый ТаблицаЗначений;
	КоллекцияУдержано.Колонки.Добавить("ВидРасчета");
	КоллекцияУдержано.Колонки.Добавить("ДокументОснование");
	КоллекцияУдержано.Колонки.Добавить("ПериодДействия");
	КоллекцияУдержано.Колонки.Добавить("Сумма");
	КоллекцияУдержано.Колонки.Добавить("Показатель");
	КоллекцияУдержано.Колонки.Добавить("Значение");
	
	КоллекцияВыплачено = Новый ТаблицаЗначений;
	КоллекцияВыплачено.Колонки.Добавить("ВидРасчета");
	КоллекцияВыплачено.Колонки.Добавить("ПериодДействия");
	КоллекцияВыплачено.Колонки.Добавить("Сумма");
	КоллекцияВыплачено.Колонки.Добавить("Номер");
	КоллекцияВыплачено.Колонки.Добавить("Дата");
	
	КоллекцииРазделов = Новый Структура;
	КоллекцииРазделов.Вставить("Начислено", Новый Массив);
	КоллекцииРазделов.Вставить("Удержано", 	КоллекцияУдержано);
	КоллекцииРазделов.Вставить("Выплачено", КоллекцияВыплачено);
	КоллекцииРазделов.Вставить("Льготы", 	Новый Массив);
	КоллекцииРазделов.Вставить("Справочно", Новый Массив);
	
	ИзвестныеСтрокиСправочно = Новый Соответствие;
	ИзвестныеСтрокиВыплат = Новый Соответствие;
	ИзвестныеСтрокиЛьготы = Новый Соответствие;
	
	Для каждого СтрокаГоловногоСотрудника Из СтрокиГоловныхСотрудников Цикл
		
		Для каждого СтрокаСотрудника Из СтрокаГоловногоСотрудника.Строки Цикл
			
			// Начислено
			РабочееМесто = Новый Структура("СтрокаСотрудника,СтрокиНачислений", СтрокаСотрудника, КоллекцияНачислено.Скопировать());
			
			СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.Начислено, "Группа");
			Если СтрокаГруппы <> Неопределено Тогда
				
				СторноСтроки = Новый Соответствие;
				РегистраторыНачислений = Новый Соответствие;
				Для каждого СтрокаНачислений Из СтрокаГруппы.Строки Цикл
					
					СтрокаКоллекции = РабочееМесто.СтрокиНачислений.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаКоллекции, СтрокаНачислений);
					
					Если СтрокаНачислений.ПериодДействия = СтрокаНачислений.МесяцНачисления Тогда
						
						Если СтрокаНачислений.Сторно = Истина Тогда
							СторноСтроки.Вставить(СтрокаКоллекции, СтрокаНачислений.Регистратор);
						Иначе
							
							СтрокиРегистратора = РегистраторыНачислений.Получить(СтрокаНачислений.Регистратор);
							Если СтрокиРегистратора = Неопределено Тогда
								СтрокиРегистратора = Новый Соответствие;
								РегистраторыНачислений.Вставить(СтрокаНачислений.Регистратор, СтрокиРегистратора);
							КонецЕсли;
							
							СтрокиРегистратора.Вставить(СтрокаНачислений.ВидРасчета, СтрокаКоллекции);
							
						КонецЕсли;
						
					КонецЕсли;
					
					Если НастройкиВывода.ВыводитьПериодыНачислений = Истина Тогда
						
						СтрокаКоллекции.ПериодДействияДатой = НачалоМесяца(СтрокаКоллекции.ПериодДействия);
						Если ЗначениеЗаполнено(СтрокаНачислений.ДатаНачала) И ЗначениеЗаполнено(СтрокаНачислений.ДатаОкончания) Тогда
							
							Если СтрокаНачислений.ДатаНачала <> СтрокаНачислений.ПериодДействия
								Или СтрокаНачислений.ДатаОкончания <> НачалоДня(КонецМесяца(СтрокаНачислений.ПериодДействия)) Тогда
								
								Если СтрокаНачислений.ДатаНачала = СтрокаНачислений.ДатаОкончания Тогда
									СтрокаКоллекции.ПериодДействия = Формат(СтрокаНачислений.ДатаНачала, "ДФ=дд.ММ");
								Иначе
									
									СтрокаКоллекции.ПериодДействия = Формат(СтрокаНачислений.ДатаНачала, "ДФ=дд.ММ") + "-"
										+ Формат(СтрокаНачислений.ДатаОкончания, "ДФ=дд.ММ");
									
								КонецЕсли;
								
							КонецЕсли;
							
						КонецЕсли;
						
					КонецЕсли;
					
					Если НастройкиВывода.ВыводитьОтработанноеОплаченноеВремя = Истина Тогда
						
						СтрокаКоллекции.ОплаченоДней = 0;
						СтрокаКоллекции.ОплаченоЧасов = 0;
						
						УчетВремениВЧасах = СтрокаНачислений.ВремяВЧасах = Истина;
						
						Если УчетВремениВЧасах И ЗначениеЗаполнено(СтрокаНачислений.ОплаченоЧасов) И СтрокаНачислений.ОплаченоЧасов <> 0 Тогда
							СтрокаКоллекции.ОплаченоЧасов = СтрокаНачислений.ОплаченоЧасов;
						ИначеЕсли НЕ УчетВремениВЧасах И ЗначениеЗаполнено(СтрокаНачислений.ОплаченоДней) И СтрокаНачислений.ОплаченоДней <> 0 Тогда
							СтрокаКоллекции.ОплаченоДней = СтрокаНачислений.ОплаченоДней;
						КонецЕсли;
						
					КонецЕсли;
					
					СтрокаКоллекции.Сумма = СтрокаНачислений[ИмяПоляСуммаНачислено];
					
					ИтогиРазделов.Начислено = ИтогиРазделов.Начислено + СтрокаНачислений[ИмяПоляСуммаНачислено];
					ИтогиРазделов.НачисленоВНатуральнойФорме = ИтогиРазделов.НачисленоВНатуральнойФорме + СтрокаНачислений[ИмяПоляСуммаНачисленоВНатуральнойФорме];					
					
				КонецЦикла;
				
			КонецЕсли;
			
			Если РабочееМесто.СтрокиНачислений.Количество() > 0 Тогда
				
				Если СторноСтроки.Количество() > 0 Тогда
					
					Для Каждого ОписаниеСторноСтроки Из СторноСтроки Цикл
						
						СторноСтрока = ОписаниеСторноСтроки.Ключ;
						СтрокиРегистратора = РегистраторыНачислений.Получить(ОписаниеСторноСтроки.Значение);
						Если СтрокиРегистратора <> Неопределено Тогда
							
							СтрокаВидаРасчета = СтрокиРегистратора.Получить(СторноСтрока.ВидРасчета);
							Если СтрокаВидаРасчета <> Неопределено Тогда
								ЗаполнитьЗначенияСвойств(СторноСтрока, СтрокаВидаРасчета, "ПериодДействия,ПериодДействияДатой,МесяцНачисления,ДокументОснование");
							КонецЕсли;
							
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
				СворачиваемыеКолонки = "ВидРасчета";
				СуммируемыеКолонки = "Сумма";
				
				Если НастройкиВывода.ВыводитьПериодыНачислений = Истина Тогда
					СворачиваемыеКолонки = СворачиваемыеКолонки + ",ПериодДействия,ПериодДействияДатой,МесяцНачисления";
				КонецЕсли;
				
				Если НастройкиВывода.ВыводитьОтработанноеОплаченноеВремя = Истина Тогда
					СуммируемыеКолонки = СуммируемыеКолонки + ",ОтработаноДней,ОтработаноЧасов,ОплаченоДней,ОплаченоЧасов";
				КонецЕсли;
				
				Если НастройкиВывода.ВыводитьОснованияНачисленийИУдержаний = Истина Тогда
					СворачиваемыеКолонки = СворачиваемыеКолонки + ",ДокументОснование";
				КонецЕсли;
				
				Если НастройкиВывода.ВыводитьПоказателиНачисленийИУдержаний = Истина Тогда
					СворачиваемыеКолонки = СворачиваемыеКолонки + ",Показатель,Значение";
				КонецЕсли;
				
				РабочееМесто.СтрокиНачислений.Свернуть(СворачиваемыеКолонки, СуммируемыеКолонки);
				
				Если НастройкиВывода.ВыводитьПериодыНачислений = Истина Тогда
					
					УдаляемыеСтроки = Новый Массив;
					Для каждого СтрокаНачислений Из РабочееМесто.СтрокиНачислений Цикл
						
						Если СтрокаНачислений.ПериодДействияДатой = СтрокаНачислений.МесяцНачисления Тогда
							Продолжить;
						КонецЕсли;
						
						УдалитьСтроку = Истина;
						Для каждого ИмяКолонки Из СтрРазделить(СуммируемыеКолонки, ",") Цикл
							Если СтрокаНачислений[ИмяКолонки] <> 0 Тогда
								УдалитьСтроку = Ложь;
								Прервать;
							КонецЕсли;
						КонецЦикла;
						
						Если УдалитьСтроку Тогда
							УдаляемыеСтроки.Добавить(СтрокаНачислений);
						КонецЕсли;
						
					КонецЦикла;
					
					Для Каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
						РабочееМесто.СтрокиНачислений.Удалить(УдаляемаяСтрока);
					КонецЦикла;
					
				КонецЕсли;
				
				КоллекцииРазделов.Начислено.Добавить(РабочееМесто);
				
			КонецЕсли;
			
			// Удержано
			СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано, "Группа");
			Если СтрокаГруппы <> Неопределено Тогда
				
				Для каждого СтрокаУдержаний Из СтрокаГруппы.Строки Цикл
					
					СтрокаКоллекции = КоллекцииРазделов.Удержано.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаКоллекции, СтрокаУдержаний);
					СтрокаКоллекции.Сумма = СтрокаУдержаний[ИмяПоляСуммаУдержано];
					
					ИтогиРазделов.Удержано = ИтогиРазделов.Удержано + СтрокаУдержаний[ИмяПоляСуммаУдержано];
					
				КонецЦикла;
				
				СворачиваемыеКолонки = "ВидРасчета";
				
				Если НастройкиВывода.ВыводитьОснованияНачисленийИУдержаний = Истина Тогда
					СворачиваемыеКолонки = СворачиваемыеКолонки + ",ДокументОснование";
				КонецЕсли;
				
				Если НастройкиВывода.ВыводитьПериодыНачислений = Истина Тогда
					СворачиваемыеКолонки = СворачиваемыеКолонки + ",ПериодДействия";
				КонецЕсли;
				
				Если НастройкиВывода.ВыводитьПоказателиНачисленийИУдержаний = Истина Тогда
					СворачиваемыеКолонки = СворачиваемыеКолонки + ",Показатель,Значение";
				КонецЕсли;
				
				КоллекцииРазделов.Удержано.Свернуть(СворачиваемыеКолонки, "Сумма");
				
			КонецЕсли;
			
			// Выплачено
			СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.Выплачено, "Группа");
			Если СтрокаГруппы <> Неопределено Тогда
				
				Для каждого СтрокаВыплачено Из СтрокаГруппы.Строки Цикл
					
					Для каждого СтрокаСРегистратором Из СтрокаВыплачено.Строки Цикл
						
						СтрокаКоллекции = ИзвестныеСтрокиВыплат.Получить(СтрокаСРегистратором.Регистратор);
						Если СтрокаКоллекции = Неопределено Тогда
							
							СтрокаКоллекции = КоллекцииРазделов.Выплачено.Добавить();
							СтрокаКоллекции.ВидРасчета = Строка(СтрокаСРегистратором.ВидРасчета);
							
							Если НастройкиВывода.ДетализироватьВыплатыПоВедомостям = Истина 
								И ЗначениеЗаполнено(СтрокаСРегистратором.Регистратор) Тогда
								
								УстановитьПривилегированныйРежим(Истина);
								СвойстваРегистратора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаСРегистратором.Регистратор, "Номер,Дата");
								УстановитьПривилегированныйРежим(Ложь);
								
								СтрокаКоллекции.Номер = СвойстваРегистратора.Номер;
								СтрокаКоллекции.Дата = СвойстваРегистратора.Дата;
								
								СтрокаКоллекции.ВидРасчета = СтрокаКоллекции.ВидРасчета + " "
									+ ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(СвойстваРегистратора.Номер, Истина, Истина)	+ " "
									+ НСтр("ru='от'") + " " + Формат(СвойстваРегистратора.Дата, "ДФ=dd.MM.yy");	
								
							КонецЕсли;
							
							Если НастройкиВывода.ВыводитьПериодыНачислений = Истина Тогда
								СтрокаКоллекции.ПериодДействия = СтрокаСРегистратором.ПериодДействия;
							КонецЕсли;
							
							СтрокаКоллекции.Сумма = СтрокаСРегистратором[ИмяПоляСуммаВыплачено];
							
							ИзвестныеСтрокиВыплат.Вставить(СтрокаСРегистратором.Регистратор, СтрокаКоллекции);
							
						Иначе
							СтрокаКоллекции.Сумма = СтрокаКоллекции.Сумма + СтрокаСРегистратором[ИмяПоляСуммаВыплачено];
						КонецЕсли;
						
					КонецЦикла;
					
					ИтогиРазделов.Выплачено = ИтогиРазделов.Выплачено + СтрокаВыплачено[ИмяПоляСуммаВыплачено];
					
				КонецЦикла;
				
			КонецЕсли;
			
			// Льготы
			Если НастройкиВывода.ВыводитьИнформациюОЛьготах = Истина Тогда
				
				СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.Льготы, "Группа");
				Если СтрокаГруппы <> Неопределено Тогда
					
					Для каждого СтрокаЛьготы Из СтрокаГруппы.Строки Цикл
						
						СтрокаКоллекции = ИзвестныеСтрокиЛьготы.Получить(СтрокаЛьготы.ВидРасчета);
						Если СтрокаКоллекции = Неопределено Тогда
							СтрокаКоллекции = Новый Структура("ВидРасчета,Сумма");
							СтрокаКоллекции.ВидРасчета = СтрокаЛьготы.ВидРасчета;
							СтрокаКоллекции.Сумма = СтрокаЛьготы[ИмяПоляСуммаЛьготы];
							КоллекцииРазделов.Льготы.Добавить(СтрокаКоллекции);
							ИзвестныеСтрокиЛьготы.Вставить(СтрокаЛьготы.ВидРасчета, СтрокаКоллекции);
						Иначе
							СтрокаКоллекции.Сумма = СтрокаКоллекции.Сумма + СтрокаЛьготы[ИмяПоляСуммаЛьготы];
						КонецЕсли;
						
					КонецЦикла;
				
				КонецЕсли;
			
			КонецЕсли;
			
			// Справочно
			Если НастройкиВывода.ВыводитьСправочнуюИнформацию = Истина Тогда
				
				СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.Справочно, "Группа");
				Если СтрокаГруппы <> Неопределено Тогда
					
					Для каждого СтрокаСправочно Из СтрокаГруппы.Строки Цикл
						
						СтрокаКоллекции = ИзвестныеСтрокиСправочно.Получить(СтрокаСправочно.ВидРасчета);
						Если СтрокаКоллекции = Неопределено Тогда
							СтрокаКоллекции = Новый Структура("ВидРасчета,Сумма");
							СтрокаКоллекции.ВидРасчета = СтрокаСправочно.ВидРасчета;
							СтрокаКоллекции.Сумма = СтрокаСправочно[ИмяПоляСуммаСправочно];
							КоллекцииРазделов.Справочно.Добавить(СтрокаКоллекции);
							ИзвестныеСтрокиСправочно.Вставить(СтрокаСправочно.ВидРасчета, СтрокаКоллекции);
						Иначе
							СтрокаКоллекции.Сумма = СтрокаКоллекции.Сумма + СтрокаСправочно[ИмяПоляСуммаСправочно];
						КонецЕсли;
						
					КонецЦикла;
				
				КонецЕсли;
				
			КонецЕсли;
			
			// Начальное сальдо
			СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.НачальноеСальдо, "Группа");
			Если СтрокаГруппы <> Неопределено Тогда
				ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца = ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца + СтрокаГруппы.Сумма;
			КонецЕсли;
			
			СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.НачальноеСальдоПоМесяцамРасчета, "Группа");
			Если СтрокаГруппы <> Неопределено Тогда
				ИтогиПоГруппам.СуммаДолгаНаНачалоМесяцаРасчета = ИтогиПоГруппам.СуммаДолгаНаНачалоМесяцаРасчета + СтрокаГруппы.Сумма;
			КонецЕсли;
			
			// КонечноеСальдо
			СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.КонечноеСальдо, "Группа");
			Если СтрокаГруппы <> Неопределено Тогда
				ИтогиПоГруппам.СуммаДолгаНаКонецМесяца = ИтогиПоГруппам.СуммаДолгаНаКонецМесяца + СтрокаГруппы.Сумма;
			КонецЕсли;
			
			СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.КонечноеСальдоПоМесяцамРасчета, "Группа");
			Если СтрокаГруппы <> Неопределено Тогда
				ИтогиПоГруппам.СуммаДолгаНаКонецМесяцаРасчета = ИтогиПоГруппам.СуммаДолгаНаКонецМесяцаРасчета + СтрокаГруппы.Сумма;
			КонецЕсли;
			
			Если НастройкиВывода.ПредварительныйПросмотр Тогда
				
				ИтогиПоГруппам.СуммаДолгаНаКонецМесяца =
					ИтогиПоГруппам.СуммаДолгаНаКонецМесяца
					+ ИтогиРазделов.Начислено
					- ИтогиРазделов.Удержано
					- ИтогиРазделов.Выплачено;
				
				ИтогиПоГруппам.СуммаДолгаНаКонецМесяцаРасчета =
					ИтогиПоГруппам.СуммаДолгаНаКонецМесяцаРасчета
					+ ИтогиРазделов.Начислено
					- ИтогиРазделов.Удержано
					- ИтогиРазделов.Выплачено;
				
				КонецЕсли;
				
		КонецЦикла;
		
	КонецЦикла;
	
	СворачиваемыеКолонки = "ВидРасчета";
	Если НастройкиВывода.ДетализироватьВыплатыПоВедомостям Тогда
		СворачиваемыеКолонки = СворачиваемыеКолонки + ",Дата,Номер";
		КоллекцииРазделов.Выплачено.Сортировать("Дата,Номер");
	КонецЕсли;
	Если НастройкиВывода.ВыводитьПериодыНачислений = Истина Тогда
		СворачиваемыеКолонки = СворачиваемыеКолонки + ",ПериодДействия";
	КонецЕсли;
	КоллекцииРазделов.Выплачено.Свернуть(СворачиваемыеКолонки, "Сумма");
	
	УдалитьСтрокиСНулевойСуммой(КоллекцииРазделов.Выплачено);
	
	#КонецОбласти
	
	#Область ВыводТела
	
	СтрокНачислено = 0;
	Для каждого СтрокиРабочегоМеста Из КоллекцииРазделов.Начислено Цикл
		СтрокНачислено = СтрокНачислено + СтрокиРабочегоМеста.СтрокиНачислений.Количество() + 1;
	КонецЦикла;
	
	Если СтрокНачислено > 0
		И КоллекцииРазделов.Начислено[0].СтрокаСотрудника.Сотрудник <> КоллекцииРазделов.Начислено[0].СтрокаСотрудника.ГоловнойСотрудник Тогда
		
		СтрокНачислено = СтрокНачислено + 1;
		
	КонецЕсли;
	
	Если ИтогиРазделов.НачисленоВНатуральнойФорме <> 0 Тогда
		ЕстьИтогиПоНачисленоВНатуральнойФорме = Истина;
		СтрокНачислено = СтрокНачислено + 1;
	Иначе
		ЕстьИтогиПоНачисленоВНатуральнойФорме = Ложь;
	КонецЕсли;
	
	КоличествоСтрокУдержано = КоллекцииРазделов.Удержано.Количество();
	КоличествоСтрокВыплачено = КоллекцииРазделов.Выплачено.Количество();
	СтрокУдержаноВыплачено = КоличествоСтрокУдержано + КоличествоСтрокВыплачено + 2;
	
	// Вывод шапки
	Если НастройкиВывода.ВыводитьОтработанноеОплаченноеВремя = Истина Тогда
		ВывестиОбластиСтроки(ДокументРезультат, Макет, "ШапкаТаблицы", НастройкиВывода);
	Иначе
		ВывестиОбластиСтроки(ДокументРезультат, Макет, "ШапкаТаблицыНизкая", НастройкиВывода);
	КонецЕсли;
	
	// Вывод таблиц Начислено, Удержано, Выплачено
	
	СтрокВыведено = 0;
	НомерРабочегоМеста = 0;
	ИндексРабочегоМеста = 0;
	СтрокРабочегоМеста = 0;
	ВывестиЗаголовокРабочегоМеста = Ложь;
	
	Пока СтрокВыведено < СтрокНачислено Или СтрокВыведено < СтрокУдержаноВыплачено Цикл
		
		РасшифровкаНачисленийУдержаний = Новый ТаблицаЗначений;
		РасшифровкаНачисленийУдержаний.Колонки.Добавить("РасшифровкаНачисления");
		РасшифровкаНачисленийУдержаний.Колонки.Добавить("РасшифровкаУдержания");
		
		// Вывод Начислено
		ПрисоединитьОбласть = Ложь;
		Если КоллекцииРазделов.Начислено.Количество() = 0 Тогда
			
			Если СтрокВыведено = 0 Тогда
				ВывестиОбластиСтрокиНачислено(ДокументРезультат, Макет, "ЗаголовокНачисленоУдержано", НастройкиВывода);
			Иначе
				ВывестиОбластиСтрокиНачислено(ДокументРезультат, Макет, "Строка", НастройкиВывода);
			КонецЕсли;
			
		ИначеЕсли ИндексРабочегоМеста < КоллекцииРазделов.Начислено.Количество() Тогда
			
			ПараметрыЗаполнения = Новый Структура("РабочееМесто,ВидРасчета,ПериодДействия,ОтработаноДней,ОтработаноЧасов,ОплаченныеДниЧасы,Сумма");
			
			РабочееМесто = КоллекцииРазделов.Начислено[ИндексРабочегоМеста];
			Если СтрокВыведено = 0 Или ЕстьИтогиПоНачисленоВНатуральнойФорме И СтрокВыведено = 1 Тогда
				
				Если СтрокВыведено = 0 Тогда
					
					ИмяОбластиСтроки = "ЗаголовокНачисленоУдержано";
					
					Если РабочееМесто.СтрокаСотрудника.Сотрудник <> РабочееМесто.СтрокаСотрудника.ГоловнойСотрудник Тогда
						ВывестиЗаголовокРабочегоМеста = Истина;
					КонецЕсли;
					
					ИндексСтрокиНачислено = 0;
					ПараметрыЗаполнения.Сумма = ИтогиРазделов.Начислено;
					
				Иначе
					ИмяОбластиСтроки = "ЗаголовокНачисленоВНатуральнойФорме";
					ПараметрыЗаполнения.Сумма = ИтогиРазделов.НачисленоВНатуральнойФорме;
				КонецЕсли;
				
			Иначе
				
				Если ВывестиЗаголовокРабочегоМеста Тогда
					
					ИмяОбластиСтроки = "ЗаголовокРабочееМестоВыплачено";
					Если РабочееМесто.СтрокаСотрудника.Сотрудник <> РабочееМесто.СтрокаСотрудника.ГоловнойСотрудник Тогда
						ПараметрыЗаполнения.РабочееМесто = РабочееМесто.СтрокаСотрудника.СотрудникУточнениеНаименования;
					Иначе
						ПараметрыЗаполнения.РабочееМесто = 
							Строка(РабочееМесто.СтрокаСотрудника.ДолжностьНаКонецПериода)
							+ " (" + РабочееМесто.СтрокаСотрудника.ВидЗанятостиГоловногоСотрудникаНаКонецПериода + ")";
					КонецЕсли;
					
				Иначе
					
					ИмяОбластиСтроки = "Строка";
					Если ИндексСтрокиНачислено < РабочееМесто.СтрокиНачислений.Количество() Тогда
						
						СтрокаНачислений = РабочееМесто.СтрокиНачислений[ИндексСтрокиНачислено];
						ЗаполнитьЗначенияСвойств(ПараметрыЗаполнения, СтрокаНачислений);
						
						Если НастройкиВывода.ВыводитьОснованияНачисленийИУдержаний = Истина
							И ЗначениеЗаполнено(СтрокаНачислений.ДокументОснование) Тогда
							
							ДобавитьРасшифровкуНачисления(РасшифровкаНачисленийУдержаний, "РасшифровкаНачисления", Строка(СтрокаНачислений.ДокументОснование));
							
						КонецЕсли;
						
						Если НастройкиВывода.ВыводитьПоказателиНачисленийИУдержаний = Истина
							И ЗначениеЗаполнено(СтрокаНачислений.Показатель) Тогда
							
							ДобавитьРасшифровкуНачисления(РасшифровкаНачисленийУдержаний, "РасшифровкаНачисления", Строка(СтрокаНачислений.Показатель) + ":  " + СтрокаНачислений.Значение);
							
						КонецЕсли;
						
						Если НастройкиВывода.ВыводитьОтработанноеОплаченноеВремя = Истина Тогда
							
							Если ЗначениеЗаполнено(СтрокаНачислений.ОплаченоДней) И СтрокаНачислений.ОплаченоДней <> 0 Тогда
								ПараметрыЗаполнения.ОплаченныеДниЧасы = Формат(СтрокаНачислений.ОплаченоДней, "ЧДЦ=2; ЧН=") + " " + НСтр("ru='дн'") + ".";
							КонецЕсли;
							
							Если ЗначениеЗаполнено(СтрокаНачислений.ОплаченоЧасов) И СтрокаНачислений.ОплаченоЧасов <> 0 Тогда
								
								Если Не ПустаяСтрока(ПараметрыЗаполнения.ОплаченныеДниЧасы) Тогда
									ПараметрыЗаполнения.ОплаченныеДниЧасы = ПараметрыЗаполнения.ОплаченныеДниЧасы + " " + НСтр("ru='и'") + " ";
								КонецЕсли;
								
								ПараметрыЗаполнения.ОплаченныеДниЧасы = Формат(СтрокаНачислений.ОплаченоЧасов, "ЧДЦ=2; ЧН=") + " " + НСтр("ru='чс'") + ".";
								
							КонецЕсли;
							
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
				
				Если Не ВывестиЗаголовокРабочегоМеста Тогда
					ИндексСтрокиНачислено = ИндексСтрокиНачислено + 1;
				КонецЕсли;
				
				Если ИндексСтрокиНачислено >= РабочееМесто.СтрокиНачислений.Количество() Тогда
					ИндексРабочегоМеста = ИндексРабочегоМеста + 1;
					ИндексСтрокиНачислено = 0;
					ВывестиЗаголовокРабочегоМеста = Истина;
				Иначе
					ВывестиЗаголовокРабочегоМеста = Ложь;
				КонецЕсли;
				
			КонецЕсли;
			
			ВывестиОбластиСтрокиНачислено(ДокументРезультат, Макет, ИмяОбластиСтроки, НастройкиВывода, ПараметрыЗаполнения);
			
		Иначе
			
			ВывестиОбластиСтрокиНачислено(ДокументРезультат, Макет, "Строка", НастройкиВывода);
			
		КонецЕсли;
		
		// Вывод Удержано, Выплачено
		Если СтрокВыведено <= СтрокУдержаноВыплачено Тогда
			
			ПараметрыЗаполнения = Новый Структура("ВидРасчета,ПериодДействия,Сумма");
			
			ИндексСтрокиУдержано = СтрокВыведено - 1;
			Если ИндексСтрокиУдержано < КоличествоСтрокУдержано Тогда
				
				Если ИндексСтрокиУдержано = -1 Тогда
					ИмяОбластиСтроки = "ЗаголовокНачисленоУдержано";
					ИмяОбластиРасшифровки = "ЗаголовокРасшифровка";
					ПараметрыЗаполнения.Сумма = ИтогиРазделов.Удержано;
				Иначе
					
					ИмяОбластиСтроки = "Строка";
					ИмяОбластиРасшифровки = "СтрокаРасшифровка";
					СтрокаУдержаний = КоллекцииРазделов.Удержано[ИндексСтрокиУдержано];
					
					Если НастройкиВывода.ВыводитьОснованияНачисленийИУдержаний = Истина
						И ЗначениеЗаполнено(СтрокаУдержаний.ДокументОснование) Тогда
						
						ДобавитьРасшифровкуНачисления(РасшифровкаНачисленийУдержаний, "РасшифровкаУдержания", Строка(СтрокаУдержаний.ДокументОснование));
						
					КонецЕсли;
				
					Если НастройкиВывода.ВыводитьПоказателиНачисленийИУдержаний = Истина
						И ЗначениеЗаполнено(СтрокаУдержаний.Показатель) Тогда
						
						ДобавитьРасшифровкуНачисления(РасшифровкаНачисленийУдержаний, "РасшифровкаУдержания", Строка(СтрокаУдержаний.Показатель) + ":  " + СтрокаУдержаний.Значение);
						
					КонецЕсли;
					
					ПараметрыЗаполнения = СтрокаУдержаний;
					
				КонецЕсли;
				
			Иначе
				
				ИндексСтрокиВыплачено = СтрокВыведено - 2 - КоличествоСтрокУдержано;
				Если ИндексСтрокиВыплачено < КоличествоСтрокВыплачено Тогда
					
					Если ИндексСтрокиВыплачено = -1 Тогда
						ИмяОбластиСтроки = "ЗаголовокРабочееМестоВыплачено";
						ИмяОбластиРасшифровки = "ЗаголовокРасшифровка";
						ПараметрыЗаполнения.Сумма = ИтогиРазделов.Выплачено;
					Иначе
						
						ИмяОбластиСтроки = "Строка";
						ИмяОбластиРасшифровки = "СтрокаРасшифровка";
						ПараметрыЗаполнения = КоллекцииРазделов.Выплачено[ИндексСтрокиВыплачено];
						
					КонецЕсли;
					
				Иначе
					ИмяОбластиСтроки = "Строка";
					ИмяОбластиРасшифровки = "СтрокаРасшифровка";
				КонецЕсли;
				
			КонецЕсли;
			
			ПрисоединитьОбластиСтрокиУдержаноВыплачено(ДокументРезультат, Макет, ИмяОбластиСтроки, НастройкиВывода, ПараметрыЗаполнения);
			
		Иначе
			ПрисоединитьОбластиСтрокиУдержаноВыплачено(ДокументРезультат, Макет, "Строка", НастройкиВывода);
		КонецЕсли;
		
		Для Каждого СтрокаРасшифровкаНачисленийУдержаний Из РасшифровкаНачисленийУдержаний Цикл
			
			ПараметрыЗаполненияРасшифровки = Новый Структура("ВидРасчетаРасшифровка", СтрокаРасшифровкаНачисленийУдержаний.РасшифровкаНачисления);
			ВывестиОбластиСтрокиНачислено(ДокументРезультат, Макет, "СтрокаРасшифровка", НастройкиВывода, ПараметрыЗаполненияРасшифровки);
			
			ПараметрыЗаполненияРасшифровки = Новый Структура("ВидРасчетаРасшифровка", СтрокаРасшифровкаНачисленийУдержаний.РасшифровкаУдержания);
			ПрисоединитьОбластиСтрокиУдержаноВыплачено(ДокументРезультат, Макет, ИмяОбластиРасшифровки, НастройкиВывода, ПараметрыЗаполненияРасшифровки);
			
		КонецЦикла;
		
		СтрокВыведено = СтрокВыведено + 1;
		
	КонецЦикла;
	
	ВывестиОбластиСтроки(ДокументРезультат, Макет, "ПодвалТаблицы", НастройкиВывода);
	
	// Льготы
	
	Если КоллекцииРазделов.Льготы.Количество() > 0 Тогда
		
		Область = Макет.ПолучитьОбласть("ШапкаЛьготы");
		ДокументРезультат.Вывести(Область);
		
		ВывестиТелоСправочнойИнформации(ДокументРезультат, Макет, КоллекцииРазделов.Льготы);
		
	КонецЕсли;
	
	// Справочно
	
	Если КоллекцииРазделов.Справочно.Количество() > 0 Тогда
		
		Область = Макет.ПолучитьОбласть("ШапкаСправочно");
		ДокументРезультат.Вывести(Область);
		
		ВывестиТелоСправочнойИнформации(ДокументРезультат, Макет, КоллекцииРазделов.Справочно);
		
	КонецЕсли;
	
	#КонецОбласти
	
	Возврат ИтогиПоГруппам;
	
КонецФункции

Процедура ДобавитьРасшифровкуНачисления(РасшифровкаНачисленийУдержаний, ИдентификаторКолонки, РасшифровкаНачисленияУдержания)
	
	СтрокаНачисленийУдержаний = Неопределено;
	Для Каждого СтрокаКоллекции Из РасшифровкаНачисленийУдержаний Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаКоллекции[ИдентификаторКолонки]) Тогда
			СтрокаНачисленийУдержаний = СтрокаКоллекции;
		КонецЕсли;
		
	КонецЦикла;
	
	Если СтрокаНачисленийУдержаний = Неопределено Тогда
		СтрокаНачисленийУдержаний = РасшифровкаНачисленийУдержаний.Добавить();
	КонецЕсли;
	
	СтрокаНачисленийУдержаний[ИдентификаторКолонки] = РасшифровкаНачисленияУдержания;
	
КонецПроцедуры

Функция ВывестиТелоРасчетногоЛисткаПоРабочимМестам(ДокументРезультат, СтрокаГоловногоСотрудника, Макеты, СоответствиеПользовательскихПолей)
	
	ИтогиПоГруппам = ОписаниеИтоговПоГруппам();
	
	СуммаНачисленоВНатуральнойФорме = 0;
	СуммаНачисления = 0;
	СуммаУдержания = 0;
	
	КоллекцияРабочихМест = Новый Массив;
	СтрокиУдержаний = Новый Соответствие;
	ИмяПоляСуммаУдержано = СоответствиеПользовательскихПолей.Получить("СуммаУдержано");
	
	Для каждого СтрокаСотрудника Из СтрокаГоловногоСотрудника.Строки Цикл
		
		ЭлементКоллекции = Новый Структура("СтрокаСотрудника,СтрокиНачислений,СтрокиУдержаний", СтрокаСотрудника, Новый Массив, Новый Массив);
		
		СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.Начислено, "Группа");
		Если СтрокаГруппы <> Неопределено Тогда
			
			Для каждого СтрокаНачислений Из СтрокаГруппы.Строки Цикл
				ЭлементКоллекции.СтрокиНачислений.Добавить(СтрокаНачислений);
			КонецЦикла;
			
		КонецЕсли;
		
		СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано, "Группа");
		Если СтрокаГруппы <> Неопределено Тогда
			
			Для каждого СтрокаУдержаний Из СтрокаГруппы.Строки Цикл
				
				СтрокаКоллекции = СтрокиУдержаний.Получить(СтрокаУдержаний.ВидРасчета);
				Если СтрокаКоллекции = Неопределено Тогда
					СтрокаКоллекции = СтрокаУдержаний;
				Иначе
					СтрокаКоллекции[ИмяПоляСуммаУдержано] = СтрокаКоллекции[ИмяПоляСуммаУдержано] + СтрокаУдержаний[ИмяПоляСуммаУдержано];
				КонецЕсли;
				СтрокиУдержаний.Вставить(СтрокаКоллекции.ВидРасчета, СтрокаКоллекции);
				
			КонецЦикла;
			
		КонецЕсли;
		
		КоллекцияРабочихМест.Добавить(ЭлементКоллекции);
		
	КонецЦикла;
	
	Если СтрокиУдержаний.Количество() > 0 Тогда
		КоллекцияРабочихМест[0].СтрокиУдержаний = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиУдержаний, "Значение");
	КонецЕсли;
	
	ШапкаВыводилась = Ложь;
	Для каждого РабочееМесто Из КоллекцияРабочихМест Цикл
		
		СтрокаСотрудника = РабочееМесто.СтрокаСотрудника;
		СтрокиНачислений = РабочееМесто.СтрокиНачислений;
		СтрокиУдержаний = РабочееМесто.СтрокиУдержаний;
		
		Если СтрокиНачислений.Количество() > 0 ИЛИ СтрокиУдержаний.Количество() > 0 Тогда
			
			Если НЕ ШапкаВыводилась Тогда
				ДокументРезультат.Вывести(Макеты.НачисленоУдержано);
				ШапкаВыводилась = Истина;
			КонецЕсли; 
			
			Если СтрокаСотрудника.Сотрудник <> СтрокаСотрудника.ГоловнойСотрудник Тогда
				ЗаполнитьЗначенияСвойств(Макеты.Подработка.Параметры, СтрокаСотрудника);
				ДокументРезультат.Вывести(Макеты.Подработка);
			КонецЕсли;
		
			НомерСтроки = 0;
			Пока НомерСтроки < СтрокиНачислений.Количество() ИЛИ НомерСтроки < СтрокиУдержаний.Количество() Цикл
				
				Для НомерПараметра = 0 По Макеты.СтрокаДвижений.Параметры.Количество() -1 Цикл
					Макеты.СтрокаДвижений.Параметры.Установить(НомерПараметра, "");
				КонецЦикла;
				
				Если НомерСтроки < СтрокиНачислений.Количество() Тогда
					
					СтрокаНачислений = СтрокиНачислений[НомерСтроки];
					
					Макеты.СтрокаДвижений.Параметры.ПериодДействияНачислений = СтрокаНачислений.ПериодДействия;
					
					УчетВремениВЧасах = СтрокаНачислений.ВремяВЧасах = Истина;
					
					Если УчетВремениВЧасах И ЗначениеЗаполнено(СтрокаНачислений.ОплаченоЧасов) И СтрокаНачислений.ОплаченоЧасов > 0 Тогда
						ОплаченныеДниЧасы = Формат(СтрокаНачислений.ОплаченоЧасов, "ЧДЦ=2; ЧН=") + " " + НСтр("ru='чс'") + ".";
					ИначеЕсли НЕ УчетВремениВЧасах И ЗначениеЗаполнено(СтрокаНачислений.ОплаченоДней) И СтрокаНачислений.ОплаченоДней > 0 Тогда
						ОплаченныеДниЧасы = Формат(СтрокаНачислений.ОплаченоДней, "ЧДЦ=2; ЧН=") + " " + НСтр("ru='дн'") + ".";
					Иначе
						ОплаченныеДниЧасы = 0;
					КонецЕсли;
					
					Макеты.СтрокаДвижений.Параметры.ОплаченныеДниЧасы = ОплаченныеДниЧасы;
					ЗаполнитьЗначенияСвойств(Макеты.СтрокаДвижений.Параметры, СтрокаНачислений, "ОтработаноДней,ОтработаноЧасов");
					
					ЗаполнитьПараметрыПользовательскихПолей(
						Макеты.СтрокаДвижений,
						СтрокаНачислений,
						СоответствиеПользовательскихПолей,
						"Начисление,СуммаНачислено");
					
					СуммаНачисления = СуммаНачисления + СтрокаНачислений[СоответствиеПользовательскихПолей.Получить("СуммаНачислено")];
					СуммаНачисленоВНатуральнойФорме = СуммаНачисленоВНатуральнойФорме + СтрокаНачислений[СоответствиеПользовательскихПолей.Получить("СуммаНачисленоВНатуральнойФорме")];
					
				КонецЕсли;
				
				Если НомерСтроки < СтрокиУдержаний.Количество() Тогда
					
					СтрокаУдержаний = СтрокиУдержаний[НомерСтроки];
					
					Макеты.СтрокаДвижений.Параметры.ПериодДействияУдержаний = СтрокаУдержаний.ПериодДействия;
					ЗаполнитьПараметрыПользовательскихПолей(
						Макеты.СтрокаДвижений,
						СтрокаУдержаний,
						СоответствиеПользовательскихПолей,
						"Удержание,СуммаУдержано");
					
					СуммаУдержания = СуммаУдержания + СтрокаУдержаний[СоответствиеПользовательскихПолей.Получить("СуммаУдержано")];
					
				КонецЕсли;
				
				НомерСтроки = НомерСтроки + 1;
				
				ДокументРезультат.Вывести(Макеты.СтрокаДвижений);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
		
	Если СуммаНачисления <> 0 ИЛИ СуммаУдержания <> 0 Тогда
		Макеты.Итог12.Параметры.СуммаНачисления = СуммаНачисления;
		Макеты.Итог12.Параметры.СуммаУдержания = СуммаУдержания;
		ДокументРезультат.Вывести(Макеты.Итог12);
	КонецЕсли;
	
	Если СуммаНачисленоВНатуральнойФорме <> 0 Тогда
		Макеты.Итог12ВНатуральнойФорме.Параметры.СуммаНачисления = СуммаНачисленоВНатуральнойФорме;
		ДокументРезультат.Вывести(Макеты.Итог12ВНатуральнойФорме);
	КонецЕсли;
	
	СуммаДолгаНаНачалоМесяца = 0;
	СуммаДолгаНаКонецМесяца = 0;
	
	СуммаНачисления = 0;
	СуммаВыплат = 0;
	
	ШапкаВыводилась = Ложь;
	Для каждого СтрокаСотрудника Из СтрокаГоловногоСотрудника.Строки Цикл
		
		СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.Справочно, "Группа");
		Если СтрокаГруппы = Неопределено Тогда
			СтрокиСправочно = Новый Массив;
		Иначе
			СтрокиСправочно = СтрокаГруппы.Строки;
		КонецЕсли;
		
		СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.Выплачено, "Группа");
		Если СтрокаГруппы = Неопределено Тогда
			СтрокиВыплачено = Новый Массив;
		Иначе
			СтрокиВыплачено = СтрокаГруппы.Строки;
		КонецЕсли;
		
		Если СтрокиСправочно.Количество() > 0 ИЛИ СтрокиВыплачено.Количество() > 0 Тогда
			
			Если НЕ ШапкаВыводилась Тогда
				ДокументРезультат.Вывести(Макеты.ПрочиеВыплачено);
				ШапкаВыводилась = Истина;
			КонецЕсли;
			
			Если СтрокаСотрудника.Сотрудник <> СтрокаСотрудника.ГоловнойСотрудник Тогда
				ЗаполнитьЗначенияСвойств(Макеты.Подработка.Параметры, СтрокаСотрудника);
				ДокументРезультат.Вывести(Макеты.Подработка);
			КонецЕсли;
		
			НомерСтроки = 0;
			Пока НомерСтроки < СтрокиСправочно.Количество() ИЛИ НомерСтроки < СтрокиВыплачено.Количество() Цикл
				
				Для НомерПараметра = 0 По Макеты.СтрокаДвижений.Параметры.Количество() -1 Цикл
					Макеты.СтрокаДвижений.Параметры.Установить(НомерПараметра, "");
				КонецЦикла;
				
				Если НомерСтроки < СтрокиСправочно.Количество() Тогда
					
					СтрокаСправочно = СтрокиСправочно[НомерСтроки];
					
					Макеты.СтрокаДвижений.Параметры.ПериодДействияНачислений = СтрокаСправочно.ПериодДействия;
					ЗаполнитьПараметрыПользовательскихПолей(
						Макеты.СтрокаДвижений,
						СтрокаСправочно,
						СоответствиеПользовательскихПолей,
						"Начисление,СуммаНачислено");
					
				КонецЕсли;
				
				Если НомерСтроки < СтрокиВыплачено.Количество() Тогда
					
					СтрокаВыплачено = СтрокиВыплачено[НомерСтроки];
					
					ПредставлениеРегистраторов = "";
					Регистраторы = Новый Соответствие;
					Для каждого СтрокаСРегистратором Из СтрокаВыплачено.Строки Цикл
						
						Если ЗначениеЗаполнено(СтрокаСРегистратором.Регистратор) Тогда
							СвойстваРегистратора = Новый Структура("Номер,Дата");
							ЗаполнитьЗначенияСвойств(СвойстваРегистратора, СтрокаСРегистратором.Регистратор);
							Регистраторы.Вставить(СтрокаСРегистратором.Регистратор, СвойстваРегистратора);
						КонецЕсли;
						
					КонецЦикла;
					
					Для каждого СвойстваРегистратора Из Регистраторы Цикл
						
						ПредставлениеРегистраторов = ?(ПустаяСтрока(ПредставлениеРегистраторов), "", ПредставлениеРегистраторов + ", ")
							+ ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(СвойстваРегистратора.Значение.Номер, Истина, Истина)
							+ " " + НСтр("ru='от'") + " " + Формат(СвойстваРегистратора.Значение.Дата, "ДФ=dd.MM.yy");
						
					КонецЦикла;
					
					Макеты.СтрокаДвижений.Параметры.ПериодДействияУдержаний = СтрокаВыплачено.ПериодДействия;
					ЗаполнитьПараметрыПользовательскихПолей(
						Макеты.СтрокаДвижений,
						СтрокаВыплачено,
						СоответствиеПользовательскихПолей,
						"Удержание,СуммаУдержано");
						
					Если Не ПустаяСтрока(ПредставлениеРегистраторов) Тогда
						Макеты.СтрокаДвижений.Параметры.Удержание = Строка(Макеты.СтрокаДвижений.Параметры.Удержание) + " (" + ПредставлениеРегистраторов + ")";
					КонецЕсли;
					
				КонецЕсли;
				
				НомерСтроки = НомерСтроки + 1;
				
				ДокументРезультат.Вывести(Макеты.СтрокаДвижений);
				
			КонецЦикла;
			
			Если ТипЗнч(СтрокиСправочно) <> Тип("Массив") Тогда
				СуммаНачисления = СуммаНачисления + СтрокиСправочно.Итог(СоответствиеПользовательскихПолей.Получить("СуммаНачислено"));
			КонецЕсли;
			
			Если ТипЗнч(СтрокиВыплачено) <> Тип("Массив") Тогда
				СуммаВыплат	 = СуммаВыплат + СтрокиВыплачено.Итог(СоответствиеПользовательскихПолей.Получить("СуммаУдержано"));
			КонецЕсли;
			
		КонецЕсли;
		
		СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.НачальноеСальдо, "Группа");
		Если СтрокаГруппы <> Неопределено Тогда
			СуммаДолгаНаНачалоМесяца = СуммаДолгаНаНачалоМесяца + СтрокаГруппы.Сумма;
		КонецЕсли;
		
		СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.КонечноеСальдо, "Группа");
		Если СтрокаГруппы <> Неопределено Тогда
			СуммаДолгаНаКонецМесяца = СуммаДолгаНаКонецМесяца + СтрокаГруппы.Сумма;
		КонецЕсли;
		
	КонецЦикла;
	
	Если СуммаНачисления <> 0 ИЛИ СуммаВыплат <> 0 Тогда
		Макеты.Итог34.Параметры.СуммаНачисления = СуммаНачисления;
		Макеты.Итог34.Параметры.СуммаВыплат	 = СуммаВыплат;
		ДокументРезультат.Вывести(Макеты.Итог34);
	КонецЕсли;
	
	ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца = СуммаДолгаНаНачалоМесяца;
	ИтогиПоГруппам.СуммаДолгаНаКонецМесяца = СуммаДолгаНаКонецМесяца;
	
	СуммаНачисления = 0;
	ШапкаВыводилась = Ложь;
	
	Для Каждого СтрокаСотрудника Из СтрокаГоловногоСотрудника.Строки Цикл
		
		СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.Льготы, "Группа");
		Если СтрокаГруппы = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокиЛьгот = СтрокаГруппы.Строки;
		Для Каждого СтрокаЛьгот Из СтрокиЛьгот Цикл 
			
			Если Не ШапкаВыводилась Тогда
				ДокументРезультат.Вывести(Макеты.Льготы);
				ШапкаВыводилась = Истина;
			КонецЕсли;
			
			Для НомерПараметра = 0 По Макеты.СтрокаДвижений.Параметры.Количество() - 1 Цикл
				Макеты.СтрокаДвижений.Параметры.Установить(НомерПараметра, "");
			КонецЦикла;
			
			Макеты.СтрокаДвижений.Параметры.ПериодДействияНачислений = СтрокаЛьгот.ПериодДействия;
			
			УчетВремениВЧасах = СтрокаЛьгот.ВремяВЧасах = Истина;
			
			Если УчетВремениВЧасах И ЗначениеЗаполнено(СтрокаЛьгот.ОплаченоЧасов) И СтрокаЛьгот.ОплаченоЧасов > 0 Тогда
				ОплаченныеДниЧасы = Формат(СтрокаЛьгот.ОплаченоЧасов, "ЧДЦ=2; ЧН=") + " " + НСтр("ru='чс'") + ".";
			ИначеЕсли НЕ УчетВремениВЧасах И ЗначениеЗаполнено(СтрокаЛьгот.ОплаченоДней) И СтрокаЛьгот.ОплаченоДней > 0 Тогда
				ОплаченныеДниЧасы = Формат(СтрокаЛьгот.ОплаченоДней, "ЧДЦ=2; ЧН=") + " " + НСтр("ru='дн'") + ".";
			Иначе
				ОплаченныеДниЧасы = 0;
			КонецЕсли;
			
			Макеты.СтрокаДвижений.Параметры.ОплаченныеДниЧасы = ОплаченныеДниЧасы;
			ЗаполнитьЗначенияСвойств(Макеты.СтрокаДвижений.Параметры, СтрокаЛьгот, "ОтработаноДней,ОтработаноЧасов");
			
			ЗаполнитьПараметрыПользовательскихПолей(
				Макеты.СтрокаДвижений,
				СтрокаЛьгот,
				СоответствиеПользовательскихПолей,
				"Начисление,СуммаНачислено");
				
			ДокументРезультат.Вывести(Макеты.СтрокаДвижений);
			
		КонецЦикла;
		
		Если ТипЗнч(СтрокиЛьгот) <> Тип("Массив") Тогда
			СуммаНачисления = СуммаНачисления + СтрокиЛьгот.Итог(СоответствиеПользовательскихПолей.Получить("СуммаНачислено"));
		КонецЕсли;
		
	КонецЦикла;
	
	Если СуммаНачисления <> 0 Тогда
		Макеты.Итог56.Параметры.СуммаНачисления = СуммаНачисления;
		ДокументРезультат.Вывести(Макеты.Итог56);
	КонецЕсли;
	
	Возврат ИтогиПоГруппам;
	
КонецФункции

Функция МожноГруппировать(Порядок, ИмяПоляГруппировки, СоответствиеПользовательскихПолей = Неопределено)
	
	ГруппировкаВозможна = Ложь;
	
	Для каждого ЭлементПорядка Из Порядок.Элементы Цикл
		
		Если ЭлементПорядка.Использование Тогда
			
			Если ТипЗнч(ЭлементПорядка) = Тип("АвтоЭлементПорядкаКомпоновкиДанных") Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЭлементПорядка.Поле = Новый ПолеКомпоновкиДанных("МесяцНачисления") Тогда
				Продолжить;
			ИначеЕсли СоответствиеПользовательскихПолей <> Неопределено Тогда
				
				ПолеПорядкаОсновногоСотрудника = СоответствиеПользовательскихПолей.Получить(НСтр("ru='Порядок основного рабочего места'"));
				Если ПолеПорядкаОсновногоСотрудника <> Неопределено Тогда
					
					Если ПолеПорядкаОсновногоСотрудника = СтрЗаменить(ЭлементПорядка.Поле, ".", "") Тогда
						Продолжить;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если ЭлементПорядка.Поле = Новый ПолеКомпоновкиДанных(ИмяПоляГруппировки) Тогда
				
				ГруппировкаВозможна = Истина;
				Прервать;
				
			ИначеЕсли ЭлементПорядка.Поле <> Новый ПолеКомпоновкиДанных("Организация") Тогда
				Прервать;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ГруппировкаВозможна;
	
КонецФункции

// Вспомогательные методы вывода настраиваемого расчетного листка

Функция ОписаниеИтоговПоГруппам()
	
	ИтогиПоГруппам = Новый Структура;
	
	ИтогиПоГруппам.Вставить("СуммаДолгаНаНачалоМесяца", 0);
	ИтогиПоГруппам.Вставить("СуммаДолгаНаКонецМесяца", 0);
	ИтогиПоГруппам.Вставить("СуммаДолгаНаНачалоМесяцаРасчета", 0);
	ИтогиПоГруппам.Вставить("СуммаДолгаНаКонецМесяцаРасчета", 0);
	
	Возврат ИтогиПоГруппам;
	
КонецФункции

Процедура ВывестиОбластиСтроки(ДокументРезультат, Макет, ИмяОбластиСтроки, НастройкиВывода, Параметры = Неопределено)
	
	ВывестиОбластиСтрокиНачислено(ДокументРезультат, Макет, ИмяОбластиСтроки, НастройкиВывода, Параметры);
	ПрисоединитьОбластиСтрокиУдержаноВыплачено(ДокументРезультат, Макет, ИмяОбластиСтроки, НастройкиВывода, Параметры)
	
КонецПроцедуры

Процедура ВывестиОбластиТекстовуюОбласть(ДокументРезультат, Макет, НастройкиВывода, ТекстСекции)
	
	Если Не ПустаяСтрока(ТекстСекции) Тогда
		
		ПараметрыТекстовойСекции = Новый Структура("ТекстСекции", ТекстСекции);
		
		ВывестиОбластиСтроки(ДокументРезультат, Макет, "ТекстоваяИнформация", НастройкиВывода, ПараметрыТекстовойСекции);
		НастроитьОбъединение(ДокументРезультат, 1, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено) + ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейУдержаноВыплачено));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиОбластиСтрокиНачислено(ДокументРезультат, Макет, ИмяОбластиСтроки, НастройкиВывода, Параметры = Неопределено)
	
	ПрисоединитьОбласть = Ложь;
	Для каждого ИмяОбласти Из НастройкиВывода.ИменаОбластейНачислено Цикл
		
		Область = Макет.ПолучитьОбласть(ИмяОбластиСтроки + "|" + ИмяОбласти);
		Если Параметры <> Неопределено Тогда
			Область.Параметры.Заполнить(Параметры);
		КонецЕсли;
		
		Если ПрисоединитьОбласть Тогда
			ДокументРезультат.Присоединить(Область);
		Иначе
			ДокументРезультат.Вывести(Область);
			ПрисоединитьОбласть = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПрисоединитьОбластиСтрокиУдержаноВыплачено(ДокументРезультат, Макет, ИмяОбластиСтроки, НастройкиВывода, Параметры = Неопределено)
	
	Для каждого ИмяОбласти Из НастройкиВывода.ИменаОбластейУдержаноВыплачено Цикл
		
		Область = Макет.ПолучитьОбласть(ИмяОбластиСтроки + "|" + ИмяОбласти);
		Если Параметры <> Неопределено Тогда
			Область.Параметры.Заполнить(Параметры);
		КонецЕсли;
		
		ДокументРезультат.Присоединить(Область);
		
	КонецЦикла;
	
КонецПроцедуры

Функция НастройкиВыводаРасчетногоЛистка(НастройкиОтчета, ПредварительныйПросмотр)
	
	НастройкиВывода = Новый Структура;
	
	НастройкиВывода.Вставить("ПредварительныйПросмотр", ПредварительныйПросмотр);
	
	ИмяПараметра = "ВыводитьСуммуНачисленнойЗарплатыВРазделеКВыплате";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра));
	
	ИмяПараметра = "ВыводитьПериодыНачислений";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра));
	
	ИмяПараметра = "ВыводитьОтработанноеОплаченноеВремя";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра));
	
	ИмяПараметра = "ДетализироватьВыплатыПоВедомостям";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра));
	
	ИмяПараметра = "ВыводитьСправочнуюИнформацию";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра));
	
	ИмяПараметра = "ВыводитьИнформациюОЛьготах";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра));
	
	ИмяПараметра = "ВыводитьОснованияНачисленийИУдержаний";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра));
	
	ИмяПараметра = "ВыводитьПоказателиНачисленийИУдержаний";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра));
	
	ИмяПараметра = "ВыводитьОсобенностиРасчетаНДФЛ";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра));
	
	ИмяПараметра = "ВыводитьИнформациюОНачисленныхВзносахВПФР";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра));
	
	ИменаОбластейНачислено = "ВидНачислено";
	ИменаОбластейУдержаноВыплачено = "ВидУдержаноВыплачено";
	
	Если НастройкиВывода.ВыводитьПериодыНачислений <> Ложь Тогда
		ИменаОбластейНачислено = ИменаОбластейНачислено + ",ПериодНачислено";
		ИменаОбластейУдержаноВыплачено = ИменаОбластейУдержаноВыплачено + ",ПериодУдержаноВыплачено";
	КонецЕсли;
	
	Если НастройкиВывода.ВыводитьОтработанноеОплаченноеВремя <> Ложь Тогда
		ИменаОбластейНачислено = ИменаОбластейНачислено + ",РабочиеОплаченныеДни";
	КонецЕсли;
	
	ИменаОбластейНачислено = ИменаОбластейНачислено + ",СуммаНачислено";
	ИменаОбластейУдержаноВыплачено = ИменаОбластейУдержаноВыплачено + ",СуммаУдержаноВыплачено";
	
	НастройкиВывода.Вставить("ИменаОбластейНачислено", СтрРазделить(ИменаОбластейНачислено, ","));
	НастройкиВывода.Вставить("ИменаОбластейУдержаноВыплачено", СтрРазделить(ИменаОбластейУдержаноВыплачено, ","));
	
	Возврат НастройкиВывода;
	
КонецФункции

Функция ЗначениеПараметра(НастройкиОтчета, ИмяПараметра)

	Значение = Неопределено;
	ПараметрОтчета = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
	Если ПараметрОтчета <> Неопределено И ПараметрОтчета.Использование = Истина Тогда
		Значение = ПараметрОтчета.Значение;
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

Процедура ОтключитьИспользованиеПолей(НастройкиОтчета, ИменаПолей)
	
	СписокПолей = СтрРазделить(ИменаПолей, ",");
	ОтключитьИспользованиеВыбранныхПолей(НастройкиОтчета.Выбор, СписокПолей);
	ОтключитьИспользованиеВыбранныхПолейСтруктуры(НастройкиОтчета.Структура, СписокПолей);
	
КонецПроцедуры

Процедура ОтключитьИспользованиеВыбранныхПолейСтруктуры(СтруктураНастроек, СписокПолей)
	
	Для каждого ЭлементСтруктуры Из СтруктураНастроек Цикл
		
		ОтключитьИспользованиеВыбранныхПолей(ЭлементСтруктуры.Выбор, СписокПолей);
		ОтключитьИспользованиеВыбранныхПолей(ЭлементСтруктуры.ПоляГруппировки, СписокПолей);
		
		ОтключитьИспользованиеВыбранныхПолейСтруктуры(ЭлементСтруктуры.Структура, СписокПолей);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОтключитьИспользованиеВыбранныхПолей(ЭлементСтруктурыСПолями, СписокПолей)
	
	Для каждого ВыбранныйЭлемент Из ЭлементСтруктурыСПолями.Элементы Цикл
		
		Для каждого ИмяПоля Из СписокПолей Цикл
			
			Если ВыбранныйЭлемент.Использование Тогда
				
				Если ТипЗнч(ВыбранныйЭлемент) = Тип("ВыбранноеПолеКомпоновкиДанных")
					Или ТипЗнч(ВыбранныйЭлемент) = Тип("ПолеГруппировкиКомпоновкиДанных") Тогда
					
					Если ВыбранныйЭлемент.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля) Тогда
						ВыбранныйЭлемент.Использование = Ложь;
					КонецЕсли;
					
				КонецЕсли; 
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция НастроитьОбъединение(ДокументРезультат, НомерКолонки, Ширина, НомерСтроки = Неопределено)
	
	Если НомерСтроки = Неопределено Тогда
		НомерСтроки = ДокументРезультат.ВысотаТаблицы;
	КонецЕсли;
	
	Область = ДокументРезультат.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки + Ширина - 1);
	Область.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
	Область.Объединить();
	
КонецФункции

Функция ШиринаОбластей(Макет, ИменаОбластей, СтрокаИменИсключаемыхОбластей = "")
	
	Ширина = 0;
	ИсключаемыеОбласти = СтрРазделить(ВРег(СтрокаИменИсключаемыхОбластей), ",");
	
	Для каждого ИмяОбласти Из ИменаОбластей Цикл
		
		Если ИсключаемыеОбласти.Найти(ВРег(ИмяОбласти)) <> Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		
		Область = Макет.ПолучитьОбласть("Строка|" + ИмяОбласти);
		Ширина = Ширина + Область.ШиринаТаблицы;
		
	КонецЦикла;
	
	Возврат Ширина;
	
КонецФункции

Процедура УдалитьСтрокиСНулевойСуммой(КоллекцияСтрок)
	
	СтрокиКУдалению = Новый Массив;
	
	Для каждого СтрокаКоллекции Из КоллекцияСтрок Цикл
		Если СтрокаКоллекции.Сумма = 0 Тогда
			СтрокиКУдалению.Добавить(СтрокаКоллекции);
		КонецЕсли; 
	КонецЦикла;
	
	Для каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
		КоллекцияСтрок.Удалить(УдаляемаяСтрока);
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиИтогиВзаиморасчетов(ДокументРезультат, Макет, НастройкиВывода, Итоги)
	
	ПараметрыИтогов = Новый Структура("ДолгНаНачало,ДолгНаКонец");
	
	ИмяОбластиСтроки = "ДолгЗаПредприятием";
	Если ЗначениеЗаполнено(Итоги.СуммаДолгаНаНачалоМесяца) Тогда
		
		Если Итоги.СуммаДолгаНаНачалоМесяца > 0 Тогда
			ПараметрыИтогов.ДолгНаНачало = Итоги.СуммаДолгаНаНачалоМесяца;
		Иначе
			ИмяОбластиСтроки = "ДолгЗаРаботником";
			ПараметрыИтогов.ДолгНаНачало = - Итоги.СуммаДолгаНаНачалоМесяца;
		КонецЕсли;
		
	КонецЕсли;
	
	ВывестиОбластиСтрокиНачислено(ДокументРезультат, Макет, ИмяОбластиСтроки, НастройкиВывода, ПараметрыИтогов);
	НастроитьОбъединение(ДокументРезультат, 1, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено, "СуммаНачислено"));
	
	ИмяОбластиСтроки = "ДолгЗаПредприятием";
	Если ЗначениеЗаполнено(Итоги.СуммаДолгаНаКонецМесяца) Тогда
		
		Если Итоги.СуммаДолгаНаКонецМесяца > 0 Тогда
			ПараметрыИтогов.ДолгНаКонец = Итоги.СуммаДолгаНаКонецМесяца;
		Иначе
			ИмяОбластиСтроки = "ДолгЗаРаботником";
			ПараметрыИтогов.ДолгНаКонец = - Итоги.СуммаДолгаНаКонецМесяца;
		КонецЕсли;
		
	КонецЕсли;
	
	ПрисоединитьОбластиСтрокиУдержаноВыплачено(ДокументРезультат, Макет, ИмяОбластиСтроки, НастройкиВывода, ПараметрыИтогов);
	
	НастроитьОбъединение(ДокументРезультат, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено) + 1,
		ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейУдержаноВыплачено, "СуммаУдержаноВыплачено"));
	
КонецПроцедуры

Процедура ВывестиТелоСправочнойИнформации(ДокументРезультат, Макет, КоллекцияСправочныхДанных)
	
	КоличествоСекций = КоличествоСекций(ДокументРезультат);
	
	НомерСекции = 0;
	Для каждого СтрокаСправочно Из КоллекцияСправочныхДанных Цикл
		
		Область = Макет.ПолучитьОбласть("СуммаЛьготыСправочно|ЛьготыСправочно");
		ЗаполнитьЗначенияСвойств(Область.Параметры, СтрокаСправочно);
		
		Если НомерСекции = 0 Тогда
			ДокументРезультат.Вывести(Область);
		Иначе
			ДокументРезультат.Присоединить(Область);
		КонецЕсли;
		
		НомерСекции = НомерСекции + 1;
		Если НомерСекции = КоличествоСекций Тогда
			НомерСекции = 0;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция КоличествоСекций(ДокументРезультат)
	
	Возврат Цел((ДокументРезультат.ШиринаТаблицы  + 1) / 11);
	
КонецФункции

Функция СтрокаСНепереносимымиПробелами(ИсходнаяСтрока)
	
	Возврат СтрЗаменить(ИсходнаяСтрока, " ", Символы.НПП);
	
КонецФункции

#КонецОбласти


#Область РассылкиОтчетов

// Проверка настроек выполняется всегда, даже когда Объект.ОбменДанными.Загрузка = Истина.
Процедура КонтрольНастроекОтчетовПриЗаписиРассылкиОтчетов(Рассылка, Отказ) Экспорт
	РезультатПроверки = ПроверитьНастройкиРассылки(Рассылка);
	Если ЗначениеЗаполнено(РезультатПроверки.ТекстОшибки) Тогда
		ВызватьИсключение РезультатПроверки.ТекстОшибки;
	КонецЕсли;
КонецПроцедуры

// Проверяет настройки отчетов рассылки.
//
// Параметры:
//  Рассылка - СправочникОбъект.РассылкаОтчетов - Проверяемая рассылка отчетов.
//
// Возвращаемое значение:
//  Структура - Информация о результатах проверки, в т.ч. пригодная для записи в журнал регистрации.
//      * ТекстОшибки - Строка - Текст ошибки. Если ошибок нет, то возвращается пустая строка.
//      * ИмяСобытия  - Строка - Имя события для записи в журнал регистрации (если требуется).
//      * Метаданные  - ОбъектМетаданных - Метаданные для привязки события журнала регистрации.
//      * Данные      - Произвольный     - Данные для привязки события журнала регистрации.
//
Функция ПроверитьНастройкиРассылки(Рассылка) Экспорт
	Результат = Новый Структура("ИмяСобытия, Метаданные, Данные, ТекстОшибки");
	Результат.ИмяСобытия  = НСтр("ru = 'Рассылка отчетов. Проверка настроек'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	Результат.Данные      = Рассылка.Ссылка;
	Результат.Метаданные  = Результат.Данные.Метаданные();
	Результат.ТекстОшибки = "";
	
	Если Рассылка.ЭтоГруппа Тогда
		Возврат Результат;
	КонецЕсли;
	Если Не Рассылка.Подготовлена
		Или Не Рассылка.ИспользоватьЭлектроннуюПочту
		Или Рассылка.Личная
		Или Рассылка.Отчеты.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	СсылкаОтчета = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Отчеты.АнализНачисленийИУдержаний);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОтчетыРассылки.НомерСтроки,
	|	ОтчетыРассылки.ОтправлятьЕслиПустой,
	|	ОтчетыРассылки.Отчет
	|ПОМЕСТИТЬ втОтчетыРассылки
	|ИЗ
	|	&ОтчетыРассылки КАК ОтчетыРассылки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОтчетыРассылки.НомерСтроки,
	|	втОтчетыРассылки.Отчет КАК Вариант,
	|	втОтчетыРассылки.ОтправлятьЕслиПустой,
	|	ВариантыОтчетов.Отчет.Имя КАК ИмяОтчета,
	|	ВариантыОтчетов.КлючВарианта,
	|	ВариантыОтчетов.Пользовательский
	|ИЗ
	|	втОтчетыРассылки КАК втОтчетыРассылки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|		ПО втОтчетыРассылки.Отчет = ВариантыОтчетов.Ссылка
	|			И (ВариантыОтчетов.Отчет = &ОтчетАнализНачисленийИУдержаний)";
	Запрос.УстановитьПараметр("ОтчетыРассылки", Рассылка.Отчеты.Выгрузить(, "НомерСтроки, Отчет, ОтправлятьЕслиПустой"));
	Запрос.УстановитьПараметр("ОтчетАнализНачисленийИУдержаний", СсылкаОтчета);
	
	ТаблицаЗначений = Запрос.Выполнить().Выгрузить();
	Для Каждого ИнформацияОВарианте Из ТаблицаЗначений Цикл
		СтрокаОтчет = Рассылка.Отчеты.Найти(ИнформацияОВарианте.НомерСтроки, "НомерСтроки");
		
		// Инициализация отчета.
		ПараметрыОтчета = Новый Структура("Отчет, Настройки, Форматы, ОтправлятьЕслиПустой");
		ЗаполнитьЗначенияСвойств(ПараметрыОтчета, СтрокаОтчет);
		ПараметрыОтчета.Настройки = СтрокаОтчет.Настройки.Получить();
		
		МодульРассылкаОтчетов = ОбщегоНазначения.ОбщийМодуль("РассылкаОтчетов");
		Успех = МодульРассылкаОтчетов.ИнициализироватьОтчет(Результат, ПараметрыОтчета, Рассылка.Персонализирована);
		Если Не Успех Тогда
			Продолжить;
		КонецЕсли;
		
		// Проверка настроек отчета.
		НастройкиКД = ПараметрыОтчета.КомпоновщикНастроекКД.ПолучитьНастройки();
		
		Если ТребоватьПерсонализацииОтчетаПриРассылке(ИнформацияОВарианте.ИмяОтчета, КлючВарианта(НастройкиКД))
			И Не ОтчетПерсонализирован(НастройкиКД) Тогда
			
			Результат.ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Отчет ""%1"" запрещено рассылать без отбора по физическому лицу'"),
				Строка(ИнформацияОВарианте.Вариант));
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

// Определяет, должен-ли быть персонализирован отчет.
Функция ТребоватьПерсонализацииОтчетаПриРассылке(ИмяОтчета, КлючВарианта)
	Если ИмяОтчета = "АнализНачисленийИУдержаний" Тогда
		Если ЭтоКлючВариантаОтчетаРасчетныйЛисток(КлючВарианта)
			Или КлючВарианта = "РегламентированнаяФормаСправкаПоДСВ" Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

// Определяет, персонализирован-ли отчет.
Функция ОтчетПерсонализирован(НастройкиКД)
	ПолеФизическоеЛицо = Новый ПолеКомпоновкиДанных("ФизическоеЛицо");
	ПолеСотрудник      = Новый ПолеКомпоновкиДанных("Сотрудник");
	
	Для Каждого ЭлементОтбораКД Из НастройкиКД.Отбор.Элементы Цикл
		Если ТипЗнч(ЭлементОтбораКД) = Тип("ЭлементОтбораКомпоновкиДанных")
			И ЭлементОтбораКД.Использование
			И ЭлементОтбораКД.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно
			И (ЭлементОтбораКД.ЛевоеЗначение = ПолеФизическоеЛицо
				Или ЭлементОтбораКД.ЛевоеЗначение = ПолеСотрудник) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции

#КонецОбласти

Функция ИОФамилияФизическогоЛица(ФизическоеЛицо) Экспорт
	
	ИОФамилия = "";
	Если ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		
		КадровыеДанные = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, ФизическоеЛицо, "ИОФамилия");
		Если КадровыеДанные.Количество() > 0 Тогда
			ИОФамилия = КадровыеДанные[0].ИОФамилия;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИОФамилия;
	
КонецФункции

Процедура ИзменитьГруппировкиВариантаТ49Т51(Структура, ЕстьГруппировкаПоПодразделению)
	
	Для каждого ЭлементСтруктуры Из Структура Цикл
		
		Если ЭлементСтруктуры.Имя = "Подразделение" Тогда
			
			Если ЕстьГруппировкаПоПодразделению Тогда
				ЭлементСтруктуры.Использование = Истина;
			Иначе
				ЭлементСтруктуры.Использование = Ложь;
			КонецЕсли;
			
		ИначеЕсли ЭлементСтруктуры.Имя = "Сотрудник" Тогда
			
			Если ЕстьГруппировкаПоПодразделению Тогда
				ЭлементСтруктуры.Использование = Ложь;
			Иначе
				ЭлементСтруктуры.Использование = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		ИзменитьГруппировкиВариантаТ49Т51(ЭлементСтруктуры.Структура, ЕстьГруппировкаПоПодразделению);
		
	КонецЦикла;
	
КонецПроцедуры

// Дополнение строк промежуточного итога.
//
Процедура ДобавитьВПромежуточныйИтог(ПромежуточныеИтоги, ДанныеСтроки) Экспорт
	
	Для каждого ПромежуточныйИтог Из ПромежуточныеИтоги Цикл
		ПромежуточныеИтоги.Вставить(ПромежуточныйИтог.Ключ, ПромежуточныйИтог.Значение + ДанныеСтроки[ПромежуточныйИтог.Ключ]);
	КонецЦикла;
	
КонецПроцедуры

Функция ЕстьИтогиПоГоловномуСотруднику(ПромежуточныеИтоги)
	
	Для Каждого ОписаниеИтога Из ПромежуточныеИтоги Цикл
		
		Если ЗначениеЗаполнено(ОписаниеИтога.Значение) Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Проверяет, есть ли в настройках отчета установленный отбор по физическому лицу
//
// Параметры:
//		ОбъектОтчета	- ОтчетОбъект
//
// Возвращаемое значение:
//		Булево - Истина (отбор установлен), Ложь (нет).
//
Функция ОтчетНастроенПерсонифицировано(ОбъектОтчета) Экспорт
	
	ЭтоПерсонифицированныйОтчет = Ложь;
	ПолеФизическоеЛицо = Новый ПолеКомпоновкиДанных("ФизическоеЛицо");
	
	Для Каждого ЭлементКоллекции Из ОбъектОтчета.КомпоновщикНастроек.ПолучитьНастройки().Отбор.Элементы Цикл
		
		Если ТипЗнч(ЭлементКоллекции) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			
			Если ЭлементКоллекции.Использование
				И ЭлементКоллекции.ЛевоеЗначение = ПолеФизическоеЛицо
				И ЭлементКоллекции.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
				
				ЭтоПерсонифицированныйОтчет = Истина;
				Прервать;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ЭтоПерсонифицированныйОтчет;
	
КонецФункции

Функция НаборыВнешнихДанныхАнализНачисленийИУдержаний() Экспорт
	
	Возврат ЗарплатаКадрыОтчетыВнутренний.НаборыВнешнихДанныхАнализНачисленийИУдержаний();
	
КонецФункции

Функция ТабличныеЧастиДляФормированияНабораДанныхДокументов() Экспорт
	
	Возврат Новый Структура("ТаблицыНачислений,ТаблицыУдержаний", Новый Массив, Новый Массив);
	
КонецФункции

Функция ДанныеДокументаФизическихЛиц(Форма, ФизическиеЛица, ТабличныеЧасти) Экспорт
	
	Объект = Форма.Объект;
	
	НачисленияУдержанияПоСотрудникам = Новый ТаблицаЗначений;
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("Период",								Новый ОписаниеТипов("Дата"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("Регистратор",						Новый ОписаниеТипов("ДокументСсылка.НачислениеЗарплаты"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("Организация",						Новый ОписаниеТипов("СправочникСсылка.Организации"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ФизическоеЛицо",						Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("Сотрудник",							Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("Подразделение",						Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("НачислениеУдержание",				Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления,ПланВидовРасчетаСсылка.Удержания,ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ГруппаНачисленияУдержанияВыплаты",	Новый ОписаниеТипов("ПеречислениеСсылка.ГруппыНачисленияУдержанияВыплаты"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ПериодДействия",						Новый ОписаниеТипов("Дата"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ДокументОснование",					Метаданные.ОпределяемыеТипы.ОснованиеНачисленияУдержания.Тип);
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ДатаНачала",							Новый ОписаниеТипов("Дата"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ДатаОкончания",						Новый ОписаниеТипов("Дата"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("Сумма",								Новый ОписаниеТипов("Число"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ОтработаноДней",						Новый ОписаниеТипов("Число"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ОтработаноЧасов",					Новый ОписаниеТипов("Число"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ОплаченоДней",						Новый ОписаниеТипов("Число"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ОплаченоЧасов",						Новый ОписаниеТипов("Число"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ВремяВЧасах",						Новый ОписаниеТипов("Булево"));
	
	Для Каждого ФизическоеЛицо Из ФизическиеЛица Цикл
		
		Для Каждого ОписаниеТаблицы Из ТабличныеЧасти.ТаблицыНачислений Цикл
			
			ТабличнаяЧасть = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(
				Форма, ОписаниеТаблицы.ПутьКДанным);
			
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("ФизическоеЛицо", ФизическоеЛицо);
			
			СтрокиТаблицы = ТабличнаяЧасть.НайтиСтроки(СтруктураПоиска);
			Если СтрокиТаблицы.Количество() = 0 Тогда
				
				СтруктураПоПустомуФизическомуЛицу = Новый Структура("ФизическоеЛицо", Справочники.ФизическиеЛица.ПустаяСсылка());
				СтрокиТаблицыБезФизическогоЛица = ТабличнаяЧасть.НайтиСтроки(СтруктураПоПустомуФизическомуЛицу);
				Если СтрокиТаблицыБезФизическогоЛица.Количество() > 0 Тогда
					
					Для Каждого СтрокаТаблицы Из СтрокиТаблицыБезФизическогоЛица Цикл
						
						СтрокаТаблицы.ФизическоеЛицо = СотрудникиКлиентСерверПовтИсп.ФизическиеЛицаСотрудников(СтрокаТаблицы.Сотрудник)[0];
						Если СтрокаТаблицы.ФизическоеЛицо = СтруктураПоиска.ФизическоеЛицо Тогда
							СтрокиТаблицы.Добавить(СтрокаТаблицы);
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Для Каждого СтрокаТаблицы Из СтрокиТаблицы Цикл
				
				НоваяСтрока = НачисленияУдержанияПоСотрудникам.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
				
				НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Начислено;
				НоваяСтрока.Период = Объект.МесяцНачисления;
				НоваяСтрока.Организация = Объект.Организация;
				НоваяСтрока.Сумма = СтрокаТаблицы[ОписаниеТаблицы.ИмяПоляРезультат];
				
				Если ПустаяСтрока(ОписаниеТаблицы.ИмяРеквизитаДатаНачала) Тогда
					НоваяСтрока.ДатаНачала = Объект.МесяцНачисления;
				Иначе
					НоваяСтрока.ДатаНачала = СтрокаТаблицы[ОписаниеТаблицы.ИмяРеквизитаДатаНачала];
				КонецЕсли;
				
				Если ПустаяСтрока(ОписаниеТаблицы.ИмяРеквизитаДатаОкончания) Тогда
					НоваяСтрока.ДатаОкончания = НачалоДня(КонецМесяца(Объект.МесяцНачисления));
				Иначе
					НоваяСтрока.ДатаОкончания = СтрокаТаблицы[ОписаниеТаблицы.ИмяРеквизитаДатаОкончания];
				КонецЕсли;
				
				Если Не ПустаяСтрока(ОписаниеТаблицы.ИмяРеквизитаВидРасчета) Тогда
					НоваяСтрока.НачислениеУдержание = СтрокаТаблицы[ОписаниеТаблицы.ИмяРеквизитаВидРасчета];
				ИначеЕсли ОписаниеТаблицы.ИмяТаблицы = "НачисленияПоДоговорам" Тогда
					НоваяСтрока.НачислениеУдержание = НачислениеДоговораГПХПоДокументуОснованию(СтрокаТаблицы.ДокументОснование);
				КонецЕсли;
				
				Если ОписаниеТаблицы.Свойство("ЗаполнятьОплаченноеВремяОтработанным") Тогда
					
					НоваяСтрока.ОплаченоДней = НоваяСтрока.ОтработаноДней;
					НоваяСтрока.ОплаченоЧасов = НоваяСтрока.ОплаченоЧасов;
					
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(НоваяСтрока.ПериодДействия) Тогда
					НоваяСтрока.ПериодДействия = НачалоМесяца(НоваяСтрока.ДатаНачала);
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
		Для Каждого ОписаниеТаблицы Из ТабличныеЧасти.ТаблицыУдержаний Цикл
			
			ТабличнаяЧасть = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(
				Форма, ОписаниеТаблицы.ПутьКДанным);
			
			СтруктураПоиска = Новый Структура;
			Если ОписаниеТаблицы.Свойство("ИмяРеквизитаФизическоеЛицо") Тогда
				СтруктураПоиска.Вставить(ОписаниеТаблицы.ИмяРеквизитаФизическоеЛицо, ФизическоеЛицо);
			Иначе
				СтруктураПоиска.Вставить("ФизическоеЛицо", ФизическоеЛицо);
			КонецЕсли;
			
			СтрокиТаблицы = ТабличнаяЧасть.НайтиСтроки(СтруктураПоиска);
			Для Каждого СтрокаТаблицы Из СтрокиТаблицы Цикл
				
				Если ОписаниеТаблицы.ИмяТаблицы = "ПогашениеЗаймов" Тогда
					
					Если СтрокаТаблицы.МатериальнаяВыгода <> 0 Тогда
						
						НоваяСтрока = НачисленияУдержанияПоСотрудникам.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
						
						НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Справочно;
						НоваяСтрока.Период = Объект.МесяцНачисления;
						НоваяСтрока.Организация = Объект.Организация;
						НоваяСтрока.Сумма = СтрокаТаблицы.МатериальнаяВыгода;
						НоваяСтрока.ПериодДействия = НоваяСтрока.Период;
						НоваяСтрока.НачислениеУдержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.МатериальнаяВыгодаПоЗаймам;
						
					КонецЕсли;
					
					Если СтрокаТаблицы.НалогНаМатериальнуюВыгоду <> 0 Тогда
						
						НоваяСтрока = НачисленияУдержанияПоСотрудникам.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
						
						НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано;
						НоваяСтрока.Период = Объект.МесяцНачисления;
						НоваяСтрока.Организация = Объект.Организация;
						НоваяСтрока.Сумма = СтрокаТаблицы.НалогНаМатериальнуюВыгоду;
						НоваяСтрока.ПериодДействия = НоваяСтрока.Период;
						НоваяСтрока.НачислениеУдержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.НДФЛ;
						
					КонецЕсли;
					
					Если СтрокаТаблицы.ПогашениеЗайма <> 0 Тогда
						
						НоваяСтрока = НачисленияУдержанияПоСотрудникам.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
						
						НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано;
						НоваяСтрока.Период = Объект.МесяцНачисления;
						НоваяСтрока.Организация = Объект.Организация;
						НоваяСтрока.Сумма = СтрокаТаблицы.ПогашениеЗайма;
						НоваяСтрока.ПериодДействия = НоваяСтрока.Период;
						НоваяСтрока.НачислениеУдержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.ПогашениеЗаймаИзЗарплаты;
						
					КонецЕсли;
					
				Иначе
					
					НоваяСтрока = НачисленияУдержанияПоСотрудникам.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
					
					Если ОписаниеТаблицы.Свойство("ИмяРеквизитаФизическоеЛицо") Тогда
						НоваяСтрока.ФизическоеЛицо = СтрокаТаблицы[ОписаниеТаблицы.ИмяРеквизитаФизическоеЛицо];
					КонецЕсли;
					
					НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано;
					НоваяСтрока.Период = Объект.МесяцНачисления;
					НоваяСтрока.Организация = Объект.Организация;
					НоваяСтрока.Сумма = СтрокаТаблицы[ОписаниеТаблицы.ИмяПоляРезультат];
					НоваяСтрока.ПериодДействия = НоваяСтрока.Период;
					
					Если ОписаниеТаблицы.ИмяТаблицы = "НДФЛ" Тогда
						НоваяСтрока.НачислениеУдержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.НДФЛ;
					Иначе
						НоваяСтрока.НачислениеУдержание = СтрокаТаблицы[ОписаниеТаблицы.ИмяРеквизитаВидРасчета];
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Запрос = ЗапросДанныеДокументаФизическихЛиц();
	Запрос.УстановитьПараметр("НачисленияУдержанияПоСотрудникам", НачисленияУдержанияПоСотрудникам);
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаменитьЗапросыКПредставлениямВиртуальныхТаблиц(Запрос.Текст, Запрос);
	
	НаборыВнешнихДанных = ЗарплатаКадрыОтчеты.НаборыВнешнихДанныхАнализНачисленийИУдержаний();
	НаборыВнешнихДанных.НачисленияУдержанияДокумента = Запрос.Выполнить().Выгрузить();
	
	Возврат НаборыВнешнихДанных;
	
КонецФункции

Функция ЗапросДанныеДокументаФизическихЛиц()
	
	Возврат ЗарплатаКадрыОтчетыВнутренний.ЗапросДанныеДокументаФизическихЛиц();
	
КонецФункции

Функция НаборыДанныхЗапросы(НаборыДанных) Экспорт
	
	НаборыЗапросы = Новый Массив;
	
	Для Каждого НаборДанных Из НаборыДанных Цикл
		
		Если ТипЗнч(НаборДанных) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных") Тогда
			НаборыЗапросы.Добавить(НаборДанных);
		ИначеЕсли ТипЗнч(НаборДанных) = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных") Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(НаборыЗапросы, НаборыДанныхЗапросы(НаборДанных.Элементы));
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НаборыЗапросы;
	
КонецФункции

Функция НачислениеДоговораГПХПоДокументуОснованию(ДокументОснование) Экспорт
	
	Возврат ЗарплатаКадрыОтчетыВнутренний.НачислениеДоговораГПХПоДокументуОснованию(ДокументОснование);
	
КонецФункции

#КонецОбласти

