////////////////////////////////////////////////////////////////////////////////
// Подсистема "Ограничение использования документов".
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Функция ТипыОграничиваемыхДокументов() Экспорт
	
	МассивТипов = Новый Массив();
	
	Для каждого ТипДокумента Из Метаданные.ОпределяемыеТипы.ОграничиваемыйДокумент.Тип.Типы() Цикл
		МассивТипов.Добавить(ТипДокумента);
	КонецЦикла;
	
	Возврат Новый ФиксированныйМассив(МассивТипов);
	
КонецФункции

Функция ОперацииОграниченияДокументов() Экспорт
	
	ОперацииОграниченияДокументов = Новый Соответствие();
	
	ТипыДокументов = ОграничениеИспользованияДокументовПовтИсп.ТипыОграничиваемыхДокументов();
	
	Для каждого ТипДокумента Из ТипыДокументов Цикл
		
		ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипДокумента);
		МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ОбъектМетаданных.ПолноеИмя());
		ОперацииОграниченияДокументов.Вставить(ТипДокумента, МенеджерДокумента.ОперацияОграниченияДокумента());
		
	КонецЦикла;
	
	Возврат Новый ФиксированноеСоответствие(ОперацииОграниченияДокументов);
	
КонецФункции

#КонецОбласти
