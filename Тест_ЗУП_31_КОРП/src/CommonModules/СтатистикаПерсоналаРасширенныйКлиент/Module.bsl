
#Область СлужебныйПрограммныйИнтерфейс

// Для переданного описания ячейки рег.отчета выбирает подходящий отчет-расшифровку,
// настраивает соотв. вариант и получает подготовленную к показу форму отчета.
// Параметры:
//	ИДОтчета - Строка - идентификатор отчета (совпадает с именем объекта метаданных).
// 	ИДРедакцииОтчета - Строка - идентификатор редакции формы отчета (совпадает с именем формы объекта метаданных).
//  ИДИменПоказателей - Массив - массив идентификаторов имен показателей, по которым формируется расшифровка.
//  ПараметрыОтчета - Структура - структура параметров отчета, необходимых для формирования расшифровки.
// 
Функция ФормаРасшифровкиРегламентированногоОтчета(ИДОтчета, ИДРедакцииОтчета, ИДИменПоказателей, ПараметрыОтчета) Экспорт
	
	Если Не ЗначениеЗаполнено(ИДИменПоказателей) Тогда
		Возврат Неопределено
	КонецЕсли;
	
	ИмяПоказателя = ИДИменПоказателей[0];
	ИмяОтчетаРасшифровки = "";
	
	Если ИДОтчета = "РегламентированныйОтчетСтатистикаФормаП4" Тогда
		Если ИДРедакцииОтчета = "ФормаОтчета2013Кв1" 
			Или ИДРедакцииОтчета = "ФормаОтчета2014Кв1"
			Или ИДРедакцииОтчета = "ФормаОтчета2017Кв1"
			Или ИДРедакцииОтчета = "ФормаОтчета2018Кв1" Тогда
			ИмяОтчетаРасшифровки = "СтатистикаПерсонала";
			ПараметрыОтчета.Вставить("ИмяСКД");
			ПараметрыОтчета.Вставить("ТолькоГражданские", Истина);
			Если ИДРедакцииОтчета = "ФормаОтчета2013Кв1" Или ИДРедакцииОтчета = "ФормаОтчета2014Кв1" Тогда
				ПараметрыОтчета.Вставить("ВидОтчета", "РегламентированныйОтчетП4_2013");
			ИначеЕсли ИДРедакцииОтчета = "ФормаОтчета2017Кв1" Или ИДРедакцииОтчета = "ФормаОтчета2018Кв1" Тогда
				ПараметрыОтчета.Вставить("ВидОтчета", "РегламентированныйОтчетП4_2017");
			КонецЕсли;
			ДополнитьПараметрыОтчетаП4За2013Год(ПараметрыОтчета, ИмяПоказателя);
		КонецЕсли;
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФорма1ТГМС" Тогда
		Если Прав(ИмяПоказателя,1) = "3" Тогда
			// Показатели из штатного расписания
			ИмяОтчетаРасшифровки = "ШтатноеРасписание";
			ПараметрыОтчета.Вставить("ИмяСКД","Расшифровка");
			ПараметрыОтчета.Вставить("ВидОтчета","РегламентированныйОтчет1ТГМС");
			ДополнитьПараметрыОтчета1ТГМС(ПараметрыОтчета, ИмяПоказателя);
		Иначе
			Если ИДРедакцииОтчета = "ФормаОтчета2013Кв1" 
				Или ИДРедакцииОтчета = "ФормаОтчета2015Кв1"
				Или ИДРедакцииОтчета = "ФормаОтчета2016Кв1"
				Или ИДРедакцииОтчета = "ФормаОтчета2017Кв1" Тогда
				ИмяОтчетаРасшифровки = "СтатистикаПерсонала";
				ПараметрыОтчета.Вставить("ИмяСКД");
				ПараметрыОтчета.Вставить("ВидОтчета","РегламентированныйОтчет1ТГМС");
				ДополнитьПараметрыОтчета1ТГМС(ПараметрыОтчета, ИмяПоказателя);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФормаП4НЗ" Тогда
		Если ИДРедакцииОтчета = "ФормаОтчета2015Кв1" 
			Или ИДРедакцииОтчета = "ФормаОтчета2017Кв1"
			Или ИДРедакцииОтчета = "ФормаОтчета2018Кв1" Тогда
			ИмяОтчетаРасшифровки = "СтатистикаПерсонала";
			ПараметрыОтчета.Вставить("ИмяСКД");
			ПараметрыОтчета.Вставить("ВидОтчета","РегламентированныйОтчетП4НЗ_2015");
			ДополнитьПараметрыОтчетаП4НЗЗа2015Год(ПараметрыОтчета, ИмяПоказателя);
		КонецЕсли;
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПЗдрав"
		Или ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПКультура"
		Или ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПНаука"
		Или ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПОбразование"
		Или ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПСоц" Тогда
		
		Если ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПЗдрав" Тогда
			ПараметрыОтчета.Вставить("ВидОтчета","РегламентированныйОтчетЗПЗдрав_2013");
		ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПКультура" Тогда
			ПараметрыОтчета.Вставить("ВидОтчета","РегламентированныйОтчетЗПКультура_2013");
		ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПНаука" Тогда
			ПараметрыОтчета.Вставить("ВидОтчета","РегламентированныйОтчетЗПНаука_2013");
		ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПОбразование" Тогда
			ПараметрыОтчета.Вставить("ВидОтчета","РегламентированныйОтчетЗПОбразование_2013");
		ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПСоц" Тогда
			ПараметрыОтчета.Вставить("ВидОтчета","РегламентированныйОтчетЗПСоц_2013");
		КонецЕсли;
		ИмяОтчетаРасшифровки = "СтатистикаПерсонала";
		ПараметрыОтчета.Вставить("ИмяСКД");
		Если ИДРедакцииОтчета = "ФормаОтчета2013Кв1" Или ИДРедакцииОтчета = "ФормаОтчета2014Кв1" Тогда
			ПараметрыОтчета.Вставить("ВариантРасчетаПоказателей", 2013);
		Иначе	
			ПараметрыОтчета.Вставить("ВариантРасчетаПоказателей", 2015);
		КонецЕсли;
		ДополнитьПараметрыОтчетаЗПЗа2013Год(ПараметрыОтчета, ИмяПоказателя);
	Иначе
		
	КонецЕсли;
	
	// Подготовка отчета-расшифровки к показу.
	Если ЗначениеЗаполнено(ИмяОтчетаРасшифровки) Тогда
		
		ПараметрыОтчета.Вставить("ИмяОтчетаРасшифровки",ИмяОтчетаРасшифровки);
		ПараметрыОтчета.Вставить("ИДИменПоказателей",ИДИменПоказателей);
		
		ФормаРасшифровки = ПолучитьФорму("ОбщаяФорма.РасшифровкаРегламентированногоОтчетаЗарплата", ПараметрыОтчета);
		Возврат ФормаРасшифровки
		
	КонецЕсли;
	
	Возврат Неопределено
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////

Процедура ДополнитьПараметрыОтчетаП4За2013Год(ПараметрыОтчета, ИмяПоказателя)
	
	Если ВРег(Лев(ИмяПоказателя,1)) <> "П" Или СтрДлина(ИмяПоказателя) < 10 Тогда
		ПараметрыОтчета.Вставить("ИмяСКД", "");
		Возврат;
	КонецЕсли;
	
	НомерСтроки = Сред(ИмяПоказателя, 6, 3);
	НомерКолонки = Прав(ИмяПоказателя, 2);
	
	Если НомерСтроки = Неопределено Или НомерКолонки = Неопределено
		Или НомерСтроки = "001"
		Или НомерКолонки = "А1"
		Или НомерКолонки = "Б1"
		Или НомерКолонки = "В1"
		Или НомерКолонки = "01"
		Или НомерКолонки = "07" Тогда
		ПараметрыОтчета.Вставить("ИмяСКД", "");
		Возврат;
	КонецЕсли;
	
	Если НомерКолонки = "05" Или НомерКолонки = "06" Или НомерКолонки = "11" Тогда
		ПараметрыОтчета.Вставить("ДатаНачалаПериодаВыборки", НачалоГода(ПараметрыОтчета.ДатаКонцаПериодаОтчета));
	КонецЕсли;
	Если НачалоМесяца(ПараметрыОтчета.ДатаКонцаПериодаОтчета) <> ПараметрыОтчета.ДатаНачалаПериодаОтчета Тогда
		ПараметрыОтчета.Вставить("СНачалаГода", Истина);
	КонецЕсли;
	ПараметрыОтчета.Вставить("ИмяСКД", "Расшифровка");
	ПараметрыОтчета.Вставить("НомерСтроки", НомерСтроки);
	ПараметрыОтчета.Вставить("НомерКолонки", НомерКолонки);
	
	ЗаголовокРасшифровки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Строка %1, графа %2'"),
		Формат(Число(НомерСтроки), "ЧГ="),
		Формат(Число(НомерКолонки), "ЧГ="));
	
	ПараметрыОтчета.Вставить("ЗаголовокРасшифровки",ЗаголовокРасшифровки);
	
КонецПроцедуры

Процедура ДополнитьПараметрыОтчета1ТГМС(ПараметрыОтчета, ИмяПоказателя)
	
	Если СтрЧислоВхождений(ИмяПоказателя, "П000001000") > 0 Тогда	 
		ПараметрыОтчета.Вставить("НомерСтроки1ТГМС", Число(Лев(Прав(ИмяПоказателя, 3), 1)));	 
	КонецЕсли;
				
	Если ВРег(Лев(ИмяПоказателя,1)) <> "П" Или СтрДлина(ИмяПоказателя) < 10 Тогда
		ПараметрыОтчета.Вставить("ИмяСКД", "");
		Возврат;
	КонецЕсли;
	
	НомерСтроки = Сред(ИмяПоказателя, 10, 2);
	НомерКолонки = Прав(ИмяПоказателя, 2);
	
	Если НомерСтроки = Неопределено Или НомерКолонки = Неопределено
		Или НомерСтроки = "05" Тогда
		ПараметрыОтчета.Вставить("ИмяСКД", "");
		Возврат;
	КонецЕсли;
	
	ПараметрыОтчета.Вставить("ДатаАктуальности", ПараметрыОтчета.ДатаКонцаПериодаОтчета);
	
	ПараметрыОтчета.Вставить("СНачалаГода", Истина);
	ПараметрыОтчета.Вставить("ИмяСКД", "Расшифровка");
	ПараметрыОтчета.Вставить("НомерСтроки", НомерСтроки);
	ПараметрыОтчета.Вставить("НомерКолонки", НомерКолонки);
	
	ЗаголовокРасшифровки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Строка %1, графа %2'"),
		Формат(Число(НомерСтроки), "ЧГ="),
		Формат(Число(НомерКолонки), "ЧГ="));
	
	ПараметрыОтчета.Вставить("ЗаголовокРасшифровки",ЗаголовокРасшифровки);
	
КонецПроцедуры

Процедура ДополнитьПараметрыОтчетаЗПЗа2013Год(ПараметрыОтчета, ИмяПоказателя)
	
	Раздел = ЗарплатаКадрыКлиентСервер.РазделРегламентированногоОтчетаПоИмениПоказателя(ИмяПоказателя);
	НомерСтроки = ЗарплатаКадрыКлиентСервер.СтрокаРегламентированногоОтчетаПоИмениПоказателя(ИмяПоказателя);
	НомерКолонки = ЗарплатаКадрыКлиентСервер.КолонкаРегламентированногоОтчетаПоИмениПоказателя(ИмяПоказателя);
	
	Если НомерСтроки = Неопределено Или НомерКолонки = Неопределено
		Или НомерСтроки = "001" Или НомерКолонки = "03" Или НомерКолонки = "05" Тогда
		ПараметрыОтчета.Вставить("ИмяСКД", "");
		Возврат;
	КонецЕсли;
	
	ПараметрыОтчета.Вставить("ИмяСКД", "Расшифровка");
	ПараметрыОтчета.Вставить("НомерСтроки", НомерСтроки);
	ПараметрыОтчета.Вставить("НомерКолонки", НомерКолонки);
	ПараметрыОтчета.Вставить("СНачалаГода", Истина);
	
	ЗаголовокРасшифровки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Строка %1, графа %2'"),
		Формат(Число(НомерСтроки), "ЧГ="),
		Формат(Число(НомерКолонки), "ЧГ="));
	
	ПараметрыОтчета.Вставить("ЗаголовокРасшифровки",ЗаголовокРасшифровки);
	
КонецПроцедуры

Процедура ДополнитьПараметрыОтчетаП4НЗЗа2015Год(ПараметрыОтчета, ИмяПоказателя)
	
	Если ВРег(Лев(ИмяПоказателя,1)) <> "П" Или СтрДлина(ИмяПоказателя) < 10 Тогда
		ПараметрыОтчета.Вставить("ИмяСКД", "");
		Возврат;
	КонецЕсли;
	
	НомерСтроки = Сред(ИмяПоказателя, 8, 2);
	НомерКолонки = Прав(ИмяПоказателя, 2);
	
	Если НомерСтроки = Неопределено Или НомерКолонки = Неопределено
		Или НомерСтроки = "01"
		Или НомерСтроки = "04"
		Или НомерСтроки = "11"
		Или НомерСтроки = "14"
		Или НомерСтроки = "18"
		Или НомерСтроки = "19"
		Или НомерСтроки = "21"
		Или НомерСтроки = "22" Тогда
		ПараметрыОтчета.Вставить("ИмяСКД", "");
		Возврат;
	КонецЕсли;
	
	ПараметрыОтчета.Вставить("ИмяСКД", "Расшифровка");
	ПараметрыОтчета.Вставить("НомерСтроки", НомерСтроки);
	ПараметрыОтчета.Вставить("НомерКолонки", НомерКолонки);
	
	ЗаголовокРасшифровки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Строка %1, графа %2'"),
		Формат(Число(НомерСтроки), "ЧГ="),
		Формат(Число(НомерКолонки), "ЧГ="));
	
	ПараметрыОтчета.Вставить("ЗаголовокРасшифровки", ЗаголовокРасшифровки);
	
	ПараметрыОтчета.Вставить("ТолькоОсновноеМестоРаботы", Истина);
	
КонецПроцедуры

#КонецОбласти
