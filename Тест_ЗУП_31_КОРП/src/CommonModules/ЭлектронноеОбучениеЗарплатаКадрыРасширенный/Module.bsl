
#Область СлужебныйПрограммныйИнтерфейс

#Область ЭлектронноеОбучение

Процедура ПослеИзученияЭлектронногоКурса(ЭлектронныйКурс, Контекст, Учащийся, Данные) Экспорт	
	
	Если Не КонтекстСоответствующий(Контекст) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Учащийся) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеОбИзучении = Новый Структура(
		"Оценка,
		|Завершено,
		|ДатаЗавершения");
	ДанныеОбИзучении.Оценка = Данные.Балл;
	ДанныеОбИзучении.Завершено = Истина;
	ДанныеОбИзучении.ДатаЗавершения = ТекущаяДатаСеанса();
	
	ОбновитьСтатистикуИзученияЭлектронногоКурса(ЭлектронныйКурс, Контекст, Учащийся, ДанныеОбИзучении);
	
КонецПроцедуры

Процедура ПриЗакрытииФормыЭлектронногоКурса(ЭлектронныйКурс, Контекст, Учащийся, Данные) Экспорт	
	
	Если Не КонтекстСоответствующий(Контекст) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Учащийся) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеОбИзучении = Новый Структура("Прогресс");
	ДанныеОбИзучении.Прогресс = Данные.ЗавершеноВПроцентах;
	
	ОбновитьСтатистикуИзученияЭлектронногоКурса(ЭлектронныйКурс, Контекст, Учащийся, ДанныеОбИзучении);
	
КонецПроцедуры

Процедура ПриСозданииФормыЭлектронногоКурса(ЭлектронныйКурс, Контекст, Учащийся, РежимПроверки, РежимРедактирования, РежимПросмотра, ИмяОповещенияПриЗакрытии, Отказ) Экспорт
	
	ИмяОповещенияПриЗакрытии = "ЗакрытиеФормыЭлектронногоКурса";
	
КонецПроцедуры

Процедура ПриОпределенииСоответствияВерсийОбработчиковОбновлений(СоответствиеВерсий) Экспорт
	
	СоответствиеВерсий.Вставить("3.1.1.28", "3.1.3.59");
	СоответствиеВерсий.Вставить("3.1.1.40", "3.1.3.65");
	СоответствиеВерсий.Вставить("3.2.1.1", "3.1.4.26");
	СоответствиеВерсий.Вставить("3.2.1.32", "3.1.4.26");
	СоответствиеВерсий.Вставить("3.2.1.35", "3.1.4.53");
	СоответствиеВерсий.Вставить("3.2.1.36", "3.1.4.92");
	СоответствиеВерсий.Вставить("3.2.1.37", "3.1.4.92");
	
КонецПроцедуры

#КонецОбласти

#Область НастройкиВариантовОтчетов

// Содержит настройки размещения вариантов отчетов в панели отчетов.
// Описание см. ЗарплатаКадрыВариантыОтчетов.НастроитьВариантыОтчетов.
//
Процедура НастроитьВариантыОтчетов(Настройки) Экспорт
	
	ВариантыОтчетов.НастроитьОтчетВМодулеМенеджера(Настройки, Метаданные.Отчеты.СтатистикаЭлектронногоОбучения);
	
	Вариант = ВариантыОтчетов.ОписаниеВарианта(Настройки, Метаданные.Отчеты.СтатистикаЭлектронногоОбучения, "ПоЭлектроннымКурсам");
	Вариант.ФункциональныеОпции.Добавить("ИспользоватьЭлектронноеОбучениеЗарплатаКадрыРасширенная");
	
КонецПроцедуры

#КонецОбласти

Процедура ДобавитьРольИзучениеЭлектронныхКурсов(ОписаниеПрофиля) Экспорт
	ЗарплатаКадрыРасширенный.ДобавитьВОписаниеПрофиляРоль(ОписаниеПрофиля, "ИзучениеЭлектронныхКурсов");
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция СведенияОбИзученииЭлектронногоКурса(ДокументОбучения, ФизическоеЛицо)
	
	МенеджерЗаписи = РегистрыСведений.СтатистикаЭлектронныхКурсовСотрудников.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ДокументОснование = ДокументОбучения;
	МенеджерЗаписи.ФизическоеЛицо = ФизическоеЛицо;
	
	МенеджерЗаписи.Прочитать();
	
	Возврат МенеджерЗаписи;
	
КонецФункции

Процедура ОбновитьСтатистикуИзученияЭлектронногоКурса(ЭлектронныйКурс, ДокументОбучения, ФизическоеЛицо, ДанныеОбИзучении)
	
	УстановитьПривилегированныйРежим(Истина);
	МенеджерЗаписи = СведенияОбИзученииЭлектронногоКурса(ДокументОбучения, ФизическоеЛицо);
	УстановитьПривилегированныйРежим(Ложь);
	
	МенеджерЗаписи.ДокументОснование = ДокументОбучения;
	МенеджерЗаписи.ФизическоеЛицо = ФизическоеЛицо;
	МенеджерЗаписи.ЭлектронныйКурс = ЭлектронныйКурс;
	Для Каждого КлючИЗначение Из ДанныеОбИзучении Цикл
		МенеджерЗаписи[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Истина);
	МенеджерЗаписи.Записать();
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция КонтекстСоответствующий(Контекст)
	Возврат ТипЗнч(Контекст) = Тип("ДокументСсылка.ОбучениеРазвитиеСотрудников") И ЗначениеЗаполнено(Контекст);
КонецФункции

#КонецОбласти