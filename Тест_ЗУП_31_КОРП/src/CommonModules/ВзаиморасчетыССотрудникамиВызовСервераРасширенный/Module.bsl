
#Область СлужебныеПроцедурыИФункции

Функция ИнформацияОСпособеВыплаты(СпособВыплаты) Экспорт
	
	ПоляПараметровЗаполнения= "ХарактерВыплаты, ВидДокументаОснования"; 
	ПоляПараметровРасчета	= "Округление, ПроцентВыплаты";
	
	ИнформацияОСпособеВыплаты = Новый Структура;
	ИнформацияОСпособеВыплаты.Вставить("ПараметрыЗаполнения", Новый Структура(ПоляПараметровЗаполнения));
	ИнформацияОСпособеВыплаты.Вставить("ПараметрыРасчета", Новый Структура(ПоляПараметровРасчета));
	
	Если СпособВыплаты.Пустая() Тогда
		СпособВыплатыОбъект = Неопределено;
	Иначе
		СпособВыплатыОбъект = СпособВыплаты.ПолучитьОбъект();
	КонецЕсли;	
	
	Если СпособВыплатыОбъект = Неопределено Тогда
		
		ИнформацияОСпособеВыплаты.ПараметрыЗаполнения.ВидДокументаОснования = Перечисления.ВидыДокументовМежрасчетныхНачислений.ПустаяСсылка();
		ИнформацияОСпособеВыплаты.ПараметрыЗаполнения.ХарактерВыплаты = Перечисления.ХарактерВыплатыЗарплаты.ПустаяСсылка();
		
		ИнформацияОСпособеВыплаты.ПараметрыРасчета.Округление = Справочники.СпособыОкругленияПриРасчетеЗарплаты.ПоУмолчанию();
		ИнформацияОСпособеВыплаты.ПараметрыРасчета.ПроцентВыплаты = 100;
		
	Иначе
		
		ЗаполнитьЗначенияСвойств(ИнформацияОСпособеВыплаты.ПараметрыЗаполнения, СпособВыплатыОбъект, ПоляПараметровЗаполнения);
		ЗаполнитьЗначенияСвойств(ИнформацияОСпособеВыплаты.ПараметрыРасчета, СпособВыплатыОбъект, ПоляПараметровРасчета);
		
	КонецЕсли;
	
	Возврат ИнформацияОСпособеВыплаты;
	
КонецФункции	

Процедура ВидыДокументовМежрасчетныхНачисленийОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	
	ДоступныеВидыДокументаОснования = Перечисления.ВидыДокументовМежрасчетныхНачислений.ДоступныеПоМетаданным();
	
	Для Каждого ВидДокументаОснования Из ДоступныеВидыДокументаОснования Цикл
		ДанныеВыбора.Добавить(ВидДокументаОснования);
	КонецЦикла;
	
КонецПроцедуры

Процедура ГруппыВидовДоговоровССотрудникамДляВыплатыЗарплатыОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	
	ДоступныеГруппыВидовДоговоров = Перечисления.ГруппыВидовДоговоровССотрудникамДляВыплатыЗарплаты.ДоступныеПоМетаданным();
	
	Для Каждого ГруппаВидовДоговоров Из ДоступныеГруппыВидовДоговоров Цикл
		ДанныеВыбора.Добавить(ГруппаВидовДоговоров);
	КонецЦикла;
	
КонецПроцедуры

Функция ЕстьНезачисленияПоВедомости(Ведомость, Проведен = Неопределено) Экспорт
	
	ЕстьНезачисления = Ложь;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ВнешниеХозяйственныеОперации") Тогда
		
		Модуль = ОбщегоНазначения.ОбщийМодуль("Документы.СведенияОНезачисленнойЗарплате");
		ЕстьНезачисления = Модуль.ЕстьПоВедомости(Ведомость, Истина);
		
	КонецЕсли;
	
	Возврат ЕстьНезачисления;
	
КонецФункции

Функция ЕстьДепонированияПоВедомости(Ведомость, Проведен = Неопределено) Экспорт
	
	ЕстьДепонирования = Ложь;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.УчетДепонированнойЗарплаты") Тогда
		
		Модуль = ОбщегоНазначения.ОбщийМодуль("Документы.ДепонированиеЗарплаты");
		ЕстьДепонирования = Модуль.ЕстьПоВедомости(Ведомость, Истина);
		
	КонецЕсли;
	
	Возврат ЕстьДепонирования;
	
КонецФункции

Функция ЕстьПодтверждениеВыплатыДоходовПоВедомости(Ведомость) Экспорт
	
	ЕстьПодтверждение = Ложь;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ВнешниеХозяйственныеОперации.УчетФактическиПолученныхДоходов") Тогда
		
		Модуль = ОбщегоНазначения.ОбщийМодуль("РегистрыСведений.ДатыВыплатыДоходов");
		ЕстьПодтверждение = Модуль.ЕстьПоВедомости(Ведомость);
		
	КонецЕсли;
	
	Возврат ЕстьПодтверждение;
	
КонецФункции

#КонецОбласти
