////////////////////////////////////////////////////////////////////////////////
// Обновление информационной базы Библиотеки интеграции с 1С:Документооборотом
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Сведения о библиотеке (или конфигурации).

// Заполняет основные сведения о библиотеке или основной конфигурации.
// Библиотека, имя которой имя совпадает с именем конфигурации в метаданных, определяется как основная конфигурация.
// 
// Параметры:
//  Описание - Структура - сведения о библиотеке:
//
//   * Имя                 - Строка - имя библиотеки, например, "СтандартныеПодсистемы".
//   * Версия              - Строка - версия в формате из 4-х цифр, например, "2.1.3.1".
//
//   * ТребуемыеПодсистемы - Массив - имена других библиотек (Строка), от которых зависит данная библиотека.
//                                    Обработчики обновления таких библиотек должны быть вызваны ранее
//                                    обработчиков обновления данной библиотеки.
//                                    При циклических зависимостях или, напротив, отсутствии каких-либо зависимостей,
//                                    порядок вызова обработчиков обновления определяется порядком добавления модулей
//                                    в процедуре ПриДобавленииПодсистем общего модуля
//                                    ПодсистемыКонфигурацииПереопределяемый.
//
Процедура ПриДобавленииПодсистемы(Описание) Экспорт
	
	Описание.Имя    = "БиблиотекаИнтеграцииС1СДокументооборотом";
	Описание.Версия = "1.1.10.5";
	
	Если ОбновлениеИнформационнойБазы.ВерсияИБ(Описание.Имя) = "0.0.0.0"
		И ОбновлениеИнформационнойБазы.ВерсияИБ(Метаданные.Имя) <> "0.0.0.0" Тогда
		ОбновлениеИнформационнойБазыСлужебный.УстановитьВерсиюИБ(Описание.Имя, "1.1.5.12", Ложь);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики обновления информационной базы.

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре.
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.РежимВыполнения     = "Монопольно";
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "*";
	Обработчик.МонопольныйРежим = Ложь;
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБИД.ПроверитьСоответствиеПравилИнтеграцииМетаданным";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("757a17b9-2e40-40ed-b109-0f30f7a0de5b");
	Обработчик.Комментарий = НСтр("ru = 'Проверка правил интеграции с 1С:Документооборотом'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.1.6.1";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБИД.ПерейтиНаВерсию_1_1_6_1";
	Обработчик.Комментарий = НСтр("ru = 'Обновление правил интеграции с 1С:Документооборотом'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.1.8.4";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыБИД.ПерейтиНаВерсию_1_1_8_4";
	Обработчик.Комментарий = НСтр("ru = 'Обновление правил интеграции с 1С:Документооборотом'");
	
КонецПроцедуры

// Вызывается перед процедурами-обработчиками обновления данных ИБ.
//
Процедура ПередОбновлениемИнформационнойБазы() Экспорт
	
	
	
КонецПроцедуры

// Вызывается после завершения обновления данных ИБ.
// 
// Параметры:
//   ПредыдущаяВерсия       - Строка - версия до обновления. "0.0.0.0" для "пустой" ИБ.
//   ТекущаяВерсия          - Строка - версия после обновления.
//   ВыполненныеОбработчики - ДеревоЗначений - список выполненных процедур-обработчиков обновления,
//                                             сгруппированных по номеру версии.
//   ВыводитьОписаниеОбновлений - Булево - если установить Истина, то будет выведена форма
//                                с описанием обновлений. По умолчанию, Истина.
//                                Возвращаемое значение.
//   МонопольныйРежим           - Булево - Истина, если обновление выполнялось в монопольном режиме.
//
// Пример обхода выполненных обработчиков обновления:
//
//	Для Каждого Версия Из ВыполненныеОбработчики.Строки Цикл
//		
//		Если Версия.Версия = "*" Тогда
//			// Обработчик, который может выполнятся при каждой смене версии.
//		Иначе
//			// Обработчик, который выполняется для определенной версии.
//		КонецЕсли;
//		
//		Для Каждого Обработчик Из Версия.Строки Цикл
//			...
//		КонецЦикла;
//		
//	КонецЦикла;
//
Процедура ПослеОбновленияИнформационнойБазы(Знач ПредыдущаяВерсия, Знач ТекущаяВерсия,
		Знач ВыполненныеОбработчики, ВыводитьОписаниеОбновлений, МонопольныйРежим) Экспорт
	
	
	
КонецПроцедуры

// Вызывается при подготовке табличного документа с описанием изменений в программе.
//
// Параметры:
//   Макет - ТабличныйДокумент - описание обновления всех библиотек и конфигурации.
//           Макет можно дополнить или заменить.
//           См. также общий макет ОписаниеИзмененийСистемы.
//
Процедура ПриПодготовкеМакетаОписанияОбновлений(Знач Макет) Экспорт
	
	
	
КонецПроцедуры

// Вызывается при определнии режима обновления данных.
//
// Параметры:
//   РежимОбновленияДанных - Строка - режим обновления.
//   СтандартнаяОбработка - Булево - Ложь, если нужно изменить режим по умолчанию.
//
Процедура ПриОпределенииРежимаОбновленияДанных(РежимОбновленияДанных, СтандартнаяОбработка) Экспорт
	
	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обработчик, проверяющий соответствие правил интеграции метаданным конфигурации.
//
Процедура ПроверитьСоответствиеПравилИнтеграцииМетаданным() Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюС1СДокументооборот") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросПравила = Новый Запрос(
		"ВЫБРАТЬ
		|	Правила.Ссылка
		|ИЗ
		|	Справочник.ПравилаИнтеграцииС1СДокументооборотом КАК Правила
		|ГДЕ
		|	НЕ ПометкаУдаления
		|	И ТипОбъектаПотребителя <> """"");
		
	ВыборкаПравила = ЗапросПравила.Выполнить().Выбрать();
	Пока ВыборкаПравила.Следующий() Цикл
		
		ПравилоОбъект = ВыборкаПравила.Ссылка.ПолучитьОбъект();
		НужнаЗапись = Ложь;
		
		ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ПравилоОбъект.ТипОбъектаПотребителя);
		
		Если ОбъектМетаданных = Неопределено Тогда
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Помечено на удаление правило для несуществующего объекта %1'",
					ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
				ПравилоОбъект.ТипОбъектаПотребителя);
			ЗаписьЖурналаРегистрации(
				ИнтеграцияС1СДокументооборот.ИмяСобытияЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ТекстСообщения);
				
			ПравилоОбъект.ПометкаУдаления = Истина;
			НужнаЗапись = Истина;
			
		Иначе
		
			Для Каждого Правило Из ПравилоОбъект.ПравилаЗаполненияРеквизитовПриВыгрузке Цикл
				
				Если Правило.ВариантПравилаЗаполненияРеквизитов
						<> Перечисления.ВариантыПравилЗаполненияРеквизитов.РеквизитОбъекта
					Или Правило.ДополнительныйРеквизитОбъекта Тогда
					Продолжить;
				КонецЕсли;
				
				ПозицияТочки = СтрНайти(Правило.ИмяРеквизитаОбъектаПотребителя, ".");
				Если ПозицияТочки = 0 Тогда
					ИмяРеквизитаОбъектаПотребителя = Правило.ИмяРеквизитаОбъектаПотребителя;
				Иначе
					ИмяРеквизитаОбъектаПотребителя = Лев(Правило.ИмяРеквизитаОбъектаПотребителя, ПозицияТочки - 1);
				КонецЕсли;
				
				Если ОбъектМетаданных.Реквизиты.Найти(ИмяРеквизитаОбъектаПотребителя) = Неопределено
					И ИмяРеквизитаОбъектаПотребителя <> "Представление" Тогда
					
					Найден = Ложь;
					Для Каждого СтандартныйРеквизит Из ОбъектМетаданных.СтандартныеРеквизиты Цикл
						Если СтандартныйРеквизит.Имя = ИмяРеквизитаОбъектаПотребителя Тогда
							Найден = Истина;
							Прервать;
						КонецЕсли;
					КонецЦикла;
					
					Если Не Найден Тогда
						
						ТекстСообщения = СтрШаблон(НСтр("ru = 'Очищено правило выгрузки для несуществующего реквизита %1'"),
							Правило.ИмяРеквизитаОбъектаПотребителя);
						ЗаписьЖурналаРегистрации(
							ИнтеграцияС1СДокументооборот.ИмяСобытияЖурналаРегистрации(),
							УровеньЖурналаРегистрации.Ошибка,,
							ВыборкаПравила.Ссылка,
							ТекстСообщения);
						НужнаЗапись = Истина;
						
						Правило.ВариантПравилаЗаполненияРеквизитов = Неопределено; 
						Правило.ИмяРеквизитаОбъектаПотребителя = Неопределено;
						Правило.ЗначениеРеквизитаДокументооборота = ТекстСообщения;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
		
			Для Каждого Правило Из ПравилоОбъект.ПравилаЗаполненияРеквизитовПриЗагрузке Цикл
				
				Если Правило.ВариантПравилаЗаполненияРеквизитов
						<> Перечисления.ВариантыПравилЗаполненияРеквизитов.РеквизитОбъекта
					Или Правило.ДополнительныйРеквизитОбъекта Тогда
					Продолжить;
				КонецЕсли;
				
				Если ОбъектМетаданных.Реквизиты.Найти(Правило.ИмяРеквизитаОбъектаПотребителя) = Неопределено
					И Правило.ИмяРеквизитаОбъектаПотребителя <> "Представление" Тогда
					
					Найден = Ложь;
					Для Каждого СтандартныйРеквизит Из ОбъектМетаданных.СтандартныеРеквизиты Цикл
						Если СтандартныйРеквизит.Имя = Правило.ИмяРеквизитаОбъектаПотребителя Тогда
							Найден = Истина;
							Прервать;
						КонецЕсли;
					КонецЦикла;
					
					Если Не Найден Тогда
						
						ТекстСообщения = СтрШаблон(НСтр("ru = 'Очищено правило загрузки для несуществующего реквизита %1'"),
							Правило.ИмяРеквизитаОбъектаПотребителя);
						ЗаписьЖурналаРегистрации(
							ИнтеграцияС1СДокументооборот.ИмяСобытияЖурналаРегистрации(),
							УровеньЖурналаРегистрации.Ошибка,,
							ВыборкаПравила.Ссылка,
							ТекстСообщения);
						НужнаЗапись = Истина;
						
						Правило.ВариантПравилаЗаполненияРеквизитов = Неопределено; 
						Правило.ИмяРеквизитаОбъектаПотребителя = Неопределено;
						Правило.ЗначениеРеквизитаПотребителя = ТекстСообщения;
						Правило.ОбновлятьЗначение = Ложь;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Если НужнаЗапись Тогда
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ПравилоОбъект);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Обработчик обновления БИД на версию 1.1.6.1. Обновление наименований правил интеграции
// и установка реквизита Ключевой у видов документов ДО.
//
Процедура ПерейтиНаВерсию_1_1_6_1() Экспорт
	
	ЗапросПравила = Новый Запрос(
		"ВЫБРАТЬ
		|	Правила.Ссылка
		|ИЗ
		|	Справочник.ПравилаИнтеграцииС1СДокументооборотом КАК Правила
		|ГДЕ
		|	(ТипОбъектаПотребителя <> """"
		|		И ВЫРАЗИТЬ(ПредставлениеОбъектаПотребителя КАК Строка(1)) = """")
		|	ИЛИ (ТипОбъектаДокументооборота <> """"
		|		И ВЫРАЗИТЬ(ПредставлениеОбъектаДокументооборота КАК Строка(1)) = """")");
		
	ВыборкаПравила = ЗапросПравила.Выполнить().Выбрать();
	Пока ВыборкаПравила.Следующий() Цикл
		
		ПравилоОбъект = ВыборкаПравила.Ссылка.ПолучитьОбъект();
		
		Для Каждого ПравилоВыгрузки Из ПравилоОбъект.ПравилаЗаполненияРеквизитовПриВыгрузке Цикл
			Если ПравилоВыгрузки.ИмяРеквизитаОбъектаДокументооборота = "documentType" Тогда
				ПравилоВыгрузки.Ключевой = Истина;
			КонецЕсли;
		КонецЦикла;
		
		ПравилоОбъект.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

// Обработчик обновления БИД на версию 1.1.8.4. Установка флага "Не создавать связи по ссылкам".
//
Процедура ПерейтиНаВерсию_1_1_8_4() Экспорт
	
	ЗапросПравила = Новый Запрос(
		"ВЫБРАТЬ
		|	Правила.Ссылка
		|ИЗ
		|	Справочник.ПравилаИнтеграцииС1СДокументооборотом КАК Правила
		|ГДЕ
		|	Не Правила.НеСоздаватьСвязиПоСсылкам
		|");
		
	ВыборкаПравила = ЗапросПравила.Выполнить().Выбрать();
	Пока ВыборкаПравила.Следующий() Цикл
		
		ПравилоОбъект = ВыборкаПравила.Ссылка.ПолучитьОбъект();
		ПравилоОбъект.НеСоздаватьСвязиПоСсылкам = Истина;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ПравилоОбъект);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти