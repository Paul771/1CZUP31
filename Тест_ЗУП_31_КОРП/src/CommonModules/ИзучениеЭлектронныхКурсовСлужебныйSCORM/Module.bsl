#Область СлужебныеПроцедурыИФункции

#Область РаботаСоВременем

// Получает структуру временного интервала в формате SCORM
//
Функция СтруктураВременногоИнтервалаSCORM(Знач ВременнойИнтервал) Экспорт
	
	Позиция1  = Найти(ВременнойИнтервал, "P");
	Позиция5  = Найти(ВременнойИнтервал, "T");
	
	Если Позиция1 = 1 Тогда
		
		Если Позиция5 > 0 Тогда
			ПерваяЧасть = Сред(ВременнойИнтервал, Позиция1, Позиция5-1);
		Иначе
			ПерваяЧасть = ВременнойИнтервал;
		КонецЕсли;
		
		Позиция2 = Найти(ПерваяЧасть, "Y");
		Если Позиция2 > 0 Тогда
			КоличествоЛет = Число(Сред(ПерваяЧасть, Позиция1+1, Позиция2-Позиция1-1));
		Иначе
			Позиция2 = Позиция1;
			КоличествоЛет = 0;
		КонецЕсли;
		
		Позиция3 = Найти(ПерваяЧасть, "M");
		Если Позиция3 > 0 Тогда
			КоличествоМесяцев = Число(Сред(ПерваяЧасть, Позиция2+1, Позиция3-Позиция2-1));
		Иначе
			Позиция3 = Позиция2;
			КоличествоМесяцев = 0;
		КонецЕсли;
		
		Позиция4 = Найти(ПерваяЧасть, "D");
		Если Позиция4 > 0 Тогда
			КоличествоДней = Число(Сред(ПерваяЧасть, Позиция3+1, Позиция4-Позиция3-1));
		Иначе
			Позиция4 = Позиция3;
			КоличествоДней = 0;
		КонецЕсли;
		
	Иначе
		Возврат 0;
	КонецЕсли;
	
	Если Позиция5 > 0 Тогда
		
		ВтораяЧасть = Сред(ВременнойИнтервал, Позиция5+1, СтрДлина(ВременнойИнтервал)-Позиция5);
		Позиция1 = 0;
		
		Позиция2 = Найти(ВтораяЧасть, "H");
		Если Позиция2 > 0 Тогда
			КоличествоЧасов = Число(Сред(ВтораяЧасть, Позиция1+1, Позиция2-Позиция1-1));
		Иначе
			Позиция2 = Позиция1;
			КоличествоЧасов = 0;
		КонецЕсли;
		
		Позиция3 = Найти(ВтораяЧасть, "M");
		Если Позиция3 > 0 Тогда
			КоличествоМинут = Число(Сред(ВтораяЧасть, Позиция2+1, Позиция3-Позиция2-1));
		Иначе
			Позиция3 = Позиция2;
			КоличествоМинут = 0;
		КонецЕсли;
		
		Позиция4 = Найти(ВтораяЧасть, "S");
		Если Позиция4 > 0 Тогда
			КоличествоСекунд = Сред(ВтораяЧасть, Позиция3+1, Позиция4-Позиция3-1);
			КоличествоСекунд = СтрЗаменить(КоличествоСекунд, ".", ",");
			КоличествоСекунд = Окр(Число(КоличествоСекунд));
		Иначе
			Позиция4 = Позиция3;
			КоличествоСекунд = 0;
		КонецЕсли;
		
	Иначе
		КоличествоЧасов  = 0;
		КоличествоМинут  = 0;
		КоличествоСекунд = 0;
	КонецЕсли;
	
	СтруктураИнтервала = Новый Структура();
	СтруктураИнтервала.Вставить("КоличествоЛет", КоличествоЛет);
	СтруктураИнтервала.Вставить("КоличествоМесяцев", КоличествоМесяцев);
	СтруктураИнтервала.Вставить("КоличествоДней", КоличествоДней);
	СтруктураИнтервала.Вставить("КоличествоЧасов", КоличествоЧасов);
	СтруктураИнтервала.Вставить("КоличествоМинут", КоличествоМинут);
	СтруктураИнтервала.Вставить("КоличествоСекунд", КоличествоСекунд);
	
	Возврат СтруктураИнтервала;
	
КонецФункции

// Получает количество секунд из структуры временного интервала
//
Функция КоличествоСекундВременногоИнтервалаSCORM(Знач ВременнойИнтервал) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ВременнойИнтервал) Тогда
		Возврат 0;
	КонецЕсли;
	
	СтруктураИнтервала = СтруктураВременногоИнтервалаSCORM(ВременнойИнтервал);
	
	СуммаСекунд = КоличествоСекундИзСтруктурыВременногоИнтервала(СтруктураИнтервала);
	
	Возврат СуммаСекунд;
	
КонецФункции

// Получает сумму двух временных интервалов
//
Функция СуммироватьДваВременныхИнтервала(Знач ВременнойИнтервал1, Знач ВременнойИнтервал2) Экспорт
	
	СуммаСекунд1 = КоличествоСекундВременногоИнтервалаSCORM(ВременнойИнтервал1);
	СуммаСекунд2 = КоличествоСекундВременногоИнтервалаSCORM(ВременнойИнтервал2);
	
	ОбщаяСуммаСекунд = СуммаСекунд1 + СуммаСекунд2;
	
	Возврат ВременнойИнтервалSCORMИзКоличестваСекунд(ОбщаяСуммаСекунд);
	
КонецФункции

// Получает из количества секунд временной интервал SCORM
//
Функция ВременнойИнтервалSCORMИзКоличестваСекунд(Знач СуммаСекунд) Экспорт
	
	КоличествоЛет = Цел(СуммаСекунд/31536000);
	
	Если КоличествоЛет > 0 Тогда
		СуммаСекунд = СуммаСекунд - 31536000*КоличествоЛет;
	КонецЕсли;
	
	КоличествоМесяцев = Цел(СуммаСекунд/2628000); 
	
	Если КоличествоМесяцев > 0 Тогда
		СуммаСекунд = СуммаСекунд - 2628000*КоличествоМесяцев;
	КонецЕсли;
	
	КоличествоДней = Цел(СуммаСекунд/86400); 
	
	Если КоличествоДней > 0 Тогда
		СуммаСекунд = СуммаСекунд - 86400*КоличествоДней;
	КонецЕсли;
	
	КоличествоЧасов = Цел(СуммаСекунд/3600); 
	
	Если КоличествоЧасов > 0 Тогда
		СуммаСекунд = СуммаСекунд - 3600*КоличествоЧасов;
	КонецЕсли;
	
	КоличествоМинут = Цел(СуммаСекунд/60); 
	
	Если КоличествоМинут > 0 Тогда
		СуммаСекунд = СуммаСекунд - 60*КоличествоМинут;
	КонецЕсли;
	
	СтрокаИнтервала = "P";
	
	Если КоличествоЛет > 0 Тогда
		СтрокаИнтервала = СтрокаИнтервала + Строка(КоличествоЛет) + "Y";
	КонецЕсли;
	
	Если КоличествоМесяцев > 0 Тогда
		СтрокаИнтервала = СтрокаИнтервала + Строка(КоличествоМесяцев) + "M";
	КонецЕсли;
	
	Если КоличествоДней > 0 Тогда
		СтрокаИнтервала = СтрокаИнтервала + Строка(КоличествоДней) + "D";
	КонецЕсли;
	
	Если КоличествоЧасов > 0 ИЛИ КоличествоМинут > 0 ИЛИ СуммаСекунд > 0 Тогда
		
		СтрокаИнтервала = СтрокаИнтервала + "T";
		
		Если КоличествоЧасов > 0 Тогда
			СтрокаИнтервала = СтрокаИнтервала + Строка(КоличествоЧасов) + "H";
		КонецЕсли;
		
		Если КоличествоМинут > 0 Тогда
			СтрокаИнтервала = СтрокаИнтервала + Строка(КоличествоМинут) + "M";
		КонецЕсли;
		
		Если СуммаСекунд > 0 Тогда
			СтрокаИнтервала = СтрокаИнтервала + Строка(СуммаСекунд) + "S";
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтрокаИнтервала;
	
КонецФункции

// Получает время в формате даты из временного интервала.
// Внимание: в отличие от интервала, время не может превышать 23.59.59.
Функция ДатаИзВременногоИнтервала(Знач ВременнойИнтервал) Экспорт
	
	СуммаСекунд = КоличествоСекундВременногоИнтервалаSCORM(ВременнойИнтервал);
	
	КоличествоЧасов = Цел(СуммаСекунд/3600); 
	
	Если КоличествоЧасов > 0 Тогда
		СуммаСекунд = СуммаСекунд - 3600*КоличествоЧасов;
	КонецЕсли;
	
	Если КоличествоЧасов > 23 Тогда
		КоличествоЧасов = 23;
		КоличествоМинут = 59;
		КоличествоСекунд = 59;
	Иначе
		
		КоличествоМинут = Цел(СуммаСекунд/60); 
		
		Если КоличествоМинут > 0 Тогда
			КоличествоСекунд = СуммаСекунд - 60*КоличествоМинут;
		Иначе
			КоличествоСекунд = СуммаСекунд;
		КонецЕсли;
		
	КонецЕсли;
	
	Если КоличествоЧасов < 10 Тогда
		КоличествоЧасов = Строка("0") + Строка(КоличествоЧасов);
	КонецЕсли;
	
	Если КоличествоМинут < 10 Тогда
		КоличествоМинут = Строка("0") + Строка(КоличествоМинут);
	КонецЕсли;
	
	Если КоличествоСекунд < 10 Тогда
		КоличествоСекунд = Строка("0") + Строка(КоличествоСекунд);
	КонецЕсли;
	
	Время = Дата("00010101"+Строка(КоличествоЧасов)+Строка(КоличествоМинут)+Строка(КоличествоСекунд));
	
	Возврат Время;
	
КонецФункции

// Получает время в формате даты из количества секунд.
// Внимание: в отличие от интервала, время не может превышать 23.59.59.
Функция ДатаИзКоличестваСекунд(Знач СуммаСекунд) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СуммаСекунд) Тогда
		Возврат Дата("00010101");
	КонецЕсли;
	
	ВременнойИнтервал = ВременнойИнтервалSCORMИзКоличестваСекунд(СуммаСекунд);
	
	Возврат ДатаИзВременногоИнтервала(ВременнойИнтервал);	
	
КонецФункции

Функция ДатаИзКоличестваМинут(Знач СуммаМинут) Экспорт
	
	СуммаСекунд = СуммаМинут*60;
	
	Возврат ДатаИзКоличестваСекунд(СуммаСекунд);
	
КонецФункции

// Получает количество секунд на основании даты
//
Функция КоличествоСекундИзДаты(Знач Дата) Экспорт

	КоличествоЧасов  = Час(Дата);
	КоличествоМинут  = Минута(Дата);
	КоличествоСекунд = Секунда(Дата);
	
	Возврат (КоличествоЧасов*60*60) + (КоличествоМинут*60) + КоличествоСекунд;
	
КонецФункции

// Получает количество секунд на основании даты
//
Функция КоличествоМинутИзДаты(Знач Дата) Экспорт

	Возврат Окр(КоличествоСекундИзДаты(Дата) / 60);
	
КонецФункции

// Получает строковое представление из суммы секунд
//
Функция СтрокаИзКоличестваСекунд(Знач СуммаСекунд) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СуммаСекунд) Тогда
		Возврат "";
	КонецЕсли;

	КоличествоЧасов = Цел(СуммаСекунд/3600); 
	
	Если КоличествоЧасов > 0 Тогда
		СуммаСекунд = СуммаСекунд - 3600*КоличествоЧасов;
	КонецЕсли;
		
	КоличествоМинут = Цел(СуммаСекунд/60); 
	
	Если КоличествоМинут > 0 Тогда
		КоличествоСекунд = СуммаСекунд - 60*КоличествоМинут;
	Иначе
		КоличествоСекунд = СуммаСекунд;
	КонецЕсли;
	
	КоличествоСекунд = Окр(КоличествоСекунд);
	
	Если КоличествоЧасов < 10 Тогда
		КоличествоЧасов = Строка("0") + Строка(КоличествоЧасов);
	КонецЕсли;
	
	Если КоличествоМинут < 10 Тогда
		КоличествоМинут = Строка("0") + Строка(КоличествоМинут);
	КонецЕсли;
	
	Если КоличествоСекунд < 10 Тогда
		КоличествоСекунд = Строка("0") + Строка(КоличествоСекунд);
	КонецЕсли;
	
	
	
	Время = Строка(КоличествоЧасов) + ":" + Строка(КоличествоМинут) + ":" + Строка(КоличествоСекунд);
	
	Возврат Время;
	
КонецФункции

// Получает полное строковое представление из суммы секунд
//
Функция ПолнаяСтрокаИзКоличестваСекунд(Знач СуммаСекунд, ГраницаИнтервала = Неопределено) Экспорт
	
	ИнтервалSCORM = ВременнойИнтервалSCORMИзКоличестваСекунд(СуммаСекунд);
	
	СтруктураИнтервала = СтруктураВременногоИнтервалаSCORM(ИнтервалSCORM);
	
	Если ГраницаИнтервала <> Неопределено Тогда
		
		Если ГраницаИнтервала > 0 Тогда
			СтруктураИнтервала.КоличествоМесяцев = СтруктураИнтервала.КоличествоМесяцев + (СтруктураИнтервала.КоличествоЛет * 12);
			СтруктураИнтервала.КоличествоЛет = 0;
		КонецЕсли;
		
		Если ГраницаИнтервала > 1 Тогда
			СтруктураИнтервала.КоличествоДней = СтруктураИнтервала.КоличествоДней + (СтруктураИнтервала.КоличествоМесяцев * 30);
			СтруктураИнтервала.КоличествоМесяцев = 0;
		КонецЕсли;
		
		Если ГраницаИнтервала > 2 Тогда
			СтруктураИнтервала.КоличествоЧасов = СтруктураИнтервала.КоличествоЧасов + (СтруктураИнтервала.КоличествоДней * 24);
			СтруктураИнтервала.КоличествоДней = 0;
		КонецЕсли;
		
		Если ГраницаИнтервала > 3 Тогда
			СтруктураИнтервала.КоличествоМинут = СтруктураИнтервала.КоличествоМинут + (СтруктураИнтервала.КоличествоЧасов * 60);
			СтруктураИнтервала.КоличествоЧасов = 0;
		КонецЕсли;
		
		Если ГраницаИнтервала > 4 Тогда
			СтруктураИнтервала.КоличествоСекунд = СтруктураИнтервала.КоличествоСекунд + (СтруктураИнтервала.КоличествоМинут * 60);
			СтруктураИнтервала.КоличествоМинут = 0;
		КонецЕсли;
		
	КонецЕсли;
	
	Строка     = "";
	ФормСтрока = "Л = ru_RU";
	
	Если СтруктураИнтервала.КоличествоЛет > 0 Тогда // 1
		ПарПредмета="год,года,лет,м,,,,,0";
		Строка = Строка + ЧислоПрописью(СтруктураИнтервала.КоличествоЛет, ФормСтрока, ПарПредмета) + ",";
	КонецЕсли;
	
	Если СтруктураИнтервала.КоличествоМесяцев > 0 Тогда // 2
		ПарПредмета="месяц,месяца,месяцев,м,,,,,0";
		Строка = Строка + ЧислоПрописью(СтруктураИнтервала.КоличествоМесяцев, ФормСтрока, ПарПредмета) + ",";
	КонецЕсли;
	
	Если СтруктураИнтервала.КоличествоДней > 0 Тогда // 3
		ПарПредмета="день,дня,дней,м,,,,,0";
		Строка = Строка + ЧислоПрописью(СтруктураИнтервала.КоличествоДней, ФормСтрока, ПарПредмета) + ",";
	КонецЕсли;
	
	Если СтруктураИнтервала.КоличествоЧасов > 0 Тогда // 4
		ПарПредмета="час,часа,часов,м,,,,,0";
		Строка = Строка + ЧислоПрописью(СтруктураИнтервала.КоличествоЧасов, ФормСтрока, ПарПредмета) + ",";
	КонецЕсли;
	
	Если СтруктураИнтервала.КоличествоМинут > 0 Тогда // 5
		ПарПредмета="минута,минуты,минут,ж,,,,,0";
		Строка = Строка + ЧислоПрописью(СтруктураИнтервала.КоличествоМинут, ФормСтрока, ПарПредмета) + ",";
	КонецЕсли;
	
	Если СтруктураИнтервала.КоличествоСекунд > 0 Тогда // 6
		ПарПредмета="секунда,секунды,секунд,ж,,,,,0";
		Строка = Строка + ЧислоПрописью(СтруктураИнтервала.КоличествоСекунд, ФормСтрока, ПарПредмета) + ",";
	КонецЕсли;
	
	Строка = НРег(Строка);
	
	Строка = Сред(Строка, 1, СтрДлина(Строка)-1);
	
	МассивСтроки = ЭлектронноеОбучениеСлужебныйКлиентСервер.СтрокаВебРазделить(Строка, ",");
	
	ФинальнаяСтрока = "";
	
	НомерЭлемента = 1;
	КоличествоЭлементов = МассивСтроки.Количество();
	Для каждого Элемент Из МассивСтроки Цикл
	
		ФинальнаяСтрока = ФинальнаяСтрока + Элемент;
		
		Если НомерЭлемента <> КоличествоЭлементов Тогда
			Если НомерЭлемента = (КоличествоЭлементов - 1) Тогда
				ФинальнаяСтрока = ФинальнаяСтрока + " и ";
			Иначе
				ФинальнаяСтрока = ФинальнаяСтрока + ", ";	
			КонецЕсли;
		КонецЕсли;
		
		НомерЭлемента = НомерЭлемента + 1;
		
	КонецЦикла;	
	
	Возврат ФинальнаяСтрока;
	
КонецФункции

// Получает количество секунд из структуры,
// в которой указаны параметры временного интервала.
//
Функция КоличествоСекундИзСтруктурыВременногоИнтервала(Знач СтруктураИнтервала) Экспорт
	
	СуммаСекунд = СтруктураИнтервала.КоличествоСекунд
	+ (СтруктураИнтервала.КоличествоМинут*60)
	+ (СтруктураИнтервала.КоличествоЧасов*60*60)
	+ (СтруктураИнтервала.КоличествоДней*60*60*24)
	+ (СтруктураИнтервала.КоличествоМесяцев*60*60*24*(365/12))
	+ (СтруктураИнтервала.КоличествоЛет*60*60*24*365);
	
	СуммаСекунд = Окр(СуммаСекунд);
	
	Возврат СуммаСекунд;
	
КонецФункции

#КонецОбласти

#КонецОбласти

