#Область СлужебныеПроцедурыИФункции

Функция НаименованиеПозицииШтатногоРасписания(Организация, Должность, МестоВСтруктуреПредприятия) 
	
	Шаблон = "%1";
	Если ЗначениеЗаполнено(МестоВСтруктуреПредприятия) Тогда
		Шаблон = Шаблон + " /%2/";
	КонецЕсли;
	Если ЗначениеЗаполнено(Организация) Тогда
		Шаблон = Шаблон + ", (%3)";
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, Должность, МестоВСтруктуреПредприятия, Организация);
	
КонецФункции

Процедура СформироватьНаименованиеПозицииШтатногоРасписания(НаименованиеПозиции, Должность, ДополнительныеПараметры, СтандартнаяОбработка) Экспорт
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		МестоВСтруктуреПредприятия = Неопределено;
		Если ДополнительныеПараметры.Свойство("МестоВСтруктуреПредприятия", МестоВСтруктуреПредприятия) Тогда
			
			Организация = Неопределено;
			ДополнительныеПараметры.Свойство("Организация", Организация);
			
			НаименованиеПозиции = НаименованиеПозицииШтатногоРасписания(Организация, Должность, МестоВСтруктуреПредприятия);
			СтандартнаяОбработка = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СписокСотрудниковУстановитьОтображениеСписков(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	
	ЭлементыФормаПоОрганизационнойСтруктуре = Элементы.Найти("ФормаПоОрганизационнойСтруктуре");
	Если ЭлементыФормаПоОрганизационнойСтруктуре <> Неопределено Тогда
		
		Если Элементы.ФормаПоОрганизационнойСтруктуре.Пометка
			И Не Элементы.ФормаВсеСотрудники.Пометка
			И Не Элементы.ФормаПоПодразделениям.Пометка
			И Не Элементы.ФормаПоГруппам.Пометка Тогда
			
			ТекущаяСтраницаПодразделенияГруппыСотрудниковСтраницы = Элементы.ОрганизационнаяСтруктураСтраница;
			
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				"ПодразделенияГруппыСотрудниковСтраницы",
				"ТекущаяСтраница",
				ТекущаяСтраницаПодразделенияГруппыСотрудниковСтраницы);
			
		Иначе
			Элементы.ФормаПоОрганизационнойСтруктуре.Пометка = Ложь;
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"МестоВСтруктуреПредприятия",
			"Видимость",
			Не Элементы.ФормаПоОрганизационнойСтруктуре.Пометка);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
