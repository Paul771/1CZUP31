////////////////////////////////////////////////////////////////////////////////
// ПОДСИСТЕМА ОСТАТКИ ОТПУСКОВ
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

// Возвращает доступны ли перерасчеты для переданной формы.
// Форма должна содержать реквизиты объекта.
//
Функция ПерерасчетыДоступны(Форма, ИмяРеквизитаПериодаРегистрации = "ПериодРегистрации", ИмяРеквизитаДатыНачала = "ДатаНачала", ВозможноВытеснение = Истина) Экспорт
	
	Возврат (ЗначениеЗаполнено(Форма.Объект.ИсправленныйДокумент) И Не ИсправлениеВТекущемПериоде(Форма, ИмяРеквизитаПериодаРегистрации))
		Или (ВозможноВытеснение И (ЗначениеЗаполнено(Форма.Объект[ИмяРеквизитаДатыНачала]) И Форма.Объект[ИмяРеквизитаПериодаРегистрации] > Форма.Объект[ИмяРеквизитаДатыНачала]))
		Или (Форма.Объект.НачисленияПерерасчет.Количество() > 0);
	
КонецФункции

// Возвращает признак что исправление выполняется в текущем периоде.
//
Функция ИсправлениеВТекущемПериоде(Форма, ИмяРеквизитаПериодаРегистрации = "ПериодРегистрации") Экспорт
	
	Возврат ЗначениеЗаполнено(Форма.Объект.ИсправленныйДокумент)
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ПериодРегистрацииИсправленногоДокумента")
		И Форма.ПериодРегистрацииИсправленногоДокумента = Форма.Объект[ИмяРеквизитаПериодаРегистрации];
	
КонецФункции

// Настраивает флаг ДоначислитьЗарплатуПриНеобходимости в зависимости от режима исправления.
//
Процедура УстановитьДоначислениеПриИсправлении(Форма, ПерерасчетыДоступны, ИмяРеквизитаПериодаРегистрации = "ПериодРегистрации") Экспорт
	
	ЭлементФормы = Форма.Элементы.Найти("ДоначислитьЗарплатуПриНеобходимости");
	Если ЭлементФормы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПерерасчетыДоступныПриИсправлении =	ПерерасчетыДоступны И (Форма.Элементы.Найти("Исправить") <> Неопределено) И ЗначениеЗаполнено(Форма.Объект.ИсправленныйДокумент);
	ИсправлениеВТекущемПериоде = Ложь;
	ОбновитьРеквизитОбъекта = Ложь;
	
	Если ПерерасчетыДоступныПриИсправлении Тогда
		
		ИсправлениеВТекущемПериоде = ИсправлениеВТекущемПериоде(Форма, ИмяРеквизитаПериодаРегистрации);
		
		Если ЭлементФормы.Доступность = ИсправлениеВТекущемПериоде Тогда
			ЭлементФормы.Доступность = Не ИсправлениеВТекущемПериоде;
			ОбновитьРеквизитОбъекта = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ЭлементФормы.Видимость <> ПерерасчетыДоступныПриИсправлении Тогда
		ЭлементФормы.Видимость = ПерерасчетыДоступныПриИсправлении;
		ОбновитьРеквизитОбъекта = Истина;
	КонецЕсли;
	
	Если ОбновитьРеквизитОбъекта Тогда
		Форма.Объект.ДоначислитьЗарплатуПриНеобходимости = ПерерасчетыДоступныПриИсправлении И Не ИсправлениеВТекущемПериоде;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
