#Область ОписаниеПеременных

&НаКлиенте
Перем ПриложениеYouTube; // Свойства приложения для загрузки видео на YouTube

#КонецОбласти


#Область ОбработчикиСобытийФормы

// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;	
	
	
	Для каждого ПутьКФайлу Из Параметры.ФайлыДляЗагрузки Цикл
	
		НоваяСтрока = ФайлыДляЗагрузки.Добавить();
		НоваяСтрока.ПутьКФайлу = ПутьКФайлу;
	
	КонецЦикла;
	
	ПерсональныеНастройкиYouTube = Параметры.ПерсональныеНастройкиYouTube;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)	
	
	#Если НЕ ВебКлиент Тогда
		
	НастройкиYouTube = РазработкаЭлектронныхКурсовСлужебныйВызовСервера.НастройкиYouTube();
	
	ПриложениеYouTube = Новый Структура("РабочийКаталог, ФайлОтвета, ПараметрыCURL");
	ПриложениеYouTube.РабочийКаталог = ПолучитьИмяВременногоФайла("");
	ПриложениеYouTube.ФайлОтвета = ПолучитьИмяВременногоФайла("json");
		
	СоздатьКаталог(ПриложениеYouTube.РабочийКаталог);
		
	ПриложениеYouTube.РабочийКаталог = ЭлектронноеОбучениеСлужебныйКлиентСервер.ДобавитьКонечныйРазделительПути(ПриложениеYouTube.РабочийКаталог);
		
	РазработкаЭлектронныхКурсовСлужебныйКлиент.СкопироватьПрограммуCURL(ПриложениеYouTube.РабочийКаталог);

	ПодключитьОбработчикОжидания("ПроверитьЗагрузкуВсехФайлов", 0.1, Истина);
	
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ВсеЗагружено = Истина;
	
	Для каждого Строка Из ФайлыДляЗагрузки Цикл		
		Если НЕ ЗначениеЗаполнено(Строка.ИдентификаторВидео) Тогда
			ВсеЗагружено = Ложь;
			Прервать;
		КонецЕсли;	
	КонецЦикла;
	
	Если ВсеЗагружено Тогда
		
		Возврат;
		
	Иначе
		
		Если ЗавершениеРаботы Тогда
			Отказ = Истина;
			ТекстПредупреждения = НСтр("ru = 'Загрузка видео на YouTube еще не завершена'");
		КонецЕсли;
			
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ЭлектронноеОбучениеСлужебныйКлиентСервер.УдалитьВременныйФайл(ПриложениеYouTube.РабочийКаталог);
	ЭлектронноеОбучениеСлужебныйКлиентСервер.УдалитьВременныйФайл(ПриложениеYouTube.ФайлОтвета);
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ПроверитьЗагрузкуВсехФайлов()
	
	Для каждого Строка Из ФайлыДляЗагрузки Цикл
		
		Если НЕ ЗначениеЗаполнено(Строка.ИдентификаторВидео) Тогда
			ИндексЗагружаемогоФайла = Строка.ПолучитьИдентификатор();
			ЗагрузитьФайлВидеоНаYouTube(Новый Файл(Строка.ПутьКФайлу), ПерсональныеНастройкиYouTube);
			Возврат; // Еще не все загрузили
		КонецЕсли;
	
	КонецЦикла;
	
	ЗагруженныеФайлы = Новый Соответствие;
	
	Для каждого Строка Из ФайлыДляЗагрузки Цикл	
		ЗагруженныеФайлы.Вставить(Строка.ПутьКФайлу, Строка.ИдентификаторВидео);	
	КонецЦикла;
	
	Закрыть(ЗагруженныеФайлы);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФайлВидеоНаYouTube(ФайлДляЗагрузки, ПерсональныеНастройкиYouTube)

	СтатусЗагрузки = НСтр("ru = 'Выполняется загрузка файла видео на YouTube...'");
	ПрогрессЗагрузки = 1;
	
	ПараметрыЗапуска = РазработкаЭлектронныхКурсовСлужебныйКлиент.ПараметрыЗапускаПрограммы();
	ПараметрыЗапуска.ТекущийКаталог = ПриложениеYouTube.РабочийКаталог;
	ПараметрыЗапуска.ДождатьсяЗавершения = Ложь;
	ПараметрыЗапуска.ПолучитьПотокВывода = Истина;
	ПараметрыЗапуска.ПолучитьПотокОшибок = Истина;	
	
	КомандаЗапуска = ПриложениеYouTube.РабочийКаталог + "curl.exe -o """ + ПриложениеYouTube.ФайлОтвета + """ ";
	
	КомандаЗапуска = КомандаЗапуска + " --progress-bar ";
	КомандаЗапуска = КомандаЗапуска + " --write-out %{http_code} ";	
	КомандаЗапуска = КомандаЗапуска + " --data-binary ""@" + ФайлДляЗагрузки.ПолноеИмя + """ ";	
	КомандаЗапуска = КомандаЗапуска + " --header ""Authorization:Bearer " + ПерсональныеНастройкиYouTube.access_token + " "" ";	
	КомандаЗапуска = КомандаЗапуска + " --header ""Content-Type:application/octet-stream"" ";
	
	КомандаЗапуска = КомандаЗапуска +  """https://www.googleapis.com/upload/youtube/v3/videos?part=id&key=" + НастройкиYouTube.api_key + """";	
	
	ПриложениеYouTube.ПараметрыCURL = РазработкаЭлектронныхКурсовСлужебныйКлиент.ЗапуститьПрограмму(КомандаЗапуска, ПараметрыЗапуска);		
	ПриложениеYouTube.ПараметрыCURL.Вставить("СчетчикВызовов", 0);
	ПриложениеYouTube.ПараметрыCURL.Вставить("Прогресс", 0);	
	ПриложениеYouTube.ПараметрыCURL.Вставить("ИмяБезРасширения", ФайлДляЗагрузки.ИмяБезРасширения);
	ПриложениеYouTube.ПараметрыCURL.Вставить("ПерсональныеНастройкиYouTube", ПерсональныеНастройкиYouTube);
	
	ПодключитьОбработчикОжидания("ПроверитьПроцессЗагрузкиВидеоНаYouTube", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПроцессЗагрузкиВидеоНаYouTube() Экспорт
	
	#Если НЕ ВебКлиент Тогда
		
	СтатусЗагрузки = НСтр("ru = 'Выполняется загрузка файла видео на YouTube...'");
	
	Если ПриложениеYouTube.ПараметрыCURL.Прогресс > 2 Тогда		
		ПрогрессЗагрузки = ПриложениеYouTube.ПараметрыCURL.Прогресс - 1;
	КонецЕсли;
		
	Если ПриложениеYouTube.ПараметрыCURL.СчетчикВызовов > 60*2 Тогда
		ВызватьИсключение НСтр("ru = 'Превышено время ожидания'");
	КонецЕсли;
		
	ФайлПрогресса = ПриложениеYouTube.ПараметрыCURL.ПотокОшибок;
	
	Если ФайлПрогресса.Существует() Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбновитьПрогрессЗагрузкиВидеоНаYouTube", ЭтотОбъект);
		КопияФайлаПрогресса = ПолучитьИмяВременногоФайла("tmp");
		НачатьКопированиеФайла(ОписаниеОповещения, ФайлПрогресса.ПолноеИмя, КопияФайлаПрогресса);
		
	Иначе
		
		ПриложениеYouTube.ПараметрыCURL.СчетчикВызовов = ПриложениеYouTube.ПараметрыCURL.СчетчикВызовов + 1;
		ПодключитьОбработчикОжидания("ПроверитьПроцессЗагрузкиВидеоНаYouTube", 0.5, Истина); // Рекурсия
		
	КонецЕсли;
	
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПрогрессЗагрузкиВидеоНаYouTube(ПутьККопииФайлаПрогресса, ДополнительныеПараметры) Экспорт
	
	НовыйПрогресс = РазработкаЭлектронныхКурсовСлужебныйКлиентСервер.ПрогрессВыполненияЗапросаCURL(ПутьККопииФайлаПрогресса);
	
	Если ПриложениеYouTube.ПараметрыCURL.Прогресс = НовыйПрогресс Тогда
		ПриложениеYouTube.ПараметрыCURL.СчетчикВызовов = ПриложениеYouTube.ПараметрыCURL.СчетчикВызовов + 1;
	Иначе
		ПриложениеYouTube.ПараметрыCURL.Прогресс = НовыйПрогресс;
		ПрогрессЗагрузки = ПриложениеYouTube.ПараметрыCURL.Прогресс - 1;
		ПриложениеYouTube.ПараметрыCURL.СчетчикВызовов = 0;
	КонецЕсли;
	
	Если ПриложениеYouTube.ПараметрыCURL.Прогресс = 100 Тогда
		ПодключитьОбработчикОжидания("ПроверитьКодОтветаСервераYouTube", 0.5, Истина)
	Иначе
		ПодключитьОбработчикОжидания("ПроверитьПроцессЗагрузкиВидеоНаYouTube", 0.5, Истина); // Рекурсия
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьКодОтветаСервераYouTube() Экспорт
	
	#Если НЕ ВебКлиент Тогда
		
	Если ПриложениеYouTube.ПараметрыCURL.СчетчикВызовов > 60*2 Тогда
		ВызватьИсключение НСтр("ru = 'Превышено время ожидания'");
	КонецЕсли;
	
	ФайлВывода = ПриложениеYouTube.ПараметрыCURL.ПотокВывода;
	
	Если ФайлВывода.Существует() Тогда
		
		СтатусЗагрузки = НСтр("ru = 'Идет обновление параметров видео на YouTube...'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьКодОтветаСервераYouTubeНаКопииФайла", ЭтотОбъект);		
		КопияФайлаВывода = ПолучитьИмяВременногоФайла("tmp");
		НачатьКопированиеФайла(ОписаниеОповещения, ФайлВывода.ПолноеИмя, КопияФайлаВывода);
		
	Иначе
		
		ПриложениеYouTube.ПараметрыCURL.СчетчикВызовов = ПриложениеYouTube.ПараметрыCURL.СчетчикВызовов + 1;
		ПодключитьОбработчикОжидания("ПроверитьКодОтветаСервераYouTube", 0.5, Истина); // Рекурсия
		
	КонецЕсли;
	
	#КонецЕсли	
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьКодОтветаСервераYouTubeНаКопииФайла(ПутьККопииФайлаВывода, ДополнительныеПараметры) Экспорт
	
	#Если НЕ ВебКлиент Тогда

	// Читаем код ответа сервера
		
	ЧтениеВывода = Новый ЧтениеТекста(ПутьККопииФайлаВывода, РазработкаЭлектронныхКурсовСлужебныйКлиентСервер.КодировкаСтандартныхПотоков());	
	КодОтветаСервера = ЧтениеВывода.Прочитать();
	ЧтениеВывода.Закрыть();

	ЭлектронноеОбучениеСлужебныйКлиентСервер.УдалитьВременныйФайл(ПутьККопииФайлаВывода);
	
	Если КодОтветаСервера = Неопределено Тогда
		ПриложениеYouTube.ПараметрыCURL.СчетчикВызовов = ПриложениеYouTube.ПараметрыCURL.СчетчикВызовов + 1;
		ПодключитьОбработчикОжидания("ПроверитьКодОтветаСервераYouTube", 0.5, Истина); // Рекурсия		
		Возврат;
	КонецЕсли;
	
	Если КодОтветаСервера <> "200" Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Ошибка соединения с сервером: %1'"), КодОтветаСервера);
	КонецЕсли;		
	
	// Получаем идентификатор загруженного видео
	
	ФайлОтвета = Новый Файл(ПриложениеYouTube.ФайлОтвета);
	
	Чтение = Новый ЧтениеJSON;
    Чтение.ОткрытьФайл(ФайлОтвета.ПолноеИмя);
	ОтветСервераСтруктура = ПрочитатьJSON(Чтение, Ложь);
    Чтение.Закрыть();		
	
	Если ОтветСервераСтруктура = Неопределено
		ИЛИ НЕ ОтветСервераСтруктура.Свойство("id") Тогда
		
		ВызватьИсключение НСтр("ru = 'Неожиданный ответ сервера YouTube'");
		
	КонецЕсли;	
	
	ИдентификаторВидеоYouTube = ОтветСервераСтруктура.id;	
	
	// Обновляем свойства загруженного видео

	ПараметрыЗапуска = РазработкаЭлектронныхКурсовСлужебныйКлиент.ПараметрыЗапускаПрограммы();
	ПараметрыЗапуска.ТекущийКаталог = ПриложениеYouTube.РабочийКаталог;
	ПараметрыЗапуска.ДождатьсяЗавершения = Истина;
	ПараметрыЗапуска.ПолучитьПотокВывода = Истина;
	ПараметрыЗапуска.ПолучитьПотокОшибок = Истина;
	
	ПараметрыВидео = Новый Структура("id, snippet, status");
	ПараметрыВидео.id = ИдентификаторВидеоYouTube;
	ПараметрыВидео.snippet = Новый Структура("title, categoryId");
	ПараметрыВидео.snippet.title = ПриложениеYouTube.ПараметрыCURL.ИмяБезРасширения;
	ПараметрыВидео.snippet.categoryId = 27;
	ПараметрыВидео.status = Новый Структура("privacyStatus, embeddable");
	ПараметрыВидео.status.privacyStatus = "unlisted";
	ПараметрыВидео.status.embeddable = true;
	
	ПутьКФайлуJSON = ПолучитьИмяВременногоФайла("json");
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписьJSON.ОткрытьФайл(ПутьКФайлуJSON);
	ЗаписатьJSON(ЗаписьJSON, ПараметрыВидео);						
	ЗаписьJSON.Закрыть();
	
	КомандаЗапуска = ПриложениеYouTube.РабочийКаталог + "curl.exe -X PUT ";
	КомандаЗапуска = КомандаЗапуска+ " -o """ + ПриложениеYouTube.ФайлОтвета + """ ";
	КомандаЗапуска = КомандаЗапуска + " --write-out %{http_code} ";
	КомандаЗапуска = КомандаЗапуска + " -d ""@" + ПутьКФайлуJSON + """ ";	
	КомандаЗапуска = КомандаЗапуска + " --header ""Authorization:Bearer " + ПриложениеYouTube.ПараметрыCURL.ПерсональныеНастройкиYouTube.access_token + " "" ";	
	КомандаЗапуска = КомандаЗапуска + " --header ""Content-Type: application/json"" ";	
	КомандаЗапуска = КомандаЗапуска +  """https://www.googleapis.com/youtube/v3/videos?part=snippet%2Cstatus&key=" + НастройкиYouTube.api_key + """";	
	
	РезультатОбновления = РазработкаЭлектронныхКурсовСлужебныйКлиент.ЗапуститьПрограмму(КомандаЗапуска, ПараметрыЗапуска);		
	
	Если РезультатОбновления.КодВозврата <> 0
		ИЛИ РезультатОбновления.ПотокВывода <> "200" Тогда
		
		ПоказатьПредупреждение(, НСТр("ru = 'В процессе обновления свойств видео произошла ошибка. Подробности см. в журнале регистрации.'"));
		
		ДанныеОшибки = Строка(РезультатОбновления.КодВозврата);
		ДанныеОшибки = ДанныеОшибки + Строка(РезультатОбновления.ПотокВывода);
		
		ЧтениеТекста = Новый ЧтениеТекста(ПриложениеYouTube.ФайлОтвета, РазработкаЭлектронныхКурсовСлужебныйКлиентСервер.КодировкаСтандартныхПотоков());
		СтрокаОтветаСервера = ЧтениеТекста.Прочитать();
		
		ДанныеОшибки = ДанныеОшибки + СтрокаОтветаСервера;
		
		ЭлектронноеОбучениеСлужебныйКлиент.ЗаписатьОшибкуВЖурналРегистрации(НСтр("ru = 'Ошибка обновления свойств видео на YouTube'"), ДанныеОшибки);
		
	КонецЕсли;
	
	ЭлектронноеОбучениеСлужебныйКлиентСервер.УдалитьВременныйФайл(ПутьКФайлуJSON);	
	
	// Завершаем
	
	ПрогрессЗагрузки = 100;
	
	ПослеУспешнойЗагрузкиВидеоНаYouTube(ИдентификаторВидеоYouTube);
	
	#КонецЕсли	
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеУспешнойЗагрузкиВидеоНаYouTube(ИдентификаторВидеоYouTube)
	
	#Если НЕ ВебКлиент Тогда		
		
	СтрокаФайла = ФайлыДляЗагрузки.НайтиПоИдентификатору(ИндексЗагружаемогоФайла);
	СтрокаФайла.ИдентификаторВидео = ИдентификаторВидеоYouTube;
	
	ПроверитьЗагрузкуВсехФайлов();
	
	#КонецЕсли

КонецПроцедуры


#КонецОбласти

