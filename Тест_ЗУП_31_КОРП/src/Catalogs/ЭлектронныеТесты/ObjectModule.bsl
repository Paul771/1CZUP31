#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда	
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ЭтоКопирование") И ДополнительныеСвойства.ЭтоКопирование Тогда
		Возврат;
	КонецЕсли;	
		
	Если НЕ ЭтоГруппа Тогда
		
		ПроверитьНастройкиТеста(Отказ);
			
		УстановитьОбязательныеРеквизиты();
		
	КонецЕсли;
	
	РазработкаЭлектронныхКурсовСлужебный.ПроверитьВозможностьЗаписиЭлементаПередЗаписью(ЭтотОбъект, Отказ);
	РазработкаЭлектронныхКурсовСлужебный.УстановитьДатуИзмененияПередЗаписью(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)	
	
	Если ОбменДанными.Загрузка Тогда	
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ЭтоКопирование") И ДополнительныеСвойства.ЭтоКопирование Тогда
		Возврат;
	КонецЕсли;
	
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	ЭтотОбъект.ДополнительныеСвойства.Вставить("ОбъектКопирования", ОбъектКопирования);
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьНастройкиТеста(Отказ)
	
	Если НЕ ПоказыватьНавигацию И НЕ ЭтоНовый() Тогда // Делаем проверку, чтобы каждая группа элементов содержала хотя бы один вопрос.
		    		
		ЕстьГруппыБезВопросов = Ложь;
		    		
		ГруппыЭтойПодборки = Справочники.СтраницыЭлементовЭлектронныхКурсов.СтраницыВладельца(Ссылка);
		    		
		Для каждого ТекущаяГруппа Из ГруппыЭтойПодборки Цикл
		    		   
			Если ТекущаяГруппа.ЭтоГруппа Тогда
						
				ВсеЭлементыГруппы = Справочники.СтраницыЭлементовЭлектронныхКурсов.ВсеЭлементы(Ссылка, ТекущаяГруппа.Ссылка);
				ЕстьВГруппеВопрос = Ложь;
				Для каждого ЭлементГруппы Из ВсеЭлементыГруппы Цикл
					Если ТипЗнч(ЭлементГруппы.СсылкаНаЭлементКурса) = Тип("СправочникСсылка.ТестовыеВопросы") Тогда
						ЕстьВГруппеВопрос = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
							
				Если НЕ ЕстьВГруппеВопрос Тогда
					ЕстьГруппыБезВопросов = Истина;
				КонецЕсли;
							
			Иначе
				
				Если ТипЗнч(ТекущаяГруппа.СсылкаНаЭлементКурса) <> Тип("СправочникСсылка.ТестовыеВопросы") Тогда
					ЕстьГруппыБезВопросов = Истина;
					Прервать;
				КонецЕсли;
				
			КонецЕсли;
		    		
		КонецЦикла;
		
		Если ЕстьГруппыБезВопросов Тогда
			Текст = НСтр("ru = 'При отключенной опции ""Показывать навигацию"" каждая группа элементов должна содержать хотя бы один вопрос.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает обязательные реквизиты для заданных условий.
// Вызывается в обработчике события ПередЗаписью и в форме элемента справочника.
//
Процедура УстановитьОбязательныеРеквизиты()
	
	ПоказыватьСтартовуюСтраницу = Истина;	
	
	Если НЕ ПоказыватьНавигацию Тогда
		ПереходитьВпередАвтоматически = Истина;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(НазначениеТестирования) Тогда
		НазначениеТестирования = Перечисления.НазначенияЭлектронныхТестов.Аттестация;
	КонецЕсли;
	
	ТипНавигации = Перечисления.ТипыНавигацииСтраницЭлектронногоКурса.ПустаяСсылка();		
	
	Если ЗначениеЗаполнено(ВремяТеста) Тогда
		ПоказыватьТаймер = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#КонецЕсли



