#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ЭтоГруппа И ДатаРождения > ТекущаяДатаСеанса() Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Дата рождения не может быть больше текущей'"),
			,
			"ФизическоеЛицо.ДатаРождения",
			,
			Отказ);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаРождения) Тогда
		ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, ДатаРождения, "ФизическоеЛицо.ДатаРождения", Отказ, НСтр("ru='Дата рождения'"));
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		
		НаименованиеСлужебное = ФизическиеЛицаЗарплатаКадры.НаименованиеСлужебное(Наименование);
		
		Если Не ДополнительныеСвойства.Свойство("ФИОУстановлены") Тогда
			
			ДанныеСреза = РегистрыСведений.ФИОФизическихЛиц.СрезПоследних(, Новый Структура("ФизическоеЛицо", Ссылка));
			Если ДанныеСреза.Количество() > 0
				И Не ПустаяСтрока(ДанныеСреза[0].Фамилия) Тогда
				
				ЧастиИмени = ДанныеСреза[0];
				
			Иначе
				ЧастиИмени = ФизическиеЛицаКлиентСервер.ЧастиИмени(ФИО);
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЧастиИмени, "Фамилия,Имя,Отчество");
			
		КонецЕсли;
		
		Если Не ПустаяСтрока(Имя) Тогда
			ИнициалыИмени = ФизическиеЛицаЗарплатаКадрыКлиентСервер.ИнициалыИмени(Имя);
		Иначе
			ИнициалыИмени = "";
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	Возврат ОбменДаннымиЗарплатаКадры.ОграниченияРегистрацииПоФизическомуЛицу(ЭтотОбъект, Ссылка, ТекущаяДатаСеанса());
КонецФункции

#КонецОбласти 

#КонецЕсли

