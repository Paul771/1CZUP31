#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПервоначальногоЗаполненияИОбновленияИБ

Процедура НачальноеЗаполнение() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	МероприятияАдаптацииУвольнения.Ссылка
	|ИЗ
	|	Справочник.МероприятияАдаптацииУвольнения КАК МероприятияАдаптацииУвольнения";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ТекстовыйДокумент = Справочники.МероприятияАдаптацииУвольнения.ПолучитьМакет("МероприятияАдаптацииУвольнения");
	ТаблицаМероприятий = ОбщегоНазначения.ПрочитатьXMLВТаблицу(ТекстовыйДокумент.ПолучитьТекст()).Данные;
	
	Для Каждого СтрокаМероприятия Из ТаблицаМероприятий Цикл
		
		Наименование = СтрокаМероприятия.Name;
		ИмяВидаМероприятия = СтрокаМероприятия.TypeOfActivity;
		ВидыСобытийСтрокой = СтрокаМероприятия.EventTypes;
		ИменаВидовСобытий = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ВидыСобытийСтрокой, ",");
		
		СправочникОбъект = Справочники.МероприятияАдаптацииУвольнения.СоздатьЭлемент();
		СправочникОбъект.Наименование = Наименование;
		СправочникОбъект.ИсполняетсяСотрудником = ЗначениеЗаполнено(СтрокаМероприятия.PerformedPersonally);
		
		Если Не ПустаяСтрока(ИмяВидаМероприятия) Тогда
			СправочникОбъект.ВидМероприятия = Перечисления.ВидыМероприятийАдаптацииУвольнения[ИмяВидаМероприятия];
		КонецЕсли;
		
		Для Каждого ИмяВидаСобытия Из ИменаВидовСобытий Цикл
			
			Если Не ПустаяСтрока(ИмяВидаСобытия) Тогда
				НоваяСтрока = СправочникОбъект.События.Добавить();
				НоваяСтрока.ВидСобытия = Перечисления.ВидыСобытийАдаптацииУвольнения[ИмяВидаСобытия];
			КонецЕсли;
			
		КонецЦикла;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(СправочникОбъект);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли