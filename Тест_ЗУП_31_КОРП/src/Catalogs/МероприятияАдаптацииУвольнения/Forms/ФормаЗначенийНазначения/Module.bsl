
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Организация") Тогда
		Организация = Параметры.Организация;
	КонецЕсли;

	Заголовок = Параметры.Заголовок;
	ИмяТаблицы = Параметры.ИмяТаблицы;
	ЗначенияНазначения = Параметры.ТекущиеЗначения;
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Для Каждого Значение Из ВыбранноеЗначение Цикл
		Если ЗначенияНазначения.НайтиПоЗначению(Значение) = Неопределено  Тогда
			ЗначенияНазначения.Добавить(Значение);
		КонецЕсли;
	КонецЦикла;
	
	ЗначенияНазначения.СортироватьПоПредставлению();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подбор(Команда)
	
	ТипСправочника = ЗначенияНазначения.ТипЗначения;
	
	ПараметрыОткрытия = Новый Структура();
	ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
	ПараметрыОткрытия.Вставить("МножественныйВыбор", Истина);
	ПараметрыОткрытия.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыОткрытия.Вставить("АдресСпискаПодобранных", АдресСпискаПодобранных());
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		Отбор = Новый Структура;
		Отбор.Вставить("Владелец", Организация);
		
		ПараметрыОткрытия.Вставить("Владелец", Организация);
		ПараметрыОткрытия.Вставить("Отбор", Отбор);
		
	КонецЕсли;
	
	ОткрытьФорму(ИмяТаблицы + ".ФормаВыбора", ПараметрыОткрытия, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	Закрыть(ОтфильтроватьЗначенияНазначения());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция АдресСпискаПодобранных()
	
	Возврат ПоместитьВоВременноеХранилище(ЗначенияНазначения.ВыгрузитьЗначения(), УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Функция ОтфильтроватьЗначенияНазначения()
	
	ИтоговыйСписок = ЗначенияНазначения.Скопировать();
	ИтоговыйСписок.Очистить();
	
	Для каждого Элемент Из ЗначенияНазначения Цикл
		
		Если ЗначениеЗаполнено(Элемент.Значение)
			И ИтоговыйСписок.НайтиПоЗначению(Элемент.Значение) = Неопределено Тогда
			ИтоговыйСписок.Добавить(Элемент.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	ИтоговыйСписок.СортироватьПоПредставлению();
	
	Возврат ИтоговыйСписок;
	
КонецФункции

&НаСервере
Процедура УстановитьВидимость()
	
	ТолькоПросмотр = Параметры.ТолькоПросмотр;
	
	Если ТолькоПросмотр Тогда
		
		Элементы.Подбор.Видимость = Ложь;
		Элементы.Удалить.Видимость = Ложь;
		Элементы.ФормаВыбрать.Видимость = Ложь;
		Элементы.ЗначенияНазначения.ТолькоПросмотр = Истина;
		
		Элементы.ФормаОтмена.Заголовок = НСтр("ru = 'Закрыть'");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти