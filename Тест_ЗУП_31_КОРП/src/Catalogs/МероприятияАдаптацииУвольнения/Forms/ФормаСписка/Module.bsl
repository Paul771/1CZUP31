
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.РежимВыбора Тогда
		Элементы.Список.РежимВыбора = Истина;
	КонецЕсли;
	
	УстановитьОтборы(Параметры.Отбор);
	
	ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "Список");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, Настройки);
КонецПроцедуры

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, , СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборы(Отборы)
	
	Если Отборы.Свойство("ВАрхиве") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "ВАрхиве", Отборы.ВАрхиве);
			
	КонецЕсли;
	
	Если Отборы.Свойство("ДляАдаптации") И ЗначениеЗаполнено(Отборы.ДляАдаптации) Тогда
		
		ВидыМероприятий = Новый Массив();
		ВидыМероприятий.Добавить(Перечисления.ВидыМероприятийАдаптацииУвольнения.ПустаяСсылка());
		ВидыМероприятий.Добавить(?(Отборы.ДляАдаптации, Перечисления.ВидыМероприятийАдаптацииУвольнения.Адаптация, 
			Перечисления.ВидыМероприятийАдаптацииУвольнения.Увольнение));
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "ВидМероприятия", ВидыМероприятий, ВидСравненияКомпоновкиДанных.ВСписке);
		
	КонецЕсли;
	
	Если Отборы.Свойство("ПометкаУдаления") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "ПометкаУдаления", Отборы.ПометкаУдаления);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
