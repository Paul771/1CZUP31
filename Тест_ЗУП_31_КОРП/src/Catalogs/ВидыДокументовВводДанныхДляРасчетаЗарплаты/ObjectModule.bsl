#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если ЭтоГруппа Тогда
		Возврат; // Для группы только стандартные проверки.
	КонецЕсли;
	
	Если Показатели.Количество() = 0
		И ВидыВремени.Количество() = 0
		И ВидыРабот.Количество() = 0
		И Не ВидыРаботЗаполняютсяВДокументе Тогда
		Текст = НСтр("ru = 'Не выбраны ни показатели расчета зарплаты, ни виды времени, ни виды работ'");
		СообщитьОбОшибке(Отказ, Текст);
	КонецЕсли;
	
	ПроверитьЗначенияИспользуемыеВДокументах(Отказ);
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если РегистрироватьСведенияОБухучете Тогда
		Если Не ЗначениеЗаполнено(РежимВводаСтатьиФинансирования) Тогда
			РежимВводаСтатьиФинансирования = Перечисления.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВШапке;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(РежимВводаСпособаОтраженияЗарплатыВБухучете) Тогда
			РежимВводаСпособаОтраженияЗарплатыВБухучете = Перечисления.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВШапке;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(РежимВводаОтношенияКЕНВД) Тогда
			РежимВводаОтношенияКЕНВД = Перечисления.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВШапке;
		КонецЕсли;
	Иначе
		РежимВводаСтатьиФинансирования              = Перечисления.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ПустаяСсылка();
		РежимВводаСпособаОтраженияЗарплатыВБухучете = Перечисления.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ПустаяСсылка();
		РежимВводаОтношенияКЕНВД                    = Перечисления.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ПустаяСсылка();
		РежимВводаПодразделенияУчетаЗатрат             = Перечисления.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ПустаяСсылка();
	КонецЕсли;
	
	// Заполнение краткого состава документа.
	МаксимальнаяДлинаСтроки = 50;
	
	КраткийСоставСтрока = "";
	
	МассивПоказателей = ОбщегоНазначения.ВыгрузитьКолонку(Показатели, "Показатель", Истина);
	ИменаПоказателей = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивПоказателей, "Наименование, КраткоеНаименование");
	Для Каждого Показатель Из МассивПоказателей Цикл
		Если Не ПустаяСтрока(КраткийСоставСтрока) Тогда
			КраткийСоставСтрока = КраткийСоставСтрока + ", ";
		КонецЕсли;
		ИмяПоказателя = ИменаПоказателей[Показатель];
		Если ИмяПоказателя <> Неопределено Тогда 
			КраткийСоставСтрока = КраткийСоставСтрока + ?(ЗначениеЗаполнено(ИмяПоказателя.КраткоеНаименование), ИмяПоказателя.КраткоеНаименование, ИмяПоказателя.Наименование);
		КонецЕсли;
	КонецЦикла;
	
	МассивВидовВремени = ОбщегоНазначения.ВыгрузитьКолонку(ВидыВремени, "ВидВремени", Истина);
	ИменаВидовВремени = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивВидовВремени, "Наименование");
	Для Каждого ВидВремени Из МассивВидовВремени Цикл
		Если Не ПустаяСтрока(КраткийСоставСтрока) Тогда
			КраткийСоставСтрока = КраткийСоставСтрока + ", ";
		КонецЕсли;
		КраткийСоставСтрока = КраткийСоставСтрока + ИменаВидовВремени[ВидВремени];
	КонецЦикла;
	
	МассивВидовРабот = ОбщегоНазначения.ВыгрузитьКолонку(ВидыРабот, "ВидРабот", Истина);
	ИменаВидовРабот = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивВидовРабот, "Наименование");
	Для Каждого ВидРабот Из МассивВидовРабот Цикл
		Если Не ПустаяСтрока(КраткийСоставСтрока) Тогда
			КраткийСоставСтрока = КраткийСоставСтрока + ", ";
		КонецЕсли;
		КраткийСоставСтрока = КраткийСоставСтрока + ИменаВидовРабот[ВидРабот];
	КонецЦикла;
	
	Если СтрДлина(КраткийСоставСтрока) > МаксимальнаяДлинаСтроки Тогда
		КраткийСоставСтрока = Лев(КраткийСоставСтрока, МаксимальнаяДлинаСтроки - 3) + "...";
	КонецЕсли;
	
	КраткийСостав = КраткийСоставСтрока;
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	РасчетЗарплатыРасширенный.УстановитьИспользованиеВводаДанныхДляРасчетаЗарплаты();
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//   Структура - См. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	Если ДополнительныеСвойства.Свойство("ОграниченияРегистрации") Тогда
		Возврат ДополнительныеСвойства.ОграниченияРегистрации; // Ограничения регистрации уже были получены.
	КонецЕсли;
	
	МассивОрганизаций = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
	МассивОрганизаций.Добавить(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Подразделение, "Владелец"));
	
	ОграниченияРегистрации = ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации();
	ОграниченияРегистрации.Организации = МассивОрганизаций;
	
	Возврат ОграниченияРегистрации;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьЗначенияИспользуемыеВДокументах(Отказ)
	
	// Запрещено:
	// 1. Удалять элементы таблиц Показатели, ВидыВремени, ВидыРабот если они используются в документах.
	// 2. Отключать флажок НесколькоСотрудников   если есть документы с несколькими сотрудниками.
	//    Отключать флажок НесколькоПодразделений если есть документы с несколькими подразделениями.
	//    Отключать флажок НесколькоОрганизаций   если есть документы с несколькими организациями.
	// 3. Заполнять поле Организация   если есть документ с различными организациями.
	//    Заполнять поле Подразделение если есть документ с различными подразделениями.
	//    См. флажки ОрганизацияЗаполняется и ПодразделениеЗаполняется в форме элемента.
	// 4. Изменять поле Организация   если есть документ с другой организацией.
	//    Изменять поле Подразделение если есть документ с другим подразделением.
	// 5. Включать флажок ПроверятьЗаполнениеПодразделения если есть документы с пустым подразделением.
	// 6. Отключать флажок ПоказыватьПодразделение если есть документы с заполненным подразделением.
	// 7. Отключать флажок ЗначенияПоказателейВводятсяНаРазныеДаты
	//    если есть документы с заполненными показателями на разные даты.
	// 8. Отключать флажок ИспользоватьПериодОкончания если есть документы с заполненной датой окончания у показателя.
	// 9. Включать  флажок ВремяВводитсяСводно если есть документы с детальным заполнением времени.
	//    Отключать флажок ВремяВводитсяСводно если есть документы со сводным  заполнением времени.
	// 10. Отключать флажок ВремяВводитсяЗаМесяц если есть документы с заполненным временем на разные дни.
	// 11. Отключать флажок РегистрироватьСведенияОБухучете если есть документы с заполненными сведениями о бухучете.
	//     Очищать РежимВводаПодразделенияУчетаЗатрат при включенном флажке РегистрироватьСведенияОБухучете
	//     если есть документы с заполненным подразделением бухучета.
	//     См. флажок ИспользоватьПодразделениеУчетаЗатрат в форме элемента.
	// 12. Изменять поле РежимВводаСтатьиФинансирования              если есть документы с заполненной статьей финансирования.
	//     Изменять поле РежимВводаСпособаОтраженияЗарплатыВБухучете если есть документы с заполненным способом отражения.
	//     Изменять поле РежимВводаОтношенияКЕНВД                    если есть документы с заполненным ЕНВД.
	//     Изменять поле РежимВводаПодразделенияУчетаЗатрат             если есть документы с заполненным подразделением.
	// 13. Включать  флажок ВыполненныеРаботыРаспределяютсяПоСотрудникам если есть документы с выполненными работами.
	//     (в списке выполненных работ выбраны сотрудники).
	//     Отключать флажок ВыполненныеРаботыРаспределяютсяПоСотрудникам если есть документы с значениями показателя
	//     (заработок сотрудников введен отдельно от выполненных работ).
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Показатели",  Показатели.Выгрузить());
	Запрос.УстановитьПараметр("ВидыВремени", ВидыВремени.Выгрузить());
	Запрос.УстановитьПараметр("ВидыРабот",   ВидыРабот.Выгрузить());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("НесколькоСотрудников",   НесколькоСотрудников);
	Запрос.УстановитьПараметр("НесколькоПодразделений", НесколькоПодразделений);
	Запрос.УстановитьПараметр("НесколькоОрганизаций",   НесколькоОрганизаций);
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("ПроверятьЗаполнениеПодразделения", ПроверятьЗаполнениеПодразделения);
	Запрос.УстановитьПараметр("ПоказыватьПодразделение",          ПоказыватьПодразделение);
	Запрос.УстановитьПараметр("ЗначенияПоказателейВводятсяНаРазныеДаты", ЗначенияПоказателейВводятсяНаРазныеДаты);
	Запрос.УстановитьПараметр("ИспользоватьПериодОкончания",             ИспользоватьПериодОкончания);
	Запрос.УстановитьПараметр("ВремяВводитсяСводно",  ВремяВводитсяСводно);
	Запрос.УстановитьПараметр("ВремяВводитсяЗаМесяц", ВремяВводитсяЗаМесяц);
	Запрос.УстановитьПараметр("ВыполненныеРаботыРаспределяютсяПоСотрудникам", ВыполненныеРаботыРаспределяютсяПоСотрудникам);
	
	Запрос.УстановитьПараметр("РегистрироватьСведенияОБухучете",             РегистрироватьСведенияОБухучете);
	Запрос.УстановитьПараметр("РежимВводаСтатьиФинансирования",              РежимВводаСтатьиФинансирования);
	Запрос.УстановитьПараметр("РежимВводаСпособаОтраженияЗарплатыВБухучете", РежимВводаСпособаОтраженияЗарплатыВБухучете);
	Запрос.УстановитьПараметр("РежимВводаОтношенияКЕНВД",                    РежимВводаОтношенияКЕНВД);
	Запрос.УстановитьПараметр("РежимВводаПодразделенияУчетаЗатрат",          РежимВводаПодразделенияУчетаЗатрат);
	
	Запрос.УстановитьПараметр("ПоказательСдельныйЗаработок", ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.СдельныйЗаработок"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Показатели.Показатель КАК Показатель
	|ПОМЕСТИТЬ ВТПоказателиШаблона
	|ИЗ
	|	&Показатели КАК Показатели
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыВремени.ВидВремени КАК ВидВремени
	|ПОМЕСТИТЬ ВТВидыВремениШаблона
	|ИЗ
	|	&ВидыВремени КАК ВидыВремени
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыРабот.ВидРабот КАК ВидРабот
	|ПОМЕСТИТЬ ВТВидыРаботШаблона
	|ИЗ
	|	&ВидыРабот КАК ВидыРабот
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрежниеНастройкиВида.Ссылка КАК Шаблон,
	|	ПрежниеНастройкиВида.НесколькоСотрудников КАК НесколькоСотрудников,
	|	ПрежниеНастройкиВида.НесколькоПодразделений КАК НесколькоПодразделений,
	|	ПрежниеНастройкиВида.НесколькоОрганизаций КАК НесколькоОрганизаций,
	|	ПрежниеНастройкиВида.Организация КАК Организация,
	|	ПрежниеНастройкиВида.Подразделение КАК Подразделение,
	|	ПрежниеНастройкиВида.ПроверятьЗаполнениеПодразделения КАК ПроверятьЗаполнениеПодразделения,
	|	ПрежниеНастройкиВида.ПоказыватьПодразделение КАК ПоказыватьПодразделение,
	|	ПрежниеНастройкиВида.ЗначенияПоказателейВводятсяНаРазныеДаты КАК ЗначенияПоказателейВводятсяНаРазныеДаты,
	|	ПрежниеНастройкиВида.ИспользоватьПериодОкончания КАК ИспользоватьПериодОкончания,
	|	ПрежниеНастройкиВида.ВремяВводитсяСводно КАК ВремяВводитсяСводно,
	|	ПрежниеНастройкиВида.ВремяВводитсяЗаМесяц КАК ВремяВводитсяЗаМесяц,
	|	ПрежниеНастройкиВида.ВыполненныеРаботыРаспределяютсяПоСотрудникам КАК ВыполненныеРаботыРаспределяютсяПоСотрудникам,
	|	ПрежниеНастройкиВида.РегистрироватьСведенияОБухучете КАК РегистрироватьСведенияОБухучете,
	|	ПрежниеНастройкиВида.РежимВводаСтатьиФинансирования КАК РежимВводаСтатьиФинансирования,
	|	ПрежниеНастройкиВида.РежимВводаСпособаОтраженияЗарплатыВБухучете КАК РежимВводаСпособаОтраженияЗарплатыВБухучете,
	|	ПрежниеНастройкиВида.РежимВводаОтношенияКЕНВД КАК РежимВводаОтношенияКЕНВД,
	|	ПрежниеНастройкиВида.РежимВводаПодразделенияУчетаЗатрат КАК РежимВводаПодразделенияУчетаЗатрат,
	|	ДокументыВида.Ссылка КАК Документ
	|ПОМЕСТИТЬ ВТДокументыСохраненногоВида
	|ИЗ
	|	Справочник.ВидыДокументовВводДанныхДляРасчетаЗарплаты КАК ПрежниеНастройкиВида
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДокументыВида
	|		ПО ПрежниеНастройкиВида.Ссылка = ДокументыВида.ВидДокумента
	|ГДЕ
	|	ПрежниеНастройкиВида.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыСохраненногоВида.Документ КАК Документ,
	|	ПоказателиДокумента.Показатель КАК ЗначениеОшибки,
	|	""1_Показатели"" КАК ВидОшибки
	|ПОМЕСТИТЬ ВТОшибки
	|ИЗ
	|	ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыДокументовВводДанныхДляРасчетаЗарплаты.Показатели КАК ПоказателиДокумента
	|		ПО ДокументыСохраненногоВида.Шаблон = ПоказателиДокумента.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ЗначенияПоказателей КАК ЗначенияПоказателей
	|		ПО ДокументыСохраненногоВида.Документ = ЗначенияПоказателей.Ссылка
	|			И (ПоказателиДокумента.Показатель = ЗначенияПоказателей.Показатель)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоказателиШаблона КАК ПоказателиШаблона
	|		ПО (ПоказателиДокумента.Показатель = ПоказателиШаблона.Показатель)
	|ГДЕ
	|	ПоказателиШаблона.Показатель ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВидыВремени.Документ,
	|	ВидыВремени.ВидВремени,
	|	""1_ВидыВремени""
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДокументыСохраненногоВида.Документ КАК Документ,
	|		ВидыВремени.ВидВремени КАК ВидВремени
	|	ИЗ
	|		ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыДокументовВводДанныхДляРасчетаЗарплаты.ВидыВремени КАК ВидыВремени
	|			ПО ДокументыСохраненногоВида.Шаблон = ВидыВремени.Ссылка
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ДанныеОВремениСводно КАК ДанныеОВремениСводно
	|			ПО ДокументыСохраненногоВида.Документ = ДанныеОВремениСводно.Ссылка
	|				И (ВидыВремени.ВидВремени = ДанныеОВремениСводно.ВидВремени)
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыВремениШаблона КАК ВидыВремениШаблона
	|			ПО (ВидыВремени.ВидВремени = ВидыВремениШаблона.ВидВремени)
	|	ГДЕ
	|		ВидыВремениШаблона.ВидВремени ЕСТЬ NULL
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДокументыСохраненногоВида.Документ,
	|		ВидыВремени.ВидВремени
	|	ИЗ
	|		ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыДокументовВводДанныхДляРасчетаЗарплаты.ВидыВремени КАК ВидыВремени
	|			ПО ДокументыСохраненногоВида.Шаблон = ВидыВремени.Ссылка
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ДанныеОВремениДетально КАК ДанныеОВремениДетально
	|			ПО ДокументыСохраненногоВида.Документ = ДанныеОВремениДетально.Ссылка
	|				И (ВидыВремени.ВидВремени = ДанныеОВремениДетально.ВидВремени)
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыВремениШаблона КАК ВидыВремениШаблона
	|			ПО (ВидыВремени.ВидВремени = ВидыВремениШаблона.ВидВремени)
	|	ГДЕ
	|		ВидыВремениШаблона.ВидВремени ЕСТЬ NULL) КАК ВидыВремени
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыСохраненногоВида.Документ,
	|	ВидыРабот.ВидРабот,
	|	""1_ВидыРабот""
	|ИЗ
	|	ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыДокументовВводДанныхДляРасчетаЗарплаты.ВидыРабот КАК ВидыРабот
	|		ПО ДокументыСохраненногоВида.Шаблон = ВидыРабот.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ВыполненныеРаботы КАК ВыполненныеРаботы
	|		ПО ДокументыСохраненногоВида.Документ = ВыполненныеРаботы.Ссылка
	|			И (ВидыРабот.ВидРабот = ВыполненныеРаботы.ВидРабот)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыРаботШаблона КАК ВидыРаботШаблона
	|		ПО (ВидыРабот.ВидРабот = ВидыРаботШаблона.ВидРабот)
	|ГДЕ
	|	ВидыРаботШаблона.ВидРабот ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыСохраненногоВида.Документ,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Сотрудники.Сотрудник),
	|	""2_НесколькоСотрудников""
	|ИЗ
	|	ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ДокументыСохраненногоВида.Документ КАК Документ,
	|			ВыполненныеРаботы.Сотрудник КАК Сотрудник
	|		ИЗ
	|			ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ВыполненныеРаботы КАК ВыполненныеРаботы
	|				ПО ДокументыСохраненногоВида.Документ = ВыполненныеРаботы.Ссылка
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ДокументыСохраненногоВида.Документ,
	|			ДанныеОВремениДетально.Сотрудник
	|		ИЗ
	|			ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ДанныеОВремениДетально КАК ДанныеОВремениДетально
	|				ПО ДокументыСохраненногоВида.Документ = ДанныеОВремениДетально.Ссылка
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ДокументыСохраненногоВида.Документ,
	|			ДанныеОВремениСводно.Сотрудник
	|		ИЗ
	|			ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ДанныеОВремениСводно КАК ДанныеОВремениСводно
	|				ПО ДокументыСохраненногоВида.Документ = ДанныеОВремениСводно.Ссылка
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ДокументыСохраненногоВида.Документ,
	|			ЗначенияПоказателей.Объект
	|		ИЗ
	|			ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ЗначенияПоказателей КАК ЗначенияПоказателей
	|				ПО ДокументыСохраненногоВида.Документ = ЗначенияПоказателей.Ссылка
	|					И (ЗначенияПоказателей.Объект ССЫЛКА Справочник.Сотрудники)) КАК Сотрудники
	|		ПО ДокументыСохраненногоВида.Документ = Сотрудники.Документ
	|			И (ДокументыСохраненногоВида.НесколькоСотрудников)
	|			И (НЕ &НесколькоСотрудников)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыСохраненногоВида.Документ
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Сотрудники.Сотрудник) > 1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыСохраненногоВида.Документ,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗначенияПоказателей.Объект),
	|	""2_НесколькоПодразделений""
	|ИЗ
	|	ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ЗначенияПоказателей КАК ЗначенияПоказателей
	|		ПО ДокументыСохраненногоВида.Документ = ЗначенияПоказателей.Ссылка
	|			И (ЗначенияПоказателей.Объект ССЫЛКА Справочник.ПодразделенияОрганизаций)
	|			И (ДокументыСохраненногоВида.НесколькоПодразделений)
	|			И (НЕ &НесколькоПодразделений)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыСохраненногоВида.Документ
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗначенияПоказателей.Объект) > 1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыСохраненногоВида.Документ,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗначенияПоказателей.Объект),
	|	""2_НесколькоОрганизаций""
	|ИЗ
	|	ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ЗначенияПоказателей КАК ЗначенияПоказателей
	|		ПО ДокументыСохраненногоВида.Документ = ЗначенияПоказателей.Ссылка
	|			И (ЗначенияПоказателей.Объект ССЫЛКА Справочник.Организации)
	|			И (ДокументыСохраненногоВида.НесколькоОрганизаций)
	|			И (НЕ &НесколькоОрганизаций)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыСохраненногоВида.Документ
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗначенияПоказателей.Объект) > 1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыСохраненногоВида.Документ,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеДокумента.Организация),
	|	""3_ОрганизацияЗаполняется""
	|ИЗ
	|	ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДокумента
	|		ПО ДокументыСохраненногоВида.Документ = ДанныеДокумента.Ссылка
	|			И (ДокументыСохраненногоВида.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|			И (&Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыСохраненногоВида.Документ
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеДокумента.Организация) > 1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Организации.Документ,
	|	Организации.Организация,
	|	""4_ОрганизацияИспользуется""
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДанныеДокумента.Ссылка КАК Документ,
	|		ДанныеДокумента.Организация КАК Организация
	|	ИЗ
	|		ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДокумента
	|			ПО ДокументыСохраненногоВида.Документ = ДанныеДокумента.Ссылка
	|				И (ДокументыСохраненногоВида.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|				И (&Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|				И (ДокументыСохраненногоВида.Организация <> &Организация)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДанныеДокумента.Ссылка,
	|		ДанныеДокумента.Организация
	|	
	|	ИМЕЮЩИЕ
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеДокумента.Организация) = 1) КАК Организации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыСохраненногоВида.Документ,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеДокумента.Подразделение),
	|	""3_ПодразделениеЗаполняется""
	|ИЗ
	|	ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДокумента
	|		ПО ДокументыСохраненногоВида.Документ = ДанныеДокумента.Ссылка
	|			И (ДокументыСохраненногоВида.Подразделение = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
	|			И (&Подразделение <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыСохраненногоВида.Документ
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеДокумента.Подразделение) > 1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Подразделения.Документ,
	|	Подразделения.Подразделение,
	|	""4_ПодразделениеИспользуется""
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДанныеДокумента.Ссылка КАК Документ,
	|		ДанныеДокумента.Подразделение КАК Подразделение
	|	ИЗ
	|		ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДокумента
	|			ПО ДокументыСохраненногоВида.Документ = ДанныеДокумента.Ссылка
	|				И (ДокументыСохраненногоВида.Подразделение <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
	|				И (&Подразделение <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
	|				И (ДокументыСохраненногоВида.Подразделение <> &Подразделение)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДанныеДокумента.Ссылка,
	|		ДанныеДокумента.Подразделение
	|	
	|	ИМЕЮЩИЕ
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеДокумента.Подразделение) = 1) КАК Подразделения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыСохраненногоВида.Документ,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка),
	|	""5_ПроверятьЗаполнениеПодразделения""
	|ИЗ
	|	ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДокумента
	|		ПО ДокументыСохраненногоВида.Документ = ДанныеДокумента.Ссылка
	|			И (ДанныеДокумента.Подразделение = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
	|			И (НЕ ДокументыСохраненногоВида.ПроверятьЗаполнениеПодразделения)
	|			И (&ПроверятьЗаполнениеПодразделения)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыСохраненногоВида.Документ,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка),
	|	""6_ПоказыватьПодразделение""
	|ИЗ
	|	ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДокумента
	|		ПО ДокументыСохраненногоВида.Документ = ДанныеДокумента.Ссылка
	|			И (ДанныеДокумента.Подразделение <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
	|			И (ДокументыСохраненногоВида.ПоказыватьПодразделение)
	|			И (НЕ &ПоказыватьПодразделение)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыСохраненногоВида.Документ,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗначенияПоказателей.Период),
	|	""7_ЗначенияПоказателейВводятсяНаРазныеДаты""
	|ИЗ
	|	ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ЗначенияПоказателей КАК ЗначенияПоказателей
	|		ПО ДокументыСохраненногоВида.Документ = ЗначенияПоказателей.Ссылка
	|			И (ДокументыСохраненногоВида.ЗначенияПоказателейВводятсяНаРазныеДаты)
	|			И (НЕ &ЗначенияПоказателейВводятсяНаРазныеДаты)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыСохраненногоВида.Документ
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗначенияПоказателей.Период) > 1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыСохраненногоВида.Документ,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗначенияПоказателей.ПериодОкончания),
	|	""8_ИспользоватьПериодОкончания""
	|ИЗ
	|	ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ЗначенияПоказателей КАК ЗначенияПоказателей
	|		ПО ДокументыСохраненногоВида.Документ = ЗначенияПоказателей.Ссылка
	|			И (ЗначенияПоказателей.ПериодОкончания <> ДАТАВРЕМЯ(1, 1, 1))
	|			И (ДокументыСохраненногоВида.ИспользоватьПериодОкончания)
	|			И (НЕ &ИспользоватьПериодОкончания)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыСохраненногоВида.Документ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыСохраненногоВида.Документ,
	|	ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.ПустаяСсылка),
	|	""9_ВремяВводитсяСводноИстина""
	|ИЗ
	|	ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ДанныеОВремениДетально КАК ДанныеОВремениДетально
	|		ПО ДокументыСохраненногоВида.Документ = ДанныеОВремениДетально.Ссылка
	|			И (НЕ ДокументыСохраненногоВида.ВремяВводитсяСводно)
	|			И (&ВремяВводитсяСводно)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыСохраненногоВида.Документ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыСохраненногоВида.Документ,
	|	ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.ПустаяСсылка),
	|	""9_ВремяВводитсяСводноЛожь""
	|ИЗ
	|	ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ДанныеОВремениСводно КАК ДанныеОВремениСводно
	|		ПО ДокументыСохраненногоВида.Документ = ДанныеОВремениСводно.Ссылка
	|			И (ДокументыСохраненногоВида.ВремяВводитсяСводно)
	|			И (НЕ &ВремяВводитсяСводно)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыСохраненногоВида.Документ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыСохраненногоВида.Документ,
	|	ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.ПустаяСсылка),
	|	""10_ВремяВводитсяЗаМесяц""
	|ИЗ
	|	ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ДанныеОВремениДетально КАК ДанныеОВремениДетально
	|		ПО ДокументыСохраненногоВида.Документ = ДанныеОВремениДетально.Ссылка
	|			И (ДокументыСохраненногоВида.ВремяВводитсяЗаМесяц)
	|			И (НЕ &ВремяВводитсяЗаМесяц)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыСохраненногоВида.Документ,
	|	ДанныеОВремениДетально.Часов1,
	|	ДанныеОВремениДетально.Часов2,
	|	ДанныеОВремениДетально.Часов3,
	|	ДанныеОВремениДетально.Часов4,
	|	ДанныеОВремениДетально.Часов5,
	|	ДанныеОВремениДетально.Часов6,
	|	ДанныеОВремениДетально.Часов7,
	|	ДанныеОВремениДетально.Часов8,
	|	ДанныеОВремениДетально.Часов9,
	|	ДанныеОВремениДетально.Часов10,
	|	ДанныеОВремениДетально.Часов11,
	|	ДанныеОВремениДетально.Часов12,
	|	ДанныеОВремениДетально.Часов13,
	|	ДанныеОВремениДетально.Часов14,
	|	ДанныеОВремениДетально.Часов15,
	|	ДанныеОВремениДетально.Часов16,
	|	ДанныеОВремениДетально.Часов17,
	|	ДанныеОВремениДетально.Часов18,
	|	ДанныеОВремениДетально.Часов19,
	|	ДанныеОВремениДетально.Часов20,
	|	ДанныеОВремениДетально.Часов21,
	|	ДанныеОВремениДетально.Часов22,
	|	ДанныеОВремениДетально.Часов23,
	|	ДанныеОВремениДетально.Часов24,
	|	ДанныеОВремениДетально.Часов25,
	|	ДанныеОВремениДетально.Часов26,
	|	ДанныеОВремениДетально.Часов27,
	|	ДанныеОВремениДетально.Часов28,
	|	ДанныеОВремениДетально.Часов29,
	|	ДанныеОВремениДетально.Часов30,
	|	ДанныеОВремениДетально.Часов31
	|
	|ИМЕЮЩИЕ
	|	ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов1 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов2 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов3 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов4 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов5 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов6 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов7 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов8 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов9 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов10 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов11 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов12 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов13 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов14 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов15 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов16 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов17 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов18 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов19 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов20 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов21 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов22 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов23 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов24 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов25 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов26 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов27 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов28 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов29 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов30 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеОВремениДетально.Часов31 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ > 1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Данные.Документ,
	|	ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.ПустаяСсылка),
	|	""11_РегистрироватьСведенияОБухучетеЛожь""
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДокументыСохраненногоВида.Документ КАК Документ
	|	ИЗ
	|		ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДокумента
	|			ПО (&РежимВводаСтатьиФинансирования = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ПустаяСсылка))
	|				И (ДокументыСохраненногоВида.РежимВводаСтатьиФинансирования = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВШапке))
	|				И ДокументыСохраненногоВида.Документ = ДанныеДокумента.Ссылка
	|				И (ДанныеДокумента.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДокументыСохраненногоВида.Документ
	|	ИЗ
	|		ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ВыполненныеРаботы КАК ВыполненныеРаботы
	|			ПО (&РежимВводаСтатьиФинансирования = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ПустаяСсылка))
	|				И (ДокументыСохраненногоВида.РежимВводаСтатьиФинансирования = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВТабличнойЧасти))
	|				И ДокументыСохраненногоВида.Документ = ВыполненныеРаботы.Ссылка
	|				И (ВыполненныеРаботы.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДокументыСохраненногоВида.Документ
	|	ИЗ
	|		ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДокумента
	|			ПО (&РежимВводаСпособаОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ПустаяСсылка))
	|				И (ДокументыСохраненногоВида.РежимВводаСпособаОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВШапке))
	|				И ДокументыСохраненногоВида.Документ = ДанныеДокумента.Ссылка
	|				И (ДанныеДокумента.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДокументыСохраненногоВида.Документ
	|	ИЗ
	|		ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ВыполненныеРаботы КАК ВыполненныеРаботы
	|			ПО (&РежимВводаСпособаОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ПустаяСсылка))
	|				И (ДокументыСохраненногоВида.РежимВводаСпособаОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВТабличнойЧасти))
	|				И ДокументыСохраненногоВида.Документ = ВыполненныеРаботы.Ссылка
	|				И (ВыполненныеРаботы.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДокументыСохраненногоВида.Документ
	|	ИЗ
	|		ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДокумента
	|			ПО (&РежимВводаОтношенияКЕНВД = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ПустаяСсылка))
	|				И (ДокументыСохраненногоВида.РежимВводаОтношенияКЕНВД = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВШапке))
	|				И ДокументыСохраненногоВида.Документ = ДанныеДокумента.Ссылка
	|				И (ДанныеДокумента.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДокументыСохраненногоВида.Документ
	|	ИЗ
	|		ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ВыполненныеРаботы КАК ВыполненныеРаботы
	|			ПО (&РежимВводаОтношенияКЕНВД = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ПустаяСсылка))
	|				И (ДокументыСохраненногоВида.РежимВводаОтношенияКЕНВД = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВТабличнойЧасти))
	|				И ДокументыСохраненногоВида.Документ = ВыполненныеРаботы.Ссылка
	|				И (ВыполненныеРаботы.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДокументыСохраненногоВида.Документ
	|	ИЗ
	|		ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДокумента
	|			ПО (&РегистрироватьСведенияОБухучете = ЛОЖЬ)
	|				И (&РежимВводаПодразделенияУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ПустаяСсылка))
	|				И (ДокументыСохраненногоВида.РежимВводаПодразделенияУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВШапке))
	|				И ДокументыСохраненногоВида.Документ = ДанныеДокумента.Ссылка
	|				И (ДанныеДокумента.ПодразделениеУчетаЗатрат <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДокументыСохраненногоВида.Документ
	|	ИЗ
	|		ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ВыполненныеРаботы КАК ВыполненныеРаботы
	|			ПО (&РегистрироватьСведенияОБухучете = ЛОЖЬ)
	|				И (&РежимВводаПодразделенияУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ПустаяСсылка))
	|				И (ДокументыСохраненногоВида.РежимВводаПодразделенияУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВТабличнойЧасти))
	|				И ДокументыСохраненногоВида.Документ = ВыполненныеРаботы.Ссылка
	|				И (ВыполненныеРаботы.ПодразделениеУчетаЗатрат <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))) КАК Данные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Данные.Документ,
	|	ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.ПустаяСсылка),
	|	""11_ИспользоватьПодразделениеУчетаЗатратЛожь""
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДокументыСохраненногоВида.Документ КАК Документ
	|	ИЗ
	|		ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДокумента
	|			ПО (&РегистрироватьСведенияОБухучете = ИСТИНА)
	|				И (&РежимВводаПодразделенияУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ПустаяСсылка))
	|				И (ДокументыСохраненногоВида.РежимВводаПодразделенияУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВШапке))
	|				И ДокументыСохраненногоВида.Документ = ДанныеДокумента.Ссылка
	|				И (ДанныеДокумента.ПодразделениеУчетаЗатрат <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДокументыСохраненногоВида.Документ
	|	ИЗ
	|		ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ВыполненныеРаботы КАК ВыполненныеРаботы
	|			ПО (&РегистрироватьСведенияОБухучете = ИСТИНА)
	|				И (&РежимВводаПодразделенияУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ПустаяСсылка))
	|				И (ДокументыСохраненногоВида.РежимВводаПодразделенияУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВТабличнойЧасти))
	|				И ДокументыСохраненногоВида.Документ = ВыполненныеРаботы.Ссылка
	|				И (ВыполненныеРаботы.ПодразделениеУчетаЗатрат <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))) КАК Данные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Данные.Документ,
	|	ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.ПустаяСсылка),
	|	""12_РежимВводаСтатьиФинансирования""
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДокументыСохраненногоВида.Документ КАК Документ
	|	ИЗ
	|		ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДокумента
	|			ПО (&РежимВводаСтатьиФинансирования = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВТабличнойЧасти))
	|				И (ДокументыСохраненногоВида.РежимВводаСтатьиФинансирования = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВШапке))
	|				И ДокументыСохраненногоВида.Документ = ДанныеДокумента.Ссылка
	|				И (ДанныеДокумента.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДокументыСохраненногоВида.Документ
	|	ИЗ
	|		ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ВыполненныеРаботы КАК ВыполненныеРаботы
	|			ПО (&РежимВводаСтатьиФинансирования = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВШапке))
	|				И (ДокументыСохраненногоВида.РежимВводаСтатьиФинансирования = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВТабличнойЧасти))
	|				И ДокументыСохраненногоВида.Документ = ВыполненныеРаботы.Ссылка
	|				И (ВыполненныеРаботы.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка))) КАК Данные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Данные.Документ,
	|	ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.ПустаяСсылка),
	|	""12_РежимВводаСпособаОтраженияЗарплатыВБухучете""
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДокументыСохраненногоВида.Документ КАК Документ
	|	ИЗ
	|		ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДокумента
	|			ПО (&РежимВводаСпособаОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВТабличнойЧасти))
	|				И (ДокументыСохраненногоВида.РежимВводаСпособаОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВШапке))
	|				И ДокументыСохраненногоВида.Документ = ДанныеДокумента.Ссылка
	|				И (ДанныеДокумента.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДокументыСохраненногоВида.Документ
	|	ИЗ
	|		ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ВыполненныеРаботы КАК ВыполненныеРаботы
	|			ПО (&РежимВводаСпособаОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВШапке))
	|				И (ДокументыСохраненногоВида.РежимВводаСпособаОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВТабличнойЧасти))
	|				И ДокументыСохраненногоВида.Документ = ВыполненныеРаботы.Ссылка
	|				И (ВыполненныеРаботы.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка))) КАК Данные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Данные.Документ,
	|	ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.ПустаяСсылка),
	|	""12_РежимВводаОтношенияКЕНВД""
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДокументыСохраненногоВида.Документ КАК Документ
	|	ИЗ
	|		ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДокумента
	|			ПО (&РежимВводаОтношенияКЕНВД = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВТабличнойЧасти))
	|				И (ДокументыСохраненногоВида.РежимВводаОтношенияКЕНВД = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВШапке))
	|				И ДокументыСохраненногоВида.Документ = ДанныеДокумента.Ссылка
	|				И (ДанныеДокумента.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДокументыСохраненногоВида.Документ
	|	ИЗ
	|		ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ВыполненныеРаботы КАК ВыполненныеРаботы
	|			ПО (&РежимВводаОтношенияКЕНВД = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВШапке))
	|				И (ДокументыСохраненногоВида.РежимВводаОтношенияКЕНВД = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВТабличнойЧасти))
	|				И ДокументыСохраненногоВида.Документ = ВыполненныеРаботы.Ссылка
	|				И (ВыполненныеРаботы.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка))) КАК Данные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Данные.Документ,
	|	ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.ПустаяСсылка),
	|	""12_РежимВводаПодразделенияУчетаЗатрат""
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДокументыСохраненногоВида.Документ КАК Документ
	|	ИЗ
	|		ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДокумента
	|			ПО (&РежимВводаПодразделенияУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВТабличнойЧасти))
	|				И (ДокументыСохраненногоВида.РежимВводаПодразделенияУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВШапке))
	|				И ДокументыСохраненногоВида.Документ = ДанныеДокумента.Ссылка
	|				И (ДанныеДокумента.ПодразделениеУчетаЗатрат <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДокументыСохраненногоВида.Документ
	|	ИЗ
	|		ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ВыполненныеРаботы КАК ВыполненныеРаботы
	|			ПО (&РежимВводаПодразделенияУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВШапке))
	|				И (ДокументыСохраненногоВида.РежимВводаПодразделенияУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.РежимыВводаБухучетаВДанныхДляРасчетаЗарплаты.ВводитьВТабличнойЧасти))
	|				И ДокументыСохраненногоВида.Документ = ВыполненныеРаботы.Ссылка
	|				И (ВыполненныеРаботы.ПодразделениеУчетаЗатрат <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))) КАК Данные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыСохраненногоВида.Документ,
	|	ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.ПустаяСсылка),
	|	""13_ВыполненныеРаботыРаспределяютсяПоСотрудникам_Истина""
	|ИЗ
	|	ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДокумента
	|		ПО (&ВыполненныеРаботыРаспределяютсяПоСотрудникам = ИСТИНА)
	|			И (ДокументыСохраненногоВида.ВыполненныеРаботыРаспределяютсяПоСотрудникам = ЛОЖЬ)
	|			И ДокументыСохраненногоВида.Документ = ДанныеДокумента.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ВыполненныеРаботы КАК ВыполненныеРаботыДокумента
	|		ПО ДокументыСохраненногоВида.Документ = ВыполненныеРаботыДокумента.Ссылка
	|			И (ВыполненныеРаботыДокумента.Сотрудник <> ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыСохраненногоВида.Документ,
	|	ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.ПустаяСсылка),
	|	""13_ВыполненныеРаботыРаспределяютсяПоСотрудникам_Ложь""
	|ИЗ
	|	ВТДокументыСохраненногоВида КАК ДокументыСохраненногоВида
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДокумента
	|		ПО (&ВыполненныеРаботыРаспределяютсяПоСотрудникам = ЛОЖЬ)
	|			И (ДокументыСохраненногоВида.ВыполненныеРаботыРаспределяютсяПоСотрудникам = ИСТИНА)
	|			И ДокументыСохраненногоВида.Документ = ДанныеДокумента.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ЗначенияПоказателей КАК ЗначенияПоказателейДокумента
	|		ПО ДокументыСохраненногоВида.Документ = ЗначенияПоказателейДокумента.Ссылка
	|			И (ЗначенияПоказателейДокумента.Показатель = &ПоказательСдельныйЗаработок)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Ошибки.Документ КАК Документ,
	|	Ошибки.ЗначениеОшибки КАК ЗначениеОшибки,
	|	Ошибки.ВидОшибки КАК ВидОшибки,
	|	ДанныеДокумента.Номер КАК НомерДокумента,
	|	ДанныеДокумента.Дата КАК ДатаДокумента
	|ИЗ
	|	ВТОшибки КАК Ошибки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДокумента
	|		ПО Ошибки.Документ = ДанныеДокумента.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Ошибки.Документ,
	|	Ошибки.ЗначениеОшибки,
	|	Ошибки.ВидОшибки,
	|	ДанныеДокумента.Номер,
	|	ДанныеДокумента.Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ошибки.ВидОшибки,
	|	Ошибки.Документ";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ВидОшибки") Цикл
		КоличествоДокументовВОшибке = 0;
		Представления = Новый Массив;
		Пока Выборка.СледующийПоЗначениюПоля("Документ") Цикл
			КоличествоДокументовВОшибке = КоличествоДокументовВОшибке + 1;
			Если КоличествоДокументовВОшибке > 2 Тогда
				Представления.Добавить(НСтр("ru = 'и др.,'"));
				Прервать;
			КонецЕсли;
			СтрокаПредставления = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 от %2'"),
				Выборка.НомерДокумента,
				Формат(Выборка.ДатаДокумента, "ДЛФ=Д"));
			Представления.Добавить(СтрокаПредставления);
		КонецЦикла;
		
		Если КоличествоДокументовВОшибке = 1 Тогда
			ТекстОшибки = НСтр("ru = 'В документе'") + " " + Представления[0] + " ";
		Иначе
			ТекстОшибки = НСтр("ru = 'В документах'") + " " + СтрСоединить(Представления, ", ") + " ";
		КонецЕсли;
		
		ПолеОшибки = "";
		Если Выборка.ВидОшибки = "1_Показатели" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'используются удаляемые показатели:'");
			ПолеОшибки = "Показатели";
		ИначеЕсли Выборка.ВидОшибки = "1_ВидыВремени" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'используются удаляемые виды времени:'");
			ПолеОшибки = "ВидыРабочегоВремени";
		ИначеЕсли Выборка.ВидОшибки = "1_ВидыРабот" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'используются удаляемые работы:'");
			ПолеОшибки = "Объект.ВидыРабот";
		ИначеЕсли Выборка.ВидОшибки = "2_НесколькоСотрудников" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'используется заполнение по нескольким сотрудникам'");
			ПолеОшибки = "Объект.НесколькоСотрудников";
		ИначеЕсли Выборка.ВидОшибки = "2_НесколькоПодразделений" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'используется заполнение по нескольким подразделениям'");
			ПолеОшибки = "Объект.НесколькоПодразделений";
		ИначеЕсли Выборка.ВидОшибки = "2_НесколькоОрганизаций" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'используется заполнение по нескольким организациям'");
			ПолеОшибки = "Объект.НесколькоОрганизаций";
		ИначеЕсли Выборка.ВидОшибки = "3_ОрганизацияЗаполняется" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'указаны разные организации'");
			ПолеОшибки = "ОрганизацияЗаполняется";
		ИначеЕсли Выборка.ВидОшибки = "3_ПодразделениеЗаполняется" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'указаны разные подразделения'");
			ПолеОшибки = "ПодразделениеЗаполняется";
		ИначеЕсли Выборка.ВидОшибки = "4_ОрганизацияИспользуется" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'используется организация отличная от выбранной в шаблоне'");
			ПолеОшибки = "Объект.Организация";
		ИначеЕсли Выборка.ВидОшибки = "4_ПодразделениеИспользуется" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'используется подразделение отличное от выбранного в шаблоне'");
			ПолеОшибки = "Объект.Подразделение";
		ИначеЕсли Выборка.ВидОшибки = "5_ПроверятьЗаполнениеПодразделения" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'не заполнены подразделения'");
			ПолеОшибки = "Объект.ПроверятьЗаполнениеПодразделения";
		ИначеЕсли Выборка.ВидОшибки = "6_ПоказыватьПодразделение" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'уже заполнены подразделения'");
			ПолеОшибки = "Объект.ПоказыватьПодразделение";
		ИначеЕсли Выборка.ВидОшибки = "7_ЗначенияПоказателейВводятсяНаРазныеДаты" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'используется заполнение значений показателей на разные даты'");
			ПолеОшибки = "Объект.ЗначенияПоказателейВводятсяНаРазныеДаты";
		ИначеЕсли Выборка.ВидОшибки = "8_ИспользоватьПериодОкончания" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'используется заполнение даты окончания для значений постоянных показателей'");
			ПолеОшибки = "Объект.ИспользоватьПериодОкончания";
		ИначеЕсли Выборка.ВидОшибки = "9_ВремяВводитсяСводноИстина" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'используется заполнение времени детально по дням'");
			ПолеОшибки = "Объект.ВремяВводитсяСводно";
		ИначеЕсли Выборка.ВидОшибки = "9_ВремяВводитсяСводноЛожь" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'используется заполнение времени в целом за период'");
			ПолеОшибки = "Объект.ВремяВводитсяСводно";
		ИначеЕсли Выборка.ВидОшибки = "10_ВремяВводитсяЗаМесяц" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'используется заполнение времени одним документом за весь месяц'");
			ПолеОшибки = "Объект.ВремяВводитсяЗаМесяц";
		ИначеЕсли Выборка.ВидОшибки = "11_РегистрироватьСведенияОБухучетеЛожь" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'введены сведения о бухучете (Счет, субконто и т.п.)'");
			ПолеОшибки = "Объект.РегистрироватьСведенияОБухучете";
		ИначеЕсли Выборка.ВидОшибки = "11_ИспользоватьПодразделениеУчетаЗатратЛожь" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'введены сведения о бухучете (Подразделение бухучета)'");
			ПолеОшибки = "ИспользоватьПодразделениеУчетаЗатрат";
		ИначеЕсли Выборка.ВидОшибки = "12_РежимВводаСтатьиФинансирования" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'введены сведения о бухучете (Финансирование)'");
			ПолеОшибки = "Объект.РежимВводаСтатьиФинансирования";
		ИначеЕсли Выборка.ВидОшибки = "12_РежимВводаСпособаОтраженияЗарплатыВБухучете" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'введены сведения о бухучете (Счет, субконто)'");
			ПолеОшибки = "Объект.СпособОтраженияЗарплатыВБухучете";
		ИначеЕсли Выборка.ВидОшибки = "12_РежимВводаОтношенияКЕНВД" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'введены сведения о бухучете (ЕНВД)'");
			ПолеОшибки = "Объект.ОтношениеКЕНВД";
		ИначеЕсли Выборка.ВидОшибки = "12_РежимВводаПодразделенияУчетаЗатрат" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'введены сведения о бухучете (Подразделение бухучета)'");
			ПолеОшибки = "Объект.ПодразделениеУчетаЗатрат";
		ИначеЕсли Выборка.ВидОшибки = "13_ВыполненныеРаботыРаспределяютсяПоСотрудникам_Истина" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'в списке выполненных работ выбраны сотрудники'");
			ПолеОшибки = "ВыполненныеРаботыРаспределяютсяПоСотрудникамЧисло";
		ИначеЕсли Выборка.ВидОшибки = "13_ВыполненныеРаботыРаспределяютсяПоСотрудникам_Ложь" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'заработок сотрудников введен отдельно от выполненных работ'");
			Если ВидыРаботЗаполняютсяВДокументе Тогда
				ПолеОшибки = "ВыполненныеРаботыРаспределяютсяПоСотрудникамЧисло";
			Иначе
				ПолеОшибки = "ВидыРаботЗаполняютсяВДокументеЧисло";
			КонецЕсли;
		Иначе
			Продолжить;
		КонецЕсли;
		Если Выборка.ВидОшибки = "1_Показатели" Или Выборка.ВидОшибки = "1_ВидыВремени" Или Выборка.ВидОшибки = "1_ВидыРабот" Тогда
			Пока Выборка.Следующий() Цикл
				ТекстОшибки = ТекстОшибки + Символы.ПС + Символы.Таб + Строка(Выборка.ЗначениеОшибки);
			КонецЦикла;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка, ПолеОшибки, , Отказ);
	КонецЦикла;
	
КонецПроцедуры

#Область БазоваяФункциональностьПроверокЗаполнения

Процедура СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы = Неопределено, НомерСтроки = Неопределено, ИмяРеквизита = Неопределено)
	Если Текст = Неопределено Тогда
		Текст = НСтр("ru = 'Не заполнено поле ""%1"".'");
		Если ИмяТаблицы = Неопределено Тогда
			Текст = СтрШаблон(Текст, Метаданные().Реквизиты[ИмяРеквизита].Представление());
		Иначе
			Текст = СтрШаблон(Текст, Метаданные().ТабличныеЧасти[ИмяТаблицы].Реквизиты[ИмяРеквизита].Представление());
		КонецЕсли;
	КонецЕсли;
	
	Отказ = Истина;
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Если ИмяРеквизита <> Неопределено Тогда
		Если ИмяТаблицы = Неопределено Тогда
			Сообщение.Поле = ИмяРеквизита;
		Иначе
			Сообщение.Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТаблицы, НомерСтроки, ИмяРеквизита);
		КонецЕсли;
	КонецЕсли;
	Сообщение.Сообщить();
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
