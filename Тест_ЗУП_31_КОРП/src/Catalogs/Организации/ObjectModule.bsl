#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	
	МассивОрганизаций = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ссылка);
	ДополнитьОбособленнымиОрганизациями(МассивОрганизаций, Ссылка);
	
	Возврат ОбменДаннымиЗарплатаКадрыРасширенный.ОграниченияРегистрацииПоОрганизацииИПодразделению(ЭтотОбъект, МассивОрганизаций);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДополнитьОбособленнымиОрганизациями(МассивОрганизаций, ОрганизацияСсылка)
	
	Если ОрганизацияСсылка.ЕстьОбособленныеПодразделения Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ОрганизацияСсылка", ОрганизацияСсылка);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Организации.Ссылка КАК ОбособленноеПодразделение,
		|	Организации.ЕстьОбособленныеПодразделения КАК ЕстьОбособленныеПодразделения
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.ОбособленноеПодразделение
		|	И Организации.ГоловнаяОрганизация В(&ОрганизацияСсылка)";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			МассивОрганизаций.Добавить(Выборка.ОбособленноеПодразделение);
			Если Выборка.ЕстьОбособленныеПодразделения Тогда
				ДополнитьОбособленнымиОрганизациями(МассивОрганизаций, Выборка.ОбособленноеПодразделение)
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
