#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Процедура выполняет первоначальное заполнение
//
Процедура НачальноеЗаполнение() Экспорт
	
	Мотиваторы = Новый Массив;
	Мотиваторы.Добавить(НСтр("ru = 'Деньги'"));
	Мотиваторы.Добавить(НСтр("ru = 'Интерес'"));
	Мотиваторы.Добавить(НСтр("ru = 'Отношения в коллективе'"));
	Мотиваторы.Добавить(НСтр("ru = 'Хороший руководитель'"));
	Мотиваторы.Добавить(НСтр("ru = 'Близость к дому'"));
	Мотиваторы.Добавить(НСтр("ru = 'Профессиональный рост'"));
	Мотиваторы.Добавить(НСтр("ru = 'Карьерный рост'"));
	Мотиваторы.Добавить(НСтр("ru = 'Стабильность'"));
	
	ТипСтрока = Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150));
	
	ТаблицаДляЗапроса = Новый ТаблицаЗначений;
	ТаблицаДляЗапроса.Колонки.Добавить("Наименование", ТипСтрока);
	
	Для Каждого Мотиватор Из Мотиваторы Цикл
		ТаблицаДляЗапроса.Добавить().Наименование = Мотиватор;
	КонецЦикла;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДобавляемыеМотиваторы.Наименование КАК Наименование
	|ПОМЕСТИТЬ ДобавляемыеМотиваторы
	|ИЗ
	|	&ДобавляемыеМотиваторы КАК ДобавляемыеМотиваторы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Наименование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДобавляемыеМотиваторы.Наименование КАК Наименование,
	|	Мотиваторы.Ссылка КАК Ссылка
	|ИЗ
	|	ДобавляемыеМотиваторы КАК ДобавляемыеМотиваторы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Мотиваторы КАК Мотиваторы
	|		ПО ДобавляемыеМотиваторы.Наименование = Мотиваторы.Наименование
	|ГДЕ
	|	Мотиваторы.Ссылка ЕСТЬ NULL");
	
	Запрос.УстановитьПараметр("ДобавляемыеМотиваторы", ТаблицаДляЗапроса);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НовыйЭлемент = Справочники.Мотиваторы.СоздатьЭлемент();
		НовыйЭлемент.Наименование = Выборка.Наименование;
		НовыйЭлемент.ОбменДанными.Загрузка = Истина;
		НовыйЭлемент.Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли