#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПоказыватьНедоступные = Ложь;
	УстановитьОтборИспользуемых();
	
	ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "Список",,,, "ВидДокументаОснования, ГруппаВидовДоговоров, ПометкаУдаления");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоказыватьНеиспользуемыеПриИзменении(Элемент)
	УстановитьОтборИспользуемых()
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, Настройки);
КонецПроцедуры

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, , СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборИспользуемых()
	
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(Список.Отбор, "ГруппаВидовДоговоров");
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(Список.Отбор, "ВидДокументаОснования");
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(Список.Отбор, "ПометкаУдаления");
	
	Если ПоказыватьНеиспользуемые Тогда
		ДоступныеГруппыВидовДоговоров = Перечисления.ГруппыВидовДоговоровССотрудникамДляВыплатыЗарплаты.ДоступныеПоМетаданным();
	Иначе	
		ДоступныеГруппыВидовДоговоров = Перечисления.ГруппыВидовДоговоровССотрудникамДляВыплатыЗарплаты.ДоступныеПоФункциональнымОпциям();	
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.Отбор, 
		"ГруппаВидовДоговоров", 
		ДоступныеГруппыВидовДоговоров,
		ВидСравненияКомпоновкиДанных.ВСписке);
		
	Если ПоказыватьНеиспользуемые Тогда
		ДоступныеВидыДокументов = Перечисления.ВидыДокументовМежрасчетныхНачислений.ДоступныеПоМетаданным();	
	Иначе		
		ДоступныеВидыДокументов = Перечисления.ВидыДокументовМежрасчетныхНачислений.ДоступныеПоФункциональнымОпциям();	
	КонецЕсли;
	ДоступныеВидыДокументов.Добавить(Перечисления.ВидыДокументовМежрасчетныхНачислений.ПустаяСсылка());
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.Отбор, 
		"ВидДокументаОснования", 
		ДоступныеВидыДокументов,
		ВидСравненияКомпоновкиДанных.ВСписке);
		
	Если Не ПоказыватьНеиспользуемые Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			Список.Отбор, 
			"ПометкаУдаления", 
			Ложь);
	КонецЕсли	
	
КонецПроцедуры	

#КонецОбласти