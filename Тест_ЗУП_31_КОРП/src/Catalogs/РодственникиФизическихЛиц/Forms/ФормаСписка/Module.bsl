#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.РежимВыбора = Истина Тогда
		Элементы.Список.РежимВыбора = Истина;
	КонецЕсли; 
	
	Если Не Параметры.Отбор.Свойство("Владелец") 
		И Параметры.Свойство("Сотрудник") 
		И ТипЗнч(Параметры.Сотрудник) = Тип("СправочникСсылка.Сотрудники") Тогда
		Владелец = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Сотрудник, "ФизическоеЛицо");
		Параметры.Отбор.Вставить("Владелец", Владелец);
	КонецЕсли;	
	
	Если Не Параметры.Отбор.Свойство("Владелец")
		Или Не ЗначениеЗаполнено(Параметры.Отбор.Владелец) Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Владелец", , , , Ложь, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
			
		ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "Список");
		
	Иначе
		ОткрытаСОтборомПоВладельцу = Истина;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	
	Если Не ОткрытаСОтборомПоВладельцу Тогда
		ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, Настройки);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОткрытаСОтборомПоВладельцу Тогда
		ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, , СтандартнаяОбработка);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти
