#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий	

Процедура ПередЗаписью(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		ДополнительныеСвойства.Вставить("ЭтоНоваяСтруктурнаяЕдиница", Истина);
	Иначе
		
		ТекущийРодитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Родитель");
		Если ТекущийРодитель <> Родитель Тогда
			ДополнительныеСвойства.Вставить("СменаРодителя", Истина);
			ДополнительныеСвойства.Вставить("ПредыдущийРодитель", ТекущийРодитель);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли; 
	
	ЭтоНоваяСтруктурнаяЕдиница = ДополнительныеСвойства.Свойство("ЭтоНоваяСтруктурнаяЕдиница");
	СменаРодителя = ДополнительныеСвойства.Свойство("СменаРодителя");
	
	СписокСтруктурныхЕдиниц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ссылка);
	
	Если НЕ ЭтоНоваяСтруктурнаяЕдиница И НЕ СменаРодителя Тогда
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.ПодчиненностьСтруктурныхЕдиниц.ОбновитьПодчиненностьСтруктурныхЕдиниц(СписокСтруктурныхЕдиниц);
	
	Если СменаРодителя Тогда
		
		ПредыдущийРодитель = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ПредыдущийРодитель");
		Если ПредыдущийРодитель = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ТекущийРодитель = Родитель;
		
		РегистрыСведений.ПодчиненностьСтруктурныхЕдиниц.ОбновитьПодчиненностьСтруктурныхЕдиницПриСменеРодителя(
			Ссылка, ПредыдущийРодитель, ТекущийРодитель);
			
	КонецЕсли;

КонецПроцедуры

#КонецОбласти 

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	Возврат ОбменДаннымиЗарплатаКадрыРасширенный.ОграниченияРегистрацииПоОрганизацииИПодразделению(ЭтотОбъект, , Ссылка);
КонецФункции

#КонецОбласти 

#КонецЕсли
