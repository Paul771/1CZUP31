
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)

	Если Не Константы.СтруктураПредприятияНеСоответствуетСтруктуреЮридическихЛиц.Получить() Тогда
		Параметры.Отбор.Вставить("СоответствуетСтруктуреЮридическихЛиц", Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	ПодобратьФормуПодразделенияОрганизации(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка);
	
#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПодобратьФормуПодразделенияОрганизации(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ПолучитьФункциональнуюОпцию("СтруктураПредприятияНеСоответствуетСтруктуреЮридическихЛиц") Тогда
		Возврат;
	КонецЕсли;
	
	ИмяКлючевогоСвойства = Неопределено;
	Если Параметры.Свойство("ТекущаяСтрока") Тогда
		ИмяКлючевогоСвойства = "ТекущаяСтрока";
		ВыбраннаяФорма = "Справочник.ПодразделенияОрганизаций." + ВидФормы;
		Параметры.Вставить("ОповещатьОВыбореИсточника", Истина);
		СтандартнаяОбработка = Ложь;
	Иначе
		Если Параметры.Свойство("Ключ") Тогда
			ИмяКлючевогоСвойства = "Ключ";
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИмяКлючевогоСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры[ИмяКлючевогоСвойства]) Тогда
		Возврат;
	КонецЕсли;
	
	ИсточникПодразделения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры[ИмяКлючевогоСвойства], "Источник");
	Если Не ЗначениеЗаполнено(ИсточникПодразделения) Тогда
		Возврат;
	КонецЕсли;
	
	ВыбраннаяФорма = Метаданные.НайтиПоТипу(ТипЗнч(ИсточникПодразделения)).ПолноеИмя() + "." + ВидФормы;
	Параметры[ИмяКлючевогоСвойства] = ИсточникПодразделения;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецЕсли

#КонецОбласти

