
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗакрыватьПриВыборе = Ложь;
	ЗаполнитьТаблицуДействий();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДействияСотрудников

&НаКлиенте
Процедура ДействияСотрудниковВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыборВСпискеДействий(СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ОбработатьВыборВСпискеДействий();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуДействий()
	
	БиблиотекаXML = Справочники.ДействияСотрудников.ПолучитьМакет("Библиотека").ПолучитьТекст();
	
	БиблиотекаТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(БиблиотекаXML).Данные;
	
	Для Каждого ТекущаяСтрока Из БиблиотекаТаблица Цикл
		НоваяСтрока = ХарактеристикиДействий.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
	КонецЦикла;
	
	БиблиотекаТаблица.Свернуть("Наименование");
	Для Каждого ТекущаяСтрока Из БиблиотекаТаблица Цикл
		НоваяСтрока = ДействияСотрудников.Добавить();
		НоваяСтрока.Наименование = ТекущаяСтрока.Наименование;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьВыбранныеСтроки(Знач ВыбранныеСтроки, ТекущаяСсылка)
	
	БиблиотекаХарактеристикXML = ПланыВидовХарактеристик.ХарактеристикиПерсонала.ПолучитьМакет("Библиотека").ПолучитьТекст();
	БиблиотекаХарактеристикТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(БиблиотекаХарактеристикXML).Данные;
	
	Для каждого НомерСтроки Из ВыбранныеСтроки Цикл
		ТекущиеДанные = ДействияСотрудников[НомерСтроки];
		
		СтрокаВБазе = Справочники.ДействияСотрудников.НайтиПоНаименованию(ТекущиеДанные.Наименование, Истина);
		Если ЗначениеЗаполнено(СтрокаВБазе) Тогда
			Если НомерСтроки = Элементы.ДействияСотрудников.ТекущаяСтрока Или ТекущаяСсылка = Неопределено Тогда
				ТекущаяСсылка = СтрокаВБазе;
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Справочники.ДействияСотрудников.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущиеДанные);
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Наименование", ТекущиеДанные.Наименование);
		СтрокиЗначений = ХарактеристикиДействий.НайтиСтроки(СтруктураПоиска);
		Для Каждого ТекущаяСтрока Из СтрокиЗначений Цикл
			Если Не ЗначениеЗаполнено(ТекущаяСтрока.Характеристика) Тогда
				Продолжить;
			КонецЕсли;
			СтруктураХарактеристики = ХарактеристикиПерсонала.ХарактеристикаИзМакета(ТекущаяСтрока.Характеристика, ТекущаяСтрока.Значение, БиблиотекаХарактеристикТаблица);
			Если СтруктураХарактеристики = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрокаТЧ = НоваяСтрока.ХарактеристикиПерсонала.Добавить();
			НоваяСтрокаТЧ.Характеристика = СтруктураХарактеристики.Характеристика;
			НоваяСтрокаТЧ.Значение = СтруктураХарактеристики.Значение;
			НоваяСтрокаТЧ.Вес = ТекущаяСтрока.Вес;
			НоваяСтрокаТЧ.ВесЗначения = ТекущаяСтрока.ВесЗначения;
			НоваяСтрокаТЧ.ТребуетсяОбучение = ТекущаяСтрока.ТребуетсяОбучение;
			НоваяСтрокаТЧ.ТребуетсяПроверка = ТекущаяСтрока.ТребуетсяПроверка;
		КонецЦикла;
		НоваяСтрока.Записать();
		
		Если НомерСтроки = Элементы.ДействияСотрудников.ТекущаяСтрока Или ТекущаяСсылка = Неопределено Тогда
			ТекущаяСсылка = НоваяСтрока.Ссылка;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборВСпискеДействий(СтандартнаяОбработка = Неопределено)
	
	СтандартнаяОбработка = Ложь;
	
	НовыхХарактеристик = 0;
	ПроверитьНеобходимостьСозданияНовых(Элементы.ДействияСотрудников.ВыделенныеСтроки, НовыхХарактеристик); 
	Если НовыхХарактеристик > 0 Тогда
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'При создании действий сотрудников из библиотеки также будут созданы новые характеристики персонала.
			|Количество новых элементов: %1
			|Продолжить?'"), Строка(НовыхХарактеристик));
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьВыборВСпискеДействийЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Возврат;
	КонецЕсли;

	ТекущаяСсылка = Неопределено;
	СохранитьВыбранныеСтроки(Элементы.ДействияСотрудников.ВыделенныеСтроки, ТекущаяСсылка);
	ПоказатьОповещениеПользователя(НСтр("ru = 'Добавлены действия сотрудников'"));
	ОповеститьОВыборе(ТекущаяСсылка);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборВСпискеДействийЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСсылка = Неопределено;
	СохранитьВыбранныеСтроки(Элементы.ДействияСотрудников.ВыделенныеСтроки, ТекущаяСсылка);
	ПоказатьОповещениеПользователя(НСтр("ru = 'Добавлены действия сотрудников'"));
	ОповеститьОВыборе(ТекущаяСсылка);
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьНеобходимостьСозданияНовых(Знач ВыбранныеСтроки, НовыхХарактеристик)
	
	МассивХарактеристик = Новый Массив;
	Для каждого НомерСтроки Из ВыбранныеСтроки Цикл
		ТекущиеДанные = ДействияСотрудников[НомерСтроки];
		
		СтрокаВБазе = Справочники.ДействияСотрудников.НайтиПоНаименованию(ТекущиеДанные.Наименование, Истина);
		Если ЗначениеЗаполнено(СтрокаВБазе) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Наименование", ТекущиеДанные.Наименование);
		СтрокиЗначений = ХарактеристикиДействий.НайтиСтроки(СтруктураПоиска);
		Для Каждого ТекущаяСтрока Из СтрокиЗначений Цикл
			Если (Не ЗначениеЗаполнено(ТекущаяСтрока.Характеристика)) Или (МассивХарактеристик.Найти(ТекущаяСтрока.Характеристика) <> Неопределено) Тогда
				Продолжить;
			КонецЕсли;
			ЭлектронноеИнтервью.ПроверитьНовуюХарактеристику(ТекущаяСтрока.Характеристика, МассивХарактеристик);
		КонецЦикла;		
	КонецЦикла;
	
	НовыхХарактеристик = МассивХарактеристик.Количество();
	
КонецПроцедуры

#КонецОбласти