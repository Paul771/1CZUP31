#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Формирует данные указанного профиля должности.
//
// Возвращаемое значение — структура
//
Функция ДанныеПрофиляДолжности(ПрофильДолжности) Экспорт
	
	ДанныеПрофиля = Новый Структура(
		"Требования,
		|Обязанности,
		|Условия,
		|Подразделение,
		|Должность,
		|ХарактеристикиПерсонала,
		|ДействияСотрудников,
		|ЭтапыРаботыСКандидатами");
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ПрофилиДолжностей.Ссылка,
		|	ПрофилиДолжностей.Наименование,
		|	ПрофилиДолжностей.Требования,
		|	ПрофилиДолжностей.Обязанности,
		|	ПрофилиДолжностей.Условия,
		|	ПрофилиДолжностей.Подразделение,
		|	ПрофилиДолжностей.Должность,
		|	ПрофилиДолжностей.ХарактеристикиПерсонала.(
		|		НомерСтроки,
		|		Характеристика,
		|		Значение,
		|		Вес,
		|		ТребуетсяПроверка,
		|		ТребуетсяОбучение,
		|		ВесЗначения
		|	),
		|	ПрофилиДолжностей.ДействияСотрудников.(
		|		НомерСтроки,
		|		ДействиеСотрудника
		|	),
		|	ПрофилиДолжностей.ЭтапыРаботыСКандидатами.(
		|		НомерСтроки,
		|		ЭтапРаботы,
		|		ШаблонАнкеты,
		|		Комментарий
		|	)
		|ИЗ
		|	Справочник.ПрофилиДолжностей КАК ПрофилиДолжностей
		|ГДЕ
		|	ПрофилиДолжностей.Ссылка = &ПрофильДолжности");
		
	Запрос.УстановитьПараметр("ПрофильДолжности", ПрофильДолжности);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(ДанныеПрофиля, Выборка,, "ХарактеристикиПерсонала, ДействияСотрудников, ЭтапыРаботыСКандидатами");
	
	МассивХарактеристикиПерсонала = Новый Массив;
	ДействияСотрудников = Новый Массив;
	ЭтапыРаботыСКандидатами = Новый Массив;
	Если Выборка.Количество() > 0 Тогда
		Если ПолучитьФункциональнуюОпцию("ИспользоватьЭлектронноеИнтервью") Тогда
			ВыборкаХарактеристикиПерсонала = Выборка.ХарактеристикиПерсонала.Выбрать();
			Пока ВыборкаХарактеристикиПерсонала.Следующий() Цикл
				СтруктураСтроки = Новый Структура(
					"НомерСтроки,
					|Характеристика,
					|Значение,
					|Вес,
					|ТребуетсяПроверка,
					|ТребуетсяОбучение,
					|ВесЗначения");
				ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаХарактеристикиПерсонала);
				МассивХарактеристикиПерсонала.Добавить(СтруктураСтроки);
			КонецЦикла;
			ВыборкаДействияСотрудников = Выборка.ДействияСотрудников.Выбрать();
			Пока ВыборкаДействияСотрудников.Следующий() Цикл
				СтруктураСтроки = Новый Структура(
					"НомерСтроки,
					|ДействиеСотрудника");
				ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаДействияСотрудников);
				ДействияСотрудников.Добавить(СтруктураСтроки);
			КонецЦикла;
		КонецЕсли;
		ВыборкаЭтапыРаботыСКандидатами = Выборка.ЭтапыРаботыСКандидатами.Выбрать();
		Пока ВыборкаЭтапыРаботыСКандидатами.Следующий() Цикл
			СтруктураСтроки = Новый Структура(
				"НомерСтроки,
				|ЭтапРаботы,
				|ШаблонАнкеты,
				|Комментарий");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаЭтапыРаботыСКандидатами);
			ЭтапыРаботыСКандидатами.Добавить(СтруктураСтроки);
		КонецЦикла;
	КонецЕсли;
	ДанныеПрофиля.ХарактеристикиПерсонала = МассивХарактеристикиПерсонала;
	ДанныеПрофиля.ДействияСотрудников = ДействияСотрудников;
	ДанныеПрофиля.ЭтапыРаботыСКандидатами = ЭтапыРаботыСКандидатами;
	
	Возврат ДанныеПрофиля;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВыполнитьЗапись(СтруктураПараметров, АдресХранилища) Экспорт
	
	СправочникОбъект = ЗарплатаКадры.ДесериализоватьОбъектИзДвоичныхДанных(СтруктураПараметров.ДанныеОбъекта);
	СправочникОбъект.Записать();
	
	ДанныеОбъекта = ЗарплатаКадры.СериализоватьОбъектВДвоичныеДанные(СправочникОбъект);
	ПоместитьВоВременноеХранилище(ДанныеОбъекта, АдресХранилища);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
