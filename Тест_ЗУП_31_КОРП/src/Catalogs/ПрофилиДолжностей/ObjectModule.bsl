#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Вакансии") Тогда
		ЗаполнитьПоВакансии(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если Ссылка.Пустая() Тогда
		ЭлектронноеИнтервью.СкопироватьНастройкиЭтапов(ЭтотОбъект, "ПрофилиДолжностей");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	// Обновляем все вакансии, описание которых соответствует профилю.
	Справочники.Вакансии.ОбновитьОписаниеВакансийПоПрофилюДолжности(Ссылка, Требования, Обязанности, Условия, ХарактеристикиПерсонала, ДействияСотрудников, ЭтапыРаботыСКандидатами);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	Возврат ОбменДаннымиЗарплатаКадрыРасширенный.ОграниченияРегистрацииПоОрганизацииИПодразделению(ЭтотОбъект, , Подразделение);
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоВакансии(Вакансия)
	
	ДанныеВакансии = Справочники.Вакансии.ДанныеВакансии(Вакансия);
	
	ЭтотОбъект.Требования = ДанныеВакансии.Требования;
	ЭтотОбъект.Обязанности = ДанныеВакансии.Обязанности;
	ЭтотОбъект.Условия = ДанныеВакансии.Условия;
	
	ЭтотОбъект.Подразделение = ДанныеВакансии.Подразделение;
	ЭтотОбъект.Должность = ДанныеВакансии.Должность;
	
	ПодборПерсоналаКлиентСервер.ЗаполнитьНаименованиеПрофиляДолжности(ЭтотОбъект);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеВакансии.ЭтапыРаботыСКандидатами, ЭтотОбъект.ЭтапыРаботыСКандидатами);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеВакансии.ДействияСотрудников, ЭтотОбъект.ДействияСотрудников);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеВакансии.ХарактеристикиПерсонала, ЭтотОбъект.ХарактеристикиПерсонала);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
