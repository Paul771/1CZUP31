#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;	
	КонецЕсли;	
	
	Если НЕ ЭтоНовый() И НЕ ЭтоГруппа Тогда	
		СвойстваКурсаВБазе = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Наименование, ПометкаУдаления");		
		ДополнительныеСвойства.Вставить("СтароеНаименование", СвойстваКурсаВБазе.Наименование);
		ДополнительныеСвойства.Вставить("СтараяПометкаУдаления", СвойстваКурсаВБазе.ПометкаУдаления);		
	КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда		
		ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());		
	КонецЕсли;

	РазработкаЭлектронныхКурсовСлужебный.ПроверитьВозможностьЗаписиЭлементаПередЗаписью(ЭтотОбъект, Отказ);
	РазработкаЭлектронныхКурсовСлужебный.УстановитьДатуИзмененияПередЗаписью(ЭтотОбъект, Отказ);

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
		
	Если ОбменДанными.Загрузка Тогда	
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	РазработкаЭлектронныхКурсовСлужебный.ПереименоватьГруппыХраненияЭлементовЭлектронныхКурсовПриЗаписи(ЭтотОбъект, Отказ);
	
	Если ДополнительныеСвойства.Свойство("ЭтоКопирование") И ДополнительныеСвойства.ЭтоКопирование Тогда
		Возврат;
	КонецЕсли;	
	
	Если ДополнительныеСвойства.Свойство("ПомечатьПодчиненные") Тогда
		ПомечатьПодчиненные = ДополнительныеСвойства.ПомечатьПодчиненные;
	Иначе
		ПомечатьПодчиненные = Истина;
	КонецЕсли;	
	
	Если ДополнительныеСвойства.Свойство("СтараяПометкаУдаления") И ПомечатьПодчиненные Тогда
		
		Если НЕ ДополнительныеСвойства.СтараяПометкаУдаления
			И ПометкаУдаления Тогда			
			
			Справочники.ЭлектронныеКурсы.ПриУстановкеСнятииПометкиУдаленияЭлектронногоКурса(Ссылка, Истина);
			
		КонецЕсли;
		
		Если ДополнительныеСвойства.СтараяПометкаУдаления
			И НЕ ПометкаУдаления Тогда
		
			Справочники.ЭлектронныеКурсы.ПриУстановкеСнятииПометкиУдаленияЭлектронногоКурса(Ссылка, Ложь);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	Строка = Таблица.Добавить();
	Строка.ЗначениеДоступа = Ссылка;
	
КонецПроцедуры

#КонецОбласти


#КонецЕсли


