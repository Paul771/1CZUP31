
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция НастройкиВидаДокумента(Шаблон) Экспорт
	
	СтруктураНастроек = Новый Структура(
	"Наименование,
	|НесколькоСотрудников,
	|ПодписываетРуководитель,
	|ПодписываетГлавныйБухгалтер,
	|ПодписываетКассир,
	|ПодписываетРуководительКадровойСлужбы,
	|ПодписываетБухгалтер,
	|ПоказыватьПодразделение,
	|ПроверятьЗаполнениеПодразделения");
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ШаблоныПроизвольныхКадровыхПриказов.Наименование,
	|	ШаблоныПроизвольныхКадровыхПриказов.НесколькоСотрудников,
	|	ШаблоныПроизвольныхКадровыхПриказов.ПодписываетРуководитель,
	|	ШаблоныПроизвольныхКадровыхПриказов.ПодписываетГлавныйБухгалтер,
	|	ШаблоныПроизвольныхКадровыхПриказов.ПодписываетКассир,
	|	ШаблоныПроизвольныхКадровыхПриказов.ПодписываетРуководительКадровойСлужбы,
	|	ШаблоныПроизвольныхКадровыхПриказов.ПодписываетБухгалтер,
	|	ШаблоныПроизвольныхКадровыхПриказов.ПоказыватьПодразделение,
	|	ШаблоныПроизвольныхКадровыхПриказов.ПроверятьЗаполнениеПодразделения
	|ИЗ
	|	Справочник.ШаблоныПроизвольныхКадровыхПриказов КАК ШаблоныПроизвольныхКадровыхПриказов
	|ГДЕ
	|	ШаблоныПроизвольныхКадровыхПриказов.Ссылка = &Шаблон";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Шаблон", Шаблон);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(СтруктураНастроек, Выборка);
	
	Возврат СтруктураНастроек;
	
КонецФункции

// Предназначена для получения актуальных видов документов.
//
Функция ТаблицаВидовДокументов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ШаблоныПроизвольныхКадровыхПриказов.Ссылка КАК ВидДокумента,
	|	ШаблоныПроизвольныхКадровыхПриказов.Наименование КАК Наименование,
	|	ШаблоныПроизвольныхКадровыхПриказов.Наименование КАК Представление,
	|	ШаблоныПроизвольныхКадровыхПриказов.Родитель
	|ИЗ
	|	Справочник.ШаблоныПроизвольныхКадровыхПриказов КАК ШаблоныПроизвольныхКадровыхПриказов
	|ГДЕ
	|	НЕ ШаблоныПроизвольныхКадровыхПриказов.ПометкаУдаления
	|	И НЕ ШаблоныПроизвольныхКадровыхПриказов.ЭтоГруппа
	|	И НЕ ШаблоныПроизвольныхКадровыхПриказов.ВАрхиве";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ДоступныеВнешниеПечатныеФормы(Шаблон) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Шаблон", Шаблон);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ШаблоныПроизвольныхКадровыхПриказовВнешниеПечатныеФормы.ПечатнаяФорма КАК ПечатнаяФорма
	|ИЗ
	|	Справочник.ШаблоныПроизвольныхКадровыхПриказов.ВнешниеПечатныеФормы КАК ШаблоныПроизвольныхКадровыхПриказовВнешниеПечатныеФормы
	|ГДЕ
	|	ШаблоныПроизвольныхКадровыхПриказовВнешниеПечатныеФормы.Ссылка = &Шаблон";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ПечатнаяФорма");
	
КонецФункции

#КонецОбласти

#КонецЕсли
