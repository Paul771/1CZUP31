
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Объект.Владелец) Тогда
		Элементы.Владелец.Доступность = Ложь;
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыРасписание

&НаКлиенте
Процедура РасписаниеПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока 
		И Объект.Расписание.Количество() > 1 Тогда
		
		ИндексПредыдущейСтроки = Элементы.Расписание.ТекущиеДанные.НомерСтроки - 2;
		
		Элементы.Расписание.ТекущиеДанные.ВремяНачала = Объект.Расписание[ИндексПредыдущейСтроки].ВремяОкончания;	
	КонецЕсли;		
КонецПроцедуры

&НаКлиенте
Процедура РасписаниеВремяНачалаПриИзменении(Элемент)
	 ЗаполнитьДлительностьИнтервала(Элементы.Расписание.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура РасписаниеВремяОкончанияПриИзменении(Элемент)
	ЗаполнитьДлительностьИнтервала(Элементы.Расписание.ТекущиеДанные);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьДлительностьИнтервала(СтрокаРасписания)
	Секунды = СтрокаРасписания.ВремяОкончания - СтрокаРасписания.ВремяНачала;
	СтрокаРасписания.Часы = Секунды / 3600;
	
	ВремяНачалаСмены = Объект.Расписание[0].ВремяНачала;
	Если СтрокаРасписания.ВремяНачала < СтрокаРасписания.ВремяОкончания Тогда
		Если СтрокаРасписания.НомерСтроки = 1 
			Или СтрокаРасписания.ВремяНачала > ВремяНачалаСмены Тогда	
			
			СтрокаРасписания.Часы = (СтрокаРасписания.ВремяОкончания - СтрокаРасписания.ВремяНачала) / 3600;
			СтрокаРасписания.ПереходящиеЧасы = 0;
		Иначе	
			СтрокаРасписания.Часы = (СтрокаРасписания.ВремяОкончания - СтрокаРасписания.ВремяНачала) / 3600;
			СтрокаРасписания.ПереходящиеЧасы = СтрокаРасписания.Часы;	
		КонецЕсли;	
	Иначе	
		СтрокаРасписания.ПереходящиеЧасы = (СтрокаРасписания.ВремяОкончания - '00010101') / 3600;
		СтрокаРасписания.Часы = ('00010101235959' - СтрокаРасписания.ВремяНачала + 1) / 3600 + СтрокаРасписания.ПереходящиеЧасы;
	КонецЕсли;		
КонецПроцедуры	

#КонецОбласти


