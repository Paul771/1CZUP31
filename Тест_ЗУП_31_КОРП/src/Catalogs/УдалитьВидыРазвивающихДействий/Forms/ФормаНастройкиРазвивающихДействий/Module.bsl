
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ПриПолученииДанныхНаСервере();
	
	ЗаполнитьТаблицуРазвивающихДействий();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ВозвращаемыйМассив = Новый Массив;
	Для каждого РазвивающееДействие Из ТаблицаРазвивающихДействий Цикл
		Если РазвивающееДействие.Использовать Тогда
			ВозвращаемыйМассив.Добавить(РазвивающееДействие.Ссылка);
		КонецЕсли;
	КонецЦикла; 
	
	Закрыть(ВозвращаемыйМассив);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьЭлементВверх(Команда)
	
	Если Элементы.ТаблицаРазвивающихДействий.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПереместитьЭлемент(Элементы.ТаблицаРазвивающихДействий.ТекущиеДанные, -1);
	
КонецПроцедуры	

&НаКлиенте
Процедура ПереместитьЭлементВниз(Команда)
	
	Если Элементы.ТаблицаРазвивающихДействий.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПереместитьЭлемент(Элементы.ТаблицаРазвивающихДействий.ТекущиеДанные, 1);
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриПолученииДанныхНаСервере()

	Если Параметры.Свойство("МассивРазвивающихДействий") Тогда
		МассивРазвивающихДействий = Новый ФиксированныйМассив(Параметры.МассивРазвивающихДействий);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуРазвивающихДействий()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВидыРазвивающихДействий.Ссылка КАК Ссылка,
		|	ВЫБОР
		|		КОГДА ВидыРазвивающихДействий.Ссылка В (&МассивРазвивающихДействий)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Использовать,
		|	ВЫБОР
		|		КОГДА ВидыРазвивающихДействий.Ссылка В (&МассивРазвивающихДействий)
		|			ТОГДА 1
		|		ИНАЧЕ 2
		|	КОНЕЦ КАК Порядок
		|ИЗ
		|	Справочник.УдалитьВидыРазвивающихДействий КАК ВидыРазвивающихДействий
		|ГДЕ
		|	НЕ ВидыРазвивающихДействий.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок,
		|	ВидыРазвивающихДействий.Наименование";
	Запрос.УстановитьПараметр("МассивРазвивающихДействий", МассивРазвивающихДействий);
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаРазвивающихДействий.Очистить();
	
	НомерСтроки = 1;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ТаблицаРазвивающихДействий.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.Порядок = НомерСтроки;
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьЭлемент(ТекущиеДанные, Сдвиг)

	СтарыйНомер = ТекущиеДанные.Порядок;
	НовыйНомер = СтарыйНомер + Сдвиг;
	
	МаксимальныйНомер = ТаблицаРазвивающихДействий.Количество();
	
	Если Сдвиг = 0
		ИЛИ НовыйНомер < 1
		ИЛИ НовыйНомер > МаксимальныйНомер Тогда
	
		Возврат;
	
	КонецЕсли;
	
	НайденныеСтроки = ТаблицаРазвивающихДействий.НайтиСтроки(Новый Структура("Порядок", НовыйНомер));
	Если НайденныеСтроки.Количество() > 0 Тогда
		НайденныеСтроки[0].Порядок = СтарыйНомер;
	КонецЕсли;
	
	ТекущиеДанные.Порядок = НовыйНомер;
	
	ТаблицаРазвивающихДействий.Сортировать("Порядок");
	
КонецПроцедуры

#КонецОбласти 