
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОтборТипВопроса = Неопределено;
	Если Параметры.Свойство("ОтборТипВопроса") Тогда
		ОтборТипВопроса = Параметры.ОтборТипВопроса;
	КонецЕсли;
	
	Если Параметры.Свойство("Характеристика") Тогда
		Характеристика = Строка(Параметры.Характеристика);
	КонецЕсли;
	
	ЗакрыватьПриВыборе = Ложь;
	ЗаполнитьТаблицуВопросов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ХарактеристикаПриИзменении(Элемент)
	
	КлючиВопросов.Очистить();
	КлючевыеВопросы.Очистить();
	ВопросыДляСобеседования.Очистить();
	ОтветыНаВопросы.Очистить();
	ЗаполнитьТаблицуВопросов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВопросыДляСобеседования

&НаКлиенте
Процедура ВопросыДляСобеседованияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыборВСпискеВопросов(СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ОбработатьВыборВСпискеВопросов();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуВопросов()
	
	БиблиотекаXML = Справочники.ВопросыДляСобеседования.ПолучитьМакет("Библиотека").ПолучитьТекст();
	БиблиотекаТаблицаБезОтбора = ОбщегоНазначения.ПрочитатьXMLВТаблицу(БиблиотекаXML).Данные;
	
	Если ЗначениеЗаполнено(Характеристика) Тогда
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ХарактеристикаПерсонала", Характеристика);
		БиблиотекаТаблица = БиблиотекаТаблицаБезОтбора.Скопировать(ПараметрыОтбора);
	Иначе
		БиблиотекаТаблица = БиблиотекаТаблицаБезОтбора;
	КонецЕсли;
	
	Для Каждого ТекущаяСтрока Из БиблиотекаТаблица Цикл
		НоваяСтрока = КлючиВопросов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
	КонецЦикла;
	
	ТаблицаЭлементарныхВопросов = БиблиотекаТаблица.Скопировать();
	ТаблицаЭлементарныхВопросов.Свернуть("Наименование, ЭлементарныйВопрос, Предопределенный, ТребуетсяКомментарий, ПояснениеКомментария");
	Для Каждого ТекущаяСтрока Из ТаблицаЭлементарныхВопросов Цикл
		НоваяСтрока = КлючевыеВопросы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
	КонецЦикла;
	
	БиблиотекаТаблица.Свернуть("Наименование, ТипВопроса, Формулировка, Подсказка, СпособОтображенияПодсказки");
	Для Каждого ТекущаяСтрока Из БиблиотекаТаблица Цикл
		Если ЗначениеЗаполнено(ОтборТипВопроса) И Перечисления.ТипыВопросовПоМетодуСИвановой[ТекущаяСтрока.ТипВопроса] <> ОтборТипВопроса Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ВопросыДляСобеседования.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		НоваяСтрока.ТипВопроса = Перечисления.ТипыВопросовПоМетодуСИвановой[ТекущаяСтрока.ТипВопроса];
		НоваяСтрока.СпособОтображенияПодсказки = Перечисления.СпособыОтображенияПодсказок[ТекущаяСтрока.СпособОтображенияПодсказки];
	КонецЦикла;
	
	БиблиотекаОтветовXML = Справочники.ВопросыДляСобеседования.ПолучитьМакет("БиблиотекаОтветов").ПолучитьТекст();
	БиблиотекаОтветовТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(БиблиотекаОтветовXML).Данные;
	
	Для Каждого ТекущаяСтрока Из БиблиотекаОтветовТаблица Цикл
		НоваяСтрока = ОтветыНаВопросы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьВыбранныеСтроки(Знач ВыбранныеСтроки, ТекущаяСсылка)
	
	БиблиотекаХарактеристикXML = ПланыВидовХарактеристик.ХарактеристикиПерсонала.ПолучитьМакет("Библиотека").ПолучитьТекст();
	БиблиотекаХарактеристикТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(БиблиотекаХарактеристикXML).Данные;
	
	Для каждого НомерСтроки Из ВыбранныеСтроки Цикл
		ТекущиеДанные = ВопросыДляСобеседования[НомерСтроки];
		
		СтрокаВБазе = Справочники.ВопросыДляСобеседования.НайтиПоНаименованию(ТекущиеДанные.Наименование, Истина);
		Если ЗначениеЗаполнено(СтрокаВБазе) Тогда
			Если НомерСтроки = Элементы.ВопросыДляСобеседования.ТекущаяСтрока Или ТекущаяСсылка = Неопределено Тогда
				ТекущаяСсылка = СтрокаВБазе;
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Справочники.ВопросыДляСобеседования.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущиеДанные);
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Наименование", ТекущиеДанные.Наименование);
		СтрокиКлючевыхВопросов = КлючевыеВопросы.НайтиСтроки(СтруктураПоиска);
		
		Для Каждого ТекущаяСтрока Из СтрокиКлючевыхВопросов Цикл
			Если Не ЗначениеЗаполнено(ТекущаяСтрока.ЭлементарныйВопрос) Тогда
				Продолжить;
			КонецЕсли;
			
			ЭлементарныйВопрос = ПланыВидовХарактеристик.ВопросыДляАнкетирования.СоздатьЭлемент();
			ЭлементарныйВопрос.Наименование = ТекущаяСтрока.ЭлементарныйВопрос;
			ЭлементарныйВопрос.Формулировка = ТекущаяСтрока.ЭлементарныйВопрос;
			ЭлементарныйВопрос.ТипОтвета = Перечисления.ТипыОтветовНаВопрос.ОдинВариантИз;
			ЭлементарныйВопрос.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.ВариантыОтветовАнкет");
			ЭлементарныйВопрос.ВидПереключателя = Перечисления.ВидыПереключателяВАнкетах.Тумблер;
			ЭлементарныйВопрос.ТребуетсяКомментарий = ТекущаяСтрока.ТребуетсяКомментарий;
			ЭлементарныйВопрос.ПояснениеКомментария = ТекущаяСтрока.ПояснениеКомментария;
			ЭлементарныйВопрос.Записать();
			
			НоваяСтрокаВопросов = НоваяСтрока.КлючевыеВопросы.Добавить();
			НоваяСтрокаВопросов.ЭлементарныйВопрос = ЭлементарныйВопрос.Ссылка;
			НоваяСтрокаВопросов.Предопределенный = ТекущаяСтрока.Предопределенный;
			
			МассивОтветов = Новый Массив;
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("Вопрос", ТекущиеДанные.Наименование);
			СтруктураПоиска.Вставить("ЭлементарныйВопрос", ТекущаяСтрока.ЭлементарныйВопрос);
			СтрокиОтветов = ОтветыНаВопросы.НайтиСтроки(СтруктураПоиска);
			Для Каждого ТекущийОтвет Из СтрокиОтветов Цикл
				Ответ = Справочники.ВариантыОтветовАнкет.СоздатьЭлемент();
				Ответ.Наименование = ТекущийОтвет.Ответ;
				Ответ.Владелец = ЭлементарныйВопрос.Ссылка;
				Ответ.Записать();
				МассивОтветов.Добавить(Ответ.Ссылка);
			КонецЦикла;
			
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("Наименование", ТекущиеДанные.Наименование);
			СтруктураПоиска.Вставить("ЭлементарныйВопрос", ТекущаяСтрока.ЭлементарныйВопрос);
			СтрокиКлючей = КлючиВопросов.НайтиСтроки(СтруктураПоиска);
			Для Каждого СтрокаКлючей Из СтрокиКлючей Цикл
				Если Не ЗначениеЗаполнено(СтрокаКлючей.ХарактеристикаПерсонала) Тогда
					Продолжить;
				КонецЕсли;
				СтруктураХарактеристики = ХарактеристикиПерсонала.ХарактеристикаИзМакета(СтрокаКлючей.ХарактеристикаПерсонала, СтрокаКлючей.ЗначениеХарактеристики, БиблиотекаХарактеристикТаблица);
				Если СтруктураХарактеристики = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				НоваяСтрокаТЧ = НоваяСтрока.Ключи.Добавить();
				НоваяСтрокаТЧ.ЭлементарныйВопрос = ЭлементарныйВопрос.Ссылка;
				НоваяСтрокаТЧ.ОтветНаВопрос = ОтветНаВопрос(СтрокаКлючей.ОтветНаВопрос, МассивОтветов);
				НоваяСтрокаТЧ.ХарактеристикаПерсонала = СтруктураХарактеристики.Характеристика;
				НоваяСтрокаТЧ.ЗначениеХарактеристики = СтруктураХарактеристики.Значение;
				НоваяСтрокаТЧ.Балл = СтрокаКлючей.Балл;
			КонецЦикла;
		КонецЦикла;
		НоваяСтрока.Записать();
		
		Если НомерСтроки = Элементы.ВопросыДляСобеседования.ТекущаяСтрока Или ТекущаяСсылка = Неопределено Тогда
			ТекущаяСсылка = НоваяСтрока.Ссылка;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборВСпискеВопросов(СтандартнаяОбработка = Неопределено)
	
	СтандартнаяОбработка = Ложь;
	
	НовыхХарактеристик = 0;
	ПроверитьНеобходимостьСозданияНовых(Элементы.ВопросыДляСобеседования.ВыделенныеСтроки, НовыхХарактеристик); 
	Если НовыхХарактеристик > 0 Тогда
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'При создании вопросов для собеседования из библиотеки также будут созданы новые характеристики персонала.
			|Количество новых элементов: %1
			|Продолжить?'"), Строка(НовыхХарактеристик));
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьВыборВСпискеВопросовЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Возврат;
	КонецЕсли;

	ТекущаяСсылка = Неопределено;
	СохранитьВыбранныеСтроки(Элементы.ВопросыДляСобеседования.ВыделенныеСтроки, ТекущаяСсылка);
	ПоказатьОповещениеПользователя(НСтр("ru = 'Добавлены вопросы для собеседования'"));
	ОповеститьОВыборе(ТекущаяСсылка);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборВСпискеВопросовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСсылка = Неопределено;
	СохранитьВыбранныеСтроки(Элементы.ВопросыДляСобеседования.ВыделенныеСтроки, ТекущаяСсылка);
	ПоказатьОповещениеПользователя(НСтр("ru = 'Добавлены вопросы для собеседования'"));
	ОповеститьОВыборе(ТекущаяСсылка);
	Закрыть();
	
КонецПроцедуры

&НаСервере
Функция ОтветНаВопрос(ОтветСтрокой, МассивОтветов)
	
	Для Каждого Ответ Из МассивОтветов Цикл
		Если Ответ.Наименование = ОтветСтрокой Тогда
			Возврат Ответ;
		КонецЕсли;
	КонецЦикла;
	Возврат Справочники.ВариантыОтветовАнкет.ПустаяСсылка();
	
КонецФункции

&НаСервере
Процедура ПроверитьНеобходимостьСозданияНовых(Знач ВыбранныеСтроки, НовыхХарактеристик)
	
	МассивХарактеристик = Новый Массив;
	Для каждого НомерСтроки Из ВыбранныеСтроки Цикл
		ТекущиеДанные = ВопросыДляСобеседования[НомерСтроки];
		
		СтрокаВБазе = Справочники.ВопросыДляСобеседования.НайтиПоНаименованию(ТекущиеДанные.Наименование, Истина);
		Если ЗначениеЗаполнено(СтрокаВБазе) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Наименование", ТекущиеДанные.Наименование);
		СтрокиКлючевыхВопросов = КлючевыеВопросы.НайтиСтроки(СтруктураПоиска);
		
		Для Каждого ТекущаяСтрока Из СтрокиКлючевыхВопросов Цикл
			Если Не ЗначениеЗаполнено(ТекущаяСтрока.ЭлементарныйВопрос) Тогда
				Продолжить;
			КонецЕсли;
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("Наименование", ТекущиеДанные.Наименование);
			СтруктураПоиска.Вставить("ЭлементарныйВопрос", ТекущаяСтрока.ЭлементарныйВопрос);
			СтрокиКлючей = КлючиВопросов.НайтиСтроки(СтруктураПоиска);
			Для Каждого СтрокаКлючей Из СтрокиКлючей Цикл
				Если (Не ЗначениеЗаполнено(СтрокаКлючей.ХарактеристикаПерсонала)) Или (МассивХарактеристик.Найти(СтрокаКлючей.ХарактеристикаПерсонала) <> Неопределено) Тогда
					Продолжить;
				КонецЕсли;
				ЭлектронноеИнтервью.ПроверитьНовуюХарактеристику(СтрокаКлючей.ХарактеристикаПерсонала, МассивХарактеристик);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	НовыхХарактеристик = МассивХарактеристик.Количество();
	
КонецПроцедуры

#КонецОбласти