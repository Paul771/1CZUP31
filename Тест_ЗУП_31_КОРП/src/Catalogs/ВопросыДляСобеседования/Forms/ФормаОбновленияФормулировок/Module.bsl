
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("ВопросДляСобеседования") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписок(Параметры.ВопросДляСобеседования);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	МассивВыбранных = Новый Массив;
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Выбор", Истина);
	ВыбранныеСтроки = Список.НайтиСтроки(СтруктураПоиска);
	Для Каждого ТекущаяСтрока Из ВыбранныеСтроки Цикл
		МассивВыбранных.Добавить(ТекущаяСтрока.Ссылка);
	КонецЦикла;
	Закрыть(МассивВыбранных);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	Для Каждого ТекущаяСтрока Из Список Цикл
		ТекущаяСтрока.Выбор = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	Для Каждого ТекущаяСтрока Из Список Цикл
		ТекущаяСтрока.Выбор = Ложь;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьСписок(ВопросДляСобеседования)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВопросДляСобеседования", ВопросДляСобеседования);
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВакансииЭтапыРаботыСКандидатами.Ссылка КАК Ссылка,
		|	ТИП(Справочник.Вакансии) КАК Тип
		|ИЗ
		|	РегистрСведений.ВопросыДляСобеседованияВопросовШаблоновАнкет КАК ВопросыДляСобеседованияВопросовШаблоновАнкет
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Вакансии.ЭтапыРаботыСКандидатами КАК ВакансииЭтапыРаботыСКандидатами
		|		ПО ВопросыДляСобеседованияВопросовШаблоновАнкет.ВопросШаблонаАнкеты.Владелец = ВакансииЭтапыРаботыСКандидатами.ШаблонАнкеты
		|ГДЕ
		|	ВопросыДляСобеседованияВопросовШаблоновАнкет.ВопросДляСобеседования = &ВопросДляСобеседования
		|	И НЕ ВопросыДляСобеседованияВопросовШаблоновАнкет.ВопросШаблонаАнкеты.ПометкаУдаления
		|	И НЕ ВакансииЭтапыРаботыСКандидатами.Ссылка.СоответствуетПрофилюДолжности
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПрофилиДолжностейЭтапыРаботыСКандидатами.Ссылка,
		|	ТИП(Справочник.ПрофилиДолжностей)
		|ИЗ
		|	РегистрСведений.ВопросыДляСобеседованияВопросовШаблоновАнкет КАК ВопросыДляСобеседованияВопросовШаблоновАнкет
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПрофилиДолжностей.ЭтапыРаботыСКандидатами КАК ПрофилиДолжностейЭтапыРаботыСКандидатами
		|		ПО ВопросыДляСобеседованияВопросовШаблоновАнкет.ВопросШаблонаАнкеты.Владелец = ПрофилиДолжностейЭтапыРаботыСКандидатами.ШаблонАнкеты
		|ГДЕ
		|	ВопросыДляСобеседованияВопросовШаблоновАнкет.ВопросДляСобеседования = &ВопросДляСобеседования
		|	И НЕ ВопросыДляСобеседованияВопросовШаблоновАнкет.ВопросШаблонаАнкеты.ПометкаУдаления";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Не ЗначениеЗаполнено(Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Ссылка = Выборка.Ссылка;
		НоваяСтрока.Тип = Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Выборка.Тип));
	КонецЦикла;
	Список.Сортировать("Тип, Ссылка");
	
КонецПроцедуры

#КонецОбласти
