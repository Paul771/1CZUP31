
#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Вакансия = Параметры.Вакансия;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Описания.Отбор, "Вакансия", Вакансия);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписьВариантаПечати" Тогда
		Элементы.Описания.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОписанияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Описания.ТекущиеДанные;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Вакансия", Вакансия);
	ПараметрыОткрытия.Вставить("МестоПубликации", ТекущиеДанные.МестоПубликации);
	ПараметрыОткрытия.Вставить("ИдентификаторВакансии", ТекущиеДанные.ИдентификаторВакансии);
	
	ОткрытьФорму("Справочник.Вакансии.Форма.НастройкаПечатиОписания", ПараметрыОткрытия, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура Добавить(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Вакансия", Вакансия);
	
	ОткрытьФорму("Справочник.Вакансии.Форма.НастройкаПечатиОписания", ПараметрыОткрытия, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура Удалить(Команда)
	
	МестаПубликации = Новый Массив;
	ВыделенныеСтроки = Элементы.Описания.ВыделенныеСтроки;
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		ТекущиеДанные = Элементы.Описания.ДанныеСтроки(ВыделеннаяСтрока);
		МестаПубликации.Добавить(ТекущиеДанные.МестоПубликации);
	КонецЦикла;
	
	Если МестаПубликации.Количество() > 0 Тогда
		УдалитьОписанияВакансииНаСервере(Вакансия, МестаПубликации);
		Оповестить("ЗаписьВариантаПечати", , ВладелецФормы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УдалитьОписанияВакансииНаСервере(Вакансия, МестаПубликации)
	
	ПодборПерсонала.УдалитьОписанияВакансии(Вакансия, МестаПубликации);
	
	Элементы.Описания.Обновить();
	
КонецПроцедуры

#КонецОбласти