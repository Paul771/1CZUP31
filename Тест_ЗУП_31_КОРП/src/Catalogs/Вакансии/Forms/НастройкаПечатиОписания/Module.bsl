
#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьРеквизитыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("СохранитьИЗакрыть", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЭлементСТекстомНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("ЭлементСТекстомЗавершениеВыбора", ЭтотОбъект, Новый Структура("ИмяРеквизита", Элемент.Имя));
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияМногострочногоТекста(Оповещение, Элемент.ТекстРедактирования, 
		Элемент.Заголовок);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	СохранитьИЗакрыть(Неопределено, Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	Если Модифицированность 
		И СохранитьДанныеНаСервере() Тогда
		
		Модифицированность = Ложь;
		Оповестить("ЗаписьВариантаПечати", , Вакансия);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	Если Модифицированность Тогда
		
		ТекстВопроса = НСтр("ru = 'Для печати вариант должен быть записан.
			|Продолжить ?'");
		
		Оповещение = Новый ОписаниеОповещения("ПечатьЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		ПечатьЗавершение(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьРеквизитыФормы()
	
	Вакансия = Параметры.Вакансия;
	
	Если Параметры.Свойство("МестоПубликации", НаименованиеВарианта) Тогда
		
		НаименованиеВариантаПрежнее = НаименованиеВарианта;
		ИдентификаторВакансии = Параметры.ИдентификаторВакансии;
		
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Вакансия", Вакансия);
		
		Запрос.Текст = "ВЫБРАТЬ
		|	ДанныеПубликацииВакансий.МестоПубликации КАК МестоПубликации,
		|	ДанныеПубликацииВакансий.ПолеДанных КАК ПолеДанных,
		|	ДанныеПубликацииВакансий.Значение КАК Значение
		|ИЗ
		|	РегистрСведений.ДанныеПубликацииВакансий КАК ДанныеПубликацииВакансий
		|ГДЕ
		|	ДанныеПубликацииВакансий.Вакансия = &Вакансия
		|	И ДанныеПубликацииВакансий.МестоПубликации = &МестоПубликации";
		
		Запрос.УстановитьПараметр("МестоПубликации", НаименованиеВарианта);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			ЭтотОбъект[Выборка.ПолеДанных] = Выборка.Значение;
		КонецЦикла;
		
	Иначе
		
		Модифицированность = Истина;
		
		Описания = ПодборПерсонала.ОписанияВакансийПоУмолчанию(Вакансия);
		Если Описания.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, Описания[0]);
		КонецЕсли;
		
		ЭтотОбъект.ПредполагаемыйДоход = СтрШаблон(Нстр("ru = '%1 руб.'"), ЭтотОбъект.ПредполагаемыйДоход);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭлементСТекстомЗавершениеВыбора(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = НеОпределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРеквизита = ДополнительныеПараметры.ИмяРеквизита;
	
	Если ЭтотОбъект[ИмяРеквизита] <> РезультатВыбора Тогда
		
		ЭтотОбъект[ИмяРеквизита] = РезультатВыбора;
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть(Результат, ДополнительныеПараметры) Экспорт 
	
	Если СохранитьДанныеНаСервере() Тогда
		
		Модифицированность = Ложь;
		Оповестить("ЗаписьВариантаПечати", , Вакансия);
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СохранитьДанныеНаСервере()
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если (ЗначениеЗаполнено(НаименованиеВариантаПрежнее) 
		И НаименованиеВариантаПрежнее <> НаименованиеВарианта) Тогда
		
		ПодборПерсонала.УдалитьОписанияВакансии(Вакансия, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НаименованиеВариантаПрежнее));
		
	КонецЕсли;
	
	РеквизитыФормы = ПолучитьРеквизиты();
	РеквизитыВарианта = Новый Соответствие;
	
	Для Каждого РеквизитФормы Из РеквизитыФормы Цикл
		
		Если РеквизитФормы.Имя = "НаименованиеВарианта"
			Или РеквизитФормы.Имя = "НаименованиеВариантаПрежнее"
			Или РеквизитФормы.Имя = "ИдентификаторВакансии"
			Или РеквизитФормы.Имя = "Вакансия" Тогда
			Продолжить;
		КонецЕсли;
		
		РеквизитыВарианта.Вставить(РеквизитФормы.Имя, ЭтотОбъект[РеквизитФормы.Имя]);
		
	КонецЦикла;
	
	ДанныеОписания = Новый Структура;
	ДанныеОписания.Вставить("Наименование", НаименованиеВарианта);
	ДанныеОписания.Вставить("Реквизиты", РеквизитыВарианта);
	
	ПодборПерсонала.ЗаписатьОписаниеВакансии(Вакансия, ИдентификаторВакансии, ДанныеОписания);
	
	НаименованиеВариантаПрежнее = НаименованиеВарианта;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ЗначениеЗаполнено(НаименованиеВарианта) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Укажите наименование сохраняемого варианта.'"), , , , Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЗавершение(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Записать(Неопределено);
	КонецЕсли;
	
	Если Не Модифицированность Тогда
		
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Справочник.Вакансии", ИдентификаторВакансии,
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Вакансия), ЭтотОбъект);
			
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

