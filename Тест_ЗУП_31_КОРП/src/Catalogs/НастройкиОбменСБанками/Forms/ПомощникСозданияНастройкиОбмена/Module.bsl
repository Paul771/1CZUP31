
#Область ОписаниеПеременных

&НаКлиенте
Перем ХронологияПереключенияСтраниц;

&НаКлиенте
Перем ПараметрыОбработчика;

&НаКлиенте
Перем ПодключаемыйМодуль;

&НаКлиенте
Перем СертификатXML; //данные сертификата дополнительной обработки в виде строки

&НаКлиенте
Перем МенеджерКриптографииНаКлиенте;

&НаКлиенте
Перем ОтпечаткиСертификатов;

&НаКлиенте
Перем ИдентификаторЗадания; // идентификатор фоновых операций

&НаКлиенте
Перем Отпечаток; // отпечаток текущего сертификата

&НаКлиенте
Перем ИдентификаторХранилища; // Идентификатор банковского ключа

&НаКлиенте
Перем URLВК; // ссылка на скачивание внешней компоненты

&НаКлиенте
Перем ВерсияВК; // Ожидаемая версия внешней компоненты

&НаКлиенте
Перем КомуВыданСертификатВК;

&НаКлиенте
Перем СертификатВК; //двоичные данные сертификата внешней компоненты

&НаКлиенте
Перем ПараметрыОбработчикаОжидания; // содержит параметры подключаемых процедур

&НаКлиенте
Перем ТикетОбменаСбербанк; //тикет, который используется при получении настроек обмена из Сбербанка

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	РазделениеВключено = ОбщегоНазначения.РазделениеВключено();
	
	Если Не РазделениеВключено Тогда
		НаименованиеЗадания = НСтр("ru = '1С:ДиректБанк: Обновление списка банков, поддерживающих обмен.'");
		ФоновыеЗадания.Выполнить("ОбменСБанкамиСлужебный.ЗагрузитьСписокБанковССайта", , , НаименованиеЗадания);
	КонецЕсли;
	
	ЕстьПравоДобавленияНастройкиЭДО = ПравоДоступа("Добавление", Метаданные.Справочники.НастройкиОбменСБанками);
	
	Если Не ЕстьПравоДобавленияНастройкиЭДО Тогда
		ТекстСообщения = НСтр("ru = 'Нет прав настройки прямого обмена с банком.
									|Обратитесь к администратору'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		Возврат;
	КонецЕсли;
	
	Элементы.Шаги.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Элементы.КомандыНавигации.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Элементы.СтраницыВнешнегоМодуля.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	
	ПрограммаБанка = Перечисления.ПрограммыБанка.АсинхронныйОбмен;
	
	ИспользуетсяНесколькоОрганизацийЭД = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизацийЭД");
	
	Если Не ИспользуетсяНесколькоОрганизацийЭД Тогда
		Организация = ЭлектронноеВзаимодействиеСлужебный.ОрганизацияПоУмолчанию();
		Если ЗначениеЗаполнено(Организация) Тогда
			Элементы.Организация.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		Организация = Параметры.Организация;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Банк) Тогда
		Банк = Параметры.Банк;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ИдентификаторОрганизации) Тогда
		ИдентификаторОрганизации = Параметры.ИдентификаторОрганизации;
	КонецЕсли;
	
	Если Параметры.Свойство("ЗначенияЗаполнения") И Параметры.ЗначенияЗаполнения.Свойство("Организация")
		И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.Организация) Тогда
		Организация = Параметры.ЗначенияЗаполнения.Организация;
	КонецЕсли;
	
	Если  Параметры.Свойство("ЗначенияЗаполнения") И Параметры.ЗначенияЗаполнения.Свойство("Банк")
		И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.Банк) Тогда
		Банк = Параметры.ЗначенияЗаполнения.Банк;
	КонецЕсли;
	
	Параметры.Свойство("УпрощенныйРежимНастройки", УпрощенныйРежимНастройки);
	
	Если НЕ ЗначениеЗаполнено(Банк) И УпрощенныйРежимНастройки Тогда
		БанкиДляОбмена = ОбменСБанкамиСлужебный.БанкиДляНастройкиОбменаЧерезДиректБанк();
		Если ЗначениеЗаполнено(БанкиДляОбмена) И БанкиДляОбмена.Количество() > 0 Тогда
			УпрощенныйРежимНастройки = Истина;
			
			Элементы.Организация.РежимВыбораИзСписка = Истина;
			Элементы.Банк.РежимВыбораИзСписка = Истина;
			
			ОрганизацияСписокВыбора = Элементы.Организация.СписокВыбора;
			БанкСписокВыбора = Элементы.Банк.СписокВыбора;
			
			Для Каждого СтрокаТаблицы Из БанкиДляОбмена Цикл
				
				Если ОрганизацияСписокВыбора.НайтиПоЗначению(СтрокаТаблицы.Организация) = Неопределено Тогда
					ОрганизацияСписокВыбора.Добавить(СтрокаТаблицы.Организация);
				КонецЕсли;
				
				Если БанкСписокВыбора.НайтиПоЗначению(СтрокаТаблицы.Банк) = Неопределено Тогда
					БанкСписокВыбора.Добавить(СтрокаТаблицы.Банк);
				КонецЕсли;
				
			КонецЦикла;
			
			Если ОрганизацияСписокВыбора.Количество() = 1 Тогда
				Организация = ОрганизацияСписокВыбора[0].Значение;
			КонецЕсли;
			
			Если БанкСписокВыбора.Количество() = 1 Тогда
				Банк = БанкСписокВыбора[0].Значение;
			КонецЕсли;
		
		КонецЕсли;	
	КонецЕсли;
	
	НеПоказыватьСтраницуВыборОрганизацииИБанка = ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(Банк);
	
	АдресСервера = "https://";
	АдресСервераНастроек = "https://";
	
	Если Параметры.НеИспользоватьРучноеЗаполнение Тогда
		Элементы.РучноеЗаполнение.Видимость = Ложь;
		НеИспользоватьРучноеЗаполнение = Параметры.НеИспользоватьРучноеЗаполнение;
	КонецЕсли;
	
	РасширенныйРежим = Истина;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбменСБанкамиПереопределяемый.ПроверитьИспользованиеТестовогоРежима(ТестовыйРежим);
	
	Если Не ТестовыйРежим Тогда
		Элементы.ПолучениеНастроекССервераБанка.Видимость = Ложь;
		СпособСозданияНастройки = 1;
	КонецЕсли;
	
	СохранитьЛогин = Истина;
	
	Если РазделениеВключено Тогда
		Элементы.СтраницыВыбораВнешнегоМодуля.ТекущаяСтраница = Элементы.СтраницаСуществующийМодуль;
		Элементы.ГруппаПредупреждение.Видимость = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(Банк) Тогда
		НастройкаОбмена = ОбменСБанкамиСлужебныйВызовСервера.НастройкаОбмена(Организация, Банк, Ложь, Ложь);
		Если ЗначениеЗаполнено(НастройкаОбмена) Тогда
			ВерсияФормата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаОбмена, "ВерсияФормата");
		КонецЕсли;
	ИначеЕсли РазделениеВключено Тогда
		СписокВКВИБ = ДополнительныеВнешниеКомпонентыВызовСервера.СписокВнешнихКомпонент();
		Для Каждого Запись Из СписокВКВИБ Цикл
			Если Запись.Значение <> "VPNKeyTLS" Тогда // нельзя выбрать внешнюю компоненту Сбербанка
				НовЗапись = Элементы.ИмяВнешнегоМодуля.СписокВыбора.Добавить();
				ЗаполнитьЗначенияСвойств(НовЗапись, Запись);
			КонецЕсли;
		КонецЦикла;
		Если Элементы.ИмяВнешнегоМодуля.СписокВыбора.Количество() = 1 Тогда
			ТекЗапись = Элементы.ИмяВнешнегоМодуля.СписокВыбора[0];
			ИмяВнешнегоМодуля = ТекЗапись.Значение;
			Элементы.ИмяВнешнегоМодуля.ТолькоПросмотр = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ИнформацияНаСайтеБанка.Видимость = Ложь;
	
	Если ЗначениеЗаполнено(Банк) Тогда
		НастройкиОбменаСБанком = ОбменСБанкамиСлужебныйВызовСервера.ПоставляемыеНастройкиОбмена(Банк, Истина);
		Если ЗначениеЗаполнено(НастройкиОбменаСБанком) Тогда
			ПрограммаБанка = НастройкиОбменаСБанком.ПрограммаБанка;
			
			Если ПрограммаБанка = Перечисления.ПрограммыБанка.АсинхронныйОбмен
				ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн
				ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезВК Тогда
				УпрощенныйРежимНастройки = Истина;
			КонецЕсли;
			
			Если ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезВК Тогда
				Элементы.ПолучениеНастроекССервераБанка.Видимость = Ложь;
				СпособСозданияНастройки = 0; // Автоматическое получение настроек
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	НеПоказыватьСтраницуИнформации = Параметры.НеПоказыватьСтраницуИнформации;
	
	Элементы.ПолучитьНастройки.Видимость = Ложь;
	
	// Загрузка настроек из файла в форме списка настроек обмена
	Если ЗначениеЗаполнено(Параметры.АдресДанныхФайлаНастроек) Тогда
		СпособСозданияНастройки = 1;
		АдресФайлаНастроекОбмена = Параметры.АдресДанныхФайлаНастроек;
		НастройкиЗагруженыИзФайла = Истина;
	КонецЕсли;
	
	НомерБанковскогоСчета = Параметры.НомерБанковскогоСчета;
	
	Если УпрощенныйРежимНастройки Тогда
		КлючСохраненияПоложенияОкна = "УпрощенныйРежимНастройки";
		НеПоказыватьСтраницуИнформации = Истина;
		НастройкаФормыУпрощенногоРежима();
	Иначе
		КлючСохраненияПоложенияОкна = "ОбычныйРежимНастройки";
		Элементы.СтраницаУпрощенныйРежимНастройки.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ОткрытьНастройкуОбмена.Видимость = Ложь;
	
	ЛокальныйФайл = Параметры.ЛокальныйФайл;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ХронологияПереключенияСтраниц = Новый Массив;
	
	#Если ВебКлиент Тогда
		Элементы.ПутьКФайлуВнешнегоМодуля.РедактированиеТекста = Ложь;
		Элементы.ПутьКФайлуНастроек.РедактированиеТекста = Ложь;
	#КонецЕсли
	
	ИзменитьЗаголовок();
	
	СпособАутентификацииПриИзменении(Неопределено);
	
	Если НеПоказыватьСтраницуИнформации Тогда
		Если НеПоказыватьСтраницуВыборОрганизацииИБанка Тогда
			ПереключитьШагПослеВыбораОрганизацииИБанка();
		Иначе
			Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаВыборОрганизацииИБанка;
		КонецЕсли;
	КонецЕсли;
	
	Если УпрощенныйРежимНастройки Тогда
		Возврат;
	КонецЕсли;
	
	// Загрузка из файла формы списка настроек обмена.
	Если СпособСозданияНастройки = 1 И ЗначениеЗаполнено(АдресФайлаНастроекОбмена) Тогда
		
		УчастникиОбменаИзФайла = УчастникиОбменаИзФайла(АдресФайлаНастроекОбмена, Организация, ЛокальныйФайл);
		
		Если УчастникиОбменаИзФайла = Неопределено Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Банк = УчастникиОбменаИзФайла.Банк;
		Организация = УчастникиОбменаИзФайла.Организация;
		
		ИзменитьЗаголовок();
		
		Если УчастникиОбменаИзФайла.ЭтоНастройкиСбербанка Тогда
			ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн");
			СменитьСтраницуШаги(Элементы.СтраницаФинал);
			URLВК = УчастникиОбменаИзФайла.URLВК;
			ПодключитьОбработчикОжидания("ОпределитьURLИнфоФайлаСбербанк", 0.1, Истина);
		Иначе
			Элементы.СозданиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
			СменитьСтраницуШаги(Элементы.СтраницаФинал);
			#Если ВебКлиент Тогда
				ОбновитьОтображениеДанных();
			#КонецЕсли
			ПодключитьОбработчикОжидания("СоздатьНастройкуОбмена", 0.1, Истина);
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если НЕ РаботаПомощникаЗавершена Тогда
	
		СтандартнаяОбработка = Ложь;
		Если ЗавершениеРаботы Тогда
			ТекстПредупреждения = НСтр("ru = 'Подключение к сервису 1С:ДиректБанк не завершено.
											|Продолжить закрытие программы?'");
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Подключение к сервису 1С:ДиректБанк не завершено.
											|Закрыть помощник подключения?'");
		КонецЕсли;
	
		Оповещение = Новый ОписаниеОповещения("ПриЗакрытииПомощника", ЭтотОбъект);
		
		ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияПроизвольнойФормы(
			ЭтотОбъект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, "ЗакрытьФорму", Оповещение);

	КонецЕсли;
	
	Если НЕ ЗавершениеРаботы И ЗначениеЗаполнено(СертификатАутентификацииСсылка) Тогда
		ЭлектроннаяПодписьКлиент.УстановитьПарольСертификата(СертификатАутентификацииСсылка, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы Тогда
		ПрерватьДлительнуюОперацию(ИдентификаторЗадания);
		ОбновитьИнтерфейс(); // Возможно была включена функциональная опция
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияСинхронизацииНажатие(Элемент)
	
	ОбщегоНазначенияКлиент.ОткрытьНавигационнуюСсылку("http://www.v8.1c.ru/edi/edi_app/bank/?utm_source=led&utm_campaign=2017&utm_medium=wiz");
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатСмешанныйНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПодключитьОбработчикОжидания("ЗаполнитьСписокВыбораСертификатовАутентификацииСВыводомОшибки", 0.1, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ПутьКФайлуВнешнегоМодуляНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
СтандартнаяОбработка = Ложь;
	
	ВыбранФайлВнешнегоМодуля = Ложь;
	#Если ВебКлиент Тогда
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ПослеВыбораФайлаВнешнегоМодуляИзВеб", ЭтотОбъект);
		НачатьПомещениеФайла(ОписаниеОповещенияОЗавершении, АдресФайлаВнешнегоМодуля, , , УникальныйИдентификатор);
	#Иначе
		Режим = РежимДиалогаВыбораФайла.Открытие;
		ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
		Фильтр = НСтр("ru = 'Внешний модуль(*.zip;*.epf)|*.zip;*.epf'");
		ДиалогОткрытияФайла.Фильтр = Фильтр;
		ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
		ДиалогОткрытияФайла.Заголовок = НСтр("ru = 'Выберите файл внешнего модуля банка'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораФайлаВнешнегоМодуляИзТонкогоКлиента", ЭтотОбъект);
		ДиалогОткрытияФайла.Показать(ОписаниеОповещения);
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользуетсяКриптографияПриИзменении(Элемент)
	
	Элементы.ГруппаСертификаты.Доступность = ИспользуетсяКриптография;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользуетсяВнешнийМодульПриИзменении(Элемент)
	
	Если ИспользуетсяВнешнийМодуль Тогда
		Элементы.СтраницыВнешнегоМодуля.ТекущаяСтраница = Элементы.ГруппаВнешнийМодуль;
	Иначе
		Элементы.СтраницыВнешнегоМодуля.ТекущаяСтраница = Элементы.ПустаяСтраница;
	КонецЕсли;
	
	Если ИспользуетсяВнешнийМодуль Тогда
		СпособАутентификации = 1;
		ИспользуетсяКриптография = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнструкцияНажатие(Элемент)
	
	ЭлектроннаяПодписьКлиент.ОткрытьИнструкциюПоРаботеСПрограммами();
	
КонецПроцедуры

&НаКлиенте
Процедура БанкПриИзменении(Элемент)
	
	ПриИзмененииБанка();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ПриИзмененииОрганизации();
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКФайлуНастроекНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбранФайлНастроекОбмена = Ложь;
	#Если ВебКлиент Тогда
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ПослеВыбораФайлаНастроекОбменаИзВеб", ЭтотОбъект);
		НачатьПомещениеФайла(ОписаниеОповещенияОЗавершении, АдресФайлаНастроекОбмена, "*.xml", , УникальныйИдентификатор);
	#Иначе
		Режим = РежимДиалогаВыбораФайла.Открытие;
		ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
		Фильтр = НСтр("ru = 'Файл настроек обмена'") + "(*.xml)|*.xml";
		ДиалогОткрытияФайла.Фильтр = Фильтр;
		ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
		ДиалогОткрытияФайла.Заголовок = НСтр("ru = 'Выберите файл настроек обмена'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораФайлаНастроекОбменаИзТонкогоКлиента", ЭтотОбъект);
		ДиалогОткрытияФайла.Показать(ОписаниеОповещения);
	#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкуОбменаНажатие(Элемент)
	
	ПоказатьЗначение( , НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособАутентификацииПриИзменении(Элемент)
	
	Если СпособАутентификации Тогда
		Элементы.СтраницаПользовательСмешанный.Видимость = Истина;
		Элементы.СтраницаСертификатСмешанный.Видимость = Ложь;
	Иначе
		Элементы.СтраницаПользовательСмешанный.Видимость = Ложь;
		Элементы.СтраницаСертификатСмешанный.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлНастроекНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПутьКФайлуНастроекНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСертификаты

&НаКлиенте
Процедура СертификатыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ЗаполнитьОрганизациюВСертификате(ВыбранноеЗначение, Организация);
	
	НовСтрока = Сертификаты.Добавить();
	НовСтрока.Сертификат = ВыбранноеЗначение;
	
	ПеренумероватьСертификаты();
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатыПриИзменении(Элемент)
	
	ПеренумероватьСертификаты();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьСертификат(Команда)
	
	ОчиститьСообщения();
	
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен")
		ИЛИ ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АльфаБанкОнлайн") Тогда
		
		ЭлектроннаяПодписьКлиент.СертификатНачалоВыбораСПодтверждением(Элементы.Сертификаты, Неопределено, Ложь, Ложь);
	
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
		
		Устройства = ОбменСБанкамиСлужебныйКлиент.ПодключенныеХранилищаЧерезДополнительнуюОбработку(ПодключаемыйМодуль);
		Если Устройства = Неопределено ИЛИ Устройства.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;

		СписокВыбора = Новый СписокЗначений;
		Для Каждого Элемент Из Устройства Цикл
			ТекЗапись = СписокВыбора.Добавить(Элемент);
		КонецЦикла;
		
		Если Устройства.Количество() > 1 Тогда
			ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПослеВыбораКлючаЧерезДополнительнуюОбработку", ЭтотОбъект);
			ЗаголовокВыбора = НСтр("ru = 'Выберите электронный ключ:'");
			СписокВыбора.ПоказатьВыборЭлемента(ОповещениеОЗакрытии, ЗаголовокВыбора);
		Иначе
			ПослеВыбораКлючаЧерезДополнительнуюОбработку(ТекЗапись)
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	
	Закрыть(НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура Далее(Команда)
	
	ОчиститьСообщения();
	Если Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаОписание Тогда
		
		ХронологияПереключенияСтраниц.Добавить(Элементы.СтраницаОписание);
		
		Если НеПоказыватьСтраницуВыборОрганизацииИБанка Тогда
			ПереключитьШагПослеВыбораОрганизацииИБанка();
		Иначе
			Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаВыборОрганизацииИБанка;
		КонецЕсли;
		
		Элементы.СтраницаОписание.Видимость = Ложь;
		
	ИначеЕсли Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаВыборОрганизацииИБанка Тогда
		
		Отказ = Ложь;
		Если Не ЗначениеЗаполнено(Организация) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Выберите организацию'"), , "Организация", , Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Банк) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Выберите банк'"), , "Банк", , Отказ);
		КонецЕсли;
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		ХронологияПереключенияСтраниц.Добавить(Элементы.СтраницаВыборОрганизацииИБанка);
		
		НастройкаОбмена = ОбменСБанкамиСлужебныйВызовСервера.НастройкаОбмена(Организация, Банк, Ложь, Ложь);
		
		ПереключитьШагПослеВыбораОрганизацииИБанка();
		
	ИначеЕсли Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаВыборСпособаСозданияНастройки Тогда
		ХронологияПереключенияСтраниц.Добавить(Элементы.СтраницаВыборСпособаСозданияНастройки);
		Если СпособСозданияНастройки = 0 Тогда // получение настроек с сервера банка
			ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен");
			Оповещение = Новый ОписаниеОповещения(
				"СменитьСтраницуПослеПолучениеССервераПослеЗаполненияПрограммКриптографии", ЭтотОбъект);
			ЗаполнитьПрограммыКриптографии(Оповещение);
		ИначеЕсли СпособСозданияНастройки = 1 Тогда // загрузка настроек из файла
			СменитьСтраницуШаги(Элементы.СтраницаВыборФайлаНастроек);
		ИначеЕсли СпособСозданияНастройки = 2 Тогда // ручной режим
			Если РазделениеВключено Тогда
				Если Элементы.ИмяВнешнегоМодуля.СписокВыбора.Количество() Тогда
					СменитьСтраницуШаги(Элементы.СтраницаЗапросИспользованияВнешнегоМодуля);
				Иначе
					СменитьСтраницуШаги(Элементы.СтраницаАсинхронныйОбмен);
				КонецЕсли;
			Иначе
				СменитьСтраницуШаги(Элементы.СтраницаЗапросИспользованияВнешнегоМодуля);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаЗапросИспользованияВнешнегоМодуля Тогда
		
		Если ИспользуетсяВнешнийМодуль Тогда
			Если Элементы.СтраницыВыбораВнешнегоМодуля.ТекущаяСтраница = Элементы.СтраницаВыбратьФайл Тогда
				#Если ВебКлиент Тогда
					Если НЕ ВыбранФайлВнешнегоМодуля Тогда
						ТекстСообщения = НСтр("ru = 'Не выбран файл внешнего модуля'");
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПутьКФайлуВнешнегоМодуля");
						Возврат;
					КонецЕсли;
				#Иначе
					Если ЗначениеЗаполнено(ПутьКФайлуВнешнегоМодуля) Тогда
						Попытка
							ДвоичныеДанныеВнешнегоМодуля = Новый ДвоичныеДанные(ПутьКФайлуВнешнегоМодуля);
						Исключение
							ТекстСообщения = НСтр("ru = 'Неверно указан путь к файлу внешнего модуля'");
							ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПутьКФайлуВнешнегоМодуля");
							Возврат;
						КонецПопытки;
						АдресФайлаВнешнегоМодуля = ПоместитьВоВременноеХранилище(ДвоичныеДанныеВнешнегоМодуля, УникальныйИдентификатор);
					Иначе
						ТекстСообщения = НСтр("ru = 'Не выбран файл внешнего модуля'");
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПутьКФайлуВнешнегоМодуля");
						Возврат;
					КонецЕсли;
				#КонецЕсли
				РасширениеФайла = ВРег(ОбменСБанкамиСлужебныйКлиент.РасширениеФайла(ПутьКФайлуВнешнегоМодуля));
				Если Не РасширениеФайла = "EPF" И НЕ РасширениеФайла = "ZIP" Тогда
					ТекстСообщения = НСтр("ru = 'Указан неверный файл.
												|Файл внешнего модуля должен иметь расширение ""zip"" или ""epf""'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПутьКФайлуВнешнегоМодуля");
					Возврат;
				КонецЕсли;
				
				Если РасширениеФайла = "ZIP" Тогда // внешняя компонента
					
					ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК");
					ИнформацияОКомпоненте = ИнформацияОВнешнейКомпоненте(АдресФайлаВнешнегоМодуля);
					
					ВерсияВК = ИнформацияОКомпоненте.Версия;
					
					Если ИнформацияОКомпоненте = Неопределено Тогда
						Возврат;
					КонецЕсли;
					
					Если ИнформацияОКомпоненте.ВерсияВБазе <> ИнформацияОКомпоненте.Версия Тогда
						СохранитьВКБанка(АдресФайлаВнешнегоМодуля);
					КонецЕсли;
					
					ИмяВнешнегоМодуля = ИнформацияОКомпоненте.ИмяМодуля;
					// Пользователь мог выбрать другую версию файла внешней компоненты.
					ДополнительныеВнешниеКомпонентыКлиент.УдалитьВнешнююКомпонентуИзКэш(ИмяВнешнегоМодуля);
					
					ХронологияПереключенияСтраниц.Добавить(Элементы.СтраницаЗапросИспользованияВнешнегоМодуля);
					СменитьСтраницуШаги(Элементы.СтраницаФинал);
					
					ПолучитьНастройкиОбменаССервераБанкаЧерезВК();
					
				Иначе //внешняя обработка
					
					Оповещение = Новый ОписаниеОповещения("ПослеИнициализацииДополнительнойОбработки", ЭтотОбъект);
					ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку");
					НачатьИнициализациюДополнительнойОбработки(Оповещение);
					
				КонецЕсли;
			Иначе // разделенный режим
				ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК");
				ДанныеВнешнейКомпоненты = ДополнительныеВнешниеКомпонентыВызовСервера.ПараметрыВнешнейКомпоненты(
					ИмяВнешнегоМодуля);
				ВерсияВК = ДанныеВнешнейКомпоненты.Версия;
				ХронологияПереключенияСтраниц.Добавить(Элементы.СтраницаЗапросИспользованияВнешнегоМодуля);
				СменитьСтраницуШаги(Элементы.СтраницаФинал);
				ПолучитьНастройкиОбменаССервераБанкаЧерезВК();
			КонецЕсли;
		Иначе
			ХронологияПереключенияСтраниц.Добавить(Элементы.СтраницаЗапросИспользованияВнешнегоМодуля);
			ПодключитьОбработчикОжидания("ЗаполнитьСписокВыбораСертификатовАутентификации", 0.1, Истина);
			СменитьСтраницуШаги(Элементы.СтраницаПолучениеССервера);
		КонецЕсли;
		
	ИначеЕсли Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаАсинхронныйОбмен Тогда
		
		НастройкиЗагруженыИзФайла = Ложь;
		
		Отказ = Ложь;
		
		АдресСервера = АдресСервераНастроек;
		
		Если Не ЗначениеЗаполнено(ИдентификаторОрганизации) Тогда
			ТекстСообщения = НСтр("ru = 'Не указан идентификатор организации'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ИдентификаторОрганизации", , Отказ);
		КонецЕсли;
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен");
		
		ХронологияПереключенияСтраниц.Добавить(Элементы.СтраницаАсинхронныйОбмен);
		
		СменитьСтраницуШаги(Элементы.СтраницаСертификаты);
	
	ИначеЕсли Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаСертификаты Тогда
		Если ИспользуетсяКриптография И НЕ Сертификаты.Количество() Тогда
			ТекстСообщения = НСтр("ru = 'Добавьте сертификаты электронной подписи'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Сертификаты");
			Возврат;
		КонецЕсли;
		
		ХронологияПереключенияСтраниц.Добавить(Элементы.СтраницаСертификаты);
		СменитьСтраницуШаги(Элементы.СтраницаФинал);
		
		Если СохранитьВнешнююОбработку Тогда
			СохранитьВнешнююОбработку(
				АдресФайлаВнешнегоМодуля, ВерсияДополнительнойОбработки, ИмяВнешнегоМодуля, НаименованиеВнешнейОбработки);
		КонецЕсли;
		
		Элементы.СозданиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
		#Если ВебКлиент Тогда
			ОбновитьОтображениеДанных();
		#КонецЕсли
		ПодключитьОбработчикОжидания("СоздатьНастройкуОбмена", 0.1, Истина);
		
	ИначеЕсли Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаПолучениеССервера Тогда
		
		НастройкиЗагруженыИзФайла = Ложь;
		
		Отказ = Ложь;
		
		Если НЕ ЗначениеЗаполнено(АдресСервераНастроек) Тогда
			ТекстСообщения = НСтр("ru = 'Укажите адрес сервера банка'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "АдресСервераНастроек", , Отказ);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(АдресСервера)
			И НЕ ОбменСБанкамиСлужебныйКлиентСервер.ПравильныйФорматАдреса(АдресСервераНастроек) Тогда
			ТекстСообщения = НСтр("ru = 'Адрес сервера банка должен начинаться с ""https://"" или ""http://""'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "АдресСервераНастроек", , Отказ);
		КонецЕсли;
		
		Если СпособАутентификации = 0 И Не ЗначениеЗаполнено(СертификатАутентификации) Тогда  // по сертификату
			ТекстСообщения = НСтр("ru = 'Выберите сертификат для аутентификации на сервере банка'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "СертификатАутентификации", , Отказ);
		КонецЕсли;
		
		Если СпособАутентификации = 1 И НЕ ВведенЛогинИПароль() Тогда
			Отказ = Истина;
		КонецЕсли;
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		ХронологияПереключенияСтраниц.Добавить(Элементы.СтраницаПолучениеССервера);
		Если СпособСозданияНастройки = 2 Тогда // ручное создание настройки
			СменитьСтраницуШаги(Элементы.СтраницаПодождите);
		Иначе
			СменитьСтраницуШаги(Элементы.СтраницаФинал);
		КонецЕсли;
	
		Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
			ПодключитьОбработчикОжидания("ЗагрузитьВКСбербанка", 0.1, Истина);
		ИначеЕсли СпособАутентификации = 0 Тогда // по сертификату
			ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьДанныеСертификатаПослеПолучения", ЭтотОбъект);
			ЭлектроннаяПодписьКлиент.ПолучитьСертификатПоОтпечатку(ОписаниеОповещения, СертификатАутентификации, Истина);
		Иначе
			ПодключитьОбработчикОжидания("ПолучитьНастройкиОбменаССервераБанкаБазоваяАутентификация", 0.1, Истина);
		КонецЕсли;
	ИначеЕсли Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаВыборФайлаНастроек Тогда
		
		НастройкиЗагруженыИзФайла = Истина;
		
		#Если ВебКлиент Тогда
			Если Не ВыбранФайлНастроекОбмена Тогда
				ТекстСообщения = НСтр("ru = 'Не выбран файл настроек обмена'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПутьКФайлуНастроекОбмена");
				Возврат;
			КонецЕсли;
		#Иначе
			Если ЗначениеЗаполнено(ПутьКФайлуНастроекОбмена) Тогда
				Попытка
					ДвоичныеДанныеФайлаНастроек = Новый ДвоичныеДанные(ПутьКФайлуНастроекОбмена);
				Исключение
					ТекстСообщения = НСтр("ru = 'Неверно указан путь к файлу настроек обмена'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПутьКФайлуНастроекОбмена");
					Возврат;
				КонецПопытки;
				АдресФайлаНастроекОбмена = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайлаНастроек, УникальныйИдентификатор);
			Иначе
				ТекстСообщения = НСтр("ru = 'Не выбран файл настроек обмена'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПутьКФайлуНастроекОбмена");
				Возврат;
			КонецЕсли;
		#КонецЕсли
		
		Файл = Новый Файл(ПутьКФайлуНастроекОбмена);
		Если Не Файл.Расширение = ".xml" Тогда
			ТекстСообщения = НСтр("ru = 'Указан неверный файл.
										|Файл настроек должен иметь расширение ""xml""'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПутьКФайлуНастроекОбмена");
			Возврат;
		КонецЕсли;
		
		УчастникиОбменаИзФайла = УчастникиОбменаИзФайла(АдресФайлаНастроекОбмена, Организация);
		
		Если УчастникиОбменаИзФайла = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если УчастникиОбменаИзФайла.Организация <> Организация Тогда
			ТекстСообщения = НСтр("ru = 'Указан неверный файл.
										|Требуются настройки для организации: %1
										|Файл настроек содержит настройки для организации: %2'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Организация, УчастникиОбменаИзФайла.Организация);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
	
		Если УчастникиОбменаИзФайла.Банк <> Банк Тогда
			ТекстСообщения = НСтр("ru = 'Указан неверный файл.
										|Требуются настройки для банка: %1
										|Файл настроек содержит настройки для банка: %2'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Банк, УчастникиОбменаИзФайла.Банк);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
	
		СпособСозданияНастройки = 1;
		
		Если УчастникиОбменаИзФайла.ЭтоНастройкиСбербанка Тогда
			ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн");
			СменитьСтраницуШаги(Элементы.СтраницаФинал);
			URLВК = УчастникиОбменаИзФайла.URLВК;
			ПодключитьОбработчикОжидания("ОпределитьURLИнфоФайлаСбербанк", 0.1, Истина);
		Иначе
			ХронологияПереключенияСтраниц.Добавить(Элементы.СтраницаВыборФайлаНастроек);
			СменитьСтраницуШаги(Элементы.СтраницаФинал);
			Элементы.СозданиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
			#Если ВебКлиент Тогда
				ОбновитьОтображениеДанных();
			#КонецЕсли
			ПодключитьОбработчикОжидания("СоздатьНастройкуОбмена", 0.1, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)

	Если Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаФинал Тогда
		Элементы.ПолучениеНастроекОбменаКартинка.Картинка = Новый Картинка;
		Элементы.СозданиеНастройкиОбменаКартинка.Картинка = Новый Картинка;
		Элементы.ТестированиеНастройкиОбменаКартинка.Картинка = Новый Картинка;
	КонецЕсли;
	
	ОбменСБанкамиСлужебныйКлиент.ПрерватьПроцессыНаКлиенте(НастройкаОбмена);
	ВернутьсяНаПредыдущуюСтраницу();

КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ОбменСБанкамиСлужебныйКлиент.ПрерватьПроцессыНаКлиенте(НастройкаОбмена);
	Закрыть(НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьНастройки(Команда)
	
	ОчиститьСообщения();
	
	Элементы.ПолучитьНастройки.Видимость = Ложь;
	Элементы.ПолучениеНастроекОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
	Элементы.СтраницыРезультатПодключения.ТекущаяСтраница = Элементы.СтраницаНеЗавершено;
	
	ОбменЧерезВК = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК");
	
	Если ПрограммаБанка = ОбменЧерезВК Тогда
		Если СпособСозданияНастройки = 2 Тогда // ручная настройка
			ПолучитьНастройкиОбменаССервераБанкаЧерезВК();
		Иначе
			#Если ВебКлиент Тогда
				ОбновитьОтображениеДанных();
			#КонецЕсли
			ПодключитьОбработчикОжидания("СкачатьВКССервераБанка", 0.1, Истина);
		КонецЕсли;
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
		ПодключитьОбработчикОжидания("ЗагрузитьВКСбербанка", 0.1, Истина);
	Иначе // асинхронный обмен
		Если СпособАутентификации = 0 Тогда // по сертификату
			ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьДанныеСертификатаПослеПолучения", ЭтотОбъект);
			ЭлектроннаяПодписьКлиент.ПолучитьСертификатПоОтпечатку(ОписаниеОповещения, СертификатАутентификации, Истина);
		Иначе
			#Если ВебКлиент Тогда
				ОбновитьОтображениеДанных();
			#КонецЕсли
			ПодключитьОбработчикОжидания("ПолучитьНастройкиОбменаССервераБанкаБазоваяАутентификация", 0.1, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьСервис(Команда)
	
	ПередНачаломПодключенияСервиса();
	
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен")
		И (НастройкиОбменаСБанком.СпособАутентификации = "ПоЛогину"
			ИЛИ (НастройкиОбменаСБанком.СпособАутентификации = "ПоЛогинуИлиСертификату"
					И НЕ ЕстьСертификаты)) Тогда
		
		НастройкиЗагруженыИзФайла = Ложь;
		
		Если НЕ ВведенЛогинИПароль() Тогда
			Возврат;
		КонецЕсли;
		
		ПолучитьНастройкиОбменаССервераБанкаБазоваяАутентификация();
		
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
		
		Если НЕ ВведенЛогинИПароль() Тогда
			Возврат;
		КонецЕсли;
		
		URLВК = НастройкиОбменаСБанком.АдресСервера;
		Если ЗначениеЗаполнено(URLВК) Тогда
			ПодключитьОбработчикОжидания("ЗагрузитьВКСбербанка", 0.1, Истина);
		Иначе
			НачатьЗагрузкуНастроекИзФайла();
			ОбработатьРезультатПолученияНастроекССервераБанка(НастройкиЗагруженыИзФайла);
		КонецЕсли;
		
	ИначеЕсли НастройкиОбменаСБанком.СпособАутентификации = "ИзФайла" Тогда
		
		НачатьЗагрузкуНастроекИзФайла();
		ОбработатьРезультатПолученияНастроекССервераБанка(НастройкиЗагруженыИзФайла);
		
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен") Тогда
		
		Если ЗначениеЗаполнено(СертификатАутентификации) Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьДанныеСертификатаПослеПолучения", ЭтотОбъект);
			ЭлектроннаяПодписьКлиент.ПолучитьСертификатПоОтпечатку(ОписаниеОповещения, СертификатАутентификации, Истина);
		Иначе
			ТекстСообщения = НСтр("ru = 'Выберите сертификат для аутентификации на сервере банка'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "СертификатАутентификации");
		КонецЕсли;
		
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК") Тогда
		
		ПодключитьОбработчикОжидания("СкачатьВКССервераБанка", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиСобытий

&НаКлиенте
Процедура ПриИзмененииОрганизации()
	
	Сертификаты.Очистить();
	
	БанкиОрганизации = БанкиОрганизации(Организация);
	
	Если БанкиОрганизации.Найти(Банк) = Неопределено Тогда
		Если БанкиОрганизации.Количество() = 1 Тогда 
			Банк = БанкиОрганизации[0];
		Иначе 
			Банк = "";
		КонецЕсли;
		ПриИзмененииБанка();
	Иначе
		ИзменитьЗаголовок();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииБанка()
	
	ВнешняяКомпонентаЗагружена = Ложь;
	ИзменитьЗаголовок();
	Элементы.ИнформацияНаСайтеБанка.Видимость = Ложь;
	
	Если ЗначениеЗаполнено(Банк) Тогда
		НастройкиОбменаСБанком = ОбменСБанкамиСлужебныйВызовСервера.ПоставляемыеНастройкиОбмена(Банк, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция БанкиОрганизации(Организация)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	БанковскиеСчетаОрганизаций.Банк КАК Банк
	               |ИЗ
	               |	&БанковскиеСчетаОрганизаций КАК БанковскиеСчетаОрганизаций
	               |ГДЕ
	               |	БанковскиеСчетаОрганизаций.Владелец = &Организация
	               |	И НЕ БанковскиеСчетаОрганизаций.ПометкаУдаления
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	БанковскиеСчетаОрганизаций.Банк";
	
	ИмяСправочникаБанковскиеСчета = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяПрикладногоСправочника("БанковскиеСчетаОрганизаций");
	
	Если ИмяСправочникаБанковскиеСчета = Неопределено Тогда 
		Возврат Новый Массив;
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&БанковскиеСчетаОрганизаций", "Справочник." + ИмяСправочникаБанковскиеСчета);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Банк");
	
КонецФункции

#КонецОбласти

#Область ИзменениеИнтерфейса

&НаКлиенте
Процедура СоздатьМенеджерКриптографииПослеЗаполненияПрограммКриптографии(Результат, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ПереключитьСтраницыПослеСозданияМенеджераКриптографии", ЭтотОбъект);
	ЭлектроннаяПодписьКлиент.СоздатьМенеджерКриптографии(Оповещение, "");
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренумероватьСертификаты()
	
	Счетчик = 0;
	Для Каждого Строка Из Сертификаты Цикл
		Счетчик = Счетчик + 1;
		Строка.Номер = Счетчик;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СменитьСтраницуКомандНавигации(НоваяСтраница)
	
	Если НоваяСтраница = Элементы.ДалееОтмена Тогда
		Элементы.ДалееПервая.КнопкаПоУмолчанию = Истина;
		Элементы.ДалееПервая.АктивизироватьПоУмолчанию = Истина;
		ТекущийЭлемент = Элементы.ДалееПервая;
	ИначеЕсли НоваяСтраница = Элементы.НазадДалееОтмена Тогда
		Элементы.ДалееВторая.КнопкаПоУмолчанию = Истина;
		Элементы.ДалееВторая.АктивизироватьПоУмолчанию = Истина;
		ТекущийЭлемент = Элементы.ДалееВторая;
	ИначеЕсли НоваяСтраница = Элементы.НазадГотово Тогда
		Элементы.ГотовоПервая.КнопкаПоУмолчанию = Истина;
	ИначеЕсли НоваяСтраница = Элементы.Готово Тогда
		Элементы.ГотовоВторая.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	Элементы.КомандыНавигации.ТекущаяСтраница = НоваяСтраница;
	
КонецПроцедуры

&НаКлиенте
Процедура СменитьСтраницуШаги(НоваяСтраница)
	
	Если СменитьСтраницуНаУпрощеннуюНастройку(НоваяСтраница) Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ИнформацияНаСайтеБанка.Видимость = Ложь;
	Элементы.ПолучитьНастройки.Видимость = Ложь;
	Элементы.КомандыНавигации.Доступность = Истина;
	
	КомандыСВозвратом = Элементы.НазадДалееОтмена;
	КомандыБезВозврата = Элементы.ДалееОтмена;

	Если НоваяСтраница = Элементы.СтраницаФинал Тогда
		
		Элементы.СтраницыРезультатПодключения.ТекущаяСтраница = Элементы.СтраницаНеЗавершено;
		
		Элементы.ОткрытьНастройкуОбмена.Видимость = Ложь;
		
		Если ХронологияПереключенияСтраниц.Количество() Тогда
			КомандыСВозвратом = Элементы.НазадОтмена;
		Иначе
			КомандыБезВозврата = Элементы.Отмена;
		КонецЕсли;
		
		Элементы.ТестированиеНастройкиОбменаКартинка.Картинка = Новый Картинка;
		СинхронныйОбмен = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АльфаБанкОнлайн");
		Элементы.ГруппаТестированиеНастройкиОбмена.Видимость = ПрограммаБанка <> СинхронныйОбмен;
		Если СпособСозданияНастройки <> 0
			И НЕ (ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК")
					И СпособСозданияНастройки = 2)
			И НЕ ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
			Элементы.ГруппаПолучениеНастроекССервераБанка.Видимость = Ложь;
		Иначе // Получение настроек с сервера банка
			Элементы.ГруппаПолучениеНастроекССервераБанка.Видимость = Истина;
			Элементы.ПолучениеНастроекОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
			Элементы.СозданиеНастройкиОбменаКартинка.Картинка = Новый Картинка;
		КонецЕсли;
	ИначеЕсли НоваяСтраница = Элементы.СтраницаАсинхронныйОбмен Тогда
		ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен");
		Если Не ЗначениеЗаполнено(ВерсияФормата) Тогда
			ВерсияФормата = ОбменСБанкамиКлиентСервер.АктуальнаяВерсияФорматаАсинхронногоОбмена();
		КонецЕсли;
	ИначеЕсли НоваяСтраница = Элементы.СтраницаВыборСпособаСозданияНастройки Тогда
		Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
			Элементы.ПолучениеНастроекССервераБанка.Видимость = Ложь;
			Если СпособСозданияНастройки = 0 Тогда
				СпособСозданияНастройки = 1;
			КонецЕсли
		Иначе
			Элементы.ПолучениеНастроекССервераБанка.Видимость = ТестовыйРежим;
		КонецЕсли;
	ИначеЕсли НоваяСтраница = Элементы.СтраницаПолучениеССервера Тогда
		СпособАутентификацииПриИзменении(Неопределено);
		Если ЗначениеЗаполнено(НастройкиОбменаСБанком) И ЗначениеЗаполнено(НастройкиОбменаСБанком.АдресСтраницыБанка) Тогда
			ВывестиСсылкуНаСайтБанка(Элементы.ИнформацияНаСайтеБанка, НастройкиОбменаСБанком.АдресСтраницыБанка);
		КонецЕсли;
	ИначеЕсли НоваяСтраница = Элементы.СтраницаВыборОрганизацииИБанка Тогда
		НастройкиЗагруженыИзФайла = Ложь;
		Элементы.СтраницаОписание.Видимость = Ложь;
	ИначеЕсли НоваяСтраница = Элементы.СтраницаСертификаты Тогда
		Элементы.ГруппаСертификаты.Доступность = ИспользуетсяКриптография;
		АсинхронныйОбмен = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен");
		Элементы.ГруппаНастроекКриптографии.Видимость = ПрограммаБанка = АсинхронныйОбмен;
	ИначеЕсли НоваяСтраница = Элементы.СтраницаВыборФайлаНастроек Тогда
		Если ЗначениеЗаполнено(НастройкиОбменаСБанком) И ЗначениеЗаполнено(НастройкиОбменаСБанком.АдресСтраницыБанка) Тогда
			ВывестиСсылкуНаСайтБанка(Элементы.ИнформацияНаСайтеБанка, НастройкиОбменаСБанком.АдресСтраницыБанка);
		КонецЕсли;
	ИначеЕсли НоваяСтраница = Элементы.СтраницаПодождите Тогда
		КомандыСВозвратом = Элементы.Отмена;
		КомандыБезВозврата = Элементы.Отмена;
	ИначеЕсли НоваяСтраница = Элементы.СтраницаОписание Тогда
		Элементы.СтраницаОписание.Видимость = Истина;
	КонецЕсли;
	
	Если ХронологияПереключенияСтраниц.Количество() Тогда
		СменитьСтраницуКомандНавигации(КомандыСВозвратом)
	Иначе
		СменитьСтраницуКомандНавигации(КомандыБезВозврата)
	КонецЕсли;
	
	#Если ВебКлиент Тогда
		ОбновитьОтображениеДанных();
	#КонецЕсли
	
	Элементы.Шаги.ТекущаяСтраница = НоваяСтраница;

КонецПроцедуры

&НаКлиенте
Процедура ПереключитьСтраницыПослеСозданияМенеджераКриптографии(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда  // ошибка создания менеджера криптографии
		ВернутьсяНаПредыдущуюСтраницу();
		Возврат;
	КонецЕсли;
	
	СменитьСтраницуШаги(Элементы.СтраницаСертификаты);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьШагПослеВыбораОрганизацииИБанка()
	
	Элементы.АдресСервераНастроек.Видимость = Истина;
	Элементы.СпособАутентификации.Видимость = Истина;
	ОбновитьОтображениеДанных();
	
	УпрощенныйРежимНастройки = НастройкиОбменаСБанком <> Неопределено;
	
	Если НастройкиОбменаСБанком = Неопределено Тогда // Неизвестный банк
		УпрощенныйРежимНастройки = Ложь;
		ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен");
		Если Не ТестовыйРежим И НеИспользоватьРучноеЗаполнение Тогда // осталась только загрузка из файла
			СпособСозданияНастройки = 1; // загрузка из файла
			СменитьСтраницуШаги(Элементы.СтраницаВыборФайлаНастроек);
		Иначе
			СменитьСтраницуШаги(Элементы.СтраницаВыборСпособаСозданияНастройки);
		КонецЕсли;
	ИначеЕсли НастройкиОбменаСБанком.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
		Если Не ЕстьПлатежныеДокументыВКонфигурации() Тогда // ЗУП
			ТекстСообщения = НСтр("ru = 'Обмен документами по заработной плате через сервис 1С:ДиректБанк с выбранным банком не поддерживается.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
		ПрограммаБанка = НастройкиОбменаСБанком.ПрограммаБанка;
		URLВК = НастройкиОбменаСБанком.АдресСервера;
		СменитьСтраницуШаги(Элементы.СтраницаУпрощенныйРежимНастройки);
	ИначеЕсли НастройкиОбменаСБанком.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК") Тогда
		Если Не ЕстьПлатежныеДокументыВКонфигурации() Тогда // ЗУП
			ТекстСообщения = НСтр("ru = 'Обмен документами по заработной плате через сервис 1С:ДиректБанк с выбранным банком не поддерживается.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
		ПрограммаБанка = НастройкиОбменаСБанком.ПрограммаБанка;
		URLВК = НастройкиОбменаСБанком.АдресСервера;
		СменитьСтраницуШаги(Элементы.СтраницаФинал);
		Если НЕ УпрощенныйРежимНастройки Тогда
			ПодключитьОбработчикОжидания("СкачатьВКССервераБанка", 0.1, Истина);
		КонецЕсли;
	ИначеЕсли НастройкиОбменаСБанком.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АльфаБанкОнлайн") Тогда
		Если Не ЕстьПлатежныеДокументыВКонфигурации() Тогда // ЗУП
			ТекстСообщения = НСтр("ru = 'Обмен документами по заработной плате через сервис 1С:ДиректБанк с выбранным банком не поддерживается.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
		РесурсПриемник = "/wsi2/order";
		РесурсИсточник = "/wsi2/statement";
		АдресСервера = "https://ic.alfabank.ru/";
		СпособСозданияНастройки = 2;
		ПрограммаБанка = НастройкиОбменаСБанком.ПрограммаБанка;
		ИспользуетсяКриптография = Истина;
		Оповещение = Новый ОписаниеОповещения("СоздатьМенеджерКриптографииПослеЗаполненияПрограммКриптографии", ЭтотОбъект);
		ЗаполнитьПрограммыКриптографии(Оповещение);
	ИначеЕсли НастройкиОбменаСБанком.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен") Тогда
		ПрограммаБанка = НастройкиОбменаСБанком.ПрограммаБанка;
		Если Не ЕстьПлатежныеДокументыВКонфигурации() И СтрНайти(НастройкиОбменаСБанком.Проект, "2") = 0 Тогда
			ТекстСообщения = НСтр("ru = 'Обмен документами по заработной плате через сервис 1С:ДиректБанк с выбранным банком не поддерживается.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
		Если ЗначениеЗаполнено(НастройкиОбменаСБанком.АдресСервера) Тогда
			АдресСервераНастроек = НастройкиОбменаСБанком.АдресСервера;
			СпособСозданияНастройки = 0;
			Если Не ТестовыйРежим Тогда
				Элементы.АдресСервераНастроек.Видимость = Ложь;
			КонецЕсли;
		КонецЕсли;
		Если ЗначениеЗаполнено(НастройкиОбменаСБанком.СпособАутентификации) Тогда
			Если НастройкиОбменаСБанком.СпособАутентификации = "ИзФайла" Тогда
				СменитьСтраницуШаги(Элементы.СтраницаВыборФайлаНастроек);
			ИначеЕсли НастройкиОбменаСБанком.СпособАутентификации = "ПоЛогинуИлиСертификату" Тогда
				Оповещение = Новый ОписаниеОповещения(
					"СменитьСтраницуПослеПолучениеССервераПослеЗаполненияПрограммКриптографии", ЭтотОбъект);
				ЗаполнитьПрограммыКриптографии(Оповещение);
			ИначеЕсли НастройкиОбменаСБанком.СпособАутентификации = "ПоСертификату" Тогда
				СпособАутентификации = 0;
				Элементы.СпособАутентификации.Видимость = Ложь;
				Оповещение = Новый ОписаниеОповещения(
					"СменитьСтраницуСертификатДляПолученияНастроекПослеЗаполненияПрограммКриптографии", ЭтотОбъект);
				ЗаполнитьПрограммыКриптографии(Оповещение);
			ИначеЕсли НастройкиОбменаСБанком.СпособАутентификации = "ПоЛогину" Тогда
				Элементы.СпособАутентификации.Видимость = Ложь;
				СпособАутентификации = 1;
				СменитьСтраницуШаги(Элементы.СтраницаПолучениеССервера);
			Иначе
				СменитьСтраницуШаги(Элементы.СтраницаВыборСпособаСозданияНастройки);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СменитьСтраницуПослеПолучениеССервераПослеЗаполненияПрограммКриптографии(Результат, ДополнительныеПараметры) Экспорт
	
	ЗаполнитьСписокВыбораСертификатовАутентификации();
	Если УпрощенныйРежимНастройки Тогда
		ПодключитьОбработчикОжидания("ПослеЗаполненияСертификатовАутентификации", 0.1, Истина);
	Иначе
		СменитьСтраницуШаги(Элементы.СтраницаПолучениеССервера);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СменитьСтраницуСертификатДляПолученияНастроекПослеЗаполненияПрограммКриптографии(Результат, ДополнительныеПараметры) Экспорт
	
	ЗаполнитьСписокВыбораСертификатовАутентификации();
	СменитьСтраницуШаги(Элементы.СтраницаПолучениеССервера);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЗаголовок()
	
	Если ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(Банк) Тогда
		Шаблон = "%1 - %2";
		Элементы.Заголовок.Заголовок = СтрШаблон(Шаблон, Организация, Банк);
	Иначе
		Элементы.Заголовок.Заголовок = "";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область АсинхронныйОбмен

&НаКлиенте
Процедура ПослеУстановкиРасширенияДляРаботыСКриптографией(ДополнительныеПараметры) Экспорт
	
	ОповещениеПослеПодключенияРасширенияДляРаботыСКриптографией = Новый ОписаниеОповещения(
		"ПослеПовторногоПодключенияРасширенияДляРаботыСКриптографией", ЭтотОбъект, ДополнительныеПараметры);
	
	НачатьПодключениеРасширенияРаботыСКриптографией(ОповещениеПослеПодключенияРасширенияДляРаботыСКриптографией);

КонецПроцедуры

&НаКлиенте
Процедура ПриОшибкеПолученияИнформацииМодуляКриптографии(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПроверитьОчереднуюПрограммуКриптографии(ДополнительныеПараметры.МассивПрограмм,
		ДополнительныеПараметры.ПрограммыКриптографии, ДополнительныеПараметры.Оповещение,
		ДополнительныеПараметры.МассивПрограммДляСоздания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСертификатЦиклПослеСозданияМенеджераКриптографии(Результат, Контекст) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("МенеджерКриптографии") Тогда
		Если Результат.Ошибки.Количество() > 0 Тогда
			Контекст.ОшибкаНаКлиенте.Ошибки.Добавить(Результат.Ошибки[0]);
		КонецЕсли;
		ПроверитьСертификатЦиклНачало(Контекст);
		Возврат;
	КонецЕсли;
	Контекст.Вставить("МенеджерКриптографии", Результат);
	
	Контекст.МенеджерКриптографии.ПарольДоступаКЗакрытомуКлючу = ПарольСертификата;
	
	Оповещение = Новый ОписаниеОповещения("ПроверитьСертификатЦиклПослеПодписания", ЭтотОбъект, Контекст,
		"ПроверитьСертификатЦиклПослеОшибкиПодписания", ЭтотОбъект);
	
	Контекст.МенеджерКриптографии.НачатьПодписывание(
		Оповещение, Контекст.ДвоичныеДанныеСертификата, Контекст.СертификатКриптографии);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСертификатЦиклПослеПодписания(ДанныеПодписи, Контекст) Экспорт
	
	ПредставлениеОшибки = "";
	Попытка
		ЭлектроннаяПодписьСлужебныйКлиентСервер.ПустыеДанныеПодписи(ДанныеПодписи, ПредставлениеОшибки);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	КонецПопытки;
	
	Если ЗначениеЗаполнено(ПредставлениеОшибки) Тогда
		ЗаполнитьОшибкуПодписания(Контекст.ОшибкаНаКлиенте, Контекст.ОписаниеОшибки, Контекст.ОписаниеПрограммы,
			ПредставлениеОшибки, ИнформацияОбОшибке = Неопределено);
		ПроверитьСертификатЦиклНачало(Контекст);
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура("Программа", Контекст.ОписаниеПрограммы.Ссылка);
	ВыполнитьОбработкуОповещения(Контекст.Оповещение, Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСертификатЦиклПослеОшибкиПодписания(ИнформацияОбОшибке, СтандартнаяОбработка, Контекст) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ЗаполнитьОшибкуПодписания(Контекст.ОшибкаНаКлиенте, Контекст.ОписаниеОшибки, Контекст.ОписаниеПрограммы,
		КраткоеПредставлениеОшибки(ИнформацияОбОшибке), Ложь);
	
	ПроверитьСертификатЦиклНачало(Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСертификатЦиклНачало(Контекст)
	
	Если Контекст.ОписанияПрограмм.Количество() <= Контекст.Индекс + 1 Тогда
		ВыполнитьОбработкуОповещения(Контекст.Оповещение);
		Возврат;
	КонецЕсли;
	Контекст.Индекс = Контекст.Индекс + 1;
	Контекст.Вставить("ОписаниеПрограммы", Контекст.ОписанияПрограмм[Контекст.Индекс]);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПроверитьСертификатЦиклПослеСозданияМенеджераКриптографии", ЭтотОбъект, Контекст);
	
	ЭлектроннаяПодписьСлужебныйКлиент.СоздатьМенеджерКриптографии(
		Оповещение, "", Неопределено, Контекст.ОписаниеПрограммы.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСертификатПослеИнициализацииСертификата(СертификатКриптографии, Контекст) Экспорт
	
	Контекст.Вставить("СертификатКриптографии", СертификатКриптографии);
	
	Контекст.Вставить("ОписаниеОшибки", "");
	Контекст.Вставить("ОшибкаНаКлиенте", Новый Структура);
	
	Контекст.ОшибкаНаКлиенте.Вставить("ОписаниеОшибки", "");
	Контекст.ОшибкаНаКлиенте.Вставить("Ошибки", Новый Массив);
	
	Контекст.Вставить("ОписанияПрограмм", ЭлектроннаяПодписьКлиентСервер.ОбщиеНастройки().ОписанияПрограмм);
	Контекст.Вставить("Индекс", -1);
	
	ПроверитьСертификатЦиклНачало(Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьОчереднуюПрограммуКриптографии(МассивПрограмм, ПрограммыКриптографии, Оповещение, МассивПрограммДляСоздания)
	
	Если МассивПрограмм.Количество() = 0 Тогда
		
		Если МассивПрограммДляСоздания.Количество() Тогда
			ЗаписатьПрограммыКриптографии(ПрограммыКриптографии, МассивПрограммДляСоздания);
		КонецЕсли;
		ВыполнитьОбработкуОповещения(Оповещение);
		Возврат;
	КонецЕсли;
	
	ТекПрограмма = МассивПрограмм.Получить(0);
	МассивПрограмм.Удалить(0);
	
	ПараметрыПрограммы = Неопределено;
	Если НЕ ПрограммыКриптографии.Свойство(ТекПрограмма, ПараметрыПрограммы) Тогда
		ПроверитьОчереднуюПрограммуКриптографии(
			МассивПрограмм, ПрограммыКриптографии, Оповещение, МассивПрограммДляСоздания);
		Возврат;
	КонецЕсли;
		
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Оповещение", Оповещение);
	ДополнительныеПараметры.Вставить("МассивПрограмм", МассивПрограмм);
	ДополнительныеПараметры.Вставить("ПрограммыКриптографии", ПрограммыКриптографии);
	ДополнительныеПараметры.Вставить("ТекущаяПрограмма", ТекПрограмма);
	ДополнительныеПараметры.Вставить("МассивПрограммДляСоздания", МассивПрограммДляСоздания);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПолученияИнформацииМодуляКриптографии", ЭтотОбъект,
		ДополнительныеПараметры, "ПриОшибкеПолученияИнформацииМодуляКриптографии", ЭтотОбъект);
	
	СредстваКриптографии.НачатьПолучениеИнформацииМодуляКриптографии(
		ОписаниеОповещения, ПараметрыПрограммы.ИмяПрограммы, "", ПараметрыПрограммы.ТипПрограммы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияИнформацииМодуляКриптографии(ИнформацияМодуля, ДополнительныеПараметры) Экспорт
	
	Если ИнформацияМодуля <> Неопределено Тогда
		ДополнительныеПараметры.МассивПрограммДляСоздания.Добавить(ДополнительныеПараметры.ТекущаяПрограмма);
	КонецЕсли;
	ПроверитьОчереднуюПрограммуКриптографии(ДополнительныеПараметры.МассивПрограмм,
		ДополнительныеПараметры.ПрограммыКриптографии, ДополнительныеПараметры.Оповещение,
		ДополнительныеПараметры.МассивПрограммДляСоздания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодключенияРасширенияДляРаботыСКриптографией(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Подключено Тогда
		НачатьЗаполнениеПрограммКриптографии(ДополнительныеПараметры.Оповещение);
	Иначе
		ОповещениеПослеУстановкиРасширенияДляРаботыСКриптографией = Новый ОписаниеОповещения(
			"ПослеУстановкиРасширенияДляРаботыСКриптографией", ЭтотОбъект, ДополнительныеПараметры);
		НачатьУстановкуРасширенияРаботыСКриптографией(ОповещениеПослеУстановкиРасширенияДляРаботыСКриптографией);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПовторногоПодключенияРасширенияДляРаботыСКриптографией(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Подключено Тогда
		НачатьЗаполнениеПрограммКриптографии(ДополнительныеПараметры.Оповещение);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаполненияСертификатовАутентификации()
	
	СменитьСтраницуШаги(Элементы.СтраницаУпрощенныйРежимНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьПрограммуКриптографииДляСертификата(Оповещение, ДвоичныеДанныеСертификата)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДвоичныеДанныеСертификата", ДвоичныеДанныеСертификата);
	ДополнительныеПараметры.Вставить("Оповещение", Оповещение);
	
	СертификатКриптографии = Новый СертификатКриптографии;
	Оповещение = Новый ОписаниеОповещения(
		"ПроверитьСертификатПослеИнициализацииСертификата", ЭтотОбъект, ДополнительныеПараметры);
	СертификатКриптографии.НачатьИнициализацию(Оповещение, ДвоичныеДанныеСертификата);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьОшибкуПодписания(Ошибка, ОписаниеОшибки, ОписаниеПрограммы, ПредставлениеОшибки, ПустыеДанные)
	
	ТекущаяОшибка = Новый Структура;
	ТекущаяОшибка.Вставить("Описание", ПредставлениеОшибки);
	
	Если Не ПустыеДанные Тогда
		ТекущаяОшибка.Вставить("НастройкаПрограмм", Истина);
		ТекущаяОшибка.Вставить("Инструкция", Истина);
	КонецЕсли;
	
	Ошибка.Ошибки.Добавить(ТекущаяОшибка);
	
	ОписаниеОшибки = ОписаниеОшибки + Символы.ПС + Символы.ПС + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Не удалось пройти проверку подписания с помощью программы %1 по причине:
					|%2'"),
		ОписаниеПрограммы.Наименование,
		ПредставлениеОшибки);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПрограммыКриптографии(Оповещение)
	
	СменитьСтраницуШаги(Элементы.СтраницаПодождите);
	ДополнительныеПараметры = Новый Структура("Оповещение", Оповещение);
	
	ОповещениеПослеПодключенияРасширенияДляРаботыСКриптографией = Новый ОписаниеОповещения(
		"ПослеПодключенияРасширенияДляРаботыСКриптографией", ЭтотОбъект, ДополнительныеПараметры);
	
	НачатьПодключениеРасширенияРаботыСКриптографией(ОповещениеПослеПодключенияРасширенияДляРаботыСКриптографией);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьПрограммыКриптографии(ПрограммыКриптографии, МассивПрограммДляСоздания)
	
	ОписаниеПрограмм = Новый Массив;
	
	Для Каждого ТекПрограмма Из МассивПрограммДляСоздания Цикл
		ПараметрыПрограммы = ПрограммыКриптографии[ТекПрограмма];
		ОписаниеПрограммы = ЭлектроннаяПодпись.НовоеОписаниеПрограммы();
		ЗаполнитьЗначенияСвойств(ОписаниеПрограммы, ПараметрыПрограммы);
		ОписаниеПрограмм.Добавить(ОписаниеПрограммы);
	КонецЦикла;
	
	ЭлектроннаяПодпись.ЗаполнитьСписокПрограмм(ОписаниеПрограмм);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайлаНастроекОбменаИзВеб(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	ВыбранФайлНастроекОбмена = Результат;
	АдресФайлаНастроекОбмена = Адрес;
	ПутьКФайлуНастроекОбмена = ВыбранноеИмяФайла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайлаНастроекОбменаИзТонкогоКлиента(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено ИЛИ НЕ ВыбранныеФайлы.Количество() Тогда
		ПутьКФайлуНастроекОбмена = "";
	Иначе
		ПутьКФайлуНастроекОбмена = ВыбранныеФайлы[0];
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТестНастройкиАсинхронныйОбмен()
	
	ВидЭДЗапросЗонд = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ЗапросЗонд");
	
	РеквизитыНастройкиОбмена = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыОбменаПоВидуЭД(
		НастройкаОбмена, ВидЭДЗапросЗонд);
	
	Если РеквизитыНастройкиОбмена.ТребуетсяПодпись ИЛИ РеквизитыНастройкиОбмена.АутентификацияПоСертификату Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("РеквизитыНастройкиОбмена", РеквизитыНастройкиОбмена);
		Оповещение = Новый ОписаниеОповещения(
			"ПродолжитьТестНастроекАсинхронныйОбменПослеПолученияОтпечатков", ЭтотОбъект, ДополнительныеПараметры);
		ЭлектроннаяПодписьКлиент.ПолучитьОтпечаткиСертификатов(Оповещение, Истина);
		
	Иначе
	
		НастройкиОбмена = Неопределено;
		СообщениеЗапросЗонд = Неопределено;
		ОбменСБанкамиСлужебныйВызовСервера.СформироватьЗапросЗонд(
			НастройкаОбмена, Новый Массив, СообщениеЗапросЗонд, НастройкиОбмена);
	
		Если НЕ ЗначениеЗаполнено(СообщениеЗапросЗонд) ИЛИ НастройкиОбмена = Неопределено Тогда
			ОбработатьРезультатТестаНастройки(Ложь);
			Возврат;
		КонецЕсли;
	
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ЗапросЗонд", СообщениеЗапросЗонд);
		ДополнительныеПараметры.Вставить("РеквизитыНастройкиОбмена", РеквизитыНастройкиОбмена);
		
		Если НастройкиЗагруженыИзФайла Тогда
			Оповещение = Новый ОписаниеОповещения(
				"ПолучитьИдентификаторСессииПослеЗапросаПароляБазоваяАутентификация", ЭтотОбъект, ДополнительныеПараметры);
			ОбменСБанкамиСлужебныйКлиент.ПолучитьДанныеАутентификации(Оповещение, НастройкаОбмена);
		Иначе
			ПолучитьРеквизитыНастройкиОбмена(НастройкаОбмена, АдресСервера, ИдентификаторОрганизации, ВерсияФормата);
			Оповещение = Новый ОписаниеОповещения(
			"ВыполнитьТестНастроекПослеПолученияИдентификатораСессии", ЭтотОбъект, ДополнительныеПараметры);
			ПолучитьИдентификаторСессииБазоваяАутентификация(
				Оповещение, АдресСервера, ИдентификаторОрганизации, Логин, Пароль, ВерсияФормата);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИдентификаторСессииПослеЗапросаПароляБазоваяАутентификация(ДанныеАутентификации, ДополнительныеПараметры) Экспорт
	
	Если ДанныеАутентификации = Неопределено Тогда
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;

	Оповещение = Новый ОписаниеОповещения(
		"ВыполнитьТестНастроекПослеПолученияИдентификатораСессии", ЭтотОбъект, ДополнительныеПараметры);
	ПолучитьИдентификаторСессииБазоваяАутентификация(Оповещение,
		ДополнительныеПараметры.РеквизитыНастройкиОбмена.АдресСервера,
		ДополнительныеПараметры.РеквизитыНастройкиОбмена.ИдентификаторОрганизации,
		ДанныеАутентификации.Логин, ДанныеАутентификации.Пароль,
		ДополнительныеПараметры.РеквизитыНастройкиОбмена.ВерсияФормата);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПолучитьРеквизитыНастройкиОбмена(Знач НастройкаОбмена, АдресСервера, ИдентификаторОрганизации, ВерсияФормата)
	
	РеквизитыНастройкиОбмена = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		НастройкаОбмена, "АдресСервера, ИдентификаторОрганизации, ВерсияФормата");
	АдресСервера = РеквизитыНастройкиОбмена.АдресСервера;
	ИдентификаторОрганизации = РеквизитыНастройкиОбмена.ИдентификаторОрганизации;
	ВерсияФормата = РеквизитыНастройкиОбмена.ВерсияФормата;
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьТестНастроекАсинхронныйОбменПослеПолученияОтпечатков(ДанныеОтпечатков, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(ДанныеОтпечатков) = Тип("Строка") Тогда
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;
	
	ОтпечаткиСертификатов = Новый Массив;
	Для Каждого КлючЗначение Из ДанныеОтпечатков Цикл
		ОтпечаткиСертификатов.Добавить(КлючЗначение.Ключ);
	КонецЦикла;
	
	РеквизитыНастройкиОбмена = ДополнительныеПараметры.РеквизитыНастройкиОбмена;

	НастройкиОбмена = Неопределено;
	СообщениеЗапросЗонд = Неопределено;
	ОбменСБанкамиСлужебныйВызовСервера.СформироватьЗапросЗонд(
		НастройкаОбмена, ОтпечаткиСертификатов, СообщениеЗапросЗонд, НастройкиОбмена);
	
	Если НЕ ЗначениеЗаполнено(СообщениеЗапросЗонд) ИЛИ НастройкиОбмена = Неопределено Тогда
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ЗапросЗонд", СообщениеЗапросЗонд);

	Если НастройкиОбмена.Подписывать Тогда
		
		Если НастройкиОбмена.ДоступныеСертификаты.Количество() = 0 Тогда
			ТекстСообщения = НСтр("ru = 'Не найден подходящий сертификат подписи.
										|Убедитесь, что сертификаты, указанные в настройке обмена, установлены в личное хранилище сертификатов операционной системы'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			ОбработатьРезультатТестаНастройки(Ложь);
			Возврат;
		КонецЕсли;
		
		Операция = НСтр("ru = 'Подписание электронного документа'");
		ОписаниеДанных = Новый Структура;
		ОписаниеДанных.Вставить("Операция", Операция);
		ОписаниеДанных.Вставить("ОтборСертификатов", НастройкиОбмена.ДоступныеСертификаты);
		ОписаниеДанных.Вставить("ПоказатьКомментарий", Ложь);
		ОписаниеДанных.Вставить("БезПодтверждения", Истина);
		НаборДанных = Новый Массив;
		Данные = Новый Структура;
		ПараметрыДляПолученияДД = Новый Структура("СообщениеОбмена, ОписаниеДанных", СообщениеЗапросЗонд, ОписаниеДанных);
		
		ПредставлениеИПрисоединенныйФайл = ПредставлениеИПрисоединенныйФайл(СообщениеЗапросЗонд);
		
		СсылкаНаДД = Новый ОписаниеОповещения(
			"ПолучитьДвоичныеДанныеДляСообщенияОбмена", ОбменСБанкамиСлужебныйКлиент, ПараметрыДляПолученияДД);
		Данные.Вставить("Данные", СсылкаНаДД);
		Данные.Вставить("Объект", ПредставлениеИПрисоединенныйФайл.ПрисоединенныйФайл);
		Представление = ПредставлениеИПрисоединенныйФайл.Представление;
		ОбработчикОткрытияЭД = Новый ОписаниеОповещения(
			"ПриОткрытииЭлектронногоДокумента", ОбменСБанкамиСлужебныйКлиент, СообщениеЗапросЗонд);
		ДанныеДляПредставления = Новый Структура("Представление, Значение", Представление, ОбработчикОткрытияЭД);
		Данные.Вставить("Представление", ДанныеДляПредставления);
		НаборДанных.Добавить(Данные);
		ОписаниеДанных.Вставить("НаборДанных", НаборДанных);
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодписанияЗапросаЗонда", ЭтотОбъект, ДополнительныеПараметры);
		ЭлектроннаяПодписьКлиент.Подписать(ОписаниеДанных, , ОписаниеОповещения);
	Иначе
		ПродолжитьТестНастроекПослеПроверкиЭПЗапросаЗонда(Истина, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПредставлениеИПрисоединенныйФайл(СообщениеОбмена)
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Представление", Строка(СообщениеОбмена));
	ПрисоединенныйФайл = ОбменСБанкамиСлужебныйВызовСервера.ПрисоединенныйФайл(СообщениеОбмена);
	СтруктураВозврата.Вставить("ПрисоединенныйФайл", ПрисоединенныйФайл);
	Возврат СтруктураВозврата;
	
КонецФункции

&НаКлиенте
Процедура ПослеПодписанияЗапросаЗонда(Результат, ПараметрыОбработки) Экспорт
	
	Если Результат.Успех Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПродолжитьТестНастроекПослеПроверкиЭПЗапросаЗонда", ЭтотОбъект, ПараметрыОбработки);
		ОбменСБанкамиСлужебныйКлиент.ПроверитьПодписи(ОписаниеОповещения, ПараметрыОбработки.ЗапросЗонд);
	Иначе
		ОбработатьРезультатТестаНастройки(Ложь);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьТестНастроекПослеПроверкиЭПЗапросаЗонда(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат Тогда // Подпись неверна
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СертификатАутентификацииСсылка) Тогда
		МассивСертификатов = Новый Массив;
		МассивСертификатов.Добавить(СертификатАутентификацииСсылка);
		ЭлектроннаяПодписьКлиент.УстановитьПарольСертификата(СертификатАутентификации, ПарольСертификата);
	Иначе
		МассивСертификатов = СертификатыАутентификации(НастройкаОбмена, ОтпечаткиСертификатов);
	КонецЕсли;
	
	Если ДополнительныеПараметры.РеквизитыНастройкиОбмена.АутентификацияПоСертификату Тогда
	
		ОписаниеДанных = Новый Структура;
		ОписаниеДанных.Вставить("ОтборСертификатов", МассивСертификатов);
		ОписаниеДанных.Вставить("БезПодтверждения",  Истина);
		ОписаниеДанных.Вставить("ЭтоАутентификация", Истина);
		ОписаниеДанных.Вставить("Операция", НСтр("ru = 'Аутентификация на сервере банка'"));
		ОписаниеДанных.Вставить("РазрешитьЗапоминатьПароль", Ложь);
		
		ПараметрыПолученияМаркера = Новый Структура("НастройкаОбмена", НастройкаОбмена);

		ОписаниеПолученияДанных = Новый ОписаниеОповещения(
			"ПолучитьЗашифрованныйИдентификаторСессии", ОбменСБанкамиСлужебныйКлиент, ПараметрыПолученияМаркера);
				
		ОписаниеДанных.Вставить("Данные", ОписаниеПолученияДанных);
					
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПродолжитьОтправкуЗапросаЗондаПослеРасшифровкиМаркера", ЭтотОбъект, ДополнительныеПараметры);
		ЭлектроннаяПодписьКлиент.Расшифровать(ОписаниеДанных, , ОписаниеОповещения);
	Иначе
		Оповещение = Новый ОписаниеОповещения(
			"ПолучитьИдентификаторСессииПослеЗапросаПароляБазоваяАутентификация", ЭтотОбъект, ДополнительныеПараметры);
		ОбменСБанкамиСлужебныйКлиент.ПолучитьДанныеАутентификации(Оповещение, НастройкаОбмена);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьОтправкуЗапросаЗондаПослеРасшифровкиМаркера(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Успех Тогда
		ИдентификаторСессии = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.СтрокаИзДвоичныхДанных(
			Результат.РасшифрованныеДанные);
		ВыполнитьТестНастроекПослеПолученияИдентификатораСессии(ИдентификаторСессии, ДополнительныеПараметры);
	Иначе
		ОбработатьРезультатТестаНастройки(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИдентификаторСессииБазоваяАутентификация(Оповещение, АдресСервера, ИдентификаторОрганизации, Логин, Пароль, ВерсияФормата)
	
	ДанныеSMSАвторизации = Неопределено;
	ДанныеАутентификации = Новый Структура("Логин, Пароль", Логин, Пароль);
	Если Не ЗначениеЗаполнено(ВерсияФормата) Тогда
		ВерсияФормата = ОбменСБанкамиКлиентСервер.БазоваяВерсияФорматаАсинхронногоОбмена();
	КонецЕсли;
	ИдентификаторСессии = ОбменСБанкамиСлужебныйВызовСервера.МаркерБанкаБазоваяАутентификация(АдресСервера,
		ИдентификаторОрганизации, ДанныеАутентификации, ВерсияФормата, ДанныеSMSАвторизации);
	Если ЗначениеЗаполнено(ДанныеSMSАвторизации) Тогда
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ИдентификаторСессии", ИдентификаторСессии);
		ПараметрыФормы.Вставить("Телефон", ДанныеSMSАвторизации.МаскаТелефона);
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияИдентификатораСессии", Оповещение);
		ДополнительныеПараметры.Вставить("НеподтвержденныйИдентификаторСессии", ИдентификаторСессии);
		ДополнительныеПараметры.Вставить("АдресСервера", АдресСервера);
		ДополнительныеПараметры.Вставить("ИдентификаторОрганизации", ИдентификаторОрганизации);
		ДополнительныеПараметры.Вставить("ВерсияAPI", ВерсияФормата);
		ОО = Новый ОписаниеОповещения("ОтправитьОдноразовыйПарольПослеВводаИзSMS", ЭтотОбъект, ДополнительныеПараметры);
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросОдноразовогоПароля", ПараметрыФормы, , , , , ОО);
	Иначе
		ВыполнитьОбработкуОповещения(Оповещение, ИдентификаторСессии);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьОдноразовыйПарольПослеВводаИзSMS(ОдноразовыйПароль, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ОдноразовыйПароль) Тогда
		ИдентификаторСессии = ОбменСБанкамиСлужебныйВызовСервера.МаркерБанкаПоSMS(ДополнительныеПараметры.АдресСервера,
			ДополнительныеПараметры.ИдентификаторОрганизации, ДополнительныеПараметры.НеподтвержденныйИдентификаторСессии,
			ОдноразовыйПароль, ДополнительныеПараметры.ВерсияAPI);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(
		ДополнительныеПараметры.ОповещениеПослеПолученияИдентификатораСессии, ИдентификаторСессии);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьТестНастроекПослеПолученияИдентификатораСессии(ИдентификаторСессии, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторСессии) Тогда
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;
		
	ПараметрыЗапроса = Новый Структура;

	ПараметрыЗапроса.Вставить("ИдентификаторСессии", ИдентификаторСессии);
	ПараметрыЗапроса.Вставить("СообщениеОбмена", ДополнительныеПараметры.ЗапросЗонд);
	ПараметрыЗапроса.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	МассивСообщений = Новый Массив;
	Результат = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияОтправкиЗапросЗондаНаСервере(
			ПараметрыЗапроса, УникальныйИдентификатор);
			
	ИдентификаторЗадания = Результат.ИдентификаторЗадания;
			
	Если Результат.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		Оповещение = Новый ОписаниеОповещения("ПослеОтправкиЗапросаЗонда", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	Иначе
		ПослеОтправкиЗапросаЗонда(Результат, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиЗапросаЗонда(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.КраткоеПредставлениеОшибки);
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;
	
	Результат = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияПолученияИзвещенияОСостоянииЭДНаСервере(
		ПараметрыЗапроса, УникальныйИдентификатор);
		
	ИдентификаторЗадания = Результат.ИдентификаторЗадания;
	
	Если Результат.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		Оповещение = Новый ОписаниеОповещения("ПослеПолученияИзвещенияДляЗапросаЗонда", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	Иначе
		ПослеПолученияИзвещенияДляЗапросаЗонда(Результат, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияИзвещенияДляЗапросаЗонда(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.КраткоеПредставлениеОшибки);
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;
	
	СтруктураВозврата = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Оповестить("ОбновитьСостояниеОбменСБанками", СтруктураВозврата.ПараметрОповещения);
	
	Если СтруктураВозврата <> Неопределено И СтруктураВозврата.Свойство("ДанныеЭП")
		И СтруктураВозврата.ДанныеЭП.Количество() Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ДанныеЭП", СтруктураВозврата.ДанныеЭП);
		ДополнительныеПараметры.Вставить("ЕстьОшибка", Ложь);
		СохранитьОчереднуюПодпись(ДополнительныеПараметры);
		
		Возврат;
		
	КонецЕсли;
	
	ПослеОбработкиПолученныхПодписей(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОбработкиПолученныхПодписей(ЕстьОшибка)
	
	Если ЕстьОшибка Тогда
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;

	ОбработатьРезультатТестаНастройки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьОчереднуюПодпись(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.ДанныеЭП.Количество() = 0 Тогда
		ПослеОбработкиПолученныхПодписей(ДополнительныеПараметры.ЕстьОшибка);
		Возврат;
	КонецЕсли;
	
	Для Каждого КлючЗначение Из ДополнительныеПараметры.ДанныеЭП Цикл
		Прервать;
	КонецЦикла;
	ДополнительныеПараметры.ДанныеЭП.Удалить(КлючЗначение.Ключ);
	
	Оповещение = Новый ОписаниеОповещения("ПослеСохраненияПодписейСообщенияОбмена", ЭтотОбъект, ДополнительныеПараметры);
	ОбменСБанкамиСлужебныйКлиент.ДобавитьПодписиИОпределитьСтатусы(Оповещение, КлючЗначение.Ключ, КлючЗначение.Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияПодписейСообщенияОбмена(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		ДополнительныеПараметры.Вставить("ЕстьОшибка", Истина);
	КонецЕсли;
	
	СохранитьОчереднуюПодпись(ДополнительныеПараметры)
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНастройкуОбменаИзФайла()
	
	Если СпособСозданияНастройки = 0 ИЛИ СпособСозданияНастройки = 2 Тогда // автоматическое получение и ручной режим
		
		УчастникиОбменаИзФайла = УчастникиОбменаИзФайла(АдресФайлаНастроекОбмена, Организация);
		
		Если УчастникиОбменаИзФайла = Неопределено Тогда
			ОбработатьРезультатСозданияНастройкиОбмена(Ложь);
			Возврат;
		КонецЕсли;
		
		Если УчастникиОбменаИзФайла.Организация <> Организация Тогда
			ТекстСообщения = НСтр("ru = 'Получены некорректные настройки обмена из банка.
										|Требуются настройки для организации: %1
										|Получены настройки для организации: %2'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Организация, УчастникиОбменаИзФайла.Организация);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			ОбработатьРезультатСозданияНастройкиОбмена(Ложь);
			Возврат;
		КонецЕсли;
	
		Если УчастникиОбменаИзФайла.Банк <> Банк Тогда
			ТекстСообщения = НСтр("ru = 'Получены некорректные настройки обмена из банка.
										|Требуются настройки для банка: %1
										|Получены настройки для банка: %2'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Банк, УчастникиОбменаИзФайла.Банк);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			ОбработатьРезультатСозданияНастройкиОбмена(Ложь);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗагружатьВК = СпособСозданияНастройки <> 2;

	Задание = ЗагрузитьНастройкуОбменаИзФайлаНаСервере(
		АдресФайлаНастроекОбмена, УникальныйИдентификатор, ЗагружатьВК, Организация, ЛокальныйФайл);
	
	АдресХранилища = Задание.АдресХранилища;
	
	Если Задание.Завершено Тогда
		Если Задание.Успешно Тогда
			ПослеСозданияНастройкиОбмена();
		Иначе
			ОбработатьРезультатСозданияНастройкиОбмена(Ложь);
		КонецЕсли;
	Иначе
		ИдентификаторЗадания = Задание.Идентификатор;
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
		ПодключитьОбработчикОжидания("ПроверитьВыполнениеЗаданияЗагрузкиНастройкиОбменаИзФайла", 1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыполнениеЗаданияЗагрузкиНастройкиОбменаИзФайла()
	
	Задание = ОбменСБанкамиСлужебныйВызовСервера.ПроверитьВыполнениеФоновогоЗаданияНаСервере(
		ИдентификаторЗадания, УникальныйИдентификатор);
	
	Если Задание.Завершено Тогда
		Если Задание.Успешно Тогда
			ПодключитьОбработчикОжидания("ПослеСозданияНастройкиОбмена", 0.1, Истина);
		Иначе
			ОбработатьРезультатСозданияНастройкиОбмена(Ложь);
		КонецЕсли;
	Иначе
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
		ПодключитьОбработчикОжидания(
			"ПроверитьВыполнениеЗаданияЗагрузкиНастройкиОбменаИзФайла", ПараметрыОбработчика.ТекущийИнтервал, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗагрузитьНастройкуОбменаИзФайлаНаСервере(Знач АдресФайлаНастроекОбмена, Знач ИдентификаторФормы, Знач ЗагружатьВК, Знач Организация, Знач ЛокальныйФайл = Ложь)
	
	Задание = Новый Структура();
	Задание.Вставить("Завершено", Ложь);
	Задание.Вставить("Успешно", Ложь);
	Задание.Вставить("Идентификатор");
	Задание.Вставить("АдресХранилища");
	
	Попытка
		ДвоичныеДанныеФайлаНастроек = ПолучитьИзВременногоХранилища(АдресФайлаНастроекОбмена);
		Параметры = Новый Структура;
		Параметры.Вставить("ДвоичныеДанныеФайлаНастроек", ДвоичныеДанныеФайлаНастроек);
		Параметры.Вставить("ЗагружатьВК", ЗагружатьВК);
		Параметры.Вставить("Организация", Организация);
		Параметры.Вставить("ЛокальныйФайл", ЛокальныйФайл);
		НазваниеЗадания = НСтр("ru = 'Прямой обмен с банком: Загрузка настроек обмена из файла'");
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НазваниеЗадания;
		РезультатФоновогоЗадания = ДлительныеОперации.ВыполнитьВФоне(
			"Справочники.НастройкиОбменСБанками.ЗагрузитьНастройкиОбменаИзФайла", Параметры, ПараметрыВыполнения);

		Если РезультатФоновогоЗадания.Статус = "Выполнено" Тогда
			Задание.Успешно = Истина;
			Задание.Завершено = Истина;
			Задание.АдресХранилища = РезультатФоновогоЗадания.АдресРезультата;
		ИначеЕсли РезультатФоновогоЗадания.Статус = "Ошибка" Тогда
			Задание.Завершено = Истина;
			ВидОперации = НСтр("ru = 'Загрузка настроек обмена из файла'");
			ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации,
				РезультатФоновогоЗадания.ПодробноеПредставлениеОшибки, РезультатФоновогоЗадания.КраткоеПредставлениеОшибки,
				"ОбменСБанками");
		ИначеЕсли РезультатФоновогоЗадания.Статус = "Отменено" Тогда
			Задание.Завершено = Истина;
		Иначе // Выполняется
			Задание.АдресХранилища = РезультатФоновогоЗадания.АдресРезультата;
			Задание.Идентификатор = РезультатФоновогоЗадания.ИдентификаторЗадания;
		КонецЕсли;
	Исключение
		Задание.Завершено = Истина;
		Текст = НСтр("ru = 'Не удалось загрузить настройки обмена.'");
		ВидОперации = НСтр("ru = 'Загрузка настроек обмена из файла'");
		ПодробныйТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			ВидОперации, ПодробныйТекстОшибки, Текст, "ОбменСБанками");
	КонецПопытки;
	
	Возврат Задание;
	
КонецФункции

&НаКлиенте
Процедура ПодобратьПрограммуКриптографииПослеПолученияДвоичныхДанныхСертификата(ВыгруженныеДанные, ДополнительныеПараметры) Экспорт
	
	ПараметрыОбработки = Новый Структура;
	ПараметрыОбработки.Вставить("ДанныеСертификата", ВыгруженныеДанные);
	Оповещение = Новый ОписаниеОповещения(
		"СохранитьСертификатВИнформационнойБазеПослеОпределенияПрограммыКриптографии", ЭтотОбъект, ПараметрыОбработки);
	
	ПодобратьПрограммуКриптографииДляСертификата(Оповещение, ВыгруженныеДанные);

КонецПроцедуры

&НаКлиенте
Процедура СохранитьСертификатВИнформационнойБазеПослеОпределенияПрограммыКриптографии(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда // не удалось подобрать программу криптографии
		Обработчик = Новый ОписаниеОповещения(
			"ПолучитьИдентификаторСессииПослеПолученияИнформацииОМенеджереКриптографии", ЭтотОбъект, ДополнительныеПараметры);
		МенеджерКриптографииНаКлиенте.НачатьПолучениеИнформацииМодуляКриптографии(Обработчик);
	Иначе
		СертификатАутентификацииСсылка = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.НайтиСоздатьСертификатЭП(
			ДополнительныеПараметры.ДанныеСертификата, Организация, Результат.Программа);
		Если Не ЗначениеЗаполнено(СертификатАутентификацииСсылка) Тогда
			Если СпособСозданияНастройки = 0 Тогда // автоматическое получение с сервера банка
				ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
			Иначе // ручное создание настройки обмена, переключаем на предыдущий шаг
				СменитьСтраницуШаги(Элементы.СтраницаПолучениеССервера);
			КонецЕсли;
			Возврат;
		КонецЕсли;
		ПолучитьИдентификаторСессииПоСертификату(СертификатАутентификацииСсылка, ДополнительныеПараметры.ДанныеСертификата)
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИдентификаторСессииПослеПолученияИнформацииОМенеджереКриптографии(ИнформацияМодуля, ДополнительныеПараметры) Экспорт
	
	СертификатАутентификацииСсылка = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.НайтиСоздатьСертификатЭП(
		ДополнительныеПараметры.ДанныеСертификата, Организация, ИнформацияМодуля.Имя);
			
	Если Не ЗначениеЗаполнено(СертификатАутентификацииСсылка) Тогда
		Если СпособСозданияНастройки = 0 Тогда // автоматическое получение с сервера банка
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Иначе // ручное создание настройки обмена, переключаем на предыдущий шаг
			СменитьСтраницуШаги(Элементы.СтраницаПолучениеССервера);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ПолучитьИдентификаторСессииПоСертификату(СертификатАутентификацииСсылка, ДополнительныеПараметры.ДанныеСертификата)
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДанныеСертификатаПослеПолучения(СертификатКриптографии, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(СертификатКриптографии) <> Тип("СертификатКриптографии") Тогда
		Если СпособСозданияНастройки = 0 Тогда // автоматическое получение с сервера банка
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Иначе // ручное создание настройки обмена, переключаем на предыдущий шаг
			СменитьСтраницуШаги(Элементы.СтраницаПолучениеССервера);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПодобратьПрограммуКриптографииПослеПолученияДвоичныхДанныхСертификата", ЭтотОбъект);
	
	СертификатКриптографии.НачатьВыгрузку(Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИдентификаторСессииПоСертификату(СертификатАутентификацииСсылка, ДанныеСертификата)
	
	Если Не ЗначениеЗаполнено(СертификатАутентификацииСсылка) Тогда
		Если СпособСозданияНастройки = 0 Тогда // автоматическое получение с сервера банка
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Иначе // ручное создание настройки обмена, переключаем на предыдущий шаг
			СменитьСтраницуШаги(Элементы.СтраницаПолучениеССервера);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ЭлектроннаяПодписьКлиент.УстановитьПарольСертификата(СертификатАутентификацииСсылка, ПарольСертификата);

	ПробнаяОперация = СпособСозданияНастройки = 2;
	Попытка
		ИдентификаторСессии = ОбменСБанкамиСлужебныйВызовСервера.МаркерБанкаПоСертификату(
			АдресСервераНастроек, "0", ДанныеСертификата, , ПробнаяОперация);
	Исключение
		Если НЕ ПробнаяОперация Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецЕсли;
	КонецПопытки;
	
	Если Не ЗначениеЗаполнено(ИдентификаторСессии) Тогда
		Если СпособСозданияНастройки = 0 Тогда // автоматическое получение с сервера банка
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Иначе // автоматическое получение настроек не поддерживается банком, переключаем на следующий шаг.
			СменитьСтраницуШаги(Элементы.СтраницаАсинхронныйОбмен);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ОписаниеДанных = Новый Структура;
	ОписаниеДанных.Вставить("Операция", НСтр("ru = 'Аутентификация на сервере банка'"));
	ОписаниеДанных.Вставить("ЗаголовокДанных", НСтр("ru = 'Аутентификация на сервере банка'"));
	ОписаниеДанных.Вставить("СообщитьОЗавершении", Ложь);
	МассивСертификатов = Новый Массив;
	МассивСертификатов.Добавить(СертификатАутентификацииСсылка);
	ОписаниеДанных.Вставить("ОтборСертификатов", МассивСертификатов);
	ОписаниеДанных.Вставить("БезПодтверждения", Истина);
	ОписаниеДанных.Вставить("ЭтоАутентификация", Истина);
	ОписаниеДанных.Вставить("Данные", ИдентификаторСессии);
	
	ОбработкаРезультата = Новый ОписаниеОповещения("ПослеРасшифровкиИдентификатораСессии", ЭтотОбъект);
	
	ЭлектроннаяПодписьКлиент.Расшифровать(ОписаниеДанных, , ОбработкаРезультата);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеРасшифровкиИдентификатораСессии(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		Если СпособСозданияНастройки = 0 Тогда // автоматическое получение с сервера банка
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Иначе // ручное создание настройки обмена, переключаем на предыдущий шаг, т.к. маркер не расшифровывается
			СменитьСтраницуШаги(Элементы.СтраницаПолучениеССервера);
		КонецЕсли;
		Возврат;
	КонецЕсли;

	ИдентификаторСессии = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.СтрокаИзДвоичныхДанных(Результат.РасшифрованныеДанные);

	ПолучитьНастройкиОбменаПослеПолученияИдентификатораСессии(ИдентификаторСессии);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьНастройкиОбменаССервераБанкаБазоваяАутентификация()
	
	НастройкаОбмена = ПредопределенноеЗначение("Справочник.НастройкиОбменСБанками.ПустаяСсылка");
	Обработчик = Новый ОписаниеОповещения("ПолучитьНастройкиОбменаПослеПолученияИдентификатораСессии", ЭтотОбъект);
	
	ДанныеАутентификации = Новый Структура("Логин, Пароль", Логин, Пароль);
	БазоваяВерсияФорматаАсинхронногоОбмена = ОбменСБанкамиКлиентСервер.БазоваяВерсияФорматаАсинхронногоОбмена();
	ОбменСБанкамиСлужебныйКлиент.ПолучитьМаркерБанкаПоЛогинуИПаролю(Обработчик, АдресСервераНастроек, "0",
		ДанныеАутентификации, БазоваяВерсияФорматаАсинхронногоОбмена, НастройкаОбмена, СпособСозданияНастройки = 2);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьНастройкиОбменаПослеПолученияИдентификатораСессии(ИдентификаторСессии, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(ИдентификаторСессии) Тогда
		Если СпособСозданияНастройки = 0 Тогда // автоматическое получение с сервера банка
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Иначе // ручное создание настройки обмена, переключаем на предыдущий шаг, т.к. пустой идентификатор сессии.
			СменитьСтраницуШаги(Элементы.СтраницаПолучениеССервера);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	// При ручном создании настройки обмена пробуем получить настройки с сервера банка.
	ПробнаяОперация = СпособСозданияНастройки = 2;
	
	ПараметрыПолученияНастроек = Новый Структура;
	ПараметрыПолученияНастроек.Вставить("АдресСервера", АдресСервераНастроек);
	ПараметрыПолученияНастроек.Вставить("Организация", Организация);
	ПараметрыПолученияНастроек.Вставить("Банк", Банк);
	ПараметрыПолученияНастроек.Вставить("ИдентификаторСессии", ИдентификаторСессии);
	ПараметрыПолученияНастроек.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	ПараметрыПолученияНастроек.Вставить("ПробнаяОперация", ПробнаяОперация);
	ПараметрыПолученияНастроек.Вставить("НомерБанковскогоСчета", НомерБанковскогоСчета);
	ПараметрыПолученияНастроек.Вставить("ИдентификаторОрганизации", "0");
	ПараметрыПолученияНастроек.Вставить("ВерсияФормата", ВерсияФормата);
	
	Задание = ЗагрузитьНастройкиОбменаНаСервере(ПараметрыПолученияНастроек, АдресФайлаНастроекОбмена);
	
	Если Задание.Завершено Тогда
		Если Задание.Успешно Тогда
			НастройкиОбмена = ПолучитьИзВременногоХранилища(АдресФайлаНастроекОбмена);
			Если ПробнаяОперация Тогда
				Если НастройкиОбмена = Неопределено Тогда // не удалось получить настройки в автоматическом режиме
					СменитьСтраницуШаги(Элементы.СтраницаАсинхронныйОбмен);
					Возврат;
				Иначе
					НастройкиЗагруженыИзФайла = Истина;
					СменитьСтраницуШаги(Элементы.СтраницаФинал);
				КонецЕсли;
			КонецЕсли;
			Если НастройкиОбмена = Неопределено Тогда
				ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
			Иначе
				ОбработатьРезультатПолученияНастроекССервераБанка(Истина);
				Элементы.СозданиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
				#Если ВебКлиент Тогда
					ОбновитьОтображениеДанных();
				#КонецЕсли
				ПодключитьОбработчикОжидания("СоздатьНастройкуОбмена", 0.1, Истина);
			КонецЕсли;
		Иначе
			Если ПробнаяОперация Тогда
				СменитьСтраницуШаги(Элементы.СтраницаАсинхронныйОбмен);
			Иначе
				ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
			КонецЕсли;
		КонецЕсли;
	Иначе
		ИдентификаторЗадания = Задание.Идентификатор;
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
		ПодключитьОбработчикОжидания("ПроверитьВыполнениеЗаданияПолученияНастроекОбмена", 1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗагрузитьНастройкиОбменаНаСервере(Знач ПараметрыПолученияНастроек, АдресФайлаНастроекОбмена)
	
	Задание = Новый Структура();
	Задание.Вставить("Завершено", Ложь);
	Задание.Вставить("Успешно", Ложь);
	Задание.Вставить("Значение");
	Задание.Вставить("Идентификатор");
	
	Попытка
		НазваниеЗадания = НСтр("ru = 'Прямой обмен с банком: Получение настроек обмена с сервера банка'");
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(
			ПараметрыПолученияНастроек.УникальныйИдентификатор);
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НазваниеЗадания;
		РезультатФоновогоЗадания = ДлительныеОперации.ВыполнитьВФоне(
			"Справочники.НастройкиОбменСБанками.ПолучитьНастройкиОбменаССервераБанка", ПараметрыПолученияНастроек,
			ПараметрыВыполнения);
			
		Если РезультатФоновогоЗадания.Статус = "Выполнено" Тогда
			Задание.Успешно = Истина;
			Задание.Завершено = Истина;
			АдресФайлаНастроекОбмена = РезультатФоновогоЗадания.АдресРезультата;
		ИначеЕсли РезультатФоновогоЗадания.Статус = "Ошибка" Тогда
			Задание.Завершено = Истина;
			Если НЕ ПараметрыПолученияНастроек.ПробнаяОперация Тогда
				ВидОперации = НСтр("ru = 'Получение настроек обмена с сервера банка'");
				ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации,
					РезультатФоновогоЗадания.ПодробноеПредставлениеОшибки, РезультатФоновогоЗадания.КраткоеПредставлениеОшибки,
					"ОбменСБанками");
			КонецЕсли;
		ИначеЕсли РезультатФоновогоЗадания.Статус = "Отменено" Тогда
			Задание.Завершено = Истина;
		Иначе // выполняется
			АдресФайлаНастроекОбмена = РезультатФоновогоЗадания.АдресРезультата;
			Задание.Идентификатор = РезультатФоновогоЗадания.ИдентификаторЗадания;
		КонецЕсли;
	Исключение
		Задание.Завершено = Истина;
		Если НЕ ПараметрыПолученияНастроек.ПробнаяОперация Тогда
			Текст = НСтр("ru = 'Не удалось загрузить настройки обмена с сервера банка.'");
			ВидОперации = НСтр("ru = 'Получение настроек обмена с сервера банка'");
			ПодробныйТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
				ВидОперации, ПодробныйТекстОшибки, Текст, "ОбменСБанками");
		КонецЕсли;
	КонецПопытки;
	
	Возврат Задание;
	
КонецФункции

&НаКлиенте
Процедура ПроверитьВыполнениеЗаданияПолученияНастроекОбмена()
	
	Задание = ОбменСБанкамиСлужебныйВызовСервера.ПроверитьВыполнениеФоновогоЗаданияНаСервере(
		ИдентификаторЗадания, УникальныйИдентификатор);
	Если Задание.Завершено Тогда
		Если Задание.Успешно Тогда
			Если СпособСозданияНастройки = 2 Тогда // ручной режим, попытка получить настройки автоматически
				НастройкиОбмена = ПолучитьИзВременногоХранилища(АдресФайлаНастроекОбмена);
				Если НастройкиОбмена = Неопределено Тогда // не удалось получить настройки в автоматическом режиме
					СменитьСтраницуШаги(Элементы.СтраницаАсинхронныйОбмен);
					Возврат;
				Иначе
					СменитьСтраницуШаги(Элементы.СтраницаФинал);
				КонецЕсли;
			КонецЕсли;
			ОбработатьРезультатПолученияНастроекССервераБанка(Истина);
			Элементы.СозданиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
			#Если ВебКлиент Тогда
				ОбновитьОтображениеДанных();
			#КонецЕсли
			ПодключитьОбработчикОжидания("СоздатьНастройкуОбмена", 0.1, Истина);
		Иначе
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		КонецЕсли;
	Иначе
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
		ПодключитьОбработчикОжидания(
			"ПроверитьВыполнениеЗаданияПолученияНастроекОбмена", ПараметрыОбработчика.ТекущийИнтервал, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокВыбораСертификатовАутентификации()
	
	Оповещение = Новый ОписаниеОповещения("ПолучитьЛичныеСертификатыПослеСозданияМенеджераКриптографии", ЭтотОбъект);
	ЭлектроннаяПодписьКлиент.СоздатьМенеджерКриптографии(Оповещение, "ПолучениеСертификатов", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокВыбораСертификатовАутентификацииСВыводомОшибки()
	
	Оповещение = Новый ОписаниеОповещения("ПолучитьЛичныеСертификатыПослеСозданияМенеджераКриптографии", ЭтотОбъект);
	ЭлектроннаяПодписьКлиент.СоздатьМенеджерКриптографии(Оповещение, "ПолучениеСертификатов");
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьЛичныеСертификатыПослеСозданияМенеджераКриптографии(МенеджерКриптографии, ДополнительныеПараметры) Экспорт

	Если НЕ ТипЗнч(МенеджерКриптографии) = Тип("МенеджерКриптографии") Тогда
		Возврат;
	Иначе
		МенеджерКриптографииНаКлиенте = МенеджерКриптографии;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПолучитьСертификатыПослеПолученияХранилища", ЭтотОбъект, ДополнительныеПараметры);
		
	МенеджерКриптографии.НачатьПолучениеХранилищаСертификатов(
		ОписаниеОповещения, ТипХранилищаСертификатовКриптографии.ПерсональныеСертификаты);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСертификатыПослеПолученияХранилища(ХранилищеСертификатовКриптографии, ДополнительныеПараметры) Экспорт
	
	ОповещениеПослеПолученияСертификатов = Новый ОписаниеОповещения(
		"ЗаполнитьСписокВыбораСертификатовПослеПолученияИзХранилища", ЭтотОбъект, ДополнительныеПараметры);
		
	ХранилищеСертификатовКриптографии.НачатьПолучениеВсех(ОповещениеПослеПолученияСертификатов);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокВыбораСертификатовПослеПолученияИзХранилища(Сертификаты, ДополнительныеПараметры) Экспорт
	
	Если НЕ Сертификаты.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	Если УпрощенныйРежимНастройки Тогда
		СертификатСписокВыбора = Элементы.СертификатСмешанныйУН.СписокВыбора;
	Иначе
		СертификатСписокВыбора = Элементы.СертификатСмешанный.СписокВыбора;
	КонецЕсли;
	
	СертификатСписокВыбора.Очистить();
	ТекущаяДатаСеанса = ОбщегоНазначенияКлиент.ДатаСеанса();
	
	Для Каждого Сертификат Из Сертификаты Цикл
		Если Сертификат.ДатаОкончания < ТекущаяДатаСеанса Тогда
			Продолжить;
		КонецЕсли;
		
		ПредставлениеСертификата = ЭлектроннаяПодписьКлиентСервер.ПредставлениеСертификата(Сертификат);
		Если Сертификат.Субъект.Свойство("O") И ЗначениеЗаполнено(Сертификат.Субъект.O) Тогда
			ПредставлениеСертификата = ПредставлениеСертификата + " (" + Сертификат.Субъект.O + ")";
		КонецЕсли;
		ОтпечатокСертификата = Base64Строка(Сертификат.Отпечаток);
		СертификатСписокВыбора.Добавить(ОтпечатокСертификата, ПредставлениеСертификата);
	КонецЦикла;
	
	СертификатСписокВыбора.СортироватьПоПредставлению();
	
	Если УпрощенныйРежимНастройки Тогда
		ЕстьСертификаты = СертификатСписокВыбора.Количество() > 0;
		СменитьСтраницуШаги(Элементы.СтраницаУпрощенныйРежимНастройки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СертификатыАутентификации(НастройкаОбмена, ОтпечаткиСертификатов)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиОбменСБанкамиСертификатыПодписейОрганизации.СертификатЭП
	|ИЗ
	|	Справочник.НастройкиОбменСБанками.СертификатыПодписейОрганизации КАК НастройкиОбменСБанкамиСертификатыПодписейОрганизации
	|ГДЕ
	|	НастройкиОбменСБанкамиСертификатыПодписейОрганизации.Ссылка = &НастройкаОбмена
	|	И &ПроверкаПользователя
	|	И НЕ НастройкиОбменСБанкамиСертификатыПодписейОрганизации.СертификатЭП.ПометкаУдаления
	|	И НастройкиОбменСБанкамиСертификатыПодписейОрганизации.СертификатЭП.Отпечаток В(&ОтпечаткиСертификатов)";
	Запрос.УстановитьПараметр("НастройкаОбмена", НастройкаОбмена);
	Запрос.УстановитьПараметр("ОтпечаткиСертификатов", ОтпечаткиСертификатов);
	
	Если Пользователи.ЭтоПолноправныйПользователь( , , Ложь) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПроверкаПользователя", "ИСТИНА");
	Иначе
		Запрос.УстановитьПараметр("ПользовательНеУказан", Пользователи.СсылкаНеуказанногоПользователя());
		Запрос.УстановитьПараметр("Пользователь",  Пользователи.АвторизованныйПользователь());
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПроверкаПользователя",
			"НастройкиОбменСБанкамиСертификатыПодписейОрганизации.СертификатЭП.Пользователь В (&Пользователь, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка), &ПользовательНеУказан)");
	КонецЕсли;
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат.ВыгрузитьКолонку("СертификатЭП");
	
КонецФункции

&НаСервереБезКонтекста
Функция БИКБанка(Банк)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Банк, "Код");
	
КонецФункции

#КонецОбласти

#Область Сбербанк

&НаСервереБезКонтекста
Функция ЗапросСертификатовСбербанка()
	
	Попытка
		Request = ОбменСБанкамиСлужебный.ОбъектТипаCML(ФабрикаXDTO, "Request", "http://bssys.com/upg/request");
		ИдентификаторЗапроса = Новый УникальныйИдентификатор;
		ТекстОшибки = "";
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(
			Request, "requestId", Строка(ИдентификаторЗапроса), Истина, ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(
			Request, "orgId", "00000000-0000-0000-0000-000000000000", Истина, ТекстОшибки);
		ВерсияФорматаСбербанк = ОбменСБанкамиСлужебный.ВерсияФорматаСбербанк();
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(Request, "version", ВерсияФорматаСбербанк, Истина, ТекстОшибки);
		ПредставлениеОтправителяСбербанк = ОбменСБанкамиСлужебный.ПредставлениеОтправителяСбербанк();
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(
			Request, "sender", ПредставлениеОтправителяСбербанк, Истина, ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(Request, "receiver", "SBBOL_DBO", Истина, ТекстОшибки);
		
		ЗапросСертификатов = ОбменСБанкамиСлужебный.ОбъектТипаCML(ФабрикаXDTO,
			"Request.CryptoIncoming.Certificates", "http://bssys.com/upg/request");
			
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(ЗапросСертификатов, "client", "1", , ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(ЗапросСертификатов, "bank", "1", , ТекстОшибки);
		
		ЗапросКриптографии = ОбменСБанкамиСлужебный.ОбъектТипаCML(
			ФабрикаXDTO, "Request.CryptoIncoming", "http://bssys.com/upg/request");

		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(ЗапросКриптографии, "Certificates", ЗапросСертификатов, , ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(Request, "CryptoIncoming", ЗапросКриптографии, Истина, ТекстОшибки);
		
		Request.Проверить();
			
		Запись = Новый ЗаписьXML;
		Запись.УстановитьСтроку("UTF-8");
		Запись.ЗаписатьОбъявлениеXML();
		ФабрикаXDTO.ЗаписатьXML(Запись, Request);
		ТекстЗапроса = Запись.Закрыть();
		
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Возврат Неопределено;
		КонецЕсли;

		
	Исключение
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Операция = НСтр("ru = 'Формирование запроса персональных данных организации'");
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ТекстОшибки, ТекстСообщения, "ОбменСБанками");
		Возврат Неопределено;
	КонецПопытки;
		
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаданиеПоПолучениюURLИнфоФайлаСбербанк(Знач URLВК)

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Скачивание внешней компоненты с интернет.'");
	
	ПараметрыПроцедуры = Новый Структура("URLВК", URLВК);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСБанкамиСлужебный.ОпределитьURLИнфоФайла", ПараметрыПроцедуры, ПараметрыВыполнения);
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗагрузитьСертификатыСбербанка(Знач Данные, Знач Организация, Знач УникальныйИдентификатор, Успех, СертификатыКлиента, СертификатБанкаСсылка)
	
	Успех = Ложь;
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(Данные);
	
	Попытка
		ЭД = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML);
	Исключение
		ЧтениеXML.Закрыть();
		ТекстСообщения = НСтр("ru = 'Возникла ошибка при чтении полученных данных из банка'");
		ТекстОшибки = НСтр("ru = 'Возникла ошибка при чтении полученных данных из банка
							|Текст ошибки: %1'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстОшибки = СтрШаблон(ТекстОшибки, ПодробноеПредставлениеОшибки);
		ВидОперации = НСтр("ru = 'Чтение полученных данных из банка'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			ВидОперации, ТекстОшибки, ТекстСообщения, "ОбменСБанками");
		Возврат;
	КонецПопытки;
	
	СертификатыКлиента = Новый Массив;
	
	Для Каждого ДанныеСертификата Из ЭД.Certificates.Certificate Цикл
		
		Bank = ЭлектронноеВзаимодействиеСлужебный.ЗначениеСвойстваXDTO(ДанныеСертификата, "Bank");
		active = ЭлектронноеВзаимодействиеСлужебный.ЗначениеСвойстваXDTO(ДанныеСертификата, "active");
		client = ЭлектронноеВзаимодействиеСлужебный.ЗначениеСвойстваXDTO(ДанныеСертификата, "client");
		
		Если Bank = "1" И active = "1" Тогда
			СертификатСтрокой = ДанныеСертификата.Последовательность().ПолучитьТекст(0);
			ДвоичныеДанныеСертификата = Base64Значение(СертификатСтрокой);
			Попытка
				ДвоичныеДанныеСертификата = ОбменСБанкамиСлужебный.ДанныеСертификатаВФорматеDER(ДвоичныеДанныеСертификата);
			Исключение
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				Возврат;
			КонецПопытки;
			СертификатБанкаСсылка = ПоместитьВоВременноеХранилище(ДвоичныеДанныеСертификата, УникальныйИдентификатор);
		ИначеЕсли client = "1" И active = "1" Тогда
			СертификатСтрокой = ДанныеСертификата.Последовательность().ПолучитьТекст(0);
			ДвоичныеДанныеСертификата = Base64Значение(СертификатСтрокой);
			Попытка
				ДвоичныеДанныеСертификата = ОбменСБанкамиСлужебный.ДанныеСертификатаВФорматеDER(ДвоичныеДанныеСертификата);
			Исключение
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				Возврат;
			КонецПопытки;
			НовыйСертификат = ОбменСБанкамиСлужебныйВызовСервера.СоздатьСертификатСбербанка(
				ДвоичныеДанныеСертификата, Организация);
			Если НЕ ЗначениеЗаполнено(НовыйСертификат) Тогда
				Возврат;
			КонецЕсли;
			
			СертификатыКлиента.Добавить(НовыйСертификат);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Успех = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьВКСбербанка()
	
	Оповещение = Новый ОписаниеОповещения("ПослеСкачиванияВнешнейКомпонентыСбербанка", ЭтотОбъект);
	ДополнительныеВнешниеКомпонентыКлиент.СкачатьВнешнююКомпоненту(Оповещение, URLВК);
	
КонецПроцедуры

&НаКлиенте
Процедура АутентификацияНаСервереПослеУстановкиКаналаСбербанк(Успех, ДополнительныеПараметры) Экспорт
	
	Если Не Успех Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеАутентификацииПоСертификатуСбербанк", ЭтотОбъект);
	ОбменСБанкамиСлужебныйКлиент.АутентифицироватьсяПоСертификатуСбербанк(Оповещение, ИмяВнешнегоМодуля, НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСкачиванияВнешнейКомпонентыСбербанка(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	ИмяВнешнегоМодуля = Результат.Идентификатор;
	
	Оповещение = Новый ОписаниеОповещения("ПослеБазовойАутентификацииСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ЛогинПароль = Новый Структура("Логин, Пароль", Логин, Пароль);
		
	ОбменСБанкамиСлужебныйКлиент.ВыполнитьБазовуюАутентификациюСбербанк(
		Оповещение, ИмяВнешнегоМодуля, УникальныйИдентификатор, НастройкаОбмена, ЛогинПароль);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеБазовойАутентификацииСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Успех Тогда
		ПолучитьНастройкиОбменаСбербанк();
	ИначеЕсли Результат.ТребуетсяТокен Тогда
		ИспользуетсяКриптография = Истина;
		Обработчик = Новый ОписаниеОповещения("УстановитьКаналПослеАутентификацииНаТокенеСбербанк", ЭтотОбъект);
		ОбменСБанкамиСлужебныйКлиент.АутентифицироватьсяНаТокенеСбербанка(
			Обработчик, ИмяВнешнегоМодуля, НастройкаОбмена, Истина);
	Иначе
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьНастройкиОбменаСбербанк()
	
	Ошибка = Ложь;
	ОтправитьЗапросПерсональныхДанныхСбербанк(НастройкаОбмена, УникальныйИдентификатор, ТикетОбменаСбербанк, Ошибка);
	
	ПараметрыЖурналирования = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыЖурналирования(НастройкаОбмена);
	
	Если ПараметрыЖурналирования.ИспользоватьЖурналирование Тогда
		ОбменСБанкамиСлужебныйКлиент.СохранитьЖурналВФайл(НастройкаОбмена, ПараметрыЖурналирования.КаталогДляЖурналирования);
	КонецЕсли;
	
	Если Ошибка Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	ПараметрыОбработчикаОжидания = Новый Структура();
	ПараметрыОбработчикаОжидания.Вставить("МинимальныйИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("МаксимальныйИнтервал", 15);
	ПараметрыОбработчикаОжидания.Вставить("ТекущийИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("КоэффициентУвеличенияИнтервала", 1.4);

	ПодключитьОбработчикОжидания("Подключаемый_ПолучитьЛичнуюИнформациюСбербанк", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиЗапросаСертификатовСбербанк(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Ответ) Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	ТикетОбменаСбербанк = Ответ;
	
	ПараметрыОбработчикаОжидания = Новый Структура();
	ПараметрыОбработчикаОжидания.Вставить("МинимальныйИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("МаксимальныйИнтервал", 15);
	ПараметрыОбработчикаОжидания.Вставить("ТекущийИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("КоэффициентУвеличенияИнтервала", 1.4);

	ПодключитьОбработчикОжидания("Подключаемый_ПолучитьСертификатыССервераСбербанк", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиЗапросаПерсональныхДанныхСбербанк(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Ответ) Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	ТикетОбменаСбербанк = Ответ;
	
	ПараметрыОбработчикаОжидания = Новый Структура();
	ПараметрыОбработчикаОжидания.Вставить("МинимальныйИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("МаксимальныйИнтервал", 15);
	ПараметрыОбработчикаОжидания.Вставить("ТекущийИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("КоэффициентУвеличенияИнтервала", 1.4);

	ПодключитьОбработчикОжидания("Подключаемый_ПолучитьЛичнуюИнформациюЧерезТокенСбербанк", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТестНастройкиОбменаСбербанк()
	
	Если Не ИспользуетсяКриптография Тогда
		Ошибка = Ложь;
		ОтправитьТестовыйЗапросСбербанк(НастройкаОбмена, ИдентификаторОрганизации, УникальныйИдентификатор, Ошибка);
		
		ПараметрыЖурналирования = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыЖурналирования(НастройкаОбмена);
		Если ПараметрыЖурналирования.ИспользоватьЖурналирование Тогда
			ОбменСБанкамиСлужебныйКлиент.СохранитьЖурналВФайл(
				НастройкаОбмена, ПараметрыЖурналирования.КаталогДляЖурналирования);
		КонецЕсли;
		ОбработатьРезультатТестаНастройки(Не Ошибка);
	ИначеЕсли НастройкиОбменаСБанком <> Неопределено И ЗначениеЗаполнено(НастройкиОбменаСБанком.АдресСервера) Тогда
		Оповещение = Новый ОписаниеОповещения("ПодписатьТестовыйЗапросПослеОпределенияСертификатаСбербанк", ЭтотОбъект);
		ОбменСБанкамиСлужебныйКлиент.ОпределитьСертификатПодписиСбербанк(Оповещение, ИмяВнешнегоМодуля, НастройкаОбмена);
	Иначе // настройки загружены из файла
		Оповещение = Новый ОписаниеОповещения("ТестНастройкиПослеУстановкиКаналаСбербанк", ЭтотОбъект);
		ОбменСБанкамиСлужебныйКлиент.УстановитьСоединениеИАутентифицироватьсяНаСервереСбербанк(Оповещение, НастройкаОбмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТестНастройкиПослеУстановкиКаналаСбербанк(Успех, ДополнительныеПараметры) Экспорт
	
	Если Не Успех Тогда
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;

	Оповещение = Новый ОписаниеОповещения("ПодписатьТестовыйЗапросПослеОпределенияСертификатаСбербанк", ЭтотОбъект);
	ОбменСБанкамиСлужебныйКлиент.ОпределитьСертификатПодписиСбербанк(Оповещение, ИмяВнешнегоМодуля, НастройкаОбмена);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьКаналПослеАутентификацииНаТокенеСбербанк(Успех, ДополнительныеПараметры) Экспорт
	
	Если Не Успех Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("АутентификацияНаСервереПослеУстановкиКаналаСбербанк", ЭтотОбъект);
	ОбменСБанкамиСлужебныйКлиент.УстановитьКаналНаТокенеСбербанк(Оповещение, ИмяВнешнегоМодуля);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьТестовыйЗапросПослеУстановкиПодписиСбербанк(ПодписьВерна, ДополнительныеПараметры) Экспорт
	
	Если Не ПодписьВерна Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Установленная подпись неверна.'"));
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;
	
	ДанныеПодписи = Новый Структура;
	ДанныеПодписи.Вставить("ЭП", ДополнительныеПараметры.ДанныеПодписи);
	ДанныеПодписи.Вставить("СертификатПодписи", ДополнительныеПараметры.СертификатСсылка);
	
	Попытка
		ТестоваяСтрока = ОбменСБанкамиСлужебныйВызовСервера.ТекстЗапросаНочнойВыписки(НастройкаОбмена,
			ДополнительныеПараметры.ИдентификаторЗапроса, ИдентификаторОрганизации, ДанныеПодписи);
	Исключение
		ВидОперации = НСтр("ru = 'Формирование запроса новых документов'");
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			ВидОперации, ТекстОшибки, ТекстСообщения, 1, НастройкаОбмена);
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецПопытки;
	
	ТикетОбменаСбербанк = Неопределено;
	
	Оповещение = Новый ОписаниеОповещения("ПослеОтправкиТестовогоЗапросаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбменСБанкамиСлужебныйКлиент.ВыполнитьОтправкуДанныхЧерезТокенСбербанк(Оповещение, ТестоваяСтрока, НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьТестовыйЗапросПослеОпределенияСертификатаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено ИЛИ Результат = КодВозвратаДиалога.Отмена Тогда
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;
	
	ИдентификаторЗапроса = Строка(Новый УникальныйИдентификатор);
	СтрокаПодписи = "ATTRIBUTES" + Символ(10) + "OrgId=" + ИдентификаторОрганизации + Символ(10) + "RequestId="
		+ ИдентификаторЗапроса;

	СтрокаПодписиBase64 = ОбменСБанкамиСлужебныйВызовСервера.СтрокаBase64БезBOM(СтрокаПодписи);
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СтрокаПодписиBase64", СтрокаПодписиBase64);
	ДополнительныеПараметры.Вставить("СертификатСсылка", Результат.СертификатСсылка);
	ДополнительныеПараметры.Вставить("ИдентификаторЗапроса", ИдентификаторЗапроса);
	
	Представление = НСтр("ru = 'Тестовый запрос'");
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПроверитьПодписьПослеУстановкиСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ОбменСБанкамиСлужебныйКлиент.ПодписатьДанныеСбербанк(
		ОписаниеОповещения, ИмяВнешнегоМодуля, СтрокаПодписиBase64, Результат.ИдентификаторСертификата, Представление);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодписьПослеУстановкиСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено ИЛИ Результат = КодВозвратаДиалога.Отмена Тогда // Пользователь отказался подписывать
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	ИначеЕсли Результат.Успех Тогда
		
		ПараметрыСертификата = Новый Структура("СертификатBase64");
		
		ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовСертификата(
			ДополнительныеПараметры.СертификатСсылка, ПараметрыСертификата);
		
		ДополнительныеПараметры.Вставить("ДанныеПодписи", Результат.ЭП);
		
		Оповещение = Новый ОписаниеОповещения(
			"ОтправитьТестовыйЗапросПослеУстановкиПодписиСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		
		ОбменСБанкамиСлужебныйКлиент.ПроверитьПодписьНаТокенеСбербанк(Оповещение,
			ДополнительныеПараметры.СтрокаПодписиBase64, Результат.ЭП, ПараметрыСертификата.СертификатBase64,
			ИмяВнешнегоМодуля);
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ТекстОшибки);
		ОбработатьРезультатТестаНастройки(Ложь);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиТестовогоЗапросаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	ТикетОбменаСбербанк = Результат;
	
	Если ЗначениеЗаполнено(ТикетОбменаСбербанк) Тогда
		ОбработатьРезультатТестаНастройки(Истина);
	Иначе
		ОбработатьРезультатТестаНастройки(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеАутентификацииПоСертификатуСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;

	ЗапросПерсональныхДанных = ОбменСБанкамиСлужебныйВызовСервера.ЗапросПерсональныхДанныхОрганизацииСбербанк();
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеОтправкиЗапросаПерсональныхДанныхСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбменСБанкамиСлужебныйКлиент.ВыполнитьОтправкуДанныхЧерезТокенСбербанк(Оповещение, ЗапросПерсональныхДанных);
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьURLИнфоФайлаСбербанк()
	
	Результат = ЗаданиеПоПолучениюURLИнфоФайлаСбербанк(URLВК);
	
	Если Результат.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		Оповещение = Новый ОписаниеОповещения("ПослеПолученияURLИнфоФайлаСбербанк", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'Скачивание внешней компоненты.'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			ВидОперации, Результат.ПодробноеПредставлениеОшибки, Результат.КраткоеПредставлениеОшибки);
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
	Иначе
		ПослеПолученияURLИнфоФайлаСбербанк(Результат, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолучитьЛичнуюИнформациюСбербанк()
	
	Ошибка = Ложь; Ожидать = Ложь; ПараметрыОбмена = Неопределено;
	ПолучитьПерсональныеДанныеОрганизацииПоТикетуЗапроса(НастройкаОбмена,
		УникальныйИдентификатор, ТикетОбменаСбербанк, ПараметрыОбмена, Ожидать, Ошибка);
		
	ПараметрыЖурналирования = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыЖурналирования(
		НастройкаОбмена);
	Если ПараметрыЖурналирования.ИспользоватьЖурналирование Тогда
		ОбменСБанкамиСлужебныйКлиент.СохранитьЖурналВФайл(
			НастройкаОбмена, ПараметрыЖурналирования.КаталогДляЖурналирования);
	КонецЕсли;
		
	Если Ошибка Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
		
	Если Ожидать Тогда
		ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.ТекущийИнтервал
												* ПараметрыОбработчикаОжидания.КоэффициентУвеличенияИнтервала;
		Если ПараметрыОбработчикаОжидания.ТекущийИнтервал > ПараметрыОбработчикаОжидания.МаксимальныйИнтервал Тогда
			ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.МаксимальныйИнтервал;
		КонецЕсли;
		ПодключитьОбработчикОжидания(
			"Подключаемый_ПолучитьЛичнуюИнформациюСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	Иначе
		ИдентификаторОрганизации = ПараметрыОбмена.ИдентификаторОрганизации;
		СпособАутентификации = 1;
		ИспользуетсяКриптография = Ложь;
		ОбработатьРезультатПолученияНастроекССервераБанка(Истина);
		Элементы.СозданиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
		ПодключитьОбработчикОжидания("СоздатьНастройкуОбмена", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолучитьСертификатыССервераСбербанк()
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияОтветаНаЗапросСертификатовСбербанк", ЭтотОбъект);
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьСтатусЗапросаСбербанк(Оповещение, ИдентификаторОрганизации, ТикетОбменаСбербанк);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСертификатыССервераСбербанка()
	
	ЗапросСертификатов = ЗапросСертификатовСбербанка();

	Если Не ЗначениеЗаполнено(ЗапросСертификатов) Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеОтправкиЗапросаСертификатовСбербанк", ЭтотОбъект);
	ОбменСБанкамиСлужебныйКлиент.ВыполнитьОтправкуДанныхЧерезТокенСбербанк(Оповещение, ЗапросСертификатов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияURLИнфоФайлаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		URLВК = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		ПодключитьОбработчикОжидания("ЗагрузитьВКСбербанка", 0.1, Истина);
	Иначе // Ошибка
		ВидОперации = НСтр("ru = 'Скачивание внешней компоненты.'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			ВидОперации, Результат.ПодробноеПредставлениеОшибки, Результат.КраткоеПредставлениеОшибки);
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияОтветаНаЗапросСертификатовСбербанк(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = Неопределено Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	Если Ответ = "<!--NOT PROCESSED YET-->" ИЛИ Ответ = "<!--NOT_PROCESSED_YET-->" Тогда
		ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.ТекущийИнтервал
												* ПараметрыОбработчикаОжидания.КоэффициентУвеличенияИнтервала;
		Если ПараметрыОбработчикаОжидания.ТекущийИнтервал > ПараметрыОбработчикаОжидания.МаксимальныйИнтервал Тогда
			ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.МаксимальныйИнтервал;
		КонецЕсли;
		ПодключитьОбработчикОжидания(
			"Подключаемый_ПолучитьСертификатыССервераСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	ИначеЕсли Ответ = "<!--REQUEST NOT FOUND-->" ИЛИ Ответ = "<!--REQUEST_NOT_FOUND-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка: запрос не найден.
							|Повторите операцию позже.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
	ИначеЕсли Ответ = "<!--REQUESTID DUBLIC-->" ИЛИ Ответ = "<!--REQUESTID_DUBLIC-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка: запрос с таким идентификатором уже существует.
							|Повторите операцию позже.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
	Иначе
		СертификатыКлиента = Новый Массив;
		АдресФайлаСертификатаСбербанка = "";
		Успех = Ложь;
		ЗагрузитьСертификатыСбербанка(
			Ответ, Организация, УникальныйИдентификатор, Успех, СертификатыКлиента, АдресФайлаСертификатаСбербанка);
		Если Не Успех Тогда
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
			Возврат;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(АдресФайлаСертификатаСбербанка) Тогда
			ТекстСообщения = НСтр("ru = 'Не получен сертификат банка.
										|Повторите операцию позже или обратитесь в техподдержку банка.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
			Возврат;
		КонецЕсли;
		
		Если СертификатыКлиента.Количество() = 0 Тогда
			ТекстСообщения = НСтр("ru = 'Не получен клиентский сертификат из банка.
										|Повторите операцию позже или обратитесь в техподдержку банка.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
			Возврат;
		КонецЕсли;
		
		Для Каждого СертификатКлиента Из СертификатыКлиента Цикл
			НовСтрока = Сертификаты.Добавить();
			НовСтрока.Сертификат = СертификатКлиента;
		КонецЦикла;
		
		ОбработатьРезультатПолученияНастроекССервераБанка(Истина);
		Элементы.СозданиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
		#Если ВебКлиент Тогда
			ОбновитьОтображениеДанных();
		#КонецЕсли
		ПодключитьОбработчикОжидания("СоздатьНастройкуОбмена", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияСтатусаЗапросаСбербанк(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = Неопределено Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	Если Ответ = "<!--NOT PROCESSED YET-->" ИЛИ Ответ = "<!--NOT_PROCESSED_YET-->" Тогда
		ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.ТекущийИнтервал
												* ПараметрыОбработчикаОжидания.КоэффициентУвеличенияИнтервала;
		Если ПараметрыОбработчикаОжидания.ТекущийИнтервал > ПараметрыОбработчикаОжидания.МаксимальныйИнтервал Тогда
			ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.МаксимальныйИнтервал;
		КонецЕсли;
		ПодключитьОбработчикОжидания(
			"Подключаемый_ПолучитьЛичнуюИнформациюЧерезТокенСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	ИначеЕсли Ответ = "<!--REQUEST NOT FOUND-->" ИЛИ Ответ = "<!--REQUEST_NOT_FOUND-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка: запрос не найден.
							|Повторите операцию позже.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Элементы.ПолучениеНастроекОбменаКартинка.Картинка = БиблиотекаКартинок.Ошибка32;
		Элементы.ДекорацияНеПодключено.Видимость = Истина;
	ИначеЕсли Ответ = "<!--REQUESTID DUBLIC-->" ИЛИ Ответ = "<!--REQUESTID_DUBLIC-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка: запрос с таким идентификатором уже существует.
							|Повторите операцию позже.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Элементы.ПолучениеНастроекОбменаКартинка.Картинка = БиблиотекаКартинок.Ошибка32;
		Элементы.ДекорацияНеПодключено.Видимость = Истина;
	Иначе
		ПараметрыОбменаСбербанк = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыОбменаСбербанк(Ответ);
		Если Не ЗначениеЗаполнено(ПараметрыОбменаСбербанк) Тогда
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
			Возврат;
		КонецЕсли;
		ИдентификаторОрганизации = ПараметрыОбменаСбербанк.ИдентификаторОрганизации;
		ПолучитьСертификатыССервераСбербанка();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтправитьТестовыйЗапросСбербанк(Знач НастройкаОбмена, Знач ИдентификаторОрганизации, Знач УникальныйИдентификатор, Ошибка)
	
	ИдентификаторЗапроса = Строка(Новый УникальныйИдентификатор);
	
	ТестоваяСтрока = ОбменСБанкамиСлужебныйВызовСервера.ТекстЗапросаНочнойВыписки(
		НастройкаОбмена, ИдентификаторЗапроса, ИдентификаторОрганизации);
		
	Данные = Новый Массив;
	Данные.Добавить(ТестоваяСтрока);
	Тикеты = Неопределено; ТребуетсяАутентификация = Ложь;
	ТекущаяСессия = ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(УникальныйИдентификатор);

	Попытка
		ОбменСБанкамиСлужебный.ОтправитьДанныеВСбербанк(НастройкаОбмена, ТекущаяСессия.ИдентификаторСессии, Данные, Тикеты,
			ТребуетсяАутентификация);
	Исключение
		ВидОперации = НСтр("ru = 'Отправка тестового запроса в банк'");
		ПодробныйТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			ВидОперации, ПодробныйТекстОшибки, ТекстСообщения, 1, НастройкаОбмена);
		Ошибка = Истина;
	КонецПопытки;
	
	Если ТребуетсяАутентификация Тогда
		ТекстСообщения = НСтр("ru = 'Требуется аутентификация'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Ошибка = Истина;
	КонецЕсли;

	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПолучитьПерсональныеДанныеОрганизацииПоТикетуЗапроса(Знач НастройкаОбмена, Знач КлючСессии, Знач Тикет, ПараметрыОбмена, Ожидать, Ошибка)
	
	ВидОперации = НСтр("ru = 'Формирование запроса статуса документа'");

	Попытка
		
		МассивТикетов = Новый Массив;
		МассивТикетов.Добавить(Тикет);
		ИдентификаторСессии = ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(КлючСессии).ИдентификаторСессии;
		МассивОтветов = Неопределено;
		
		ОбменСБанкамиСлужебный.ПолучитьОтветыПоТикетамСбербанк(НастройкаОбмена, МассивТикетов, ИдентификаторСессии,
			"00000000-0000-0000-0000-000000000000", МассивОтветов);
		
	Исключение
		
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ШаблонСообщения = Нстр("ru = 'При получении статуса запроса персональных данных произошла ошибка:
								|%1'");
		ТекстОшибки = СтрШаблон(ШаблонСообщения, КраткаяИнформация);
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			ВидОперации, ПодробноеПредставлениеОшибки, ТекстОшибки, 1);
		Ошибка = Истина;
		Возврат;
		
	КонецПопытки;
	
	Ответ = МассивОтветов.Получить(0);
	
	Если Ответ = "<!--NOT PROCESSED YET-->" ИЛИ Ответ = "<!--NOT_PROCESSED_YET-->" Тогда
		Ожидать = Истина;
	ИначеЕсли Ответ = "<!--REQUEST NOT FOUND-->" ИЛИ Ответ = "<!--REQUEST_NOT_FOUND-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка: запрос не найден.
							|Повторите операцию позже.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , , , Ошибка);
	ИначеЕсли Ответ = "<!--REQUESTID DUBLIC-->" ИЛИ Ответ = "<!--REQUESTID_DUBLIC-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка: запрос с таким идентификатором уже существует.
							|Повторите операцию позже.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , , , Ошибка);
	Иначе
		ПараметрыОбмена = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыОбменаСбербанк(Ответ);
		Если ПараметрыОбмена = Неопределено Тогда
			Ошибка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолучитьЛичнуюИнформациюЧерезТокенСбербанк()
	
	ИдентификаторОрганизации = "00000000-0000-0000-0000-000000000000";
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияСтатусаЗапросаСбербанк", ЭтотОбъект);
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьСтатусЗапросаСбербанк(Оповещение, ИдентификаторОрганизации, ТикетОбменаСбербанк);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтправитьЗапросПерсональныхДанныхСбербанк(Знач НастройкаОбмена, Знач КлючСессии, ТикетОбменаСбербанк, Ошибка)
	
	ДанныеЗапрос = ОбменСБанкамиСлужебныйВызовСервера.ЗапросПерсональныхДанныхОрганизацииСбербанк();
	
	МассивДанных = Новый Массив;
	МассивДанных.Добавить(ДанныеЗапрос);
	МассивТикетов = Неопределено;
	ИдентификаторСессии = ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(КлючСессии).ИдентификаторСессии;
	ИспользуетсяЖурналирование = Ложь;
	
	Попытка
		ОбменСБанкамиСлужебный.ОтправитьДанныеВСбербанк(НастройкаОбмена, ИдентификаторСессии, МассивДанных, МассивТикетов);
		ТикетОбменаСбербанк = МассивТикетов.Получить(0);
	Исключение
		ВидОперации = НСтр("ru = 'Получение настроек обмена из банка'");
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, ТекстОшибки, ТекстСообщения, "ОбменСБанками");
		Ошибка = Истина;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область ОбменСИспользованиемДополнительнойОбработки

&НаСервереБезКонтекста
Процедура СохранитьВнешнююОбработку(Знач АдресФайлаВнешнегоМодуля, Знач ВерсияДополнительнойОбработки, Знач ИмяВнешнегоМодуля, Знач НаименованиеВнешнейОбработки)
	
	ОбменСБанкамиСлужебный.СохранитьВнешнююОбработку(
		АдресФайлаВнешнегоМодуля, ВерсияДополнительнойОбработки, ИмяВнешнегоМодуля, НаименованиеВнешнейОбработки);

КонецПроцедуры
	
&НаКлиенте
Процедура ПослеВыбораФайлаВнешнегоМодуляИзТонкогоКлиента(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено ИЛИ НЕ ВыбранныеФайлы.Количество() Тогда
		ПутьКФайлуВнешнегоМодуля = "";
	Иначе
		ПутьКФайлуВнешнегоМодуля = ВыбранныеФайлы[0];
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайлаВнешнегоМодуляИзВеб(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	ВыбранФайлВнешнегоМодуля = Результат;
	АдресФайлаВнешнегоМодуля = Адрес;
	ПутьКФайлуВнешнегоМодуля = ВыбранноеИмяФайла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТестНастройкиОбменаЧерезДополнительнуюОбработку()
	
	ОтпечатокBase64 = ОбменСБанкамиСлужебныйВызовСервера.СтрокаBase64БезBOM(Отпечаток);
	ДвоичныеДанные = Base64Значение(ОтпечатокBase64);
	МассивПодписи = Новый Массив;
	МассивПодписи.Добавить(ДвоичныеДанные);
	Попытка
		МассивПодписей = ПодключаемыйМодуль.Подписать(СертификатXML, МассивПодписи);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка установки подписи.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Установка подписи'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ПодробноеПредставлениеОшибки, , "ОбменСБанками", НастройкаОбмена);
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецПопытки;

	Попытка
		ДопПараметры = Новый Структура("ИдентификаторХранилища", ИдентификаторХранилища);
		ПодписьВалидна = ПодключаемыйМодуль.ПроверитьПодпись(СертификатXML, ДвоичныеДанные, МассивПодписей[0], ДопПараметры);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка проверки подписи.
								|Код ошибки: ДО-%1
								|%2'");
		ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Проверка подписи'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ПодробноеПредставлениеОшибки, , "ОбменСБанками", НастройкаОбмена);
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецПопытки;
	
	Попытка
		ПодключаемыйМодуль.ОтправитьЗапрос(СертификатXML, 1);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка отправки тестового запроса.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Отправка тестового запроса'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ПодробноеПредставлениеОшибки, , "ОбменСБанками", НастройкаОбмена);
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецПопытки;
	
	ОбработатьРезультатТестаНастройки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораКлючаЧерезДополнительнуюОбработку(ВыбранныйЭлемент, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторХранилища = ВыбранныйЭлемент.Значение;
	
	ТребуетсяУстановкаPINКода = ОбменСБанкамиСлужебныйКлиент.НеобходимаУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(
		ПодключаемыйМодуль, ИдентификаторХранилища);

	Если ТребуетсяУстановкаPINКода = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("ИдентификаторХранилища", ИдентификаторХранилища);
	Если ТребуетсяУстановкаPINКода Тогда
		ОбработчикВводаPIN = Новый ОписаниеОповещения(
			"ОбработатьВводPINЧерезДополнительнуюОбработку", ЭтотОбъект, ДополнительныеПараметры);
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросPINКода", ДополнительныеПараметры, , , , , ОбработчикВводаPIN);
	Иначе
		ПослеУстановкиPINЧерезДополнительнуюОбработку(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВводPINЧерезДополнительнуюОбработку(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	PINУстановлен = ОбменСБанкамиСлужебныйКлиент.УстановитьPINКодХранилищаЧерезДополнительнуюОбработку(
		ПодключаемыйМодуль, ДополнительныеПараметры.ИдентификаторХранилища, Результат);

	Если PINУстановлен Тогда
		ПослеУстановкиPINЧерезДополнительнуюОбработку(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеУстановкиPINЧерезДополнительнуюОбработку(ДополнительныеПараметры)
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеВыбораСертификатаЧерезДополнительнуюОбработку", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьСертификатЧерезДополнительнуюОбработку(
		Оповещение, ПодключаемыйМодуль, ДополнительныеПараметры.ИдентификаторХранилища);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораСертификатаЧерезДополнительнуюОбработку(ДанныеСертификата, ДополнительныеПараметры) Экспорт
	
	Если ДанныеСертификата = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СертификатXML = ДанныеСертификата.СертификатXML;
	Отпечаток = ДанныеСертификата.Отпечаток;
	
	НовыйСертификат = НайтиСоздатьСертификатВнешнегоМодуля(СертификатXML, Организация, ПрограммаБанка, ДанныеСертификата);
		
	НовСтрока = Сертификаты.Добавить();
	НовСтрока.Сертификат = НовыйСертификат;
	
КонецПроцедуры

&НаКлиенте
Функция ПодключитьДополнительнуюОбработку()
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ВремФайл = ПолучитьИмяВременногоФайла("epf");
		ДвоичныеДанныеОбработки = ПолучитьИзВременногоХранилища(АдресФайлаВнешнегоМодуля);
		ДвоичныеДанныеОбработки.Записать(ВремФайл);
		ПодключаемыйМодуль = ВнешниеОбработки.ПолучитьФорму(ВремФайл);
		ИмяВнешнегоМодуля = ПодключаемыйМодуль.ЭтотОбъект;
		ПозицияТочки = СтрНайти(ИмяВнешнегоМодуля, ".");
		ИмяВнешнегоМодуля = Сред(ИмяВнешнегоМодуля, ПозицияТочки + 1);
	#Иначе
		
		ПодключитьВнешнююОбработку();
		
		Если НЕ ЗначениеЗаполнено(ИмяВнешнегоМодуля) Тогда // обработка не подключилась
			Возврат Ложь;
		КонецЕсли;
		
		ИмяФормыОбработки = "ВнешняяОбработка." + ИмяВнешнегоМодуля + ".Форма";
		Попытка
			ПараметрыФормыОбработки = Новый Структура("РежимЭДО", Истина);
			ПодключаемыйМодуль = ПолучитьФорму(ИмяФормыОбработки, ПараметрыФормыОбработки, , УникальныйИдентификатор);
		Исключение
			Операция = НСтр("ru = 'Получение формы внешней обработки.'");
			ТекстСообщения = НСтр("ru = 'Не удалось подключить указанный внешний модуль'");
			ПодробныйТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(Операция, ПодробныйТекстОшибки, , "ОбменСБанками");
			Возврат Ложь;
		КонецПопытки;
	#КонецЕсли
	
	СведенияОВнешнейОбработке = ОбменСБанкамиСлужебныйВызовСервера.СведенияОВнешнейОбработке(ИмяВнешнегоМодуля);
	
	Если Не ЗначениеЗаполнено(СведенияОВнешнейОбработке) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВерсияДополнительнойОбработки = СведенияОВнешнейОбработке.Версия;
	НаименованиеВнешнейОбработки = СведенияОВнешнейОбработке.Наименование;
	
	Если Не ЗначениеЗаполнено(ВерсияДополнительнойОбработки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ПодключитьВнешнююОбработку()
	
	ОбменСБанкамиСлужебный.ПодключитьВнешнююОбработку(АдресФайлаВнешнегоМодуля, ИмяВнешнегоМодуля);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьИнициализациюДополнительнойОбработки(Оповещение)

	Если НЕ ПодключитьДополнительнуюОбработку() Тогда
		ВыполнитьОбработкуОповещения(Оповещение, Ложь);
		Возврат;
	КонецЕсли;

	ВерсияАктуальнойОбработки = Неопределено;
	
	ОбменСБанкамиСлужебныйВызовСервера.ПроверитьАктуальностьВнешнейОбработки(
		ИмяВнешнегоМодуля, ВерсияДополнительнойОбработки, ВерсияАктуальнойОбработки);
		
	Если ЗначениеЗаполнено(ВерсияАктуальнойОбработки) Тогда
		ТекстВопроса = НСтр("ru = 'В информационной базе уже есть модуль версии %1.
								|Выбранный модуль имеет другую версию %2.
								|Модуль какой версии нужно теперь использовать?'");
		ТекстВопроса = СтрШаблон(ТекстВопроса, ВерсияАктуальнойОбработки, ВерсияДополнительнойОбработки);
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ОповещениеПослеИнициализацииДополнительнойОбработки", Оповещение);
		ОповещениеДляВопроса = Новый ОписаниеОповещения(
			"ПослеВопросаОНовойВерсииДополнительнойОбработки", ЭтотОбъект, ДополнительныеПараметры);
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(Истина, ВерсияДополнительнойОбработки);
		Кнопки.Добавить(Ложь, ВерсияАктуальнойОбработки);
		Кнопки.Добавить(КодВозвратаДиалога.Отмена);
		ЗаголовокВопроса = НСтр("ru = 'Выбор модуля для использования'");
		ПоказатьВопрос(ОповещениеДляВопроса, ТекстВопроса, Кнопки, , Истина, ЗаголовокВопроса);
	Иначе
		СохранитьВнешнююОбработку = Истина;
		ИнициализироватьДополнительнуюОбработкуПослеПодключения(Оповещение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициализироватьДополнительнуюОбработкуПослеПодключения(Оповещение)
	
	Попытка
		ОписаниеОбработки = ПодключаемыйМодуль.ОписаниеОбработки();
		ВерсияAPI = ОписаниеОбработки.ВерсияAPI;
	Исключение
		ВерсияAPI = 1;
	КонецПопытки;
	
	Если ВерсияAPI = 1 Тогда
		Попытка
			ПодключаемыйМодуль.Инициализировать();
			ВыполнитьОбработкуОповещения(Оповещение, Истина);
		Исключение
			ШаблонОшибки = НСтр("ru = 'Ошибка инициализации внешнего модуля.
										|Код ошибки: ДО-%1
										|%2'");
			ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
			ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
			Операция = НСтр("ru = 'Инициализация внешней обработки'");
			ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
				Операция, ПодробноеПредставлениеОшибки, , "ОбменСБанками");
			ВыполнитьОбработкуОповещения(Оповещение, Ложь);
		КонецПопытки;
	Иначе
		Попытка
			АдресКомпонентыДополнительнойОбработки = "";
			ИнициализацияВыполнена = ПодключаемыйМодуль.НачатьИнициализацию(АдресКомпонентыДополнительнойОбработки);
		Исключение
			ШаблонОшибки = НСтр("ru = 'Ошибка инициализации внешнего модуля.
										|Код ошибки: ДО-%1
										|%2'");
			ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
			ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
			Операция = НСтр("ru = 'Инициализация внешней обработки'");
			ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
				Операция, ПодробноеПредставлениеОшибки, , "ОбменСБанками");
			ВыполнитьОбработкуОповещения(Оповещение, Ложь);
			Возврат;
		КонецПопытки;
		
		Если ИнициализацияВыполнена Тогда
			ЗавершитьИнициализациюВнешнейКомпонентыДополнительнойОбработки(Оповещение);
		Иначе
			ДополнительныеПараметры = Новый Структура("ОповещениеПослеИнициализацииДополнительнойОбработки", Оповещение);
			ОбработчикПослеУстановкиВК = Новый ОписаниеОповещения("ПослеУстановкиВнешнейКомпонентыДополнительногоМодуля",
				ЭтотОбъект, ДополнительныеПараметры, "ПослеОшибкиУстановкиВнешнейКомпонентыДополнительногоМодуля", ЭтотОбъект);
			НачатьУстановкуВнешнейКомпоненты(ОбработчикПослеУстановкиВК, АдресКомпонентыДополнительнойОбработки);
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаОНовойВерсииДополнительнойОбработки(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = Истина Тогда // Заменить внешнюю обработку
		СохранитьВнешнююОбработку = Истина;
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	Иначе
		СохранитьВнешнююОбработку = Ложь;
		АдресФайлаВнешнегоМодуля = АдресВнешнейОбработки(ИмяВнешнегоМодуля, УникальныйИдентификатор);
		
		Если Не ЗначениеЗаполнено(АдресФайлаВнешнегоМодуля) ИЛИ НЕ ЭтоАдресВременногоХранилища(АдресФайлаВнешнегоМодуля) Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеИнициализацииДополнительнойОбработки, Ложь);
			Возврат;
		КонецЕсли;
		
		Если НЕ ПодключитьДополнительнуюОбработку() Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеИнициализацииДополнительнойОбработки, Ложь);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ИнициализироватьДополнительнуюОбработкуПослеПодключения(
		ДополнительныеПараметры.ОповещениеПослеИнициализацииДополнительнойОбработки);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьИнициализациюВнешнейКомпонентыДополнительнойОбработки(Оповещение)
	
	Попытка
		ПодключаемыйМодуль.ЗавершитьИнициализацию();
		ВыполнитьОбработкуОповещения(Оповещение, Истина);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка завершения инициализации внешнего модуля.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Завершение инициализации внешней обработки'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ПодробноеПредставлениеОшибки, , "ОбменСБанками");
		ВыполнитьОбработкуОповещения(Оповещение, Ложь);
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеУстановкиВнешнейКомпонентыДополнительногоМодуля(ДополнительныеПараметры) Экспорт
	
	ЗавершитьИнициализациюВнешнейКомпонентыДополнительнойОбработки(
		ДополнительныеПараметры.ОповещениеПослеИнициализацииДополнительнойОбработки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОшибкиУстановкиВнешнейКомпонентыДополнительногоМодуля(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ВидОперации = НСтр("ru = 'Установка внешней компоненты дополнительной обработки'");
	ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	ТекстСообщения = НСтр("ru = 'При подключении внешнего модуля произошла ошибка.'");
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеИнициализацииДополнительнойОбработки, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеИнициализацииДополнительнойОбработки(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат Тогда
		ХронологияПереключенияСтраниц.Добавить(Элементы.СтраницаЗапросИспользованияВнешнегоМодуля);
		СменитьСтраницуШаги(Элементы.СтраницаСертификаты);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция АдресВнешнейОбработки(ИмяМодуля, УникальныйИдентификатор)
	
	УстановитьПривилегированныйРежим(Истина);

	ДанныеВнешнихМодулей = Константы.ВнешниеФайлыОбменСБанками.Получить().Получить();
	
	Если НЕ ДанныеВнешнихМодулей.Свойство("ВнешниеОбработки") Тогда
		Возврат "";
	КонецЕсли;
	
	ДанныеМодуля = ДанныеВнешнихМодулей.ВнешниеОбработки.Получить(ИмяМодуля);
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеМодуля.ДвоичныеДанныеМодуля, УникальныйИдентификатор);
	
КонецФункции

#КонецОбласти

#Область ОбменСИспользованиемВнешнейКомпоненты

&НаСервереБезКонтекста
Функция ИнформацияОВнешнейКомпоненте(Знач АдресВК)

	ИнформацияОВК = ДополнительныеВнешниеКомпонентыВызовСервера.ИнформацияОВнешнейКомпоненте(АдресВК);
	
	Если ЗначениеЗаполнено(ИнформацияОВК) Тогда
		ИнформацияОВКВБазе = ДополнительныеВнешниеКомпонентыВызовСервера.ПараметрыВнешнейКомпоненты(ИнформацияОВК.ИмяМодуля);
		Если ЗначениеЗаполнено(ИнформацияОВКВБазе) Тогда
			ВерсияВБазе = ИнформацияОВКВБазе.Версия;
		КонецЕсли;
		ИнформацияОВК.Вставить("ВерсияВБазе", ВерсияВБазе);
	КонецЕсли;
	
	Возврат ИнформацияОВК;
	
КонецФункции

&НаКлиенте
Процедура ТестНастройкиОбменаВК()
	
	// Удаляем признак прерывания процесса
	ОбменСБанкамиСлужебныйКлиент.ПроцессПрерван(НастройкаОбмена);

	ПараметрыСоединения = Неопределено;
	Если СпособСозданияНастройки <> 1 Тогда // аутентификация на токене уже выполнена
		ПараметрыСоединения = Новый Структура;
		ПараметрыСоединения.Вставить("ИдентификаторХранилища", ИдентификаторХранилища);
		ПараметрыСоединения.Вставить("Отпечаток", Отпечаток);
		ПараметрыСоединения.Вставить("СертификатBase64", Base64Строка(СертификатВК));
		ПараметрыСоединения.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеТестаНастройкиВК", ЭтотОбъект);
	ОбменСБанкамиСлужебныйКлиент.ТестНастройкиОбменаВК(Оповещение, НастройкаОбмена, ПараметрыСоединения);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьСертификатыПослеПодключенияВК(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		ОбработатьРезультатСозданияНастройкиОбмена(Ложь);
		Возврат;
	КонецЕсли;
	
	ПодключаемыйМодуль = Результат;
	
	СохранитьОчереднойСертификатВИнформационнойБазеВК(ДополнительныеПараметры.ДвоичныеДанныеСертификатовИзФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьСертификатПослеПолученияДанныхКлючаВК(СтруктураСертификата, ДополнительныеПараметры) Экспорт
	
	Если СтруктураСертификата = Неопределено Тогда
		ОбработатьРезультатСозданияНастройкиОбмена(Ложь);
		Возврат;
	КонецЕсли;
	
	ЕстьОшибка = Ложь;
	
	Успех = ДобавитьСертификатВНастройкуОбменаВК(
		НастройкаОбмена, ДополнительныеПараметры.СертификатBase64, СтруктураСертификата);
		
	Если Успех Тогда
		СохранитьОчереднойСертификатВИнформационнойБазеВК(ДополнительныеПараметры.ДанныеСертификатов);
	Иначе
		ОбработатьРезультатСозданияНастройкиОбмена(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьОчереднойСертификатВИнформационнойБазеВК(ДанныеСертификатов)
	
	Если ДанныеСертификатов.Количество() = 0 Тогда
		ОбработатьРезультатСозданияНастройкиОбмена(Истина);
		Элементы.ТестированиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
		#Если ВебКлиент Тогда
			ОбновитьОтображениеДанных();
		#КонецЕсли
		ПодключитьОбработчикОжидания("ТестНастройкиОбменаВК", 0.1, Истина);
		Возврат;
	КонецЕсли;
	
	ДвоичныеДанныеСертификата = ДанныеСертификатов.Получить(0);
	СертификатBase64 = Base64Строка(ДвоичныеДанныеСертификата);
	ДанныеСертификатов.Удалить(0);
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("СертификатBase64", СертификатBase64);
	ДополнительныеПараметры.Вставить("ДанныеСертификатов", ДанныеСертификатов);
	
	Обработчик = Новый ОписаниеОповещения(
		"СохранитьСертификатПослеПолученияДанныхКлючаВК", ЭтотОбъект, ДополнительныеПараметры);
		
	ОбменСБанкамиСлужебныйКлиент.ПолучитьДанныеКлючаЧерезВК(Обработчик, ПодключаемыйМодуль, СертификатBase64);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыполнениеЗаданияСкачкиВК()
	
	Задание = ОбменСБанкамиСлужебныйВызовСервера.ПроверитьВыполнениеФоновогоЗаданияНаСервере(
		ИдентификаторЗадания, УникальныйИдентификатор);
	Если Задание.Завершено Тогда
		Если Задание.Успешно Тогда
			СтруктураДанных = ПолучитьИзВременногоХранилища(АдресХранилища);
			ВерсияВК = СтруктураДанных.Версия;
			ИмяВнешнегоМодуля = СтруктураДанных.ИмяВнешнегоМодуля;
			ПолучитьНастройкиОбменаССервераБанкаЧерезВК();
		Иначе
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		КонецЕсли;
	Иначе
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
		ПодключитьОбработчикОжидания("ПроверитьВыполнениеЗаданияСкачкиВК", ПараметрыОбработчика.ТекущийИнтервал, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияНастроекИзБанкаВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	АдресФайлаНастроекОбмена = ПоместитьВоВременноеХранилище(Base64Значение(РезультатВызова), УникальныйИдентификатор);
	
	УчастникиОбменаИзФайла = УчастникиОбменаИзФайла(АдресФайлаНастроекОбмена, Организация);
		
	Если УчастникиОбменаИзФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если УчастникиОбменаИзФайла.Организация <> Организация Тогда
		ТекстСообщения = НСтр("ru = 'Указана неверная учетная запись.
									|Требуются настройки для организации: %1
									|Получены настройки для организации: %2'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Организация, УчастникиОбменаИзФайла.Организация);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;

	Если УчастникиОбменаИзФайла.Банк <> Банк Тогда
		ТекстСообщения = НСтр("ru = 'Подключено неверное аппаратное устройство.
									|Требуются настройки для банка: %1
									|Получены настройки для банка: %2'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Банк, УчастникиОбменаИзФайла.Банк);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	ОбработатьРезультатПолученияНастроекССервераБанка(Истина);
	Элементы.СозданиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
	#Если ВебКлиент Тогда
		ОбновитьОтображениеДанных();
	#КонецЕсли
	ПодключитьОбработчикОжидания("СоздатьНастройкуОбмена", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеИнициализацииВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат);
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ЗагрузитьНастройкиОбменаССервераБанкаПослеПолученияДанныхСертификата", ЭтотОбъект);
		
	ПараметрыСоединения = Новый Структура();
	ПараметрыСоединения.Вставить("ИдентификаторОрганизации", "");
	ПараметрыСоединения.Вставить("БИК", БИКБанка(Банк));
	ПараметрыСоединения.Вставить("КлючУникальности", "");
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьДанныеСертификатаСТокенаВК(Оповещение, ПодключаемыйМодуль, ПараметрыСоединения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьНастройкиОбменаССервераБанкаЧерезВК()
	
	Оповещение = Новый ОписаниеОповещения("ИнициализироватьПослеПодключенияВК", ЭтотОбъект);
	ДополнительныеВнешниеКомпонентыКлиент.ПодключитьВнешнююКомпонентуПоИдентификатору(Оповещение, ИмяВнешнегоМодуля);

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОшибкуПолученияНастроекИзБанкаВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстСообщения = НСтр("ru = 'При получении настроек обмена из банка произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Получение настроек обмена с сервера банка.'");
	
	Оповещение = Новый ОписаниеОповещения("СообщитьОшибкуПользователю", ЭтотОбъект);
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьИнформациюОбОшибкеВК(
		Оповещение, ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
		
	ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиСоздатьСертификатВнешнегоМодуля(Знач ДанныеСертификата, Знач Организация, Знач ПрограммаБанка, Знач СвойстваСертификата)
	
	Возврат ОбменСБанкамиСлужебный.НайтиСоздатьСертификатВнешнегоМодуля(
		ДанныеСертификата, Организация, ПрограммаБанка, СвойстваСертификата);
	
КонецФункции

&НаКлиенте
Процедура ПослеТестаНастройкиВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат);
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;
	
	Если Результат = Неопределено ИЛИ Результат = Ложь Тогда
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;
	
	ОбработатьРезультатТестаНастройки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициализироватьПослеПодключенияВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	ПодключаемыйМодуль = Результат;
	
	Оповещение = Новый ОписаниеОповещения("ПослеИнициализацииВК", ЭтотОбъект);

	ОбменСБанкамиСлужебныйКлиент.ИнициализироватьВК(Оповещение, ПодключаемыйМодуль);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНастройкиОбменаССервераБанкаПослеПолученияДанныхСертификата(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат);
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	Если Результат = Неопределено Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	// Кэширование для использование в тесте настроек.
	СертификатВК = Base64Значение(Результат.СертификатBase64);
	Отпечаток = Результат.Отпечаток;
	КомуВыданСертификатВК = Результат.ВладелецФИО;
	ИдентификаторХранилища = Результат.ИдентификаторХранилища;
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияНастроекИзБанкаВК", ЭтотОбъект, ДополнительныеПараметры,
		"ОбработатьОшибкуПолученияНастроекИзБанкаВК", ЭтотОбъект);
	
	ПодключаемыйМодуль.НачатьВызовНастройкиЭДО(Оповещение);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДобавитьСертификатВНастройкуОбменаВК(Знач НастройкаОбмена, Знач СертификатBase64, Знач СтруктураСертификата)
	
	Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаОбмена, "Организация");
	
	НовыйСертификат = ОбменСБанкамиСлужебный.НайтиСоздатьСертификатВнешнегоМодуля(
		СертификатBase64, Организация, Перечисления.ПрограммыБанка.ОбменЧерезВК, СтруктураСертификата);
		
	Попытка
		НастройкаОбменаОбъект = НастройкаОбмена.ПолучитьОбъект();
		НастройкаОбменаОбъект.Прочитать();
		
		НовСтрока = НастройкаОбменаОбъект.СертификатыПодписейОрганизации.Добавить();
		НовСтрока.СертификатЭП = НовыйСертификат;
	
		НастройкаОбменаОбъект.Записать();
	Исключение
		Операция = НСтр("ru = 'Добавление сертификата в настройку обмена'");
		ШаблонСообщения = НСтр("ru = 'При добавлении сертификата в настройку обмена произошла ошибка.
									|%1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ТекстОшибки, ТекстСообщения, "ОбменСБанками", НастройкаОбмена);
		Возврат Ложь;
	КонецПопытки;
	
	НастройкаОбменаОбъект.Разблокировать();
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура СкачатьВКССервераБанка()
	
	Задание = ОбменСБанкамиСлужебныйВызовСервера.СкачатьВКНаСервере(УникальныйИдентификатор, URLВК);
	
	АдресХранилища = Задание.АдресХранилища;
	
	Если Задание.Завершено Тогда
		Если Задание.Успешно Тогда
			СтруктураДанных = ПолучитьИзВременногоХранилища(АдресХранилища);
			ИмяВнешнегоМодуля = СтруктураДанных.ИмяВнешнегоМодуля;
			ВерсияВК = СтруктураДанных.Версия;
			ПолучитьНастройкиОбменаССервераБанкаЧерезВК();
		Иначе
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		КонецЕсли;
	Иначе
		ИдентификаторЗадания = Задание.Идентификатор;
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
		ПодключитьОбработчикОжидания("ПроверитьВыполнениеЗаданияСкачкиВК", 1, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область УниверсальныеМетоды

&НаСервереБезКонтекста
Функция ЕстьПлатежныеДокументыВКонфигурации()
	
	АктуальныеВидыЭД = ОбменСБанкамиСлужебныйПовтИсп.АктуальныеВидыЭД();
	Возврат АктуальныеВидыЭД.Найти(Перечисления.ВидыЭДОбменСБанками.ПлатежноеПоручение) <> Неопределено;
	
КонецФункции

&НаКлиенте
Функция ВведенЛогинИПароль()
	
	ЕстьОшибка = Ложь;
	Если НЕ ЗначениеЗаполнено(Логин)Тогда
		ТекстСообщения = НСтр("ru = 'Не указан логин для аутентификации на сервере банка'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Логин", , ЕстьОшибка);
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Пароль)Тогда
		ТекстСообщения = НСтр("ru = 'Не указан пароль'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Пароль", , ЕстьОшибка);
	КонецЕсли;
	
	Возврат НЕ ЕстьОшибка;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ПрерватьДлительнуюОперацию(ИдентификаторЗадания)
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьВКБанка(Знач АдресФайлаВнешнегоМодуля)

	Справочники.ДополнительныеВнешниеКомпоненты.СохранитьВнешнююКомпонентуВИнформационнойБазе(АдресФайлаВнешнегоМодуля);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИмяВнешнегоМодуля(Знач НастройкаОбмена)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаОбмена, "ИмяВнешнегоМодуля");
	
КонецФункции

&НаСервереБезКонтекста
Функция УчастникиОбменаИзФайла(Знач АдресФайла, Знач Организация, Знач ЛокальныйФайл = Ложь)
	
	Попытка
		ЭД = ОбменСБанкамиСлужебный.XDTOДанныеФайлаНастроек(АдресФайла);
	Исключение
		Операция = НСтр("ru = 'Загрузка настроек обмена из файла'");
		Если ЛокальныйФайл Тогда
			ШаблонТекстСообщения = НСтр("ru = 'Произошла ошибка при чтении файла.
									|%1'");
			ШаблонТекстОшибки = НСтр("ru = 'Ошибка чтения файла.
								|%1'");
			ТекстОшибки = СтрШаблон(ШаблонТекстОшибки, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Иначе
			ШаблонТекстСообщения = НСтр("ru = 'Произошла ошибка при чтении файла, полученного из банка.
									|%1'");
			ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(АдресФайла);
			ВремФайл = ПолучитьИмяВременногоФайла("xml");
			ДвоичныеДанныеФайла.Записать(ВремФайл);
			ШаблонТекстОшибки = НСтр("ru = 'Ошибка чтения файла:
								|%1
								|Путь к файлу: %2'");
			ТекстОшибки = СтрШаблон(ШаблонТекстОшибки, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), ВремФайл);
		КонецЕсли;
		
		ТекстСообщения = СтрШаблон(ШаблонТекстСообщения, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(Операция, ТекстСообщения, ТекстОшибки, "ОбменСБанками");
		Возврат Неопределено;
	КонецПопытки;
	
	ЭтоНастройкиСбербанка = ЭД.Свойства().Получить("Data") = Неопределено;
	
	СтруктураПоискаОрганизации = Новый Структура;
	ИННОрганизации = ?(ЭтоНастройкиСбербанка, ЭД.Customer.INN, ЭД.Recipient.inn);
	Если ЗначениеЗаполнено(ИННОрганизации) Тогда
		СтруктураПоискаОрганизации.Вставить("ИНН", ИННОрганизации);
	КонецЕсли;
	
	КППОрганизации = ?(ЭтоНастройкиСбербанка, ЭД.Customer.KPP, ЭД.Recipient.kpp);
	Если КППОрганизации = "000000000" Тогда // Для Сбербанка
		КППОрганизации = "0";
	КонецЕсли;
	Если ЗначениеЗаполнено(КППОрганизации) Тогда
		СтруктураПоискаОрганизации.Вставить("КПП", КППОрганизации);
	КонецЕсли;
	НаименованиеОрганизации = ?(ЭтоНастройкиСбербанка, ЭД.Customer.Name, ЭД.Recipient.name);
	Если ЗначениеЗаполнено(НаименованиеОрганизации) Тогда
		СтруктураПоискаОрганизации.Вставить("Наименование", НаименованиеОрганизации);
	КонецЕсли;
	
	Если СтруктураПоискаОрганизации.Количество() > 0 Тогда
		СсылкаНаОрганизацию = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.НайтиСсылкуНаОбъект(
			"Организации", , СтруктураПоискаОрганизации);
	ИначеЕсли НЕ ЗначениеЗаполнено(Организация) Тогда
		ТекстСообщения = НСтр("ru = 'В файле настроек нет информации об организации.
									|Обратитесь в свой банк.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат Неопределено;
	Иначе
		СсылкаНаОрганизацию = Организация;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СсылкаНаОрганизацию) Тогда
		ТекстСообщения = НСтр("ru = 'Файл содержит настройки для неизвестной организации: %1%2%3'");
		Если СтруктураПоискаОрганизации.Свойство("ИНН") Тогда
			ТекстИНН = Символы.ПС + НСтр("ru = 'ИНН: %1'");
			ТекстИНН = СтрШаблон(ТекстИНН, СтруктураПоискаОрганизации.ИНН);
		Иначе
			ТекстИНН = "";
		КонецЕсли;
		Если СтруктураПоискаОрганизации.Свойство("КПП") Тогда
			ТекстКПП = Символы.ПС + НСтр("ru = 'КПП: %1'");
			ТекстКПП = СтрШаблон(ТекстКПП, СтруктураПоискаОрганизации.КПП);
		Иначе
			ТекстКПП = "";
		КонецЕсли;
		Если СтруктураПоискаОрганизации.Свойство("Наименование") Тогда
			ТекстНаименование = Символы.ПС + НСтр("ru = 'Наименование: %1'");
			ТекстНаименование = СтрШаблон(
				ТекстНаименование, СтруктураПоискаОрганизации.Наименование);
		Иначе
			ТекстНаименование = "";
		КонецЕсли;
		ТекстСообщения = СтрШаблон(
			ТекстСообщения, ТекстИНН, ТекстКПП, ТекстНаименование);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат Неопределено;
	КонецЕсли;

	НаименованиеБанка = "";
	СтруктураПоискаБанка = Новый Структура;
	БИК = ?(ЭтоНастройкиСбербанка, ЭД.Bank.BIC, ЭД.Sender.bic);
	СтруктураПоискаБанка.Вставить("Код", БИК);
	НаименованиеБанка = ?(ЭтоНастройкиСбербанка, ЭД.Bank.Name, ЭД.Sender.name);
	СтруктураПоискаБанка.Вставить("Наименование", НаименованиеБанка);
	
	СсылкаНаБанк = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.НайтиСсылкуНаОбъект(
		"Банки", БИК, СтруктураПоискаБанка);
		
	Если НЕ ЗначениеЗаполнено(СсылкаНаБанк) Тогда
		ТекстСообщения = НСтр("ru = 'Файл содержит настройки для неизвестного банка: %1%2'");
		Если СтруктураПоискаБанка.Свойство("Код") Тогда
			ТекстБИК = Символы.ПС + НСтр("ru = 'БИК: %1'");
			ТекстБИК = СтрШаблон(ТекстБИК, СтруктураПоискаБанка.Код);
		Иначе
			ТекстБИК = "";
		КонецЕсли;
		Если СтруктураПоискаБанка.Свойство("Наименование") Тогда
			ТекстНаименование = Символы.ПС + НСтр("ru = 'Наименование: %1'");
			ТекстНаименование = СтрШаблон(
				ТекстНаименование, СтруктураПоискаБанка.Наименование);
		Иначе
			ТекстНаименование = "";
		КонецЕсли;
		ТекстСообщения = СтрШаблон(
			ТекстСообщения, ТекстБИК, ТекстНаименование);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат Неопределено;
	КонецЕсли;

	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Организация", СсылкаНаОрганизацию);
	СтруктураВозврата.Вставить("Банк", СсылкаНаБанк);
	СтруктураВозврата.Вставить("ЭтоНастройкиСбербанка", ЭтоНастройкиСбербанка);
	Если ЭтоНастройкиСбербанка Тогда
		СтруктураВозврата.Вставить("URLВК", ЭД.SignatureParameters.AddIn.__content);
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ВывестиСсылкуНаСайтБанка(ИнформацияНаСайтеБанка, АдресСайта)

	ИнформацияНаСайтеБанка.Видимость = Истина;
	ИнформацияНаСайтеБанка.Заголовок = Новый ФорматированнаяСтрока(
		НСтр("ru = 'Подробную информацию о порядке и условиях подключения на стороне банка можно получить'"), " ",
		Новый ФорматированнаяСтрока(НСтр("ru = 'здесь.'"), , , , АдресСайта));

КонецПроцедуры

&НаКлиенте
Процедура ВернутьсяНаПредыдущуюСтраницу()
	
	ПредыдущийШаг = ХронологияПереключенияСтраниц[ХронологияПереключенияСтраниц.ВГраница()];
	ХронологияПереключенияСтраниц.Удалить(ХронологияПереключенияСтраниц.ВГраница());
	СменитьСтраницуШаги(ПредыдущийШаг);

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатПолученияНастроекССервераБанка(Успех)
	
	Если Успех Тогда
		Если УпрощенныйРежимНастройки Тогда
			Элементы.ПолучениеНастроекОбменаКартинкаУРН.Картинка = БиблиотекаКартинок.Успешно32;
			Элементы.СозданиеНастройкиОбменаКартинкаУРН.Картинка = БиблиотекаКартинок.НезавершившаясяПроверка;
			Элементы.ГруппаСозданиеНастройкиОбменаУРН.Видимость = Истина;
		Иначе
			Элементы.ПолучениеНастроекОбменаКартинка.Картинка = БиблиотекаКартинок.Успешно32;
		КонецЕсли;
	Иначе
		Если УпрощенныйРежимНастройки Тогда
			АдресФайлаНастроекОбмена = "";
			Элементы.ПодключитьСервис.Доступность = Истина;
			Элементы.ПолучениеНастроекОбменаКартинкаУРН.Картинка = БиблиотекаКартинок.Ошибка32;
			Элементы.СтраницыРезультатПодключенияУРН.ТекущаяСтраница = Элементы.СтраницаНеПодключеноУРН;
		Иначе
			Элементы.ПолучениеНастроекОбменаКартинка.Картинка = БиблиотекаКартинок.Ошибка32;
			Элементы.СтраницыРезультатПодключения.ТекущаяСтраница = Элементы.СтраницаНеПодключено;
			Элементы.ПолучитьНастройки.Видимость = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатСозданияНастройкиОбмена(Успех)
	
	Если Успех Тогда
		
		ОповеститьОбИзменении(НастройкаОбмена);
		Элементы.Заголовок.Заголовок = Строка(НастройкаОбмена);
		
		Если УпрощенныйРежимНастройки Тогда
			Элементы.СозданиеНастройкиОбменаКартинкаУРН.Картинка = БиблиотекаКартинок.Успешно32;
			Элементы.ОткрытьНастройкуОбменаУРН.Видимость = Истина;
			Элементы.ТестированиеНастройкиОбменаКартинкаУРН.Картинка = БиблиотекаКартинок.НезавершившаясяПроверка;
			Элементы.ГруппаТестированиеНастройкиОбменаУРН.Видимость = Истина;
		Иначе
			Элементы.СозданиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.Успешно32;
			Элементы.ОткрытьНастройкуОбмена.Видимость = Истина;
		КонецЕсли;
	Иначе
		Если УпрощенныйРежимНастройки Тогда
			Элементы.ПодключитьСервис.Доступность = Истина;
			Элементы.СозданиеНастройкиОбменаКартинкаУРН.Картинка = БиблиотекаКартинок.Ошибка32;
			Элементы.СтраницыРезультатПодключенияУРН.ТекущаяСтраница = Элементы.СтраницаНеПодключеноУРН;
		Иначе
			Элементы.СозданиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.Ошибка32;
			Элементы.СтраницыРезультатПодключения.ТекущаяСтраница = Элементы.СтраницаНеПодключено;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗаполнениеПрограммКриптографии(Оповещение)
	
	ПрограммыКриптографии = ПрограммыКриптографии();
		
	МассивПрограмм = Новый Массив;
	МассивПрограмм.Добавить("CryptoPro");
	МассивПрограмм.Добавить("SignalComECGOST");
	МассивПрограмм.Добавить("eToken");
	
	МассивПрограммДляСоздания = Новый Массив;
	
	ПроверитьОчереднуюПрограммуКриптографии(МассивПрограмм, ПрограммыКриптографии, Оповещение, МассивПрограммДляСоздания);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПрограммыКриптографии()
	
	НастройкиПрограмм = Справочники.ПрограммыЭлектроннойПодписиИШифрования.ПоставляемыеНастройкиПрограмм();
	
	Настройка = НастройкиПрограмм.Добавить();
	Настройка.Представление       = НСтр("ru = 'КриптоПро eToken CSP'");
	Настройка.ИмяПрограммы        = "eToken Base Cryptographic Provider";
	Настройка.ТипПрограммы        = 1;
	Настройка.АлгоритмПодписи     = "RSA_SIGN";
	Настройка.АлгоритмХеширования = "SHA-1";
	Настройка.АлгоритмШифрования  = "DES";
	Настройка.Идентификатор       = "eToken";
	
	ТипизированнаяТаблица = Новый ТаблицаЗначений;
	КвалификаторСтроки = Новый КвалификаторыСтроки(150);
	ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка", , КвалификаторСтроки);
	ОписаниеТиповЧисло = Новый ОписаниеТипов("Число");
	
	ТипизированнаяТаблица.Колонки.Добавить("ИмяПрограммы", ОписаниеТиповСтрока);
	ТипизированнаяТаблица.Колонки.Добавить("ТипПрограммы", ОписаниеТиповЧисло);
	
	Для Каждого Строка Из НастройкиПрограмм Цикл
		НовСтрока = ТипизированнаяТаблица.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтрока, Строка);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПрограммыКриптографии.ИмяПрограммы,
	|	ПрограммыКриптографии.ТипПрограммы
	|ПОМЕСТИТЬ ПрограммыКриптографии
	|ИЗ
	|	&ПрограммыКриптографии КАК ПрограммыКриптографии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрограммыЭлектроннойПодписиИШифрования.ИмяПрограммы
	|ИЗ
	|	Справочник.ПрограммыЭлектроннойПодписиИШифрования КАК ПрограммыЭлектроннойПодписиИШифрования
	|ГДЕ
	|	(ПрограммыЭлектроннойПодписиИШифрования.ИмяПрограммы, ПрограммыЭлектроннойПодписиИШифрования.ТипПрограммы) В
	|			(ВЫБРАТЬ
	|				ПрограммыКриптографии.ИмяПрограммы,
	|				ПрограммыКриптографии.ТипПрограммы
	|			ИЗ
	|				ПрограммыКриптографии КАК ПрограммыКриптографии)";
	Запрос.УстановитьПараметр("ПрограммыКриптографии", ТипизированнаяТаблица);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтрокаПрограммы = НастройкиПрограмм.Найти(Выборка.ИмяПрограммы, "ИмяПрограммы");
		Если НЕ СтрокаПрограммы = Неопределено Тогда
			НастройкиПрограмм.Удалить(СтрокаПрограммы);
		КонецЕсли;
	КонецЦикла;
	
	СтруктураВозврата = Новый Структура;
	Для Каждого Строка Из НастройкиПрограмм Цикл
		СтруктураЗаписи = Новый Структура;
		СтруктураЗаписи.Вставить("ИмяПрограммы", Строка.ИмяПрограммы);
		СтруктураЗаписи.Вставить("Представление", Строка.Представление);
		СтруктураЗаписи.Вставить("ТипПрограммы", Строка.ТипПрограммы);
		СтруктураЗаписи.Вставить("АлгоритмПодписи", Строка.АлгоритмПодписи);
		СтруктураЗаписи.Вставить("АлгоритмХеширования", Строка.АлгоритмХеширования);
		СтруктураЗаписи.Вставить("АлгоритмШифрования", Строка.АлгоритмШифрования);
		СтруктураВозврата.Вставить(Строка.Идентификатор, СтруктураЗаписи);
	КонецЦикла;
	
	Возврат СтруктураВозврата;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьОрганизациюВСертификате(Сертификат, Организация)
	
	Попытка
		СертификатОбъект = Сертификат.ПолучитьОбъект();
		СертификатОбъект.Организация = Организация;
		СертификатОбъект.Записать();
	Исключение
		Возврат;
		// операция не критичная, исключение не обрабатывается
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Функция СтруктураСозданияНастройкиОбмена()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Организация", Организация);
	СтруктураПараметров.Вставить("Банк", Банк);
	СтруктураПараметров.Вставить("АдресСервера", АдресСервера);
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен") Тогда
		СтруктураПараметров.Вставить("АутентификацияПоСертификату", СпособАутентификации = 0);
	КонецЕсли;
	СтруктураПараметров.Вставить("АутентификацияПоСертификату", СпособАутентификации = 0);
	СтруктураПараметров.Вставить("ИмяВнешнегоМодуля", ИмяВнешнегоМодуля);
	СтруктураПараметров.Вставить("ИдентификаторОрганизации", ИдентификаторОрганизации);
	Если СохранитьЛогин Тогда
		СтруктураПараметров.Вставить("ИмяПользователя", Логин);
	КонецЕсли;
	СтруктураПараметров.Вставить("ИспользуетсяКриптография", ИспользуетсяКриптография);
	СтруктураПараметров.Вставить("ПрограммаБанка", ПрограммаБанка);
	СтруктураПараметров.Вставить("РесурсВходящихДокументов", РесурсИсточник);
	СтруктураПараметров.Вставить("РесурсИсходящихДокументов", РесурсПриемник);
	Если ЭтоАдресВременногоХранилища(АдресФайлаСертификатаСбербанка) Тогда
		СтруктураПараметров.Вставить("СертификатСбербанка", ПолучитьИзВременногоХранилища(АдресФайлаСертификатаСбербанка));
	КонецЕсли;
	МассивСертификатов = Новый Массив;
	Для Каждого Элемент Из Сертификаты Цикл
		МассивСертификатов.Добавить(Элемент.Сертификат);
	КонецЦикла;
	СтруктураПараметров.Вставить("Сертификаты", МассивСертификатов);
	СтруктураПараметров.Вставить("СертификатАутентификации", СертификатАутентификации);
	СтруктураПараметров.Вставить("Недействительна", Истина);
	СтруктураПараметров.Вставить("ВерсияФормата", ВерсияФормата);

	Возврат СтруктураПараметров;

КонецФункции

&НаКлиенте
Процедура СоздатьНастройкуОбмена()
	
	Если (ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен")
			И (СпособСозданияНастройки = 1 ИЛИ СпособСозданияНастройки = 0))
		ИЛИ ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК")
		ИЛИ (СпособСозданияНастройки = 2 И НастройкиЗагруженыИзФайла) Тогда //настройки получены пробной операцией
		ЗагрузитьНастройкуОбменаИзФайла();
		Возврат;
	КонецЕсли;
	
	ПараметрыНастройкиОбмена = СтруктураСозданияНастройкиОбмена();
	ЗаданиеВыполнено = Ложь; ЕстьОшибка = Ложь;
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	ЗапуститьСозданиеНастройкиОбмена(
		ПараметрыНастройкиОбмена, АдресХранилища, ИдентификаторЗадания, ЗаданиеВыполнено, ЕстьОшибка);
		
	Если ЗаданиеВыполнено Тогда
		Если ЕстьОшибка Тогда
			ОбработатьРезультатСозданияНастройкиОбмена(Ложь);
		Иначе
			ПослеСозданияНастройкиОбмена();
		КонецЕсли;
	ИначеЕсли Не ЗаданиеВыполнено Тогда
		ПараметрыОбработчикаОжидания = Новый Структура();
		ПараметрыОбработчикаОжидания.Вставить("МинимальныйИнтервал", 1);
		ПараметрыОбработчикаОжидания.Вставить("МаксимальныйИнтервал", 15);
		ПараметрыОбработчикаОжидания.Вставить("ТекущийИнтервал", 1);
		ПараметрыОбработчикаОжидания.Вставить("КоэффициентУвеличенияИнтервала", 1.4);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗаданияСозданияНастройкиОбмена", 1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗапуститьСозданиеНастройкиОбмена(Знач СтруктураПараметров, Знач АдресХранилища, ИдентификаторЗадания, ЗаданиеВыполнено, ЕстьОшибка)
	
	НаименованиеЗадания = НСтр("ru = 'Создание настройки прямого обмена с банком'");
	
	ПараметрыВыполнения = Новый Массив;
	ПараметрыВыполнения.Добавить(СтруктураПараметров);
	ПараметрыВыполнения.Добавить(АдресХранилища);
	
	Задание = ФоновыеЗадания.Выполнить(
			"ОбменСБанкамиСлужебный.СоздатьНастройкуОбмена", ПараметрыВыполнения, , НаименованиеЗадания);
			
	ИдентификаторЗадания = Задание.УникальныйИдентификатор;
	
	Если ДлительныеОперации.ЗаданиеВыполнено(Задание.УникальныйИдентификатор) Тогда
		ЗаданиеВыполнено = Истина;
		МассивСообщений = Задание.ПолучитьСообщенияПользователю();
		Если НЕ МассивСообщений = Неопределено И МассивСообщений.Количество() Тогда
			ЕстьОшибка = Истина;
			Для Каждого Сообщение Из МассивСообщений Цикл
				Сообщение.Сообщить();
			КонецЦикла
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗаданияСозданияНастройкиОбмена()

	Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда
		ПослеСозданияНастройкиОбмена();
		Возврат;
	КонецЕсли;
	
	ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.ТекущийИнтервал
													* ПараметрыОбработчикаОжидания.КоэффициентУвеличенияИнтервала;
	Если ПараметрыОбработчикаОжидания.ТекущийИнтервал > ПараметрыОбработчикаОжидания.МаксимальныйИнтервал Тогда
		ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.МаксимальныйИнтервал;
	КонецЕсли;
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗаданияСозданияНастройкиОбмена",
		ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		
	КонецПроцедуры

&НаКлиенте
Процедура ПослеСозданияНастройкиОбмена()
	
	СтруктураВозврата = ПолучитьИзВременногоХранилища(АдресХранилища);
	НастройкаОбмена = СтруктураВозврата.НастройкаОбмена;
	
	Если Не ЗначениеЗаполнено(НастройкаОбмена) Тогда
		ОбработатьРезультатСозданияНастройкиОбмена(Ложь);
		Возврат;
	КонецЕсли;
	
	Оповестить("ИзмененаНастройкаОбмена", НастройкаОбмена);
	
	ПрограммаБанка = ПрограммаБанка(НастройкаОбмена);
	
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен") Тогда
		ОбработатьРезультатСозданияНастройкиОбмена(Истина);
		Элементы.ТестированиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
		#Если ВебКлиент Тогда
			ОбновитьОтображениеДанных();
		#КонецЕсли
		ПодключитьОбработчикОжидания("ТестНастройкиАсинхронныйОбмен", 0.1, Истина);
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
		ОбработатьРезультатСозданияНастройкиОбмена(Истина);
		Элементы.ТестированиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
		#Если ВебКлиент Тогда
			ОбновитьОтображениеДанных();
		#КонецЕсли
		ПодключитьОбработчикОжидания("ТестНастройкиОбменаСбербанк", 0.1, Истина);
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК") Тогда
		Если СпособСозданияНастройки = 1 Тогда // загрузка из файла
			
			ИмяВнешнегоМодуля = ИмяВнешнегоМодуля(НастройкаОбмена);
			
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("ДвоичныеДанныеСертификатовИзФайла", СтруктураВозврата.ДанныеСертификатов);
			
			Оповещение = Новый ОписаниеОповещения(
				"СохранитьСертификатыПослеПодключенияВК", ЭтотОбъект, ДополнительныеПараметры);
			
			ДополнительныеВнешниеКомпонентыКлиент.ПодключитьВнешнююКомпонентуПоИдентификатору(Оповещение, ИмяВнешнегоМодуля);
			
		Иначе //соединение на токене уже установлено
			СохранитьОчереднойСертификатВИнформационнойБазеВК(СтруктураВозврата.ДанныеСертификатов);
		КонецЕсли;
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АльфаБанкОнлайн") Тогда
		ОбработатьРезультатСозданияНастройкиОбмена(Истина);
		ОбработатьРезультатТестаНастройки(Истина);
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
		ОбработатьРезультатСозданияНастройкиОбмена(Истина);
		Элементы.ТестированиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
		#Если ВебКлиент Тогда
			ОбновитьОтображениеДанных();
		#КонецЕсли
		ПодключитьОбработчикОжидания("ТестНастройкиОбменаЧерезДополнительнуюОбработку", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПрограммаБанка(Знач НастройкаОбмена)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаОбмена, "ПрограммаБанка");
	
КонецФункции

&НаКлиенте
Процедура ОбработатьРезультатТестаНастройки(Успех)
	
	РаботаПомощникаЗавершена = Ложь;
	Если Успех Тогда
		УспешноВыполненТест = Истина;
		НастройкаВключена = Ложь;
		ВключитьНастройкуОбмена(НастройкаОбмена, НастройкаВключена);
		Если НастройкаВключена Тогда
			ОповеститьОбИзменении(НастройкаОбмена);
			Оповестить("ИзмененаНастройкаОбмена", НастройкаОбмена);
			РаботаПомощникаЗавершена = Истина;
			
			Если УпрощенныйРежимНастройки Тогда
				
				Элементы.ПодключитьСервис.КнопкаПоУмолчанию = Ложь;
				Элементы.ПодключитьСервис.Доступность = Ложь;
				Элементы.ТестированиеНастройкиОбменаКартинкаУРН.Картинка = БиблиотекаКартинок.Успешно32;
				Элементы.СтраницыРезультатПодключенияУРН.ТекущаяСтраница = Элементы.СтраницаПодключеноУРН;
				Элементы.ДекорацияУспешноеПодключениеУРН.Видимость = Истина;
				
				СменитьСтраницуКомандНавигации(Элементы.Готово);
				
			Иначе
				Элементы.ТестированиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.Успешно32;
				Элементы.СтраницыРезультатПодключения.ТекущаяСтраница = Элементы.СтраницаПодключено;
				Элементы.ДекорацияУспешноеПодключение.Видимость = Истина;
				Если Элементы.КомандыНавигации.ТекущаяСтраница = Элементы.Отмена Тогда
					СменитьСтраницуКомандНавигации(Элементы.Готово);
				Иначе
					СменитьСтраницуКомандНавигации(Элементы.НазадГотово);
				КонецЕсли;
			КонецЕсли;
			
		Иначе
			Если УпрощенныйРежимНастройки Тогда
				Элементы.ПодключитьСервис.Доступность = Истина;
				Элементы.СтраницыРезультатПодключенияУРН.ТекущаяСтраница = Элементы.СтраницаНеПодключеноУРН;
				Элементы.ТестированиеНастройкиОбменаКартинкаУРН.Картинка = БиблиотекаКартинок.Ошибка32;
			Иначе
				Элементы.СтраницыРезультатПодключения.ТекущаяСтраница = Элементы.СтраницаНеПодключено;
				Элементы.ТестированиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.Ошибка32;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если УпрощенныйРежимНастройки Тогда
			Элементы.ПодключитьСервис.Доступность = Истина;
			Элементы.ДекорацияНеПодключеноУРН.Видимость = Истина;
			Элементы.ТестированиеНастройкиОбменаКартинкаУРН.Картинка = БиблиотекаКартинок.Ошибка32;
		Иначе
			Элементы.ДекорацияНеПодключено.Видимость = Истина;
			Элементы.ТестированиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.Ошибка32;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВключитьНастройкуОбмена(Знач НастройкаОбмена, Успех)
	
	УстановитьПривилегированныйРежим(Истина);
	Объект = НастройкаОбмена.ПолучитьОбъект();
	Объект.Прочитать();
	Попытка
		Объект.Недействительна = Ложь;
		Объект.Записать();
		Успех = Истина;
	Исключение
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВидОперации = НСтр("ru = 'Включение настройки.'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			ВидОперации, ТекстОшибки, ТекстСообщения, "ОбменСБанками", НастройкаОбмена);
	КонецПопытки
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(Знач ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

&НаСервереБезКонтекста
Процедура ПриЗакрытииНаСервере(Знач ИдентификаторЗадания)
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура СообщитьОшибкуПользователю(ТекстСообщения, ДополнительныеПараметры) Экспорт
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытииПомощника(Результат, ДополнительныеПараметры) Экспорт
	
	ПриЗакрытииНаСервере(ИдентификаторЗадания);

КонецПроцедуры

#КонецОбласти

#Область УпрощенныйРежимНастройки

&НаКлиенте
Процедура НачатьЗагрузкуНастроекИзФайла()
	
	ПередНачаломПодключенияСервиса();
	
	НастройкиЗагруженыИзФайла = Ложь;
	
#Если ВебКлиент Тогда
	Если Не ВыбранФайлНастроекОбмена Тогда
		ТекстСообщения = НСтр("ru = 'Не выбран файл настроек обмена'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПутьКФайлуНастроекОбмена");
		Возврат;
	КонецЕсли;
#Иначе
	Если ЗначениеЗаполнено(ПутьКФайлуНастроекОбмена) Тогда
		Попытка
			ДвоичныеДанныеФайлаНастроек = Новый ДвоичныеДанные(ПутьКФайлуНастроекОбмена);
		Исключение
			ТекстСообщения = НСтр("ru = 'Неверно указан путь к файлу настроек обмена'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПутьКФайлуНастроекОбмена");
			Возврат;
		КонецПопытки;
		АдресФайлаНастроекОбмена = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайлаНастроек, УникальныйИдентификатор);
	Иначе
		ТекстСообщения = НСтр("ru = 'Не выбран файл настроек обмена'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПутьКФайлуНастроекОбмена");
		Возврат;
	КонецЕсли;
#КонецЕсли

	Файл = Новый Файл(ПутьКФайлуНастроекОбмена);
	Если Не Файл.Расширение = ".xml" Тогда
		ТекстСообщения = НСтр("ru = 'Указан неверный файл.
									|Файл настроек должен иметь расширение ""xml""'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПутьКФайлуНастроекОбмена");
		Возврат;
	КонецЕсли;
		
	УчастникиОбменаИзФайла = УчастникиОбменаИзФайла(АдресФайлаНастроекОбмена, Организация);
	
	Если УчастникиОбменаИзФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если УчастникиОбменаИзФайла.Организация <> Организация Тогда
		ТекстСообщения = НСтр("ru = 'Указан неверный файл.
									|Требуются настройки для организации: %1
									|Файл настроек содержит настройки для организации: %2'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Организация, УчастникиОбменаИзФайла.Организация);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;

	Если УчастникиОбменаИзФайла.Банк <> Банк Тогда
		ТекстСообщения = НСтр("ru = 'Указан неверный файл.
									|Требуются настройки для банка: %1
									|Файл настроек содержит настройки для банка: %2'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Банк, УчастникиОбменаИзФайла.Банк);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	СпособСозданияНастройки = 1;
	
	НастройкиЗагруженыИзФайла = Истина;
	Если УчастникиОбменаИзФайла.ЭтоНастройкиСбербанка Тогда
		ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн");
		URLВК = УчастникиОбменаИзФайла.URLВК;
		ПодключитьОбработчикОжидания("ОпределитьURLИнфоФайлаСбербанк", 0.1, Истина);
	Иначе
		ПодключитьОбработчикОжидания("СоздатьНастройкуОбмена", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастройкаФормыУпрощенногоРежима()
	
	Элементы.СтраницаОписание.Видимость = Ложь;
	Элементы.СтраницаВыборСпособаСозданияНастройки.Видимость = Ложь;
	Элементы.СтраницаПолучениеССервера.Видимость = Ложь;
	Элементы.СтраницаВыборФайлаНастроек.Видимость = Ложь;
	Элементы.СтраницаЗапросИспользованияВнешнегоМодуля.Видимость = Ложь;
	Элементы.СтраницаАсинхронныйОбмен.Видимость = Ложь;
	Элементы.СтраницаСертификаты.Видимость = Ложь;
	Элементы.СтраницаФинал.Видимость = Ложь;
	
	Если НеПоказыватьСтраницуВыборОрганизацииИБанка Тогда
		Элементы.СтраницаВыборОрганизацииИБанка.Видимость = Ложь;
		Элементы.НазадГотово.Видимость = Ложь;
	КонецЕсли;
		
	Элементы.НазадДалееОтмена.Видимость = Ложь;
		
	Элементы.Заголовок.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.Подчеркивание);
	
КонецПроцедуры

&НаКлиенте
Функция СменитьСтраницуНаУпрощеннуюНастройку(НоваяСтраница)
	
	Если УпрощенныйРежимНастройки
		И НоваяСтраница <> Элементы.СтраницаПодождите
		И НоваяСтраница <> Элементы.СтраницаВыборОрганизацииИБанка Тогда
		
		Если НоваяСтраница = Элементы.Шаги.ТекущаяСтраница Тогда
			Возврат Истина;
		КонецЕсли;
		
		НастройкаСтраницыУпрощенногоРежима();
		Если ХронологияПереключенияСтраниц.Количество() Тогда
			СменитьСтраницуКомандНавигации(Элементы.НазадОтмена);
		Иначе
			СменитьСтраницуКомандНавигации(Элементы.Отмена);
		КонецЕсли;
		
		Элементы.СтраницаУпрощенныйРежимНастройки.Видимость = Истина;
		Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаУпрощенныйРежимНастройки;
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура НастройкаСтраницыУпрощенногоРежима()
	
	Если ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн Тогда
		
		УстановитьСтраницуЭтапа(Элементы.СтраницыСпособыПолученияНастроек, Элементы.СтраницаНастройкиПоЛогину);
		
	ИначеЕсли ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезВК Тогда
		
		УстановитьСтраницуЭтапа(Элементы.СтраницыСпособыПолученияНастроек, Элементы.СтраницаНастройкиПустая);
		Элементы.КартинкаПолучениеНастроек.Видимость = Ложь;
		Элементы.КартинкаПодключитьСервис.Картинка = БиблиотекаКартинок.Этап3ОбменСБанками;
		
	Иначе
		
		Если НастройкиОбменаСБанком.СпособАутентификации = "ИзФайла" Тогда
			УстановитьСтраницуЭтапа(Элементы.СтраницыСпособыПолученияНастроек, Элементы.СтраницаНастройкиИзФайла);
		ИначеЕсли НастройкиОбменаСБанком.СпособАутентификации = "ПоЛогину"
			ИЛИ (НастройкиОбменаСБанком.СпособАутентификации = "ПоЛогинуИлиСертификату"
			И НЕ ЕстьСертификаты) Тогда
			УстановитьСтраницуЭтапа(Элементы.СтраницыСпособыПолученияНастроек, Элементы.СтраницаНастройкиПоЛогину);
		Иначе
			УстановитьСтраницуЭтапа(Элементы.СтраницыСпособыПолученияНастроек, Элементы.СтраницаНастройкиПоСертификату);
		КонецЕсли;
		
	КонецЕсли;
	
	ЕстьИнструкция = НЕ ПустаяСтрока(НастройкиОбменаСБанком.АдресИнструкции);
	Элементы.НадписьПодробнееОзнакомлениеСУсловиями.Видимость  = ЕстьИнструкция;
	Элементы.НадписьПодробнееНастройкаНаСторонеБанка.Видимость = ЕстьИнструкция;
	
	Элементы.ГруппаЗагрузкаПараметровОбменаСБанкомУРН.Видимость = Ложь;
	Элементы.ГруппаСозданиеНастройкиОбменаУРН.Видимость = Ложь;	
	Элементы.ГруппаТестированиеНастройкиОбменаУРН.Видимость = Ложь;
	Элементы.СтраницыРезультатПодключенияУРН.ТекущаяСтраница = Элементы.СтраницаНеЗавершеноУРН;

	Элементы.ПодключитьСервис.КнопкаПоУмолчанию = Истина;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСтраницуЭтапа(ЭтапНастройки, НоваяСтраница)
	
	ЭтапНастройки.ТекущаяСтраница = НоваяСтраница;
	
	Для Каждого Страница Из ЭтапНастройки.ПодчиненныеЭлементы Цикл
		Если Страница <> НоваяСтраница Тогда
			Страница.Видимость = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	НоваяСтраница.Видимость = Истина;
	ЭтапНастройки.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПодробнееУсловияСбербанкНажатие(Элемент)
	
	ОбщегоНазначенияКлиент.ОткрытьНавигационнуюСсылку(НастройкиОбменаСБанком.АдресИнструкции);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьПодробнееНастройкаНаСторонеБанкаНажатие(Элемент)
	
	ОбщегоНазначенияКлиент.ОткрытьНавигационнуюСсылку(НастройкиОбменаСБанком.АдресИнструкции + "#instruction");
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатУННачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПодключитьОбработчикОжидания("ЗаполнитьСписокВыбораСертификатовАутентификацииСВыводомОшибки", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередНачаломПодключенияСервиса()
	
	ОчиститьСообщения();

	Элементы.ПодключитьСервис.Доступность = Ложь;
	
	Элементы.ГруппаСозданиеНастройкиОбменаУРН.Видимость = Ложь;
	Элементы.ГруппаТестированиеНастройкиОбменаУРН.Видимость= Ложь;
	
	Элементы.ГруппаЗагрузкаПараметровОбменаСБанкомУРН.Видимость = Истина;
	Элементы.ПолучениеНастроекОбменаКартинкаУРН.Картинка = БиблиотекаКартинок.НезавершившаясяПроверка;
	Элементы.СтраницыРезультатПодключенияУРН.ТекущаяСтраница = Элементы.СтраницаНеЗавершеноУРН;
		
КонецПроцедуры

#КонецОбласти

#КонецОбласти