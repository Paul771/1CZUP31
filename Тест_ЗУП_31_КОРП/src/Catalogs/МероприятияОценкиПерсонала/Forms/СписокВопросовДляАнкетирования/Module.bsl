
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	Параметры.Свойство("ХарактеристикаПерсонала", ХарактеристикаПерсонала);
	
	Если Параметры.РежимВыбора Тогда
		Элементы.СписокВопросов.РежимВыбора = Истина;
	КонецЕсли;
	
	ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "СписокВопросов");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыСписокВопросов

&НаСервере
Процедура СписокВопросовПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, Настройки);
КонецПроцедуры

&НаСервере
Процедура СписокВопросовПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, , СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВопросовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Не Группа Тогда
		ОткрытьФормуНастройкаВопросовДляАнкетированияКлиент(Элемент, Отказ, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВопросовПередНачаломИзменения(Элемент, Отказ)
	
	Если Не Элемент.ТекущиеДанные.ЭтоГруппа Тогда
		ОткрытьФормуНастройкаВопросовДляАнкетированияКлиент(Элемент, Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьФормуНастройкаВопросовДляАнкетированияКлиент(Элемент, Отказ, СозданиеЭлемента = Ложь)

	ПараметрыФормы = Новый Структура;
	
	Если СозданиеЭлемента Тогда
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура("Родитель", Элементы.СписокВопросов.ТекущийРодитель));
	Иначе
		ПараметрыФормы.Вставить("Ключ", Элемент.ТекущиеДанные.Ссылка);
	КонецЕсли;
	ПараметрыФормы.Вставить("ХарактеристикаПерсонала", ХарактеристикаПерсонала);
	
	ОткрытьФорму("Справочник.МероприятияОценкиПерсонала.Форма.НастройкаВопросовДляАнкетирования", ПараметрыФормы, ЭтаФорма, ХарактеристикаПерсонала); 
	Отказ = Истина;

КонецПроцедуры

#КонецОбласти