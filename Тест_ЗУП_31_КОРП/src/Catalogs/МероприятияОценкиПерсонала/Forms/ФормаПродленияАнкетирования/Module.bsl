
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("ДатаНачала", ДатаНачала);
	Параметры.Свойство("ДатаОкончания", ДатаОкончания);
	
	НоваяДатаОкончания = ДатаОкончания;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НоваяДатаОкончанияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыВыбораДаты = Новый Структура;
	ПараметрыВыбораДаты.Вставить("НачальноеЗначение",	НоваяДатаОкончания);
	ПараметрыВыбораДаты.Вставить("Заголовок",			НСтр("ru = 'Выбор даты переноса'"));
	ПараметрыВыбораДаты.Вставить("ПоясняющийТекст",		НСтр("ru = 'Выберите дату, на которую будет осуществлен перенос окончания анкетирования'"));
	
	ОткрытьФорму("ОбщаяФорма.ВыборДаты", ПараметрыВыбораДаты, Элементы.НоваяДатаОкончания);
	
КонецПроцедуры

&НаКлиенте
Процедура НоваяДатаОкончанияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		НоваяДатаОкончания = ВыбранноеЗначение;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Продлить(Команда)
	
	ОшибкиПользователю = Неопределено;
	
	Если НоваяДатаОкончания < ДатаНачала Тогда
		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
			ОшибкиПользователю,
			"НоваяДатаОкончания",
			НСтр("ru = 'Дата окончания не может быть меньше даты начала.'"), "");
		
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(ОшибкиПользователю);
		Возврат;
		
	КонецЕсли;
	
	Если НоваяДатаОкончания = ДатаОкончания Тогда
		// ничего не нужно делать
		Закрыть(Неопределено);
	Иначе
		Закрыть(НоваяДатаОкончания);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
