#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("АдресВариантовОтветов") Тогда
		ВариантыОтветов.Загрузить(ПолучитьИзВременногоХранилища(Параметры.АдресВариантовОтветов));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если ДанныеЗаполненныКорректно() Тогда
		Закрыть(АдресВариантовОтветов());
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте

&НаКлиенте
Процедура ВариантыОтветовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.Ссылка = ПредопределенноеЗначение("Справочник.ВариантыОтветовАнкет.ПустаяСсылка");
		Элемент.ТекущиеДанные.Модифицированность = Истина;
	ИначеЕсли Не ОтменаРедактирования Тогда
		Элемент.ТекущиеДанные.Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлуженбныеПроцедурыИФункции

&НаСервере
Функция АдресВариантовОтветов()
	Возврат ПоместитьВоВременноеХранилище(ВариантыОтветов.Выгрузить(), Новый УникальныйИдентификатор);
КонецФункции

&НаКлиенте
Функция ДанныеЗаполненныКорректно()

	ОчиститьСообщения();
	
	Ошибки = Неопределено;
	
	Если ВариантыОтветов.Количество() < 2 Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "ВариантыОтветов", НСтр("ru = 'Заполните хотя бы 2 разных варианта ответа.'"),"");
	ИначеЕсли ВариантыОтветов.Количество() > 10 Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "ВариантыОтветов", НСтр("ru = 'Количество вариантов ответов не может превышать 10.'"),"");
	КонецЕсли;
	
	Для каждого ВариантОтвета Из ВариантыОтветов Цикл
		Если НЕ ЗначениеЗаполнено(ВариантОтвета.Наименование) Тогда
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
				Ошибки,
				"ВариантыОтветов[%1].Наименование",
				НСтр("ru = 'Не заполнен вариант ответа.'"),"", ВариантыОтветов.Индекс(ВариантОтвета));
		КонецЕсли;
		Если ВариантыОтветов.НайтиСтроки(Новый Структура("Наименование", ВариантОтвета.Наименование)).Количество() > 1 Тогда
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
				Ошибки,
				"ВариантыОтветов[%1].Наименование",
				НСтр("ru = 'Дублируются варианты ответов.'"),"", ВариантыОтветов.Индекс(ВариантОтвета));
		КонецЕсли;
	КонецЦикла; 
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
	
	Возврат (Ошибки = Неопределено);

КонецФункции

#КонецОбласти