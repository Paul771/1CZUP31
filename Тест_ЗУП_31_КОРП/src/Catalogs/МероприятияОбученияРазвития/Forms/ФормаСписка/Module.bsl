#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьВидимостьАрхивныхМероприятий(ЭтаФорма);
	
	Элементы.Список.РежимВыбора = Параметры.РежимВыбора;
	ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "Список",,,, "ВАрхиве");
	
	Если Параметры.Свойство("ОтборПоХарактеристике") Тогда
		УстановитьОтборПоХарактеристике(Параметры.ОтборПоХарактеристике)
	КонецЕсли;
	
	Если Параметры.Свойство("АдресСпискаПодобранныхМероприятий") Тогда
		ПодобранныеМероприятия.ЗагрузитьЗначения(ПолучитьИзВременногоХранилища(Параметры.АдресСпискаПодобранныхМероприятий));
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ПодобранныеМероприятия", ПодобранныеМероприятия.ВыгрузитьЗначения())
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоказыватьНеИспользуемыеМероприятияПриИзменении(Элемент)
	УстановитьВидимостьАрхивныхМероприятий(ЭтаФорма);
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, Настройки);
КонецПроцедуры

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, , СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборПоХарактеристике(Характеристика)

	СписокМероприятий = СписокМероприятийПоХарактеристикам(Характеристика);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"Ссылка",
			СписокМероприятий,
			ВидСравненияКомпоновкиДанных.ВСписке);

КонецПроцедуры

&НаСервере
Функция СписокМероприятийПоХарактеристикам(Характеристика)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МероприятияОбученияРазвитияХарактеристикиПерсонала.Ссылка КАК Мероприятие
		|ИЗ
		|	Справочник.МероприятияОбученияРазвития.ХарактеристикиПерсонала КАК МероприятияОбученияРазвитияХарактеристикиПерсонала
		|ГДЕ
		|	МероприятияОбученияРазвитияХарактеристикиПерсонала.Характеристика = &Характеристика";
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Мероприятие");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьАрхивныхМероприятий(Форма)
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.Список, "ВАрхиве", Ложь, , , НЕ Форма.ПоказыватьНеИспользуемыеМероприятия);
КонецПроцедуры

#КонецОбласти 
