#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий
	
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат; 
	КонецЕсли;	
	
	Если ЭтоГруппа Тогда
		Возврат;	
	КонецЕсли;	
	
	Если НЕ ЭтоНовый() Тогда			
		СвойстваЭлементаВБазе = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ПометкаУдаления");
		ДополнительныеСвойства.Вставить("СтараяПометкаУдаления", СвойстваЭлементаВБазе.ПометкаУдаления);		
	КонецЕсли;	
	
	Если НЕ РазрешитьРедактироватьНаименование И ЗначениеЗаполнено(Элемент) Тогда
		Наименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Элемент, "Наименование");
	КонецЕсли;			
	
	РазработкаЭлектронныхКурсовСлужебный.ПроверитьВозможностьЗаписиЭлементаПередЗаписью(ЭтотОбъект, Отказ);
	РазработкаЭлектронныхКурсовСлужебный.УстановитьДатуИзмененияПередЗаписью(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	РазработкаЭлектронныхКурсовСлужебный.УстановитьКодКратныйДесятиПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс);
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда	
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ПомечатьПодчиненные") Тогда
		ПомечатьПодчиненные = ДополнительныеСвойства.ПомечатьПодчиненные;
	Иначе
		ПомечатьПодчиненные = Истина;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("СтараяПометкаУдаления") И ПомечатьПодчиненные Тогда
		
		Если НЕ ДополнительныеСвойства.СтараяПометкаУдаления
			И ПометкаУдаления Тогда			
						
			Справочники.СодержаниеЭлектронныхКурсов.УстановитьСнятьПометкуУдаленияПодчиненныхЭлементов(Ссылка, Истина, Владелец);
			
		КонецЕсли;
		
		Если ДополнительныеСвойства.СтараяПометкаУдаления
			И НЕ ПометкаУдаления Тогда
		
			Справочники.СодержаниеЭлектронныхКурсов.УстановитьСнятьПометкуУдаленияПодчиненныхЭлементов(Ссылка, Ложь, Владелец);
			
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли