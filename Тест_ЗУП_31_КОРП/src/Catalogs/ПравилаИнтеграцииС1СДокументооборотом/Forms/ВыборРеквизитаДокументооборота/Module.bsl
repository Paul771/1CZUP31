#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("ТипОбъектаДокументооборота", ТипОбъектаДокументооборота);
	ЗаполнитьДеревоРеквизитов(Параметры.РеквизитыОбъектаДокументооборота,
		Параметры.ИмяРеквизитаОбъектаДокументооборота,
		Параметры.ПредставлениеРеквизитаОбъектаПотребителя);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоРеквизитов

&НаКлиенте
Процедура ДеревоРеквизитовВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ВыбратьЗначениеРеквизита();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоРеквизитовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбратьЗначениеРеквизита();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыбратьЗначениеРеквизита();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьЗначениеРеквизита()
	
	ТекущиеДанные = Элементы.ДеревоРеквизитов.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Имя = "" И Не ТекущиеДанные.ДополнительныйРеквизитДокументооборота Тогда 
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Имя", ТекущиеДанные.Имя);
	Результат.Вставить("Представление", ТекущиеДанные.Представление);
	Результат.Вставить("ДополнительныйРеквизитДокументооборота", ТекущиеДанные.ДополнительныйРеквизитДокументооборота);
	Результат.Вставить("ДополнительныйРеквизитДокументооборотаID", ТекущиеДанные.ДополнительныйРеквизитДокументооборотаID);
	Результат.Вставить("ДополнительныйРеквизитДокументооборотаТип", ТекущиеДанные.ДополнительныйРеквизитДокументооборотаТип);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоРеквизитов(РеквизитыОбъектаДокументооборота,
	ИмяРеквизитаОбъектаДокументооборота,
	ПредставлениеРеквизитаОбъектаПотребителя)
	
	Дерево = РеквизитФормыВЗначение("ДеревоРеквизитов");

	Для Каждого Реквизит Из РеквизитыОбъектаДокументооборота Цикл
		
		НоваяСтрока = Дерево.Строки.Добавить();
		НоваяСтрока.Имя = Реквизит.Имя;
		НоваяСтрока.Представление = Реквизит.Представление;
		НоваяСтрока.Тип = ИнтеграцияС1СДокументооборотКлиентСервер.ПредставлениеТипаОбъектаXDTO(Строка(Реквизит.Тип));
		НоваяСтрока.ДополнительныйРеквизитДокументооборота = Реквизит.ДополнительныйРеквизитДокументооборота;
		НоваяСтрока.ДополнительныйРеквизитДокументооборотаID = Реквизит.ДополнительныйРеквизитДокументооборотаID;
		НоваяСтрока.ДополнительныйРеквизитДокументооборотаТип = Реквизит.ДополнительныйРеквизитДокументооборотаТип;
		Если НоваяСтрока.ДополнительныйРеквизитДокументооборота Тогда
			НоваяСтрока.Картинка = 8;
		Иначе
			НоваяСтрока.Картинка = 3;
		КонецЕсли;
		
	КонецЦикла;
	
	Дерево.Строки.Сортировать("Представление");
	
	ЗначениеВРеквизитФормы(Дерево, "ДеревоРеквизитов");
	
	// Найдем ранее выбранный реквизит.
	Для Каждого ЭлементДерева Из ДеревоРеквизитов.ПолучитьЭлементы() Цикл
		Если ЭлементДерева.Имя = ИмяРеквизитаОбъектаДокументооборота Тогда
			Элементы.ДеревоРеквизитов.ТекущаяСтрока = ЭлементДерева.ПолучитьИдентификатор();
			Возврат;
		КонецЕсли;
	КонецЦикла;
	// Найдем подходящий по представлению.
	Для Каждого ЭлементДерева Из ДеревоРеквизитов.ПолучитьЭлементы() Цикл
		Если ЭлементДерева.Представление = ПредставлениеРеквизитаОбъектаПотребителя Тогда
			Элементы.ДеревоРеквизитов.ТекущаяСтрока = ЭлементДерева.ПолучитьИдентификатор();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

