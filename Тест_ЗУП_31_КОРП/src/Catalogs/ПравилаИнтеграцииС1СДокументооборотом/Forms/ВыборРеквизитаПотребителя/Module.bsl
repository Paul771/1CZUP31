
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Метаданные.НайтиПоПолномуИмени(Параметры.ТипОбъекта);
	
	Заголовок =  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Выбор реквизита %1'"), ?(Объект.Синоним = "", Объект.Имя, Объект.Синоним));
	
	ЗаполнитьДеревоРеквизитов(Параметры.ТипОбъекта,
		Параметры.ИмяРеквизитаОбъектаПотребителя,
		Параметры.ПредставлениеРеквизитаОбъектаДокументооборота);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоРеквизитов

&НаКлиенте
Процедура ДеревоРеквизитовПередРазворачиванием(Элемент, Строка, Отказ)
	
	ДанныеСтроки = ДеревоРеквизитов.НайтиПоИдентификатору(Строка);
	
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДанныеСтроки.РеквизитыСчитаны Тогда
		ЗаполнитьПодчиненныеРеквизиты(Строка, ДанныеСтроки.ИмяМетаданных);
		ДанныеСтроки.РеквизитыСчитаны = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоРеквизитовВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ВыбратьЗначениеРеквизита();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыбратьЗначениеРеквизита();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьЗначениеРеквизита()
	
	ТекущиеДанные = Элементы.ДеревоРеквизитов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	Если ТекущиеДанные.Имя = "" Тогда 
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Имя", ТекущиеДанные.Путь);
	Результат.Вставить("Представление", ТекущиеДанные.ПредставлениеПути);
	Результат.Вставить("ДополнительныйРеквизитОбъекта", ТекущиеДанные.ДополнительныйРеквизитОбъекта);
	Результат.Вставить("ДополнительныйРеквизитОбъектаСвойство", ТекущиеДанные.ДополнительныйРеквизитОбъектаСвойство);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоРеквизитов(ИмяМетаданных,
	ИмяРеквизитаОбъектаПотребителя,
	ПредставлениеРеквизитаОбъектаДокументооборота)
	
	Дерево = РеквизитФормыВЗначение("ДеревоРеквизитов");
	Объект = Метаданные.НайтиПоПолномуИмени(ИмяМетаданных);
	
	Для Каждого Реквизит Из Объект.СтандартныеРеквизиты Цикл
		
		Если Реквизит.Имя = "Проведен"
			Или Реквизит.Имя = "ПометкаУдаления"
			Или Реквизит.Имя = "Ссылка" Тогда 
			
			Продолжить;
			
		КонецЕсли;
		
		Представление = Реквизит.Представление();
		
		НоваяСтрока = Дерево.Строки.Добавить();
		НоваяСтрока.Имя = Реквизит.Имя;
		НоваяСтрока.Путь = Реквизит.Имя;
		НоваяСтрока.Представление = Представление;
		НоваяСтрока.ПредставлениеПути = Представление;
		НоваяСтрока.Тип = Реквизит.Тип;
		
		Типы = Реквизит.Тип.Типы();
		Если Типы.Количество() > 1 Тогда 
			Продолжить;
		КонецЕсли;
			
		ОбъектРеквизит = Метаданные.НайтиПоТипу(Типы[0]);
		Если ОбъектРеквизит = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		НоваяСтрока.ИмяМетаданных = ОбъектРеквизит.ПолноеИмя();
		
		Если Метаданные.Справочники.Содержит(ОбъектРеквизит) 
			Или Метаданные.Документы.Содержит(ОбъектРеквизит) 
			Или Метаданные.БизнесПроцессы.Содержит(ОбъектРеквизит) 
			Или Метаданные.Задачи.Содержит(ОбъектРеквизит) 
			Или Метаданные.ПланыВидовХарактеристик.Содержит(ОбъектРеквизит) 
			Или Метаданные.ПланыСчетов.Содержит(ОбъектРеквизит) Тогда 
			
			НоваяСтрока.ЕстьРеквизиты = Истина;
			ДочерняяСтрока = НоваяСтрока.Строки.Добавить();
			
		КонецЕсли;
		
	КонецЦикла;
	
	НоваяСтрока = Дерево.Строки.Добавить();
	НоваяСтрока.Имя = "Представление";
	НоваяСтрока.Путь = "Представление";
	НоваяСтрока.Представление = НСтр("ru='Представление'");
	НоваяСтрока.ПредставлениеПути = НСтр("ru='Представление'");
	НоваяСтрока.Тип = "Строка";
	
	Для Каждого Реквизит Из Объект.Реквизиты Цикл
		
		Если СтрНачинаетсяС(Реквизит.Имя, "Удалить") Тогда
			Продолжить;
		КонецЕсли;
	
		Представление = Реквизит.Представление();
		
		НоваяСтрока = Дерево.Строки.Добавить();
		НоваяСтрока.Имя = Реквизит.Имя;
		НоваяСтрока.Путь = Реквизит.Имя;
		НоваяСтрока.Представление = Представление;
		НоваяСтрока.ПредставлениеПути = Представление;
		НоваяСтрока.Тип = Реквизит.Тип;
		
		Типы = Реквизит.Тип.Типы();
		Если Типы.Количество() > 1 Тогда 
			Продолжить;
		КонецЕсли;
			
		ОбъектРеквизит = Метаданные.НайтиПоТипу(Типы[0]);
		Если ОбъектРеквизит = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		НоваяСтрока.ИмяМетаданных = ОбъектРеквизит.ПолноеИмя();
		
		Если Метаданные.Справочники.Содержит(ОбъектРеквизит) 
			Или Метаданные.Документы.Содержит(ОбъектРеквизит) 
			Или Метаданные.БизнесПроцессы.Содержит(ОбъектРеквизит) 
			Или Метаданные.Задачи.Содержит(ОбъектРеквизит) 
			Или Метаданные.ПланыВидовХарактеристик.Содержит(ОбъектРеквизит) 
			Или Метаданные.ПланыСчетов.Содержит(ОбъектРеквизит) Тогда 
			
			НоваяСтрока.ЕстьРеквизиты = Истина;
			ДочерняяСтрока = НоваяСтрока.Строки.Добавить();
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Метаданные.ФункциональныеОпции.Найти("ИспользоватьДополнительныеРеквизитыИСведения") <> Неопределено
		И ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеРеквизитыИСведения") Тогда
	
		ИмяПредопределенногоНабора = СтрЗаменить(ИмяМетаданных,".","_");
		МенеджерСправочника = Справочники["НаборыДополнительныхРеквизитовИСведений"];
		Попытка
			НаборСвойств = МенеджерСправочника[ИмяПредопределенногоНабора];
		Исключение
			НаборСвойств = Неопределено;
		КонецПопытки;
		
		Если НаборСвойств <> Неопределено Тогда
			
			Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	ДополнительныеРеквизитыИСведения.Ссылка,
				|	ВЫБОР КОГДА ДополнительныеРеквизитыИСведения.Заголовок = """"
				|		ТОГДА ДополнительныеРеквизитыИСведения.Наименование
				|		ИНАЧЕ ДополнительныеРеквизитыИСведения.Заголовок
				|	КОНЕЦ КАК Наименование,
				|	ДополнительныеРеквизитыИСведения.ТипЗначения
				|ИЗ
				|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
				|ГДЕ
				|	ДополнительныеРеквизитыИСведения.НаборСвойств = &НаборСвойств
				|	И НЕ ДополнительныеРеквизитыИСведения.ПометкаУдаления");
			
			Запрос.УстановитьПараметр("НаборСвойств", НаборСвойств);
			Выборка = Запрос.Выполнить().Выбрать();
			
			Пока Выборка.Следующий() Цикл
				
				НоваяСтрока = Дерево.Строки.Добавить();
				НоваяСтрока.Имя = Выборка.Наименование;
				НоваяСтрока.Путь = Выборка.Наименование;
				НоваяСтрока.Представление = Выборка.Наименование;
				НоваяСтрока.ПредставлениеПути = Выборка.Наименование;
				НоваяСтрока.Тип = Выборка.ТипЗначения;
				НоваяСтрока.ДополнительныйРеквизитОбъекта = Истина;
				НоваяСтрока.ДополнительныйРеквизитОбъектаСвойство = Выборка.Ссылка;
				
			КонецЦикла; 
			
		КонецЕсли;; 
	
	КонецЕсли;
	
	Дерево.Строки.Сортировать("Представление");
	
	ЗначениеВРеквизитФормы(Дерево, "ДеревоРеквизитов");
	
	// Найдем ранее выбранный реквизит.
	Для Каждого ЭлементДерева Из ДеревоРеквизитов.ПолучитьЭлементы() Цикл
		Если ЭлементДерева.Имя = ИмяРеквизитаОбъектаПотребителя Тогда
			Элементы.ДеревоРеквизитов.ТекущаяСтрока = ЭлементДерева.ПолучитьИдентификатор();
			Возврат;
		КонецЕсли;
	КонецЦикла;
	// Найдем подходящий по представлению.
	Для Каждого ЭлементДерева Из ДеревоРеквизитов.ПолучитьЭлементы() Цикл
		Если ЭлементДерева.Представление = ПредставлениеРеквизитаОбъектаДокументооборота Тогда
			Элементы.ДеревоРеквизитов.ТекущаяСтрока = ЭлементДерева.ПолучитьИдентификатор();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПодчиненныеРеквизиты(Строка, ИмяМетаданных)
	
	ДанныеСтроки = ДеревоРеквизитов.НайтиПоИдентификатору(Строка);
	Объект = Метаданные.НайтиПоПолномуИмени(ИмяМетаданных);
	
	ЭлементыДерева = ДанныеСтроки.ПолучитьЭлементы();
	ЭлементыДерева.Очистить();
	
	Для Каждого Реквизит Из Объект.СтандартныеРеквизиты Цикл
		Если Реквизит.Имя = "Проведен"
		 Или Реквизит.Имя = "ПометкаУдаления"
		 Или Реквизит.Имя = "Ссылка" Тогда 
			Продолжить;
		КонецЕсли;
		
		Представление = Реквизит.Представление();
		
		НоваяСтрока = ЭлементыДерева.Добавить();
		НоваяСтрока.Имя = Реквизит.Имя;
		НоваяСтрока.Путь = ДанныеСтроки.Путь + "." + Реквизит.Имя;
		НоваяСтрока.Представление = Представление;
		НоваяСтрока.ПредставлениеПути = ДанныеСтроки.ПредставлениеПути + "." + Представление;
		НоваяСтрока.Тип = Реквизит.Тип;
		
		Типы = Реквизит.Тип.Типы();
		Если Типы.Количество() > 1 Тогда 
			Продолжить;
		КонецЕсли;
			
		ОбъектРеквизит = Метаданные.НайтиПоТипу(Типы[0]);
		Если ОбъектРеквизит = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		НоваяСтрока.ИмяМетаданных = ОбъектРеквизит.ПолноеИмя();
		
		Если Метаданные.Справочники.Содержит(ОбъектРеквизит) 
		 Или Метаданные.Документы.Содержит(ОбъектРеквизит) 
		 Или Метаданные.БизнесПроцессы.Содержит(ОбъектРеквизит) 
		 Или Метаданные.Задачи.Содержит(ОбъектРеквизит) 
		 Или Метаданные.ПланыВидовХарактеристик.Содержит(ОбъектРеквизит) 
		 Или Метаданные.ПланыСчетов.Содержит(ОбъектРеквизит) Тогда 
			НоваяСтрока.ЕстьРеквизиты = Истина;
			ДочерняяСтрока = НоваяСтрока.ПолучитьЭлементы().Добавить();
			
		КонецЕсли;
		
	КонецЦикла;
	
	НоваяСтрока = ЭлементыДерева.Добавить();
	НоваяСтрока.Имя = "Представление";
	НоваяСтрока.Путь = ДанныеСтроки.Путь + "." + "Представление";
	НоваяСтрока.Представление = НСтр("ru='Представление'");
	НоваяСтрока.ПредставлениеПути = ДанныеСтроки.ПредставлениеПути + "." + НСтр("ru='Представление'");
	НоваяСтрока.Тип = "Строка";
	
	Для Каждого Реквизит Из Объект.Реквизиты Цикл
		
		Если СтрНачинаетсяС(Реквизит.Имя, "Удалить") Тогда
			Продолжить;
		КонецЕсли;
	
		Представление = Реквизит.Представление();
		
		НоваяСтрока = ЭлементыДерева.Добавить();
		НоваяСтрока.Имя = Реквизит.Имя;
		НоваяСтрока.Путь = ДанныеСтроки.Путь + "." + Реквизит.Имя;
		НоваяСтрока.Представление =  Представление;
		НоваяСтрока.ПредставлениеПути =  ДанныеСтроки.ПредставлениеПути + "." + Представление;
		НоваяСтрока.Тип = Реквизит.Тип;
		
		Типы = Реквизит.Тип.Типы();
		Если Типы.Количество() > 1 Тогда 
			Продолжить;
		КонецЕсли;
			
		ОбъектРеквизит = Метаданные.НайтиПоТипу(Типы[0]);
		Если ОбъектРеквизит = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		НоваяСтрока.ИмяМетаданных = ОбъектРеквизит.ПолноеИмя();
		
		Если Метаданные.Справочники.Содержит(ОбъектРеквизит) 
		 Или Метаданные.Документы.Содержит(ОбъектРеквизит) 
		 Или Метаданные.БизнесПроцессы.Содержит(ОбъектРеквизит) 
		 Или Метаданные.Задачи.Содержит(ОбъектРеквизит) 
		 Или Метаданные.ПланыВидовХарактеристик.Содержит(ОбъектРеквизит) 
		 Или Метаданные.ПланыСчетов.Содержит(ОбъектРеквизит) Тогда 
			НоваяСтрока.ЕстьРеквизиты = Истина;
			ДочерняяСтрока = НоваяСтрока.ПолучитьЭлементы().Добавить();
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
	
#КонецОбласти