#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ВыгружатьИзменения = Ложь;
	ЗагружатьИзменения = Ложь;
	
	ОбновитьПредставление();
	
	// Установим флаги автообновления при необходимости.
	
	Если ЗначениеЗаполнено(ТипОбъектаПотребителя) Тогда 
		
		ОбъектПотребителя = Метаданные.НайтиПоПолномуИмени(ТипОбъектаПотребителя);
		Если ОбъектПотребителя <> Неопределено Тогда 
			
			Если Метаданные.ПланыОбмена.ИнтеграцияС1СДокументооборотом.Состав.Содержит(ОбъектПотребителя) Тогда
				СтруктураПоиска = Новый Структура("ОбновлятьЗначение", Истина);
				ОбновляемыеРеквизиты = ПравилаЗаполненияРеквизитовПриВыгрузке.НайтиСтроки(СтруктураПоиска);
				ОбновляемыеПечатныеФормы = ПрисоединяемыеПечатныеФормы.НайтиСтроки(СтруктураПоиска);
				ВыгружатьИзменения = (ОбновляемыеРеквизиты.Количество() <> 0
					Или ОбновляемыеПечатныеФормы.Количество() <> 0);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТипОбъектаДокументооборота) Тогда 
		
		СтруктураПоиска = Новый Структура("ОбновлятьЗначение", Истина);
		ОбновляемыеРеквизиты = ПравилаЗаполненияРеквизитовПриЗагрузке.НайтиСтроки(СтруктураПоиска);
		ЗагружатьИзменения = (ОбновляемыеРеквизиты.Количество() <> 0);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обновляет представление правила согласно представлениям объектов и ключевых реквизитов.
//
Процедура ОбновитьПредставление() Экспорт
	
	// Соберем представление объекта ИС.
	ПредставлениеОбъектаПотребителя = "";
	
	ИнтеграцияС1СДокументооборотПереопределяемый.ПриОпределенииПредставленияОбъектаПотребителя(ЭтотОбъект,
		ПредставлениеОбъектаПотребителя);
		
	Если ПредставлениеОбъектаПотребителя = ""
		И ЗначениеЗаполнено(ТипОбъектаПотребителя) Тогда 
		
		ОбъектПотребителя = Метаданные.НайтиПоПолномуИмени(ТипОбъектаПотребителя);
		Если ОбъектПотребителя <> Неопределено Тогда 
			
			ПредставлениеОбъектаПотребителя = ?(ОбъектПотребителя.Синоним = "", 
				ОбъектПотребителя.Имя, 
				ОбъектПотребителя.Синоним);
				
			// Дополним представление ключевыми реквизитами.
			СтруктураПоиска = Новый Структура("Ключевой", Истина);
			КлючевыеРеквизиты = ПравилаЗаполненияРеквизитовПриЗагрузке.НайтиСтроки(СтруктураПоиска);
			Для Каждого КлючевойРеквизит Из КлючевыеРеквизиты Цикл
				Если КлючевойРеквизит.ВариантПравилаЗаполненияРеквизитов
					= Перечисления.ВариантыПравилЗаполненияРеквизитов.УказанноеЗначение Тогда
					ПредставлениеОбъектаПотребителя = ПредставлениеОбъектаПотребителя + ", "
						+ Строка(КлючевойРеквизит.ЗначениеРеквизитаПотребителя);
				ИначеЕсли КлючевойРеквизит.ВариантПравилаЗаполненияРеквизитов
					= Перечисления.ВариантыПравилЗаполненияРеквизитов.ПустаяСсылка() Тогда
				КонецЕсли;
			КонецЦИкла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТипОбъектаДокументооборота) Тогда 
		
		// Выбрем представление согласно типу.
		Если ТипОбъектаДокументооборота = "DMIncomingDocument" Тогда
			ПредставлениеОбъектаДокументооборота = НСтр("ru = 'Входящий документ'");
		ИначеЕсли ТипОбъектаДокументооборота = "DMInternalDocument" Тогда
			ПредставлениеОбъектаДокументооборота = НСтр("ru = 'Внутренний документ'");
		ИначеЕсли ТипОбъектаДокументооборота = "DMOutgoingDocument" Тогда
			ПредставлениеОбъектаДокументооборота = НСтр("ru = 'Исходящий документ'");
		ИначеЕсли ТипОбъектаДокументооборота = "DMCorrespondent" Тогда
			Если ИнтеграцияС1СДокументооборотПовтИсп.ИспользоватьТерминКорреспонденты() Тогда
				ПредставлениеОбъектаДокументооборота = НСтр("ru = 'Корреспондент'");
			Иначе
				ПредставлениеОбъектаДокументооборота = НСтр("ru = 'Контрагент'");
			КонецЕсли;
		Иначе
			ПредставлениеОбъектаДокументооборота = ТипОбъектаДокументооборота;
		КонецЕсли;
		
		// Дополним представление ключевыми реквизитами.
		СтруктураПоиска = Новый Структура("Ключевой", Истина);
		КлючевыеРеквизиты = ПравилаЗаполненияРеквизитовПриВыгрузке.НайтиСтроки(СтруктураПоиска);
		Для Каждого КлючевойРеквизит Из КлючевыеРеквизиты Цикл
			Если КлючевойРеквизит.ВариантПравилаЗаполненияРеквизитов
				= Перечисления.ВариантыПравилЗаполненияРеквизитов.УказанноеЗначение Тогда
				ПредставлениеОбъектаДокументооборота = ПредставлениеОбъектаДокументооборота + ", "
					+ Строка(КлючевойРеквизит.ЗначениеРеквизитаДокументооборота);
			ИначеЕсли КлючевойРеквизит.ВариантПравилаЗаполненияРеквизитов
				= Перечисления.ВариантыПравилЗаполненияРеквизитов.ИзШаблона Тогда
				ПредставлениеОбъектаДокументооборота = ПредставлениеОбъектаДокументооборота + ", "
					+ Строка(КлючевойРеквизит.ШаблонЗначение);
			КонецЕсли;
		КонецЦИкла;
			
	Иначе
		
		ПредставлениеОбъектаДокументооборота = "";
		
	КонецЕсли;
	
	// Соберем наименование.
	Если ЗначениеЗаполнено(ПредставлениеОбъектаПотребителя)
		И ЗначениеЗаполнено(ПредставлениеОбъектаДокументооборота) Тогда
		Наименование = ПредставлениеОбъектаПотребителя 
			+ " - " 
			+ ПредставлениеОбъектаДокументооборота;
	Иначе
		Наименование = "";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли