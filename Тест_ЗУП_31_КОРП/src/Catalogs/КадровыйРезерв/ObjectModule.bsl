

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьУстановленныеОтборы(ПараметрыФормы) Экспорт

	Отборы = Новый Массив;
	
	Если ЗначениеЗаполнено(МинимальныйВозраст) Тогда
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(
		Отборы, "Возраст", ">=", МинимальныйВозраст);
	КонецЕсли;
	Если ЗначениеЗаполнено(МаксимальныйВозраст) Тогда
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(
		Отборы, "Возраст", "<=", МаксимальныйВозраст);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидОбразования) Тогда
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(
		Отборы, "ВидОбразования", ">=", ВидОбразования);
	КонецЕсли;
	Если ЗначениеЗаполнено(Специальность) Тогда
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(
		Отборы, "Специальность", "=", Специальность);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидСтажа) Тогда
		ИмяОтбораСтажа = КадровыйУчетРасширенный.ИмяОтбораПоКатегорииСтажа(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидСтажа, "КатегорияСтажа"));
		Если ЗначениеЗаполнено(МинимальныйСтажМесяцев) И ИмяОтбораСтажа <> Неопределено Тогда
			ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(
			Отборы, ИмяОтбораСтажа, ">=", МинимальныйСтажМесяцев);
		КонецЕсли;
		Если ЗначениеЗаполнено(МаксимальныйСтажМесяцев) И ИмяОтбораСтажа <> Неопределено Тогда
			ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(
			Отборы, ИмяОтбораСтажа, "<=", МаксимальныйСтажМесяцев);
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыФормы.Вставить("Отборы", Отборы);

КонецПроцедуры

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	Возврат ОбменДаннымиЗарплатаКадрыРасширенный.ОграниченияРегистрацииПоОрганизацииИПодразделению(ЭтотОбъект, , , ПозицияШтатногоРасписания);
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	Если Назначение = ПредопределенноеЗначение("Перечисление.НазначенияКадровогоРезерва.ПозицияШтатногоРасписания") Тогда
		Если ЗначениеЗаполнено(ПозицияШтатногоРасписания) И СуществуетКадровыйРезервНаДаннуюПозициюШР(ПозицияШтатногоРасписания) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru ='На данную позицию штатного расписания уже заведен кадровый резерв'"),
				ЭтотОбъект,
				"ПозицияШтатногоРасписания",,
				Отказ);
		КонецЕсли; 
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("ПозицияШтатногоРасписания");
	КонецЕсли;
	
	ПроверитьКорректностьУстановкиТребований(Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СуществуетКадровыйРезервНаДаннуюПозициюШР(ПозицияШР)
	
	СписокПозиций = КадровыйРезерв.ПодобратьПозицииРезерваПоПозицииШтатногоРасписания(ПозицияШР, Ложь);
	
	// удаляем текущую позицию
	ИндексПозиции = СписокПозиций.Найти(Ссылка);
	Если НЕ ИндексПозиции = Неопределено Тогда
		СписокПозиций.Удалить(ИндексПозиции);
	КонецЕсли;
	
	Возврат СписокПозиций.Количество() > 0;
	
КонецФункции

Процедура ПроверитьКорректностьУстановкиТребований(Отказ)

	Если МаксимальныйВозраст > 0 И МинимальныйВозраст > МаксимальныйВозраст Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru ='Неверный диапазон возраста.'"),
			ЭтотОбъект,
			"МинимальныйВозраст",,
			Отказ);
	КонецЕсли;
	Если МаксимальныйСтажМесяцев > 0 И МинимальныйСтажМесяцев > МаксимальныйСтажМесяцев Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru ='Неверный диапазон стажа.'"),
			ЭтотОбъект,
			"МинимальныйСтажМесяцев",,
			Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли