#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		// На основании физического лица.
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(Вакансия) Тогда
		ДанныеВакансии = Справочники.Вакансии.ДанныеВакансии(Вакансия);
		Если ДанныеВакансии.НазначениеНабора = Перечисления.НазначениеНабораПерсоналаНаВакансию.ДолжностьПодразделение Тогда
			// Допустимо пустое подразделение в этом случае.
			ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Подразделение");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Наименование = Неопределено;
	ФизическоеЛицо = Неопределено;
	Рейтинг = 0;
	Состояние = Неопределено;
	ДатаРешения = Неопределено;
	КомментарийРешения = Неопределено;
	УдалитьЭтапРаботы = Неопределено;
	ПричинаОтклонения = Неопределено;
	УточнениеНаименования = Неопределено;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	// Роли физических лиц
	ОрганизацияКандидата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Позиция, "Владелец");
	Если ЗначениеЗаполнено(ОрганизацияКандидата) Тогда
		РегистрыСведений.РолиФизическихЛиц.УстановитьРольФизическогоЛица(
			ФизическоеЛицо, ОрганизацияКандидата, Перечисления.РолиФизическихЛиц.Кандидат);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	Возврат ОбменДаннымиЗарплатаКадрыРасширенный.ОграниченияРегистрацииПоОрганизацииИПодразделению(ЭтотОбъект, , Подразделение, Позиция);
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьУтверждение(Отказ) Экспорт
	
	// Проверяет доступность кандидата для одобрения.
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК Поле1
		|ИЗ
		|	Справочник.Вакансии КАК Вакансии
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Кандидаты КАК Кандидаты
		|		ПО Вакансии.Ссылка = Кандидаты.Вакансия
		|			И (Кандидаты.Ссылка = &Кандидат)
		|			И (НЕ Вакансии.Состояние В (ЗНАЧЕНИЕ(Перечисление.СостоянияВакансии.Закрыта), ЗНАЧЕНИЕ(Перечисление.СостоянияВакансии.Отменена)))");
		
	Запрос.УстановитьПараметр("Кандидат", Ссылка);
	
	Если Запрос.Выполнить().Пустой() Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Кандидат %1 не может быть одобрен, вакансия не указана, закрыта или отменена.'"), Наименование);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли