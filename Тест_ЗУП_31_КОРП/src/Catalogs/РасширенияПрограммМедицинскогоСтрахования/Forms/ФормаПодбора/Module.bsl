
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("МассивСтруктурРасширений") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Заголовок = Параметры.Страхователь;
	ЭтоСотрудник = Параметры.ЭтоСотрудник;
	ДатаНачалаСтрахования = Параметры.ДатаНачалаСтрахования;
	ДатаОкончанияСтрахования = Параметры.ДатаОкончанияСтрахования;
	ДатаНачала = Параметры.ДатаНачала;
	ДатаОкончания = Параметры.ДатаОкончания;
	ДатаРождения = Параметры.ДатаРождения;
	СтраховаяКомпания = Параметры.СтраховаяКомпания;
	
	Для Каждого СтруктураРасширения Из Параметры.МассивСтруктурРасширений Цикл
		НоваяСтрока = РасширенияСтрахования.Добавить();
		НоваяСтрока.РасширениеСтрахования = СтруктураРасширения.РасширениеСтрахования;
		НоваяСтрока.СтраховаяПремия = СтруктураРасширения.СтраховаяПремия;
	КонецЦикла;
	
	Для Каждого СтрокаШкалы Из Параметры.ШкалаВозрастов Цикл
		НоваяСтрока = ШкалаВозрастов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаШкалы);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыСотрудники

&НаКлиенте
Процедура РасширенияСтрахованияРасширениеСтрахованияПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.РасширенияСтрахования.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.СтраховаяПремия = СтраховаяПремия(ТекущиеДанные.РасширениеСтрахования);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	МассивСтруктурРасширений = Новый Массив;
	
	Для Каждого СтрокаТаблицы Из РасширенияСтрахования Цикл
		СтруктураРасширения = Новый Структура("РасширениеСтрахования, СтраховаяПремия", СтрокаТаблицы.РасширениеСтрахования, СтрокаТаблицы.СтраховаяПремия);
		МассивСтруктурРасширений.Добавить(СтруктураРасширения);
	КонецЦикла;
	
	Закрыть(МассивСтруктурРасширений);
	
КонецПроцедуры

&НаКлиенте
Процедура Пересчитать(Команда)
	
	Для Каждого СтрокаТаблицы Из РасширенияСтрахования Цикл
		СтрокаТаблицы.СтраховаяПремия = СтраховаяПремия(СтрокаТаблицы.РасширениеСтрахования);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СтраховаяПремия(РасширениеСтрахования)
	
	ДатыСтрахования = Новый Структура;
	ДатыСтрахования.Вставить("ДатаНачала", ДатаНачала);
	ДатыСтрахования.Вставить("ДатаОкончания", ДатаОкончания);
	ДатыСтрахования.Вставить("ДатаНачалаСтрахования", ДатаНачалаСтрахования);
	ДатыСтрахования.Вставить("ДатаОкончанияСтрахования", ДатаОкончанияСтрахования);
	
	Возврат МедицинскоеСтрахование.СтраховаяПремия(РасширениеСтрахования, ДатыСтрахования, ДатаРождения, ШкалаВозрастов, ЭтоСотрудник);
	
КонецФункции

#КонецОбласти
