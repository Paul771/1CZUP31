
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "НомерКолонки, ИспользованиеРазрешено");
	
	Показатель = Параметры.НачислениеПоказатель;
	
	СведенияОПоказателе = ЗарплатаКадрыРасширенный.СведенияОПоказателеРасчетаЗарплаты(Показатель);
	
	Заголовок = СведенияОПоказателе.Наименование;
	
	Если Параметры.ПоказателиГрейда <> Неопределено Тогда
		ЗначенияПоказателя = Параметры.ПоказателиГрейда[Показатель];
		Если ЗначенияПоказателя <> Неопределено Тогда 
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗначенияПоказателя);
		КонецЕсли;	
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "ЗначениеМин", "ФорматРедактирования", "ЧДЦ=" + СведенияОПоказателе.Точность);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "ЗначениеМакс", "ФорматРедактирования", "ЧДЦ=" + СведенияОПоказателе.Точность);
	
	УстановитьДоступностьПолей(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗапретитьИспользованиеПоказателяПриИзменении(Элемент)
	
	УстановитьДоступностьПолей(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ДанныеПоказателя = Новый Структура("НомерКолонки, ИспользованиеРазрешено, РазмерМин, РазмерМакс, ПоказателиГрейда");
	ЗаполнитьЗначенияСвойств(ДанныеПоказателя, ЭтотОбъект);
	ДанныеПоказателя.ПоказателиГрейда = Новый Соответствие;
	ДанныеПоказателя.ПоказателиГрейда.Вставить(Показатель, Новый Структура("ЗначениеМин, ЗначениеМакс", ЗначениеМин, ЗначениеМакс));
	
	ОповеститьОВыборе(ДанныеПоказателя);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьПолей(Форма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы, "ЗначениеМин", "Доступность", Форма.ИспользованиеРазрешено);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы, "ЗначениеМакс", "Доступность", Форма.ИспользованиеРазрешено);
		
КонецПроцедуры

#КонецОбласти
