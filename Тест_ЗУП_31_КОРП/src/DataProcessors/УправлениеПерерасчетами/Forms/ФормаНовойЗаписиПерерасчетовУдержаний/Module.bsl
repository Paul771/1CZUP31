#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("Организация", Организация);
	
	ЗаполнитьФизическоеЛицоПоСотруднику();
	УстановитьДоступностьОбластейПерерасчетов(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура МесяцСтрокойПриИзменении(Элемент)
	
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(ЭтаФорма, "Месяц", "МесяцСтрокой", Модифицированность);
	ЗаполнитьСписокДокументовНачисления();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("МесяцСтрокойНачалоВыбораЗавершение", ЭтотОбъект);
	
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтаФорма, ЭтаФорма, "Месяц", "МесяцСтрокой", , Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокойРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(ЭтаФорма, "Месяц", "МесяцСтрокой", Направление, Модифицированность);
	ЗаполнитьСписокДокументовНачисления();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокойАвтоПодбор(Элемент,
	Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокойОкончаниеВводаТекста(Элемент,
	Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	
	СотрудникПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Результат = Неопределено;
	Если Модифицированность И ЗаписатьПерерасчет() Тогда
		
		Модифицированность = Ложь;
		
		Результат = Новый Структура;
		Результат.Вставить("Сотрудник", Сотрудник);
		Результат.Вставить("Период", Месяц);
		
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция ЗаписатьПерерасчет()
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ПерерасчетУдержаний.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Организация.Установить(Организация);
	НаборЗаписей.Отбор.ФизическоеЛицо.Установить(ФизическоеЛицо);
	НаборЗаписей.Отбор.ПериодДействия.Установить(Месяц);
	НаборЗаписей.Отбор.ДокументНачисления.Установить(ДокументНачисления);
	
	ПерерассчитываемыеУдержания = УдержанияСотрудника.НайтиСтроки(Новый Структура("Перерассчитывать", Истина));
	Для каждого ПерерассчитываемоеУдержание Из ПерерассчитываемыеУдержания Цикл
		
		Запись = НаборЗаписей.Добавить();
		Запись.Организация = Организация;
		Запись.ФизическоеЛицо = ФизическоеЛицо;
		Запись.ПериодДействия = Месяц;
		Запись.ДокументНачисления = ДокументНачисления;
		Запись.Удержание = ПерерассчитываемоеУдержание.Удержание;
		Запись.ДокументОснование = ПерерассчитываемоеУдержание.ДокументОснование;
		
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура МесяцСтрокойНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЗаполнитьСписокДокументовНачисления();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура СотрудникПриИзмененииНаСервере()
	
	ЗаполнитьФизическоеЛицоПоСотруднику();
	ЗаполнитьСписокДокументовНачисления();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокУдержаний()
	
	Если Не ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		
		ПерерасчетУдержаний = Ложь;
		УдержанияСотрудника.Очистить();
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Месяц", Месяц);
	Запрос.УстановитьПараметр("ФизическоеЛицо", ФизическоеЛицо);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	&Месяц КАК ДатаНачала,
		|	КОНЕЦПЕРИОДА(&Месяц, МЕСЯЦ) КАК ДатаОкончания,
		|	&ФизическоеЛицо КАК ФизическоеЛицо,
		|	&Организация КАК Организация
		|ПОМЕСТИТЬ ВТСотрудникиПериодыДляТаблицыРегистра";
	
	Запрос.Выполнить();
	
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
		"ВТСотрудникиПериодыДляТаблицыРегистра", "Организация,ФизическоеЛицо");
	
	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистра();
	ПараметрыПостроения.ВключатьЗаписиНаНачалоПериода = Истина;
	
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистра(
		"ПлановыеУдержания", Запрос.МенеджерВременныхТаблиц, Истина, ОписаниеФильтра, ПараметрыПостроения);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВЫБОР
		|		КОГДА ПерерасчетУдержаний.Удержание ЕСТЬ NULL 
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК Перерассчитывать,
		|	ВЫРАЗИТЬ(ПлановыеУдержания.Удержание КАК ПланВидовРасчета.Удержания) КАК Удержание,
		|	ПлановыеУдержания.ДокументОснование КАК ДокументОснование
		|ИЗ
		|	ВТПлановыеУдержания КАК ПлановыеУдержания
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПерерасчетУдержаний КАК ПерерасчетУдержаний
		|		ПО ПлановыеУдержания.Удержание = ПерерасчетУдержаний.Удержание
		|			И ПлановыеУдержания.ДокументОснование = ПерерасчетУдержаний.ДокументОснование
		|			И (ПерерасчетУдержаний.ПериодДействия = &Месяц)
		|ГДЕ
		|	ПлановыеУдержания.Используется
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВЫРАЗИТЬ(ПлановыеУдержания.Удержание КАК ПланВидовРасчета.Удержания).РеквизитДопУпорядочивания,
		|	ДокументОснование";
	
	УдержанияСотрудника.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьФизическоеЛицоПоСотруднику()
	
	Если ЗначениеЗаполнено(Сотрудник) Тогда
		ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сотрудник, "ФизическоеЛицо")
	Иначе
		ФизическоеЛицо = Справочники.ФизическиеЛица.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокДокументовНачисления()
	
	Элементы.ДокументНачисления.СписокВыбора.Очистить();
	
	Если Не ЗначениеЗаполнено(Месяц)
		Или Не ЗначениеЗаполнено(Сотрудник) Тогда
		
		ЗаполнитьСписокУдержаний();
		УстановитьДоступностьОбластейПерерасчетов(ЭтаФорма);
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", Месяц);
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(НачисленияУдержанияПоСотрудникам.Регистратор) КАК РегистраторПредставление,
		|	НачисленияУдержанияПоСотрудникам.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК НачисленияУдержанияПоСотрудникам
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(НачисленияУдержанияПоСотрудникам.Период, МЕСЯЦ) = &Период
		|	И НачисленияУдержанияПоСотрудникам.Сотрудник = &Сотрудник
		|	И НачисленияУдержанияПоСотрудникам.НачислениеУдержание.СпособВыполненияНачисления В (ЗНАЧЕНИЕ(Перечисление.СпособыВыполненияНачислений.ЕжемесячноПриОкончательномРасчете), ЗНАЧЕНИЕ(Перечисление.СпособыВыполненияНачислений.ВЗаданныхМесяцахПриОкончательномРасчете))
		|
		|УПОРЯДОЧИТЬ ПО
		|	Регистратор";
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Элементы.ДокументНачисления.СписокВыбора.Добавить(Выборка.Регистратор, Выборка.РегистраторПредставление);
	КонецЦикла;
	
	Если Элементы.ДокументНачисления.СписокВыбора.Количество() = 1 Тогда
		ДокументНачисления = Элементы.ДокументНачисления.СписокВыбора[0].Значение;
	КонецЕсли;
	
	ЗаполнитьСписокУдержаний();
	УстановитьДоступностьОбластейПерерасчетов(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьОбластейПерерасчетов(Форма)
	
	ДоступностьОбластей = ЗначениеЗаполнено(Форма.Месяц) И ЗначениеЗаполнено(Форма.Сотрудник);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ОбластиПерерасчетовГруппа",
		"Доступность",
		ДоступностьОбластей);
	
КонецПроцедуры

#КонецОбласти


