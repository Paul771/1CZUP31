#Область ОписаниеПеременных

&НаКлиенте
Перем ОбновитьИнтерфейс;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоБазоваяВерсия = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ЗначениеФункциональнойОпции("БазоваяВерсия");
	
	ИспользуетсяНесколькоОрганизацийЭД = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизацийЭД");
	
	// Настройки видимости при запуске.
	Элементы.ГруппаИспользоватьОбменМеждуОрганизациями.Видимость = Не ЭтоБазоваяВерсия
		И ИспользуетсяНесколькоОрганизацийЭД;
	
	ЕстьОбменСКонтрагентами = ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСКонтрагентами");
	ЕстьИнтеграцияСЯндексКассой = ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ИнтеграцияСЯндексКассой");
		
	Если ЕстьОбменСКонтрагентами Тогда
		НеИспользоватьОбменМеждуОрганизациями = Ложь;
		МодульОбменСКонтрагентамиПереопределяемый = ОбщегоНазначения.ОбщийМодуль("ОбменСКонтрагентамиПереопределяемый");
		МодульОбменСКонтрагентамиПереопределяемый.ИспользоватьОбменЭДМеждуОрганизациями(НеИспользоватьОбменМеждуОрганизациями);
		Если НеИспользоватьОбменМеждуОрганизациями Тогда
			Элементы.ГруппаИспользоватьОбменМеждуОрганизациями.Видимость = Ложь;
		Иначе
			Элементы.ГруппаИспользоватьОбменМеждуОрганизациями.Видимость = Не ЭтоБазоваяВерсия
				И ИспользуетсяНесколькоОрганизацийЭД;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ГруппаНастройкиОбменаСКонтрагентами.Видимость = ЕстьОбменСКонтрагентами;
	Элементы.ГруппаНастройкиОбменаСБанками.Видимость =
		ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСБанками");
	Элементы.ГруппаНастройкиИнтеграцияСЯндексКассой.Видимость = ЕстьИнтеграцияСЯндексКассой;
		
	// Настройки регламентных заданий.
	УстановитьНастройкиЗаданий();
	
	// Обновление состояния элементов.
	ПрочитатьУстановитьДоступность();
	
	// Добавление элементов управления.
	ДобавитьКомандыСозданияШаблона();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы Тогда
		ОбновитьИнтерфейсПрограммы();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик оповещения формы.
//
// Параметры:
//  ИмяСобытия - Строка - обрабатывается только событие Запись_НаборКонстант, генерируемое панелями администрирования.
//  Параметр   - Структура - содержит имена констант, подчиненных измененной константе, "вызвавшей" оповещение.
//  Источник   - Строка - имя измененной константы, вызвавшей оповещение.
//
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НаборКонстант" Тогда
		
		ТребуетсяОбновление = Ложь;
		Если ТипЗнч(Источник) = Тип("Строка") Тогда
			ТребуетсяОбновление = Элементы.Найти(Источник) <> Неопределено;
		ИначеЕсли ТипЗнч(Источник) = Тип("Структура") Тогда
			Для Каждого ЭлементСтруктуры Из Источник Цикл
				ТребуетсяОбновление = Элементы.Найти(ЭлементСтруктуры.Ключ) <> Неопределено;
				Если ТребуетсяОбновление Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если ТребуетсяОбновление Тогда
			ПрочитатьУстановитьДоступность();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "СинхронизацияТорговыхПредложений_ПриИзменении" Тогда
		УстановитьНастройкиЗаданий();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьЭлектронныеПодписиПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОбменСБанкамиПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОбменЭДПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОбменЭДМеждуОрганизациямиПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьНемедленнуюОтправкуЭДПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьАвтоматическуюОтправкуЭДПриИзменении(Элемент)
	
	ИзменитьИспользованиеЗадания("ОтправкаЭлектронныхДокументов", ИспользоватьАвтоматическуюОтправкуЭД);
	Элементы.ОтправкаЭлектронныхДокументов.Доступность = ИспользоватьАвтоматическуюОтправкуЭД;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьАвтоматическоеПолучениеЭДПриИзменении(Элемент)
	
	ИзменитьИспользованиеЗадания("ПолучениеЭлектронныхДокументов", ИспользоватьАвтоматическоеПолучениеЭД);
	Элементы.ПолучениеЭлектронныхДокументов.Доступность = ИспользоватьАвтоматическоеПолучениеЭД;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьРубрикаторБизнесСетьПриИзменении(Элемент)
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОбменБизнесСетьПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьАвтоматическуюПубликациюТорговыхПредложенийПриИзменении(Элемент)
	
	ИзменитьИспользованиеЗадания("СинхронизацияТорговыхПредложений", ИспользоватьАвтоматическуюПубликациюТорговыхПредложений);
	Элементы.СинхронизацияТорговыхПредложений.Доступность = ИспользоватьАвтоматическуюПубликациюТорговыхПредложений;
	Оповестить("СинхронизацияТорговыхПредложений_ПриИзменении");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИнтеграцияСЯндексКассой

&НаКлиенте
Процедура ИспользоватьИнтеграциюСЯндексКассойПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьАвтоматическоеПолучениеУведомленияОтЯндексКассыПриИзменении(Элемент)
	
	ИзменитьИспользованиеЗадания("ПолучениеУведомленийОтЯндексКассы", ИспользоватьАвтоматическоеПолучениеУведомленияОтЯндексКассы);
	Элементы.ПолучениеУведомленийОтЯндексКассы.Доступность = ИспользоватьАвтоматическоеПолучениеУведомленияОтЯндексКассы;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура ЕстьИдеяНажатие(Элемент)
	
	МодульОбработки = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбменСКонтрагентамиСлужебныйКлиент");
	МодульОбработки.ОткрытьСтраницуОпроса();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОзнакомлениеСЭлектроннымиДокументамиПриИзменении(Элемент)
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастройкиЭлектроннойПодписиИШифрования(Команда)
	
	ОткрытьФорму("ОбщаяФорма.НастройкиЭлектроннойПодписиИШифрования");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиПрофилейЭДО(Команда)
	
	ФормаСписка = "Справочник.ПрофилиНастроекЭДО.Форма.ФормаСписка";
	ОткрытьФорму(ФормаСписка, , ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСоглашенияОбИспользованииЭД(Команда)
	
	ФормаСписка = "Справочник.СоглашенияОбИспользованииЭД.Форма.ФормаСписка";
	ОткрытьФорму(ФормаСписка, , ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПомощникПодключенияКПорталу1СЭДО(Команда)
	
	МодульОбработки = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбменСКонтрагентамиКлиент");
	МодульОбработки.ПомощникПодключенияКСервису1СЭДО();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПомощникПодключенияКСервису1СТакском(Команда)
	
	МодульОбработки = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбменСКонтрагентамиКлиент");
	МодульОбработки.ПомощникПодключенияКСервису1СТакском();
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОтправкуЭД(Команда)
	
	ОткрытьНастройкуРасписанияОбмена("ОтправкаЭлектронныхДокументов");
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьПолучениеЭД(Команда)
	
	ОткрытьНастройкуРасписанияОбмена("ПолучениеЭлектронныхДокументов");
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещатьОСобытияхЭДОПриИзменении(Элемент)
	
	ИзменитьИспользованиеЗадания("ПроверкаНовыхЭлектронныхДокументов", ОповещатьОСобытияхЭДО);
	Элементы.ПроверкаНовыхЭлектронныхДокументов.Доступность = ОповещатьОСобытияхЭДО;
	
	Если ОповещатьОСобытияхЭДО Тогда
		Если ОповещатьОСобытияхЭДО() Тогда
			Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСКонтрагентами") Тогда
				МодульОбменаСКонтрагентами = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбменСКонтрагентамиКлиент");
				МодульОбменаСКонтрагентами.ИзменитьОповещенияЭДО(Истина);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСКонтрагентами") Тогда
			МодульОбменаСКонтрагентами = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбменСКонтрагентамиКлиент");
			МодульОбменаСКонтрагентами.ИзменитьОповещенияЭДО();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОповещенияЭДО(Команда)
	
	ОткрытьНастройкуРасписанияОбмена("ПроверкаНовыхЭлектронныхДокументов");	
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиОбменаСБанками(Команда)
	
	ФормаСписка = "Справочник.НастройкиОбменСБанками.Форма.ФормаСписка";
	ОткрытьФорму(ФормаСписка, , ЭтотОбъект);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Бизнес-сеть

&НаКлиенте
Процедура ОткрытьПомощникПодключенияКСервисуБизнесСеть(Команда)
	
	ФормаПомощникаБизнесСети = "Обработка.БизнесСеть.Форма.РегистрацияОрганизаций";
	ОткрытьФорму(ФормаПомощникаБизнесСети, , ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСервисныеКомандыБизнесСеть(Команда)
	
	ОчиститьСообщения();
	НастройкаПодключения = "Обработка.БизнесСеть.Форма.НастройкаПодключения";
	ОткрытьФорму(НастройкаПодключения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПомощникПубликацииТорговыхПредложений(Команда)
	
	ОчиститьСообщения();
	Форма = "Обработка.ТорговыеПредложения.Форма.ПомощникПубликации";
	ОткрытьФорму(Форма,, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьРасписаниеПубликацииТорговыхПредложений(Команда)
	
	ОткрытьНастройкуРасписанияОбмена("СинхронизацияТорговыхПредложений");

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПрофильАбонента1СБизнесСеть(Команда)
	
	ОчиститьСообщения();
	ФормаПодключения = "Обработка.ТорговыеПредложения.Форма.ПрофильАбонента";
	ОткрытьФорму(ФормаПодключения);	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИнтеграцияСЯндексКассой

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуСоздатьШаблон(Команда)
	
	Кнопка                  = Элементы.Найти(Команда.Имя);
	ИмяДокумента            = СтрЗаменить(Команда.Имя, "СоздатьШаблон", "");
	ПредставлениеДокумента  = СокрЛП(СтрЗаменить(СтрЗаменить(Кнопка.Заголовок, НСтр("ru = 'Шаблон для'"), ""), """", ""));
	НаименованиеШаблона     = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Счет на оплату через Яндекс.Кассу (%1)'"), ПредставлениеДокумента);
	
	СоздатьШаблон(Новый Структура("ПолноеИмяТипаПараметраВводаНаОсновании, Наименование", 
		"Документ." + ИмяДокумента, НаименованиеШаблона));
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьПолучениеУведомленийОтЯндексКассы(Команда)
	
	ОткрытьНастройкуРасписанияОбмена("ПолучениеУведомленийОтЯндексКассы");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиЯндексКассы(Команда)
	
	ФормаСписка = "Справочник.НастройкиЯндексКассы.Форма.ФормаСписка";
	ОткрытьФорму(ФормаСписка, , ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВШаблоныСообщений(Команда)
	
	Если Не ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ШаблоныСообщений") Тогда
		Возврат;
	КонецЕсли;
	
	ОтборФормы = Новый Структура();
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Отбор", ОтборФормы);
	
	Форма = "Справочник.ШаблоныСообщений.ФормаСписка";
	
	ОткрытьФорму(Форма, ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Маршруты подписания

&НаКлиенте
Процедура ОткрытьМаршрутыПодписания(Команда)
	
	ОткрытьФорму("Справочник.МаршрутыПодписания.ФормаСписка");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ОповещатьОСобытияхЭДО()
	
	Возврат ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОповещатьОСобытияхЭДО();
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПриИзмененииРеквизита(Элемент, ОбновлятьИнтерфейс = Истина)
	
	Результат = ПриИзмененииРеквизитаСервер(Элемент.Имя);
	
	Если ОбновлятьИнтерфейс Тогда
		
		ОбновитьИнтерфейс = Истина;
		ПодключитьОбработчикОжидания("ОбновитьИнтерфейсПрограммы", 1, Истина);
		
	КонецЕсли;
	
	Если Результат.Свойство("ОповещениеФорм") Тогда
		Оповестить(Результат.ОповещениеФорм.ИмяСобытия, Результат.ОповещениеФорм.Параметр,
			Результат.ОповещениеФорм.Источник);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнтерфейсПрограммы()
	
	Если ОбновитьИнтерфейс = Истина Тогда
		ОбновитьИнтерфейс = Ложь;
		ОбновитьИнтерфейс();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПриИзмененииРеквизитаСервер(ИмяЭлемента)
	
	Результат = Новый Структура;
	
	РеквизитПутьКДанным = Элементы[ИмяЭлемента].ПутьКДанным;
	
	СохранитьЗначениеРеквизита(РеквизитПутьКДанным, Результат);
	
	ПрочитатьУстановитьДоступность(РеквизитПутьКДанным);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура СохранитьЗначениеРеквизита(РеквизитПутьКДанным, Результат)
	
	// Сохранение значений реквизитов, не связанных с константами напрямую (в отношении один-к-одному).
	Если РеквизитПутьКДанным = "" Тогда
		Возврат;
	КонецЕсли;
	
	// Определение имени константы.
	КонстантаИмя = "";
	Если НРег(Лев(РеквизитПутьКДанным, 10)) = НРег("Константа_") Тогда
		// Если путь к данным реквизита указан через "НаборКонстант".
		КонстантаИмя = Сред(РеквизитПутьКДанным, 11);
	Иначе
		// Определение имени и запись значения реквизита в соответствующей константе из "НаборКонстант".
		// Используется для тех реквизитов формы, которые связаны с константами напрямую (в отношении один-к-одному).
	КонецЕсли;
	
	// Сохранения значения константы.
	Если КонстантаИмя <> "" Тогда
		КонстантаМенеджер = Константы[КонстантаИмя];
		КонстантаЗначение = Константы[КонстантаИмя].Получить();
		
		Если Константы[КонстантаИмя].Получить() <> ЭтотОбъект[РеквизитПутьКДанным] Тогда
			Константы[КонстантаИмя].Установить(ЭтотОбъект[РеквизитПутьКДанным]);
		КонецЕсли;
		
		ОповещениеФорм = Новый Структура(
			"ИмяСобытия, Параметр, Источник",
			"Запись_НаборКонстант", Неопределено, КонстантаИмя);
		Результат.Вставить("ОповещениеФорм", ОповещениеФорм);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьУстановитьДоступность(РеквизитПутьКДанным = "")
	
	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	
	ЕстьОбменСКонтрагентами = ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСКонтрагентами");
	// Права следует проверять до установки привилегированного режима.
	ЕстьПравоНастройкиОбменаСКонтрагентами = Ложь;
	Если ЕстьОбменСКонтрагентами Тогда
		МодульОбменСКонтрагентамиСлужебныйВызовСервера = ОбщегоНазначения.ОбщийМодуль("ОбменСКонтрагентамиСлужебныйВызовСервера");
		ЕстьПравоНастройкиОбменаСКонтрагентами = МодульОбменСКонтрагентамиСлужебныйВызовСервера.ЕстьПравоНастройкиОбмена();
	КонецЕсли;
	
	ЕстьОбменБизнесСеть     = ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.БизнесСеть");
	Если ЕстьОбменБизнесСеть Тогда
		МодульБизнесСеть = ОбщегоНазначения.ОбщийМодуль("БизнесСеть");
		ЕстьОбменБизнесСеть = МодульБизнесСеть.ПравоНастройкиОбменаДокументами();
	КонецЕсли;
	
	ЕстьТорговыеПредложения = ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ТорговыеПредложения")
		И ЕстьОбменБизнесСеть;
	Если ЕстьТорговыеПредложения Тогда
		МодульТорговыеПредложения = ОбщегоНазначения.ОбщийМодуль("ТорговыеПредложения");
		ЕстьТорговыеПредложения = МодульТорговыеПредложения.ПравоНастройкиТорговыхПредложений();
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ИспользоватьЭлектронныеПодписи = Константы["ИспользоватьЭлектронныеПодписи"].Получить();
	
	Если ЕстьОбменСКонтрагентами
		И (РеквизитПутьКДанным = "Константа_ИспользоватьОбменЭД" ИЛИ РеквизитПутьКДанным = "") Тогда
		
		Константа_ИспользоватьОбменЭД = Константы["ИспользоватьОбменЭД"].Получить();
		Константа_ИспользоватьОбменЭДМеждуОрганизациями = Константы["ИспользоватьОбменЭДМеждуОрганизациями"].Получить();
		Константа_ИспользоватьОтложеннуюОтправкуЭлектронныхДокументов =
			Константы["ИспользоватьОтложеннуюОтправкуЭлектронныхДокументов"].Получить();
		Константа_ИспользоватьОзнакомлениеСЭлектроннымиДокументами =
			Константы["ИспользоватьОзнакомлениеСЭлектроннымиДокументами"].Получить();
		
		ВключеныЭДИЭП = Константа_ИспользоватьОбменЭД И ИспользоватьЭлектронныеПодписи;
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ОткрытьТиповыеСоглашенияОбИспользованииЭД", "Доступность", Константа_ИспользоватьОбменЭД);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ОткрытьСоглашенияОбИспользованииЭД", "Доступность", Константа_ИспользоватьОбменЭД);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ИспользоватьОбменЭДМеждуОрганизациями", "Доступность", ВключеныЭДИЭП);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ОткрытьПомощникПодключенияКСервису1СТакском", "Доступность", ВключеныЭДИЭП);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ОткрытьПомощникПодключенияКСервису1СЭДО", "Доступность", ВключеныЭДИЭП);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ОткрытьАрхивЭДО", "Доступность", Константа_ИспользоватьОбменЭД);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ИспользоватьАвтоматическуюОтправкуЭД", "Доступность", ЕстьОбменСКонтрагентами);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ИспользоватьАвтоматическоеПолучениеЭД", "Доступность", ЕстьОбменСКонтрагентами);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ОповещатьОСобытияхЭДО", "Доступность", Константа_ИспользоватьОбменЭД);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ИспользоватьОтложеннуюОтправкуЭлектронныхДокументов", "Доступность", Константа_ИспользоватьОбменЭД);
		
		ДоступноАдминистрирование = Пользователи.ЭтоПолноправныйПользователь(, Истина, Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ГруппаНастройкиРегламентногоЗадания", "Доступность", Константа_ИспользоватьОбменЭД И ДоступноАдминистрирование);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ГруппаНастройкиРегламентногоЗадания", "Видимость", Не ОбщегоНазначения.РазделениеВключено());
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ГруппаОповещенияЭДО", "Доступность", Константа_ИспользоватьОбменЭД И ДоступноАдминистрирование);
		
		Если Не Константа_ИспользоватьОбменЭД И Не ОбщегоНазначения.РазделениеВключено() Тогда
			ИзменитьИспользованиеЗадания("ОтправкаЭлектронныхДокументов", Константа_ИспользоватьОбменЭД);
			ИспользоватьАвтоматическуюОтправкуЭД = Ложь;
			ИзменитьИспользованиеЗадания("ПолучениеЭлектронныхДокументов", Константа_ИспользоватьОбменЭД);
			ИспользоватьАвтоматическоеПолучениеЭД = Ложь;
			ИзменитьИспользованиеЗадания("ПроверкаНовыхЭлектронныхДокументов", Константа_ИспользоватьОбменЭД);
			ОповещатьОСобытияхЭДО = Ложь;
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ГруппаНастройкиОбменаСКонтрагентами", "Доступность", ЕстьПравоНастройкиОбменаСКонтрагентами);
		
		ЗаголовокГруппы = НСтр("ru = 'Обмен с контрагентами'") 
			+ ?(ЕстьПравоНастройкиОбменаСКонтрагентами, "", " " + НСтр("ru = '(недостаточно прав)'"));
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ГруппаНастройкиОбменаСКонтрагентами", "Заголовок", ЗаголовокГруппы);
		
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "Константа_ИспользоватьЭлектронныеПодписи" ИЛИ РеквизитПутьКДанным = "" Тогда
		Константа_ИспользоватьЭлектронныеПодписи = ИспользоватьЭлектронныеПодписи;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ИспользоватьЭлектронныеПодписи", "Доступность", ЭтоПолноправныйПользователь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"НастройкиЭлектроннойПодписиИШифрования", "Доступность", ИспользоватьЭлектронныеПодписи);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"МаршрутыПодписания", "Доступность", ИспользоватьЭлектронныеПодписи);
	КонецЕсли;
	
	ЕстьОбменСБанками = ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСБанками");
	
	Если ЕстьОбменСБанками
		И (РеквизитПутьКДанным = "Константа_ИспользоватьОбменСБанками" ИЛИ РеквизитПутьКДанным = "") Тогда
		
		МодульОбменСБанками = ОбщегоНазначения.ОбщийМодуль("ОбменСБанками");
		
		УстановитьПривилегированныйРежим(Ложь);
		ПравоНастройки = МодульОбменСБанками.ПравоНастройки();
		ПравоЧтенияНастроек = МодульОбменСБанками.ПравоЧтенияНастроек();
		УстановитьПривилегированныйРежим(Истина);
		
		Константа_ИспользоватьОбменСБанками = Константы["ИспользоватьОбменСБанками"].Получить();
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ОткрытьНастройкиОбмена", "Доступность", Константа_ИспользоватьОбменСБанками И ПравоЧтенияНастроек);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, "ИспользоватьОбменСБанками", "Доступность", ПравоНастройки);
		
	КонецЕсли;
		
	// БизнесСеть
	Если ЕстьОбменБизнесСеть
		И (РеквизитПутьКДанным = "Константа_ИспользоватьОбменБизнесСеть" ИЛИ РеквизитПутьКДанным = "") Тогда
		
		Константа_ИспользоватьОбменБизнесСеть = Константы["ИспользоватьОбменБизнесСеть"].Получить();
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ГруппаСервисныеКомандыБизнесСеть", "Доступность", Константа_ИспользоватьОбменБизнесСеть);
		
		// ТорговыеПредложения
		Если ЕстьТорговыеПредложения Тогда
			
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
				"ГруппаНастройкиБизнесСетьТорговыеПредложенияПубликация", "Доступность", Константа_ИспользоватьОбменБизнесСеть);
			Если Не Константа_ИспользоватьОбменБизнесСеть И Не ОбщегоНазначения.РазделениеВключено() Тогда
				ИзменитьИспользованиеЗадания("СинхронизацияТорговыхПредложений", Константа_ИспользоватьОбменЭД);
				ИспользоватьАвтоматическуюПубликациюТорговыхПредложений = Ложь;
			КонецЕсли;
			Константа_ИспользоватьРубрикаторБизнесСеть = Константы["ИспользоватьРубрикаторБизнесСеть"].Получить();
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
				"ГруппаПрофиляАбонента", "Доступность", Константа_ИспользоватьОбменБизнесСеть);
			
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ГруппаНастройкиБизнесСеть", "Видимость", ЕстьОбменСКонтрагентами И ЕстьОбменБизнесСеть);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ГруппаНастройкиБизнесСетьТорговыеПредложения", "Видимость", ЕстьОбменСКонтрагентами И ЕстьТорговыеПредложения);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ГруппаПрофиляАбонента", "Видимость", ЕстьТорговыеПредложения);
	
	// ИнтеграцияСЯндексКассой
	ЕстьИнтеграцияСЯндексКассой = ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ИнтеграцияСЯндексКассой");
	
	Если ЕстьИнтеграцияСЯндексКассой
		И (РеквизитПутьКДанным = "Константа_ИспользоватьИнтеграциюСЯндексКассой" ИЛИ РеквизитПутьКДанным = "") Тогда
		
		Константа_ИспользоватьИнтеграциюСЯндексКассой = Константы["ИспользоватьИнтеграциюСЯндексКассой"].Получить();
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ОткрытьНастройкиЯндексКассы", "Доступность", Константа_ИспользоватьИнтеграциюСЯндексКассой);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ГруппаПолучениеУведомлений", "Доступность", Константа_ИспользоватьИнтеграциюСЯндексКассой);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ГруппаНастройкаШаблонов", "Доступность", Константа_ИспользоватьИнтеграциюСЯндексКассой);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ГруппаНастройкаШаблонов", "Видимость", ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(
				Метаданные.НайтиПоПолномуИмени("Справочник.ШаблоныСообщений")));
		
	КонецЕсли;
	// Конец ИнтеграцияСЯндексКассой
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьИспользованиеЗадания(ИмяЗадания, Использование)
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Использование", Использование);
	РегламентноеЗадание = РегламентноеЗаданиеПоНаименованию(ИмяЗадания);
	РегламентныеЗаданияСервер.ИзменитьЗадание(РегламентноеЗадание.УникальныйИдентификатор, ПараметрыЗадания);
	
	Элемент = Элементы[ИмяЗадания];
	Если Элемент <> Неопределено Тогда
		Элемент.Заголовок = ТекстРасписанияРегламентногоЗадания(РегламентноеЗадание);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьРасписаниеЗадания(ИмяЗадания, РасписаниеРегламентногоЗадания)
	
	ИдентификаторЗадания = РегламентноеЗаданиеПоНаименованию(ИмяЗадания);
	РегламентныеЗаданияСервер.УстановитьРасписаниеРегламентногоЗадания(ИдентификаторЗадания, РасписаниеРегламентногоЗадания);
	
	Элемент = Элементы[ИмяЗадания];
	Если Элемент <> Неопределено Тогда
		Элемент.Заголовок = РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкуРасписанияОбмена(ИмяЗадания)
	
	Расписание = ПолучитьПараметрРегламентногоЗадания(ИмяЗадания, "Расписание", Новый РасписаниеРегламентногоЗадания);
	
	ДиалогРасписания = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
	ОписаниеОповещения = Новый ОписаниеОповещения("НастроитьРасписаниеЗавершение",
		ЭтотОбъект, ИмяЗадания);
	ДиалогРасписания.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаСервере
Функция ТекстРасписанияРегламентногоЗадания(ИдентификаторЗадания)
	
	Расписание = ИдентификаторЗадания.Расписание;
	Если Расписание = Неопределено Тогда
		Возврат НСтр("ru = 'Настроить расписание'")
	Иначе
		Возврат Строка(Расписание);
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура УстановитьНастройкиЗаданий()
	
	УстановитьПривилегированныйРежим(Истина);
	
	РазделениеВключено = ОбщегоНазначения.РазделениеВключено();
	ЭтоАдминистраторСистемы = Пользователи.ЭтоПолноправныйПользователь(, Истина, Ложь);
	
	// Торговые предложения
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ТорговыеПредложения") Тогда
		
		РегламентноеЗадание = РегламентноеЗаданиеПоНаименованию("СинхронизацияТорговыхПредложений");
		ЭлементНастройкиРасписания = Элементы.СинхронизацияТорговыхПредложений;
		ВидимостьФлажка = (РегламентноеЗадание <> Неопределено);
		Если ВидимостьФлажка Тогда
			ИспользоватьАвтоматическуюПубликациюТорговыхПредложений = РегламентноеЗадание.Использование;
			ЭлементНастройкиРасписания.Заголовок = ТекстРасписанияРегламентногоЗадания(РегламентноеЗадание);
			ЭлементНастройкиРасписания.Доступность = РегламентноеЗадание.Использование;
		КонецЕсли;
		ВидимостьРасписания = ВидимостьФлажка И Не РазделениеВключено И ЭтоАдминистраторСистемы;
		ЭлементНастройкиРасписания.Видимость = ВидимостьРасписания;
		
	КонецЕсли;
	
	// ЯндексКасса
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ИнтеграцияСЯндексКассой") Тогда
		
		РегламентноеЗадание = РегламентноеЗаданиеПоНаименованию("ПолучениеУведомленийОтЯндексКассы");
		ЭлементНастройкиРасписания = Элементы.ПолучениеУведомленийОтЯндексКассы;
		ВидимостьФлажка = (РегламентноеЗадание <> Неопределено);
		Если ВидимостьФлажка Тогда
			ИспользоватьАвтоматическоеПолучениеУведомленияОтЯндексКассы = РегламентноеЗадание.Использование;
			ЭлементНастройкиРасписания.Заголовок = ТекстРасписанияРегламентногоЗадания(РегламентноеЗадание);
			ЭлементНастройкиРасписания.Доступность = РегламентноеЗадание.Использование;
		КонецЕсли;
		ВидимостьРасписания = ВидимостьФлажка И Не РазделениеВключено И ЭтоАдминистраторСистемы;
		ЭлементНастройкиРасписания.Видимость = ВидимостьРасписания;
		
	КонецЕсли;
	
	// ОбменСКонтрагентам
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСКонтрагентами") Тогда
		
		РегламентноеЗадание = РегламентноеЗаданиеПоНаименованию("ОтправкаЭлектронныхДокументов");
		ЭлементНастройкиРасписания = Элементы.ОтправкаЭлектронныхДокументов;
		ВидимостьФлажка = (РегламентноеЗадание <> Неопределено);
		Если ВидимостьФлажка Тогда
			ИспользоватьАвтоматическуюОтправкуЭД = РегламентноеЗадание.Использование;
			ЭлементНастройкиРасписания.Заголовок = ТекстРасписанияРегламентногоЗадания(РегламентноеЗадание);
			ЭлементНастройкиРасписания.Доступность = РегламентноеЗадание.Использование;
		КонецЕсли;
		ВидимостьРасписания = ВидимостьФлажка И Не РазделениеВключено И ЭтоАдминистраторСистемы;
		ЭлементНастройкиРасписания.Видимость = ВидимостьРасписания;
		
		РегламентноеЗадание = РегламентноеЗаданиеПоНаименованию("ПолучениеЭлектронныхДокументов");
		ЭлементНастройкиРасписания = Элементы.ПолучениеЭлектронныхДокументов;
		ВидимостьФлажка = (РегламентноеЗадание <> Неопределено);
		Если ВидимостьФлажка Тогда
			ИспользоватьАвтоматическоеПолучениеЭД = РегламентноеЗадание.Использование;
			ЭлементНастройкиРасписания.Заголовок = ТекстРасписанияРегламентногоЗадания(РегламентноеЗадание);
			ЭлементНастройкиРасписания.Доступность = РегламентноеЗадание.Использование;
		КонецЕсли;
		ВидимостьРасписания = ВидимостьФлажка И Не РазделениеВключено И ЭтоАдминистраторСистемы;
		ЭлементНастройкиРасписания.Видимость = ВидимостьРасписания;
		
		РегламентноеЗадание = РегламентноеЗаданиеПоНаименованию("ПроверкаНовыхЭлектронныхДокументов");
		ЭлементНастройкиРасписания = Элементы.ПроверкаНовыхЭлектронныхДокументов;
		ВидимостьФлажка = (РегламентноеЗадание <> Неопределено);
		Если ВидимостьФлажка Тогда
			ОповещатьОСобытияхЭДО = РегламентноеЗадание.Использование;
			ЭлементНастройкиРасписания.Заголовок = ТекстРасписанияРегламентногоЗадания(РегламентноеЗадание);
			ЭлементНастройкиРасписания.Доступность = РегламентноеЗадание.Использование;
		КонецЕсли;
		ВидимостьРасписания = ВидимостьФлажка И Не РазделениеВключено И ЭтоАдминистраторСистемы;
		ЭлементНастройкиРасписания.Видимость = ВидимостьРасписания;		
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьРасписаниеЗавершение(РасписаниеЗадания, ДополнительныеПараметры) Экспорт
	
	Если РасписаниеЗадания = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьРасписаниеЗадания(ДополнительныеПараметры, РасписаниеЗадания);
	
КонецПроцедуры

&НаСервере
Функция РегламентноеЗаданиеПоНаименованию(ИмяЗадания)
	
	Отбор = Новый Структура("Метаданные", ИмяЗадания);
	Найденные = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	Задание = ?(Найденные.Количество() = 0, Неопределено, Найденные[0]);
	
	Возврат Задание;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ИнтеграцияСЯндексКассой

&НаСервере
Процедура ДобавитьКомандыСозданияШаблона()
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ИнтеграцияСЯндексКассой") Тогда
		Возврат;
	КонецЕсли;
	
	МодульИнтеграцияСЯндексКассойСлужебный = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияСЯндексКассойСлужебный");
	ИменаОснованийПлатежа = МодульИнтеграцияСЯндексКассойСлужебный.ОснованияПлатежа();
	
	КомандыДобавлены = Ложь;
	Для Каждого ПолноеИмя Из ИменаОснованийПлатежа Цикл
		
		ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ПолноеИмя);
		
		Если Не ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(ОбъектМетаданных) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ПравоДоступа("Чтение", ОбъектМетаданных) Тогда
			Продолжить;
		КонецЕсли;
		
		ПредставлениеОбъектаМетаданных = ?(ПустаяСтрока(ОбъектМетаданных.ПредставлениеОбъекта), ОбъектМетаданных.Представление(), ОбъектМетаданных.ПредставлениеОбъекта);
		МассивПодстрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПолноеИмя,".");
		ИмяКоманды     = МассивПодстрок[1];

		ПредставлениеКоманды = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Шаблон для ""%1""'"), ПредставлениеОбъектаМетаданных);
		ОписаниеКоманды = Новый Структура();
		ОписаниеКоманды.Вставить("ИмяКоманды", ИмяКоманды);
		ОписаниеКоманды.Вставить("Представление", ПредставлениеКоманды);
		
		ДобавитьКомандуСозданияШаблона(ОписаниеКоманды);
		КомандыДобавлены = Истина;
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ГруппаКнопокСоздать",
		"Видимость",
		КомандыДобавлены);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьКомандуСозданияШаблона(ОписаниеКоманды)

	ИмяКоманды = "СоздатьШаблон" + ОписаниеКоманды.ИмяКоманды;
	
	Если ЭтотОбъект.Команды.Найти(ИмяКоманды) = Неопределено Тогда
		КомандаФормы = ЭтотОбъект.Команды.Добавить(ИмяКоманды);
		КомандаФормы.Действие = "Подключаемый_ВыполнитьКомандуСоздатьШаблон";
		КомандаФормы.Заголовок = ОписаниеКоманды.Представление;
		КомандаФормы.ИзменяетСохраняемыеДанные = Ложь;
		КомандаФормы.Отображение = ОтображениеКнопки.Текст;
	КонецЕсли;
	
	МестоРазмещения = Элементы.ГруппаКнопокСоздатьШаблон;
	
	Если ЭтотОбъект.Элементы.Найти(ИмяКоманды) = Неопределено Тогда
		НовыйЭлемент = ЭтотОбъект.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), МестоРазмещения);
		НовыйЭлемент.Вид = ВидКнопкиФормы.Гиперссылка;
		НовыйЭлемент.ИмяКоманды = ИмяКоманды;
		НовыйЭлемент.Заголовок  = ОписаниеКоманды.Представление;
		НовыйЭлемент.Видимость  = Истина;
		НовыйЭлемент.ТолькоВоВсехДействиях = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СоздатьШаблон(ПараметрыСозданияШаблона)
	
	Если Не ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ИнтеграцияСЯндексКассой") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ШаблоныСообщений") Тогда
		Возврат;
	КонецЕсли;
	
	МодульИнтеграцияСЯндексКассойСлужебныйКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ИнтеграцияСЯндексКассойСлужебныйКлиент");
	
	СпособРедактированияЭлектронныхПисемHTML = "Перечисление.СпособыРедактированияЭлектронныхПисем.HTML";
	
	ДанныеЗаполнения = Новый Структура();
	ДанныеЗаполнения.Вставить("Наименование",            ПараметрыСозданияШаблона.Наименование);
	ДанныеЗаполнения.Вставить("ПолноеИмяТипаНазначения", ПараметрыСозданияШаблона.ПолноеИмяТипаПараметраВводаНаОсновании);
	ДанныеЗаполнения.Вставить("ТипШаблона",              "Письмо");
	ДанныеЗаполнения.Вставить("ФорматПисьма",            ПредопределенноеЗначение(СпособРедактированияЭлектронныхПисемHTML));
	ДанныеЗаполнения.Вставить("Тема",                    НСтр("ru = 'Вам выставлен счет'"));
	
	МассивПодстрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПараметрыСозданияШаблона.ПолноеИмяТипаПараметраВводаНаОсновании,".");
	ИмяДокумента   = МассивПодстрок[1];
	ДанныеЗаполнения.Вставить("Текст", СтрЗаменить(ТекстМакета(),"ИмяДокумента", ИмяДокумента));
	
	МодульИнтеграцияСЯндексКассойСлужебныйКлиент.ПолучитьДанныеЗаполненияПредопределенногоШаблона(ДанныеЗаполнения);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Основание", ДанныеЗаполнения);
	ПараметрыФормы.Вставить("ПолноеИмяТипаОснования", ДанныеЗаполнения.ПолноеИмяТипаНазначения);
	
	Форма = "Справочник.ШаблоныСообщений.ФормаОбъекта";
	
	ОткрытьФорму(Форма, ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекстМакета()
	
	НастройкиЯндексКассы = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени("Справочник.НастройкиЯндексКассы");
	Возврат НастройкиЯндексКассы.ПолучитьМакет("Макет").ПолучитьТекст();
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////

&НаСервере
Функция ПолучитьПараметрРегламентногоЗадания(ИмяЗадания, ИмяПараметра, ЗначениеПоУмолчанию)
	
	// Проверка отсутствие поставляемого регламентного задания в конфигурации.
	РегламентноеЗадание = Метаданные.РегламентныеЗадания[ИмяЗадания];
	Если РегламентноеЗадание = Неопределено Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
	// Поиск задания.
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Метаданные", РегламентноеЗадание);
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		ПараметрыЗадания.Вставить("ИмяМетода", РегламентноеЗадание.ИмяМетода);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	СписокЗаданий = РегламентныеЗаданияСервер.НайтиЗадания(ПараметрыЗадания);
	Если СписокЗаданий.Количество() Тогда
		Возврат СписокЗаданий[0][ИмяПараметра];
	Иначе
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
КонецФункции

#КонецОбласти