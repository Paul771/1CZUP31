
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Сайт = Параметры.Сайт;
	
	ИменаСвойствЗнанияЯзыка = ОбщегоНазначения.ФиксированныеДанные(ИнтеграцияРекрутинговыхСайтовКлиентСервер.ИменаСвойствЗнанияЯзыка(Сайт));
	СоответствиеПутиИКлассификатора = ОбщегоНазначения.ФиксированныеДанные(ИнтеграцияРекрутинговыхСайтовКлиентСервер.СоответствиеПутиИКлассификатораПоСайту(Сайт));
	
	Если Параметры.Свойство("СписокЯзыков") Тогда
		
		СписокЯзыков = Параметры.СписокЯзыков;
		Если ЗначениеЗаполнено(СписокЯзыков) Тогда
			
			Для Каждого Язык Из СписокЯзыков Цикл
				НоваяСтрока = УровниВладенияЯзыками.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Язык.Значение);
			КонецЦикла;
			
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	СписокЯзыков = Новый СписокЗначений;
	
	Для Каждого СтрокаЯзыка Из УровниВладенияЯзыками Цикл
		
		Если СтрокаЯзыка.Язык.Количество() > 0 
			И СтрокаЯзыка.Уровень.Количество() > 0 Тогда
			
			СтруктураЗначенияЯзыка = Новый Структура("Язык, Уровень");
			ЗаполнитьЗначенияСвойств(СтруктураЗначенияЯзыка, СтрокаЯзыка);
			
			СписокЯзыков.Добавить(СтруктураЗначенияЯзыка);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Закрыть(СписокЯзыков);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыУровниВладенияЯзыками

&НаКлиенте
Процедура УровниВладенияЯзыкамиЯзыкНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяОбъектаЯзык = ИменаСвойствЗнанияЯзыка.ОбъектЯзык;
	НачалоВыбораЗначения(Элемент.Имя, СоответствиеПутиИКлассификатора.Получить(ИмяОбъектаЯзык));
	
КонецПроцедуры

&НаКлиенте
Процедура УровниВладенияЯзыкамиУровеньНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяОбъектаУровень = ИменаСвойствЗнанияЯзыка.ОбъектУровень;
	НачалоВыбораЗначения(Элемент.Имя, СоответствиеПутиИКлассификатора.Получить(ИмяОбъектаУровень));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НачалоВыбораЗначения(ИмяЭлемента, Путь)
	
	ТекущиеДанные = Элементы.УровниВладенияЯзыками.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СписокВыбора = СписокВыбораНаСервере(Путь);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ТекущиеДанные", ТекущиеДанные);
	ДополнительныеПараметры.Вставить("ИмяЭлемента", ИмяЭлемента);
	ДополнительныеПараметры.Вставить("СписокВыбора", СписокВыбора);
	
	ИнтеграцияРекрутинговыхСайтовКлиент.НачалоВыбораЗначения(ЭтаФорма, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВыборЭлементаЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СписокВыбранныхЗначений = Новый СписокЗначений;
	СписокВыбранныхЗначений.Добавить(ВыбранныйЭлемент.Значение, ВыбранныйЭлемент.Представление);
	ДополнительныеПараметры.ТекущиеДанные[ДополнительныеПараметры.ИмяЭлемента] = СписокВыбранныхЗначений;
	
КонецПроцедуры

&НаСервере 
Функция СписокВыбораНаСервере(Путь)
	Возврат ИнтеграцияРекрутинговыхСайтов.СписокВыбораПоКлассификатору(Сайт, Путь);
КонецФункции

#КонецОбласти
