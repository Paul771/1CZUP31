
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеПоля.УстановитьHTML(Параметры.ТекстВHTML, Новый Структура);
	УстановитьЗаголовокФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ТекстВHTML = "";
	ВложенияВHTML = Новый Структура;
	
	ДанныеПоля.ПолучитьHTML(ТекстВHTML, ВложенияВHTML);
	
	Закрыть(ТелоHTMLДокумента(ТекстВHTML));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьЗаголовокФормы()
	
	Если Параметры.Свойство("Заголовок") Тогда
		ЭтотОбъект.Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ТелоHTMLДокумента(ТекстHTMLДокумента)
	
	НачалоТегаТела = Найти(ТекстHTMLДокумента, "<body>");
	Если НачалоТегаТела = 0 Тогда
		Возврат ТекстHTMLДокумента;
	КонецЕсли;
	
	ТекстHTMLДокумента = Сред(ТекстHTMLДокумента, НачалоТегаТела + 7, СтрДлина(ТекстHTMLДокумента));
	
	КонецТегаТела = Найти(ТекстHTMLДокумента, "</body>");
	ТекстHTMLДокумента = Сред(ТекстHTMLДокумента, 1, КонецТегаТела - 2);
	ТекстHTMLДокумента = СтрЗаменить(ТекстHTMLДокумента, "<p><br></p>", "<br>");
	
	Возврат ТекстHTMLДокумента;
	
КонецФункции

#КонецОбласти
