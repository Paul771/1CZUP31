#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Настройка = РегистрыСведений.ПараметрыСозданияПользователейФизическихЛиц.СоздатьМенеджерЗаписи();
	Настройка.Прочитать();
	ЗначениеВРеквизитФормы(Настройка, "ПараметрыСозданияПользователейФизическихЛиц");
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ГруппыДоступа.Ссылка КАК ГруппаДоступа,
	|	ВЫБОР
	|		КОГДА ПараметрыСозданияПользователейФизическихЛицГруппыДоступа.ГруппаДоступа ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Выбран
	|ИЗ
	|	Справочник.ГруппыДоступа КАК ГруппыДоступа
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыСозданияПользователейФизическихЛицГруппыДоступа КАК ПараметрыСозданияПользователейФизическихЛицГруппыДоступа
	|		ПО ГруппыДоступа.Ссылка = ПараметрыСозданияПользователейФизическихЛицГруппыДоступа.ГруппаДоступа
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГруппыДоступа.Наименование";
	
	ГруппыДоступа.Загрузить(Запрос.Выполнить().Выгрузить());
	
	АдресВебСервера = ОбщегоНазначения.АдресПубликацииИнформационнойБазыВИнтернете();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ОК(Команда)
	
	СохранитьНастройкиНаСервере();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьТекстУведомления(Команда)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(ПараметрыСозданияПользователейФизическихЛиц.ШаблонУведомления, ЭтотОбъект, "ПараметрыСозданияПользователейФизическихЛиц.ШаблонУведомления");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьНастройкиНаСервере()
	
	Настройки = РегистрыСведений.ПараметрыСозданияПользователейФизическихЛиц.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Настройки, ПараметрыСозданияПользователейФизическихЛиц);
	Настройки.Записать();
	
	НаборЗаписей = РегистрыСведений.ПараметрыСозданияПользователейФизическихЛицГруппыДоступа.СоздатьНаборЗаписей();
	Для Каждого ДанныеГруппыДоступа Из ГруппыДоступа Цикл 
		Если ДанныеГруппыДоступа.Выбран Тогда 
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), ДанныеГруппыДоступа);
		КонецЕсли;
	КонецЦикла;
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти
