
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОрганизацияПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура АнализПоСотрудникам(Команда)
	АнализПоСотрудникамНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЕСТЬNULL(Взаиморасчеты.СуммаВзаиморасчетовОстаток, 0) - ЕСТЬNULL(КВыплате.СуммаКВыплатеОстаток, 0) КАК ЗаниженнаяСуммаКВыплате
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыССотрудниками.Остатки(, Организация = &Организация) КАК Взаиморасчеты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗарплатаКВыплате.Остатки(, Организация = &Организация) КАК КВыплате
	|		ПО Взаиморасчеты.Организация = КВыплате.Организация");
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Не Выборка.Следующий() Тогда
		ЗаниженнаяСуммаКВыплате = 0;
	Иначе
		ЗаниженнаяСуммаКВыплате = Выборка.ЗаниженнаяСуммаКВыплате;
	КонецЕсли;
	Если ЗаниженнаяСуммаКВыплате = 0 Тогда
		Объект.РезультатАнализаПоОрганизации = НСтр("ru = 'По организации нет заниженных сумм к выплате. Дальнейший анализ не требуется'");
	ИначеЕсли ЗаниженнаяСуммаКВыплате > 0 Тогда
		Объект.РезультатАнализаПоОрганизации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'По организации в целом сумма к выплате занижена на %1. Это может быть связано с излишне удержанным НДФЛ. Для более подробного анализа используйте кнопку ""Анализ по сотрудникам""'"),
			Выборка.ЗаниженнаяСуммаКВыплате);
	Иначе
		Объект.РезультатАнализаПоОрганизации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'По организации в целом сумма к выплате завышена на %1. Это может быть связано с изменениями, внесенными ""задним числом"" (непоследовательным вводом документов). Для более подробного анализа используйте кнопку ""Анализ по сотрудникам""'"),
			- Выборка.ЗаниженнаяСуммаКВыплате);
	КонецЕсли;
	Объект.РезультатыАнализаПоСотрудникам.ПолучитьЭлементы().Очистить();
	
КонецПроцедуры


&НаСервере
Процедура АнализПоСотрудникамНаСервере()
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Взаиморасчеты.ФизическоеЛицо
	|ПОМЕСТИТЬ ВТФизлицаСРазницами
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыССотрудниками.Остатки(, Организация = &Организация) КАК Взаиморасчеты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗарплатаКВыплате.Остатки(, Организация = &Организация) КАК КВыплате
	|		ПО Взаиморасчеты.ФизическоеЛицо = КВыплате.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Остатки(, Организация = &Организация) КАК НДФЛ
	|		ПО Взаиморасчеты.ФизическоеЛицо = НДФЛ.ФизическоеЛицо
	|ГДЕ
	|	ЕСТЬNULL(Взаиморасчеты.СуммаВзаиморасчетовОстаток, 0) - ЕСТЬNULL(КВыплате.СуммаКВыплатеОстаток, 0) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВзаиморасчетыССотрудниками.ФизическоеЛицо,
	|	ВзаиморасчетыССотрудниками.Регистратор,
	|	ВзаиморасчетыССотрудниками.Период,
	|	СУММА(ВзаиморасчетыССотрудниками.СуммаВзаиморасчетов) КАК Сумма
	|ПОМЕСТИТЬ ВТВзаиморасчетыССотрудниками
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыССотрудниками КАК ВзаиморасчетыССотрудниками
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТФизлицаСРазницами КАК ФизлицаСРазницами
	|		ПО ВзаиморасчетыССотрудниками.ФизическоеЛицо = ФизлицаСРазницами.ФизическоеЛицо
	|ГДЕ
	|	ВзаиморасчетыССотрудниками.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ВзаиморасчетыССотрудниками.ФизическоеЛицо,
	|	ВзаиморасчетыССотрудниками.Период,
	|	ВзаиморасчетыССотрудниками.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗарплатаКВыплате.ФизическоеЛицо,
	|	ЗарплатаКВыплате.Регистратор,
	|	ЗарплатаКВыплате.Период,
	|	СУММА(ЗарплатаКВыплате.СуммаКВыплате) КАК Сумма
	|ПОМЕСТИТЬ ВТЗарплатаКВыплате
	|ИЗ
	|	РегистрНакопления.ЗарплатаКВыплате КАК ЗарплатаКВыплате
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТФизлицаСРазницами КАК ФизлицаСРазницами
	|		ПО ЗарплатаКВыплате.ФизическоеЛицо = ФизлицаСРазницами.ФизическоеЛицо
	|ГДЕ
	|	ЗарплатаКВыплате.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗарплатаКВыплате.ФизическоеЛицо,
	|	ЗарплатаКВыплате.Период,
	|	ЗарплатаКВыплате.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВзаиморасчетыССотрудниками.ФизическоеЛицо,
	|	ВзаиморасчетыССотрудниками.Регистратор,
	|	ВзаиморасчетыССотрудниками.Период,
	|	ВзаиморасчетыССотрудниками.Сумма
	|ПОМЕСТИТЬ ВТРасхождения
	|ИЗ
	|	ВТВзаиморасчетыССотрудниками КАК ВзаиморасчетыССотрудниками
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗарплатаКВыплате.ФизическоеЛицо,
	|	ЗарплатаКВыплате.Регистратор,
	|	ЗарплатаКВыплате.Период,
	|	-ЗарплатаКВыплате.Сумма
	|ИЗ
	|	ВТЗарплатаКВыплате КАК ЗарплатаКВыплате
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расхождения.ФизическоеЛицо КАК Сотрудник,
	|	Расхождения.Регистратор КАК Документ,
	|	СУММА(Расхождения.Сумма) КАК Сумма
	|ИЗ
	|	ВТРасхождения КАК Расхождения
	|
	|СГРУППИРОВАТЬ ПО
	|	Расхождения.ФизическоеЛицо,
	|	Расхождения.Регистратор,
	|	Расхождения.Период
	|
	|ИМЕЮЩИЕ
	|	СУММА(Расхождения.Сумма) <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Расхождения.ФизическоеЛицо.Наименование,
	|	Расхождения.Период,
	|	Расхождения.Регистратор
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	Сотрудник");
	
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	ДеревоРезультата = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	ДеревоРезультата.Колонки.Добавить("СотрудникДокумент");
	ДеревоРезультата.Колонки.Сумма.Имя = "СуммаКВыплатеЗанижена";
	ДеревоРезультата.Колонки.Добавить("СуммаКВыплатеВозвращена", ДеревоРезультата.Колонки.СуммаКВыплатеЗанижена.ТипЗначения);
	ДеревоРезультата.Колонки.Добавить("ЗаниженаТекст");
	ДеревоРезультата.Колонки.Добавить("ВозвращенаТекст");
	Для Каждого СтрокаСотрудник Из ДеревоРезультата.Строки Цикл
		СтрокаСотрудник.СотрудникДокумент = СтрокаСотрудник.Сотрудник;
		Если СтрокаСотрудник.СуммаКВыплатеЗанижена < 0 Тогда
			СтрокаСотрудник.СуммаКВыплатеВозвращена = - СтрокаСотрудник.СуммаКВыплатеЗанижена;
			СтрокаСотрудник.СуммаКВыплатеЗанижена = 0;
			СтрокаСотрудник.ВозвращенаТекст = НСтр("ru = 'завышена на'");
		Иначе
			СтрокаСотрудник.ЗаниженаТекст = НСтр("ru = 'занижена на'");
		КонецЕсли;
		Для Каждого СтрокаДокумент Из СтрокаСотрудник.Строки Цикл
			СтрокаДокумент.СотрудникДокумент = СтрокаДокумент.Документ;
			Если СтрокаДокумент.СуммаКВыплатеЗанижена < 0 Тогда
				СтрокаДокумент.СуммаКВыплатеВозвращена = - СтрокаДокумент.СуммаКВыплатеЗанижена;
				СтрокаДокумент.СуммаКВыплатеЗанижена = 0;
				СтрокаДокумент.ВозвращенаТекст = НСтр("ru = 'возвращена'");
			Иначе
				СтрокаДокумент.ЗаниженаТекст = НСтр("ru = 'занижена на'");
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	ЗначениеВРеквизитФормы(ДеревоРезультата, "Объект.РезультатыАнализаПоСотрудникам");
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатыАнализаПоСотрудникамВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Родитель = Объект.РезультатыАнализаПоСотрудникам.НайтиПоИдентификатору(ВыбраннаяСтрока).ПолучитьРодителя();
	Если Родитель <> НеОпределено Тогда
		ДокументСсылка = Элемент.ТекущиеДанные.СотрудникДокумент;
		ПоказатьЗначение(, ДокументСсылка);
	Иначе
		ФизлицоСсылка = Элемент.ТекущиеДанные.СотрудникДокумент;
		ПоказатьЗначение(, ФизлицоСсылка);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти