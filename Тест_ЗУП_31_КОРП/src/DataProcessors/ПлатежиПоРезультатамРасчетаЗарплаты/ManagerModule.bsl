
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Функция КомандыПечати() Экспорт
	КомандыПечати = УправлениеПечатью.СоздатьКоллекциюКомандПечати();
	
	// Список платежей
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "ЗарплатаКадрыКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.МенеджерПечати = "Обработка.ПлатежиПоРезультатамРасчетаЗарплаты";
	КомандаПечати.Идентификатор = "ПФ_MXL_СписокПлатежей";
	КомандаПечати.Представление = НСтр("ru = 'Печать'");
	КомандаПечати.НеВыполнятьЗаписьВФорме = Истина;
	
	Возврат КомандыПечати;
КонецФункции

// Сформировать печатные формы объектов.
//
// ВХОДЯЩИЕ:
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы.
//   ОшибкиПечати          - Список значений  - Ошибки печати  (значение - ссылка на объект, представление - текст
//                           ошибки).
//   ОбъектыПечати         - Список значений  - Объекты печати (значение - ссылка на объект, представление - имя
//                           области в которой был выведен объект).
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	НужноПечататьМакет = УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_СписокПлатежей");
	
	Если НужноПечататьМакет Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,	"ПФ_MXL_СписокПлатежей",
			НСтр("ru = 'Печать'"), ПечатьПлатежей(МассивОбъектов, ОбъектыПечати), ,
			"Обработка.ПлатежиПоРезультатамРасчетаЗарплаты.ПФ_MXL_СписокПлатежей");
	КонецЕсли;
						
КонецПроцедуры								

Функция ПечатьПлатежей(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПлатежиПоРезультатамРасчетаЗарплаты_СписокПлатежей";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПлатежиПоРезультатамРасчетаЗарплаты.ПФ_MXL_СписокПлатежей");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	
	// Формирование и вывод шапки отчета.
	ОбластьШапка.Параметры.Организация = МассивОбъектов[0];
	ОбластьШапка.Параметры.Период = ЗарплатаКадрыКлиентСервер.ПолучитьПредставлениеМесяца(МассивОбъектов[1]);
	ОбластьШапка.Параметры.Сумма = Формат(МассивОбъектов[2], "ЧДЦ=2");
	ТабличныйДокумент.Вывести(ОбластьШапка);
	
	// Формирование и вывод таблиц платежей.
	ПечатьТаблицыПлатежа(Макет, ТабличныйДокумент, МассивОбъектов[3], "ВыплатаЗарплатыНаличными"); 
	ПечатьТаблицыПлатежа(Макет, ТабличныйДокумент, МассивОбъектов[4], "ВыплатаЗарплатыЧерезБанк"); 
	ПечатьТаблицыПлатежа(Макет, ТабличныйДокумент, МассивОбъектов[5], "ВыплатаНалоги"); 
	ПечатьТаблицыПлатежа(Макет, ТабличныйДокумент, МассивОбъектов[6], "СтраховыеВзносы"); 
	ПечатьТаблицыПлатежа(Макет, ТабличныйДокумент, МассивОбъектов[7], "УдержанияПоИсполнительнымДокументам"); 
	ПечатьТаблицыПлатежа(Макет, ТабличныйДокумент, МассивОбъектов[8], "УдержанияВПользуТретьихЛиц"); 
						
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура ПечатьТаблицыПлатежа(Макет, ТабличныйДокумент, ТаблицаПлатежей, НазваниеПлатежей)
	
	Если ТаблицаПлатежей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка" + НазваниеПлатежей);
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка" + НазваниеПлатежей);
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал" + НазваниеПлатежей);
	
	ТабличныйДокумент.Вывести(ОбластьШапка);
	
	// Вывод строк таблицы платежей.
	Для Каждого СтрокаТаблицы Из ТаблицаПлатежей Цикл		
		ЗаполнитьЗначенияСвойств(ОбластьСтрока.Параметры, СтрокаТаблицы);		
		ТабличныйДокумент.Вывести(ОбластьСтрока);		
	КонецЦикла;

	// Вывод итогов по таблице
	Если НазваниеПлатежей = "УдержанияПоИсполнительнымДокументам" Тогда
		ОбластьПодвал.Параметры.ВознаграждениеАгента = ТаблицаПлатежей.Итог("ВознаграждениеАгента");
	КонецЕсли;
	
	ОбластьПодвал.Параметры.Сумма = ТаблицаПлатежей.Итог("Сумма");
	
	ТабличныйДокумент.Вывести(ОбластьПодвал);
	
КонецПроцедуры								

#КонецОбласти

#КонецОбласти

#КонецЕсли