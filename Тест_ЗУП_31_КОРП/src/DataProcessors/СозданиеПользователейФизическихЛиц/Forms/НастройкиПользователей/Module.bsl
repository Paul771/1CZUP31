
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	НастройкиПользователей = Параметры.НастройкиПользователей;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиПользователей);
	ЗаполнитьГруппыДоступа(НастройкиПользователей.ВыбранныеГруппыДоступа);
	УстановитьДоступностьСвойств(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("СохранитьИЗакрыть", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АутентификацияСтандартнаяПриИзменении(Элемент)
	
	УстановитьДоступностьСвойств(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрименитьИЗакрыть(Команда)
	
	СохранитьИЗакрытьНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьГруппыДоступа(ВыбранныеГруппыДоступа)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ГруппыДоступа.Ссылка
	               |ИЗ
	               |	Справочник.ГруппыДоступа КАК ГруппыДоступа
	               |ГДЕ
	               |	НЕ ГруппыДоступа.ЭтоГруппа
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ГруппыДоступа.Наименование";
				   
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Пометка = ВыбранныеГруппыДоступа.Найти(Выборка.Ссылка) <> Неопределено;
		ГруппыДоступа.Добавить(Выборка.Ссылка, , Пометка);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть(Результат, ДополнительныеПараметры) Экспорт 
	
	СохранитьИЗакрытьНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрытьНаКлиенте()  

	Модифицированность = Ложь;
	
	НастройкиПользователей = СамообслуживаниеСотрудниковКлиентСервер.ОписаниеНастроекСозданияПользователей();
	ЗаполнитьЗначенияСвойств(НастройкиПользователей, ЭтотОбъект);
	
	Если Не АутентификацияСтандартная Тогда 
		НастройкиПользователей.ПоказыватьВСпискеВыбора = Ложь;
		НастройкиПользователей.ЗапрещеноИзменятьПароль = Ложь;
	КонецЕсли;
	
	НастройкиПользователей.ВыбранныеГруппыДоступа = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(ГруппыДоступа);
	
	ОповеститьОВыборе(НастройкиПользователей);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьСвойств(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ПоказыватьВСпискеВыбора", "Доступность", Форма.АутентификацияСтандартная);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ЗапрещеноИзменятьПароль", "Доступность", Форма.АутентификацияСтандартная);
	
КонецПроцедуры

#КонецОбласти
