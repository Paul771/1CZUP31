#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ЗагрузитьОбщиеСправочники() Экспорт
	
	Если ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КлассификаторыОбщие") <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КлассификаторыОбщие = Новый Соответствие;
	ТабличныйДокумент = Обработки.ПоискРезюмеВИнтернете.ПолучитьМакет("Классификаторы");
	
	Чтение = Новый ЧтениеXML;
	Чтение.УстановитьСтроку(ТабличныйДокумент.ПолучитьТекст());
	
	Чтение.Прочитать();
	
	Псевдоним = "";
	ТекущийСправочник = Новый Соответствие;
	
	Пока Чтение.Прочитать() Цикл
		
		Если Чтение.ТипУзла = ТипУзлаXML.НачалоЭлемента 
			И Чтение.Имя = "Dictionary" Тогда
			
			Псевдоним = Чтение.ПолучитьАтрибут("ID");
			ТекущийСправочник = Новый Соответствие;
			
		КонецЕсли;
		
		Если Чтение.Имя = "Item" Тогда
			
			Ключ = Чтение.ПолучитьАтрибут("Code");
			Представление = Чтение.ПолучитьАтрибут("Name");
			
			ТекущийСправочник.Вставить(Ключ, Представление);
			
		КонецЕсли;
			
		Если Чтение.ТипУзла = ТипУзлаXML.КонецЭлемента 
			И Чтение.Имя = "Dictionary" Тогда
			КлассификаторыОбщие.Вставить(Псевдоним, ТекущийСправочник);
		КонецЕсли;
		
	КонецЦикла;
	
	Чтение.Закрыть();
	
	СправочникиФикс = ОбщегоНазначения.ФиксированныеДанные(КлассификаторыОбщие);
	
	ТекущиеПараметры = ИнтеграцияРекрутинговыхСайтовКлиентСервер.СоответствиеПоФиксированномуСоответствию(ПараметрыСеанса.ПараметрыКлиентаНаСервере);
	ТекущиеПараметры.Вставить("КлассификаторыОбщие", СправочникиФикс);
	
	УстановитьПривилегированныйРежим(Истина);
	ПараметрыСеанса.ПараметрыКлиентаНаСервере = Новый ФиксированноеСоответствие(ТекущиеПараметры);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ИспользуемыеСайтыПриПоиске() Экспорт
	
	Сайты = Новый Массив;
	ИспользуемыеСайты = ИнтеграцияРекрутинговыхСайтов.ИспользуемыеСайты();
	
	Если ЗначениеЗаполнено(ИспользуемыеСайты) Тогда
		
		ПериодыОтключенияПоиска = ПериодыОтключенияПоискаПоРезюме();
		ДатаАктуальности = ТекущаяДатаСеанса();
		
		Для Каждого Сайт Из ИспользуемыеСайты Цикл
			
			СтрокаПериода = ПериодыОтключенияПоиска.Найти(Сайт, "Сайт");
			Если СтрокаПериода <> Неопределено Тогда
				Если (ДатаАктуальности >= СтрокаПериода.Начало)
					И (ДатаАктуальности <= СтрокаПериода.Окончание 
						ИЛИ Не ЗначениеЗаполнено(СтрокаПериода.Окончание)) Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Сайты.Добавить(Сайт);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Сайты) Тогда
		Возврат Новый ФиксированныйМассив(Сайты);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ПериодыОтключенияПоискаПоРезюме()
	
	ТаблицаПериодов = Новый ТаблицаЗначений;
	
	ТаблицаПериодов.Колонки.Добавить("Сайт", Новый ОписаниеТипов("СправочникСсылка.ИсточникиИнформацииОКандидатах"));
	ТаблицаПериодов.Колонки.Добавить("Начало", Новый ОписаниеТипов("Дата"));
	ТаблицаПериодов.Колонки.Добавить("Окончание", Новый ОписаниеТипов("Дата"));
	
	НоваяСтрока = ТаблицаПериодов.Добавить();
	НоваяСтрока.Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter();
	НоваяСтрока.Начало = Дата(2017, 9, 1);
	
	Возврат ТаблицаПериодов;
	
КонецФункции

#КонецОбласти

#КонецЕсли