
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	HTMLПредставление = ПолучитьHTMLПредставлениеФормыЗадачи(Параметры.Type, Параметры.ID)
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура HTMLпредставлениеПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ДанныеСобытия.Anchor <> Неопределено И ЗначениеЗаполнено(ДанныеСобытия.Anchor.href) Тогда
		
		МассивПодстрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ДанныеСобытия.Anchor.href, "/");
		
		СтрокаСсылка = МассивПодстрок[МассивПодстрок.Количество() - 1];
		
		МассивПараметров = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаСсылка, "$");
		
		ОбъектТип = МассивПараметров[0];
		ОбъектID = МассивПараметров[1];
		
		ИнтеграцияС1СДокументооборотКлиент.ОткрытьОбъект(ОбъектТип, ОбъектID, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьБизнесПроцесс(Команда)
	
	ПараметрыБП = ПолучитьПараметрыБизнесПроцесса();
	ИнтеграцияС1СДокументооборотКлиент.ОткрытьОбъект(ПараметрыБП.type, ПараметрыБП.id, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьHTMLПредставлениеФормыЗадачи(Тип, Ид)
	
	Колонки = Новый Массив();
	Колонки.Добавить("HTMLView");
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	Ответ = ИнтеграцияС1СДокументооборот.ПолучитьОбъект(Прокси, Тип, Ид, Колонки);
	ID = Ответ.objects[0].objectId.id;
	Тип = Ответ.objects[0].objectId.type;
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Задача ""%1""'"), Ответ.objects[0].name);
	Возврат Ответ.objects[0].htmlView;
	
КонецФункции

&НаСервере
Функция ПолучитьПараметрыБизнесПроцесса()
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	ДанныеЗадачи = ИнтеграцияС1СДокументооборот.ПолучитьОбъект(Прокси, "DMBusinessProcessTask", ID);
	
	ПараметрыБизнесПроцесса = новый Структура;
	ПараметрыБизнесПроцесса.Вставить("id", ДанныеЗадачи.objects[0].parentBusinessProcess.objectId.id);
	ПараметрыБизнесПроцесса.Вставить("type", ДанныеЗадачи.objects[0].parentBusinessProcess.objectId.type);
	
	Возврат ПараметрыБизнесПроцесса;
	
КонецФункции

#КонецОбласти
