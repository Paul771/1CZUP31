#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.4.6.1") Тогда
		Параметры.Свойство("id", ИдентификаторФайла);
		Параметры.Свойство("Наименование", НаименованиеФайла);
		Заголовок = СтрШаблон(НСтр("ru = '%1 (Версии файла)'"), НаименованиеФайла);
		ЗаполнитьВерсииФайла();
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаФункционалНедоступен;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВерсииФайла()
	
	Версии.Очистить();
	Если Не ЗначениеЗаполнено(ИдентификаторФайла) Тогда
		Возврат;
	КонецЕсли;
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси,"DMRetrieveRequest");
	Файл = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ИдентификаторФайла, "DMFile");
	Запрос.objectIds.Добавить(Файл);
	Запрос.columnSet.Добавить("activeVersion");
	СведенияОФайлах = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, СведенияОФайлах);
	СведенияОФайле = СведенияОФайлах.objects[0];
	
	СписокОбъектов = ИнтеграцияС1СДокументооборотВызовСервера.ВерсииФайла(ИдентификаторФайла);
	Для Каждого ОбъектСписка из СписокОбъектов Цикл
		ВерсияФайла = ОбъектСписка.object;
		СтрокаВерсии = Версии.Добавить();
		СтрокаВерсии.Идентификатор = ВерсияФайла.objectId.id;
		СтрокаВерсии.ИндексКартинки =
			РаботаСФайламиСлужебныйКлиентСервер.ПолучитьИндексПиктограммыФайла(ВерсияФайла.extension);
		СтрокаВерсии.Наименование = ВерсияФайла.name;
		СтрокаВерсии.Расширение = ВерсияФайла.extension;
		СтрокаВерсии.Автор = ВерсияФайла.author.name;
		СтрокаВерсии.Размер =  ВерсияФайла.size;
		СтрокаВерсии.ПредставлениеРазмера = ИнтеграцияС1СДокументооборотКлиентСервер.
			ПодробноеПредставлениеРазмера(СтрокаВерсии.Размер);
		СтрокаВерсии.ДатаСоздания = ВерсияФайла.creationDate;
		СтрокаВерсии.Активная = (СтрокаВерсии.Идентификатор = СведенияОФайле.activeVersion.objectID.id);
		Если ВерсияФайла.Свойства().Получить("modificationDateUniversal") <> Неопределено Тогда
			СтрокаВерсии.ДатаМодификации = МестноеВремя(ВерсияФайла.modificationDateUniversal);
		Иначе
			СтрокаВерсии.ДатаМодификации = ВерсияФайла.modificationDate;
		КонецЕсли;
		Если ВерсияФайла.Свойства().Получить("comment") <> Неопределено Тогда
			СтрокаВерсии.Комментарий = ВерсияФайла.comment;
		КонецЕсли;
	КонецЦикла;
	
	Версии.Сортировать("ДатаСоздания");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьСписокВерсий();
	
КонецПроцедуры

&НаКлиенте
Процедура Просмотреть(Команда)
	
	ТекущиеДанные = Элементы.Версии.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ИнтеграцияС1СДокументооборотКлиент.ОткрытьВерсиюФайла(ТекущиеДанные.Идентификатор, 
			ТекущиеДанные.Наименование, ТекущиеДанные.Расширение, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СделатьАктивной(Команда)
	
	ТекущиеДанные = Элементы.Версии.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("СделатьАктивнойЗавершение", ЭтаФорма);
		ИнтеграцияС1СДокументооборотКлиент.СделатьВерсиюАктивной(ИдентификаторФайла,
			ТекущиеДанные.Идентификатор, ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКак(Команда)
	
	ТекущиеДанные = Элементы.Версии.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ИнтеграцияС1СДокументооборотКлиент.СохранитьВерсиюКак(ТекущиеДанные.Идентификатор, 
			ТекущиеДанные.Наименование, ТекущиеДанные.Расширение, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыВерсии

&НаКлиенте
Процедура ВерсииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Версии.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ИнтеграцияС1СДокументооборотКлиент.ОткрытьВерсиюФайла(ТекущиеДанные.Идентификатор, 
			ТекущиеДанные.Наименование, ТекущиеДанные.Расширение, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СделатьАктивнойЗавершение(Результат, Параметры) Экспорт
	
	ОбновитьСписокВерсий();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокВерсий()
	
	Если Элементы.Версии.ТекущиеДанные = Неопределено Тогда
		ТекущаяСтрока = Неопределено;
	Иначе
		ТекущаяСтрока = Элементы.Версии.ТекущиеДанные.ПолучитьИдентификатор();
	КонецЕсли;
	
	ЗаполнитьВерсииФайла();
	
	Элементы.Версии.ТекущаяСтрока = ТекущаяСтрока;
	
КонецПроцедуры

#КонецОбласти