#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("ОбъектИС", ОбъектИС);
	Параметры.Свойство("ИдентификаторОбъектаДО", ИдентификаторОбъектаДО);
	Параметры.Свойство("ТипОбъектаДО", ТипОбъектаДО);
	
	ТипОбъектаИС = ОбъектИС.Метаданные().ПолноеИмя();
	ТипПечатнойФормы = ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить("ИнтеграцияС1СДокументооборот",
		"ТипПечатнойФормы" + ТипОбъектаИС, Перечисления.ТипыФайловСохраненияПечатныхФормОбъектов.PDF);
		
	// Захват и редактирование файлов.
	Элементы.ДобавитьИРедактировать.Видимость = 
		ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.4.9.1");
	
	ИнтеграцияС1СДокументооборотПереопределяемый.ЗаполнитьТаблицуМенеджеровПечатиОбъекта(ТипОбъектаИС, ПечатныеФормы);
	
	Если ПечатныеФормы.Количество() = 0 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПечатныеФормыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДобавитьПечатнуюФорму(Элементы.ПечатныеФормы.ТекущиеДанные.ИмяКоманды, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Добавить(Команда)
	
	ТекущиеДанные = Элементы.ПечатныеФормы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ДобавитьПечатнуюФорму(ТекущиеДанные.ИмяКоманды, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИРедактировать(Команда)
	
	ТекущиеДанные = Элементы.ПечатныеФормы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ДобавитьПечатнуюФорму(ТекущиеДанные.ИмяКоманды, Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДобавитьПечатнуюФорму(ИмяКоманды, НаЧтение)
	
	СозданныеФайлы = ИнтеграцияС1СДокументооборотВызовСервера.ПрисоединитьПечатнуюФормуОбъектаКДокументу(ОбъектИС,
		ИдентификаторОбъектаДО, ТипОбъектаДО, "", ИмяКоманды, ТипПечатнойФормы);
		
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить("ИнтеграцияС1СДокументооборот",
		"ТипПечатнойФормы" + ТипОбъектаИС, ТипПечатнойФормы);
		
	Если Не НаЧтение Тогда
		Для каждого СозданныйФайл из СозданныеФайлы Цикл
			ИнтеграцияС1СДокументооборотКлиент.ОткрытьФайл(СозданныйФайл.Идентификатор, СозданныйФайл.Имя,
				СозданныйФайл.Расширение, УникальныйИдентификатор, НаЧтение);
		КонецЦикла;
	КонецЕсли;
	
	Закрыть(СозданныеФайлы);
	
КонецПроцедуры

#КонецОбласти
