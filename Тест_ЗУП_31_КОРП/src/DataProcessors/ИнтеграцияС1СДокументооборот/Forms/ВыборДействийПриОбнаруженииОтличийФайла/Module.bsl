#Если Сервер Или ТонкийКлиент Или ТолстыйКлиентУправляемоеПриложение Тогда

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(
		ЭтаФорма,
		Параметры,
		"ПолныйПуть,
		|ДатаВремяНаДиске,
		|ДатаВремяВДокументообороте,
		|РазмерНаДиске,
		|РазмерВДокументообороте,
		|Сообщение,
		|Заголовок");
		
	Если ДатаВремяНаДиске > ДатаВремяВДокументообороте Тогда
		Элементы.ИспользоватьСуществующий.КнопкаПоУмолчанию = Истина;
	Иначе
		Элементы.ВзятьИзДокументооборота.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	Если ДатаВремяНаДиске = ДатаВремяВДокументообороте Тогда
		Элементы.ДатаВремяНаДиске.Видимость = Ложь;
		Элементы.ДатаВремяВДокументообороте.Видимость = Ложь;
	Иначе
		ЧасовойПояс = ЧасовойПоясСеанса();
		ДатаВремяНаДиске = МестноеВремя(ДатаВремяНаДиске, ЧасовойПояс);
		ДатаВремяВДокументообороте = МестноеВремя(ДатаВремяВДокументообороте, ЧасовойПояс);
	КонецЕсли;
	
	Если РазмерНаДиске = РазмерВДокументообороте Тогда
		Элементы.РазмерНаДиске.Видимость = Ложь;
		Элементы.РазмерВДокументообороте.Видимость = Ложь;
	Иначе
		ПредставлениеРазмераНаДиске = ИнтеграцияС1СДокументооборотКлиентСервер.
			ПодробноеПредставлениеРазмера(РазмерНаДиске);
		ПредставлениеРазмераВДокументообороте = ИнтеграцияС1СДокументооборотКлиентСервер.
			ПодробноеПредставлениеРазмера(РазмерВДокументообороте);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВзятьИзДокументооборота(Команда)
	
	Закрыть("ВзятьИзДокументооборота");
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьСуществующий(Команда)
	
	Закрыть("ИспользоватьСуществующий");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКаталог(Команда)
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоLinuxКлиент() Тогда
		ЗапуститьПриложение(ПолныйПуть);
	Иначе
		ЗапуститьПриложение("explorer.exe /select, """ + ПолныйПуть + """");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли