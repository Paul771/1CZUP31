#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Дерево = РеквизитФормыВЗначение("ДеревоВариантов");
	
	// Самостоятельные типы предметов.
	ДобавитьСтроку(Дерево, "ФайлСДиска", НСтр("ru = 'Файл с диска'"));
	
	// Предметы в ИС.
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ТипОбъектаПотребителя КАК ТипОбъекта,
		|	ВЫРАЗИТЬ(ПредставлениеОбъектаПотребителя КАК СТРОКА(100)) КАК Вариант,
		|	Правила.Ссылка КАК Правило,
		|	КлючевыеРеквизиты.ИмяРеквизитаОбъектаПотребителя КАК КлючОтбора,
		|	КлючевыеРеквизиты.ЗначениеРеквизитаПотребителя КАК ЗначениеОтбора
		|ИЗ
		|	Справочник.ПравилаИнтеграцииС1СДокументооборотом КАК Правила
		|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаИнтеграцииС1СДокументооборотом.ПравилаЗаполненияРеквизитовПриЗагрузке КАК КлючевыеРеквизиты
		|ПО Правила.Ссылка = КлючевыеРеквизиты.Ссылка И КлючевыеРеквизиты.Ключевой И Не КлючевыеРеквизиты.ДополнительныйРеквизитОбъекта
		|ГДЕ
		|	НЕ ПометкаУдаления
		|УПОРЯДОЧИТЬ ПО
		|	Вариант
		|ИТОГИ ПО
		|	Правило
		|");
	ВыборкаПравила = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Если ВыборкаПравила.Количество() <> 0 Тогда
		НаименованиеИС = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В %1'"),
			ИнтеграцияС1СДокументооборот.СокращенноеНаименованиеКонфигурации());
		ЗаголовокИС = ДобавитьЗаголовок(Дерево, НаименованиеИС);
		Пока ВыборкаПравила.Следующий() Цикл
			Отбор = Новый СписокЗначений;
			ВыборкаКлючевыеРеквизиты = ВыборкаПравила.Выбрать();
			Пока ВыборкаКлючевыеРеквизиты.Следующий() Цикл
				Если ЗначениеЗаполнено(ВыборкаКлючевыеРеквизиты.КлючОтбора) Тогда
					Отбор.Добавить(ВыборкаКлючевыеРеквизиты.ЗначениеОтбора, ВыборкаКлючевыеРеквизиты.КлючОтбора);
				КонецЕсли;
			КонецЦикла;
			ДобавитьСтроку(ЗаголовокИС, ВыборкаПравила.ТипОбъекта, ВыборкаПравила.Вариант, Отбор);
		КонецЦикла;
	КонецЕсли;
	
	// Предметы в ДО.
	ЗаголовокДО = ДобавитьЗаголовок(Дерево, НСтр("ru = 'В 1С:Документообороте'"));
	ДобавитьСтроку(ЗаголовокДО, "DMFile", НСтр("ru = 'Файл'"));
	ДобавитьСтроку(ЗаголовокДО, "DMInternalDocument", НСтр("ru = 'Внутренний документ'"));
	ДобавитьСтроку(ЗаголовокДО, "DMIncomingDocument", НСтр("ru = 'Входящий документ'"));
	ДобавитьСтроку(ЗаголовокДО, "DMOutgoingDocument", НСтр("ru = 'Исходящий документ'"));
	ДобавитьСтроку(ЗаголовокДО, "DMProject", НСтр("ru = 'Проект'"));
	Если ИнтеграцияС1СДокументооборотПовтИсп.ИспользоватьТерминКорреспонденты() Тогда
		ДобавитьСтроку(ЗаголовокДО, "DMCorrespondent", НСтр("ru = 'Корреспондент'"));
	Иначе
		ДобавитьСтроку(ЗаголовокДО, "DMCorrespondent", НСтр("ru = 'Контрагент'"));
	КонецЕсли;
	
	ЗначениеВДанныеФормы(Дерево, ДеревоВариантов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	
	ВыбратьВариант();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДеревоВариантовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбратьВариант();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьВариант()
	
	ТекущиеДанные = Элементы.ДеревоВариантов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.ЭтоЗаголовок Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Вариант", ТекущиеДанные.Вариант);
	Результат.Вставить("Отбор", ТекущиеДанные.Отбор);
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервере
Функция ДобавитьСтроку(Дерево, Вариант, Представление, Отбор = Неопределено)
	
	Строка = Дерево.Строки.Добавить();
	Строка.Вариант = Вариант;
	Строка.Представление = Представление;
	Строка.Отбор = Отбор;
	
	Возврат Строка;
	
КонецФункции

&НаСервере
Функция ДобавитьЗаголовок(Дерево, Представление)
	
	Строка = Дерево.Строки.Добавить();
	Строка.Представление = Строка(Представление);
	Строка.ЭтоЗаголовок = Истина;
	
	Возврат Строка;
	
КонецФункции

#КонецОбласти