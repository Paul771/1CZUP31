#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ID = Параметры.ID;
	Тип = Параметры.type;
	Если Не ЗначениеЗаполнено(Тип) Тогда
		Тип = "DMInternalDocument";
	КонецЕсли;
	Параметры.Свойство("ВнешнийОбъект", ВнешнийОбъект);
	
	СокращенноеНаименованиеКонфигурации = ИнтеграцияС1СДокументооборот.СокращенноеНаименованиеКонфигурации();
	
	// Cчитаем данные документа.
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	Если ЗначениеЗаполнено(ID) И ЗначениеЗаполнено(Тип) Тогда 
		
		Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMRetrieveRequest");
		
		ОбъектИд = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ID, Тип);
		Запрос.objectIds.Добавить(ОбъектИд);
		
		Результат = Прокси.execute(Запрос);
		ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
		
		ОбъектXDTO = Результат.objects[0];
		Если Не ЗначениеЗаполнено(ВнешнийОбъект) Тогда
			ВнешнийОбъект = ИнтеграцияС1СДокументооборот.СсылкаПоВнешнемуОбъекту(ОбъектXDTO.externalObject);
		КонецЕсли;
		
	Иначе // новый документ
		
		Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetNewObjectRequest");
		Запрос.type = Тип;
		
		Если ЗначениеЗаполнено(Параметры.Правило) Тогда
			
			РеквизитыПравила = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				Параметры.Правило,
				"НеСоздаватьСвязиПоСсылкам, Шаблон, ШаблонID, ШаблонТип");
				
			// Заполнение документов по шаблонам.
			Если ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("2.0.8.1") Тогда
				
				Если ЗначениеЗаполнено(РеквизитыПравила.ШаблонID) Тогда
						
					ШаблонXDTO = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси,
						РеквизитыПравила.ШаблонТип);
					ШаблонXDTO.name = РеквизитыПравила.Шаблон;
					ШаблонXDTO.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси,
						РеквизитыПравила.ШаблонID,
						РеквизитыПравила.ШаблонТип);
						
					Запрос.dataSource = ШаблонXDTO;
						
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе // создается пустой документ
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаРеквизиты;
			
		КонецЕсли;
		
		Результат = Прокси.execute(Запрос);
		ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
		
		ОбъектXDTO = Результат;
		
	КонецЕсли;
	
	Если ИнтеграцияС1СДокументооборотПовтИсп.ИспользоватьТерминКорреспонденты() Тогда
		Элементы.ГруппаКонтрагент.Заголовок = НСтр("ru = 'Корреспондент'");
		Элементы.Контрагент.Заголовок = НСтр("ru = 'Корреспондент'");
	КонецЕсли;
	
	ИспользоватьСвязанныеДокументы1СДокументооборота = 
		Константы.ИспользоватьСвязанныеДокументы1СДокументооборота.Получить();
	// Заполним форму данными документа.
	ПрочитатьОбъектВФорму(ОбъектXDTO);
	ИнтеграцияС1СДокументооборотВызовСервера.ОбновитьСписокФайлов(
		ОбъектXDTO.files, Файлы, Элементы.ФайлыНаименование);
	// Обработаем резолюции.
	Если ОбъектXDTO.Свойства().Получить("resolutions") <> Неопределено
		И ОбъектXDTO.resolutions.Количество() <> 0 Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ОбновитьПредставлениеРезолюций(
			ОбъектXDTO.resolutions, РезолюцииHTMLПредставление, Элементы.СтраницаРезолюции);
	Иначе
		Элементы.СтраницаРезолюции.Видимость = Ложь;
	КонецЕсли;
	// Обработаем визы.
	Если ОбъектXDTO.Свойства().Получить("visas") <> Неопределено
		И ОбъектXDTO.visas.Количество() <> 0 Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ОбновитьВизыСогласования(
			ОбъектXDTO.visas, ВизыСогласования, Элементы.СтраницаВизы);
	Иначе
		Элементы.СтраницаВизы.Видимость = Ложь;
	КонецЕсли;
	
	НастройкиДО = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьНастройки();
	
	Элементы.ЗаполнитьФайлДаннымиДокумента.Видимость = НастройкиДО.ИспользоватьАвтозаполнениеФайлов;
	
	// Применим настройки использования ЭП в ДО и в ИС.
	ИспользоватьЭлектронныеЦифровыеПодписиВБСП = ИнтеграцияС1СДокументооборотВызовСервера.
		ИспользоватьЭлектронныеЦифровыеПодписи();
	
	Если НастройкиДО.ИспользоватьЭлектронныеЦифровыеПодписи = Ложь Тогда
		Элементы.ФайлыНомерКартинкиПодписанЗашифрован.Видимость = Ложь;
		Элементы.СтраницаЭП.Видимость = Ложь;
		Элементы.ГруппаКомандыЭП.Видимость = Ложь;
	Иначе
		ЗаполнитьСписокПодписей(ОбъектXDTO.signatures, ОбъектXDTO.files);
		Если ОбъектXDTO.Установлено("keyPropertiesValue") Тогда
			АдресСлепкаДокумента = ПоместитьВоВременноеХранилище(
				ОбъектXDTO.keyPropertiesValue, УникальныйИдентификатор);
		КонецЕсли;
	КонецЕсли;
	
	Если НастройкиДО.ИспользоватьЭлектронныеЦифровыеПодписи = Ложь 
		Или ИспользоватьЭлектронныеЦифровыеПодписиВБСП = Ложь Тогда
		Элементы.Подписать.Видимость = Ложь;
		Элементы.ДобавитьЭПИзФайла.Видимость = Ложь;
		Элементы.ФормаПодписатьДокумент.Видимость = Ложь;
	КонецЕсли;
	
	// Заполним документ по связанному объекту ИС.
	Если Не ЗначениеЗаполнено(ID) 
		И ЗначениеЗаполнено(ВнешнийОбъект)
		И ЗначениеЗаполнено(Параметры.Правило) Тогда
		
		Справочники.ПравилаИнтеграцииС1СДокументооборотом.ЗаполнитьФормуИзПотребителя(
			ВнешнийОбъект, ЭтаФорма, Параметры.Правило);
		ИнтеграцияС1СДокументооборотПереопределяемый.ЗаполнитьФормуИзПотребителя(
			ВнешнийОбъект, ЭтаФорма);
			
		СрокДействияПредставление = 
			ИнтеграцияС1СДокументооборотКлиентСервер.ПредставлениеСрокаДействияДокумента(
				ДатаНачалаДействия,
				Бессрочный,
				ДатаОкончанияДействия,
				ПорядокПродленияID,
				ПорядокПродления);
				
		Обработки.ИнтеграцияС1СДокументооборот.УстановитьСостоянияДокумента(ЭтаФорма, ОбъектXDTO);
		
		Если ИспользоватьСвязанныеДокументы1СДокументооборота
			И ИнтеграцияС1СДокументооборот.ДоступенФункционалВерсииСервиса("2.1.0.1")
			И Не РеквизитыПравила.НеСоздаватьСвязиПоСсылкам Тогда
			
			Обработки.ИнтеграцияС1СДокументооборот.ЗаполнитьСвязиНовогоДокумента(ЭтаФорма,
				ВнешнийОбъект,
				Элементы.СтраницаСвязи);
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Установим видимость команды копирования файлов.
	Элементы.ЗаполнитьФайлыКопированием.Видимость = Ложь;
	Если ЗначениеЗаполнено(ВнешнийОбъект) Тогда
		Если ИнтеграцияС1СДокументооборотВызовСервера.ЕстьХранимыеФайлы(ВнешнийОбъект) Тогда
			Элементы.ЗаполнитьФайлыКопированием.Видимость = Истина;
			Элементы.ЗаполнитьФайлыКопированием.Доступность = (Файлы.Количество() = 0);
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ДобавитьПечатнуюФорму.Доступность = ЗначениеЗаполнено(ВнешнийОбъект);
	
	Если Параметры.ТолькоПросмотр Тогда 
		ЭтаФорма.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	// Почта.
	Если Не ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.2.8.1.CORP") Тогда
		Элементы.ФормаСоздатьПисьмо.Видимость = Ложь;
	КонецЕсли;
	// Хронометраж.
	Если Не ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3.CORP") Тогда
		Элементы.ФормаУказатьТрудозатраты.Видимость = Ложь;
		Элементы.ФормаПереключитьХронометраж.Видимость = Ложь;
	КонецЕсли;
	ДоступенЗахватФайлов = ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.4.9.1");
	
	ОбновитьДекорацииСвязи();
	
	ИнтеграцияС1СДокументооборотПереопределяемый.ДополнительнаяОбработкаФормыДокумента(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьОбзор();
	
	УстановитьДоступностьКомандСпискаЭП();
	
	Если ЗначениеЗаполнено(АдресДляФайловСвязанныхДокументов) Тогда
		ПодключитьОбработчикОжидания("ОбновлениеФайловСвязанныхДокументов", 1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗакрытиеСПараметром Тогда 
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтаФорма);
		ТекстПредупреждения = "";
		ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы,,ТекстПредупреждения);
		
	Иначе
		
		Отказ = Истина;
		ПодключитьОбработчикОжидания("ЗакрытьСПараметром", 0.1, Истина);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ДокументооборотФайл" Тогда
		ОбновитьСписокФайловКлиент();
		ПрочитатьИОбновитьСписокПодписей();
		УстановитьДоступностьКомандСпискаЭП();
		
	ИначеЕсли ИмяСобытия = "Запись_ДокументооборотТрудозатраты" 
		И Источник = ID Тогда
		ВключенХронометраж = Ложь;
		ДатаНачалаХронометража = '00010101';
		УстановитьСвойстваЭлементовХронометражаСервер();
		
	ИначеЕсли ИмяСобытия = "Документооборот_ВыбратьЗначениеИзСпискаЗавершение" 
		И Источник = ЭтаФорма Тогда
		Если Параметр = "ВидДокумента" Тогда
			ПриИзмененииВидаДокумента();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Документооборот_ДобавлениеСвязи" 
		И Параметр.ID = ID Тогда
		ВнешнийОбъект = Параметр.Объект;
		ОбновитьДекорацииСвязи();
		
	ИначеЕсли ИмяСобытия = "Документооборот_УдалениеСвязи" 
		И Параметр.ID = ID Тогда
		ВнешнийОбъект = Неопределено;
		ОбновитьДекорацииСвязи();
		
	ИначеЕсли ИмяСобытия = "Запись_ДокументооборотБизнесПроцесс" Тогда
		Для Каждого Предмет Из Параметр.Предметы Цикл
			Если Предмет.ID = ID Тогда
				ОбновитьСостояние();
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ИмяСобытия = "Запись_ДокументооборотДокумент" Тогда
		
		Для Каждого СтрокаТипаСвязи Из ДеревоСвязей.ПолучитьЭлементы() Цикл
			Для Каждого СтрокаДокумента Из СтрокаТипаСвязи.ПолучитьЭлементы() Цикл
				Если СтрокаДокумента.Ссылка = Параметр.ВнешнийОбъект
					И Не ЗначениеЗаполнено(СтрокаДокумента.ID) Тогда
					СтрокаДокумента.ID = Параметр.id;
					СтрокаДокумента.Тип = Параметр.type;
					СтрокаДокумента.Заголовок = Параметр.name;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

///////////////////////////////////////////////////////////////////////////////////////////////////
// Общие

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	ОбновитьОбзор();
	
КонецПроцедуры

&НаКлиенте
Процедура СвязьОбъектНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(, ВнешнийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СвязьСоздатьНажатие(Элемент)
	
	Если Не ЗначениеЗаполнено(ID) Тогда
		ЗаписатьОбъект();
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СвязьСоздатьНажатиеЗавершение", ЭтаФорма);
	Если ПравилаЗаполнения.Количество() = 1 Тогда
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, ПравилаЗаполнения[0]);
	Иначе
		ПоказатьВыборИзМеню(ОписаниеОповещения, ПравилаЗаполнения, Элементы.СвязьСоздать);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СвязьВыбратьНажатие(Элемент)
	
	Если Не ЗначениеЗаполнено(ID) Тогда
		ЗаписатьОбъект();
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СвязьВыбратьНажатиеЗавершение", ЭтаФорма);
	Если ПравилаЗаполнения.Количество() = 1 Тогда
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, ПравилаЗаполнения[0]);
	Иначе
		ПоказатьВыборИзМеню(ОписаниеОповещения, ПравилаЗаполнения, Элементы.СвязьВыбрать);
	КонецЕсли; 
		
КонецПроцедуры

&НаКлиенте
Процедура СвязьОчиститьНажатие(Элемент)
	
	Если Не ЗначениеЗаполнено(ВнешнийОбъект) Тогда 
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("СвязьОчиститьНажатиеЗавершение", ЭтаФорма);
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Вы действительно хотите очистить соответствие для
					|%1?'"), Строка(ВнешнийОбъект));
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Очистить'"));
	Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Не очищать'"));
	
	ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки,, КодВозвратаДиалога.Нет);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Обзор

&НаКлиенте
Процедура ПредставлениеHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Не ЗначениеЗаполнено(ДанныеСобытия.Href) Тогда 
		Возврат;
	КонецЕсли;
	
	Если Лев(ДанныеСобытия.Href, 6) = "v8doc:" Тогда 
		
		НавигационнаяСсылкаПоля = Сред(ДанныеСобытия.Href, 7);
		ПерейтиПоНавигационнойСсылке(НавигационнаяСсылкаПоля);
		
	ИначеЕсли Лев(ДанныеСобытия.Href, 5) = "dmil:" Тогда 
		
		НавигационнаяСсылкаПоля = Сред(ДанныеСобытия.Href, 6);
		РазделеннаяСсылка = СтроковыеФункцииКлиентСервер.
			РазложитьСтрокуВМассивПодстрок(НавигационнаяСсылкаПоля, ":");
		ТипОбъекта = РазделеннаяСсылка[0];
		ИдентификаторОбъекта = РазделеннаяСсылка[1];
		
		Если РазделеннаяСсылка.Количество() = 2 Тогда
			ИнтеграцияС1СДокументооборотКлиент.ОткрытьОбъект(ТипОбъекта, ИдентификаторОбъекта, Элемент);
			
		ИначеЕсли РазделеннаяСсылка.Количество() = 3 Тогда
			ИмяРеквизита = РазделеннаяСсылка[2];
			Если ИмяРеквизита = "status" Тогда
				ИнтеграцияС1СДокументооборотКлиент.ОткрытьСостоянияДокумента(ЭтаФорма,
					Элементы.Состояние.ТолькоПросмотр);
			ИначеЕсли ИмяРеквизита = "term" Тогда
				ИнтеграцияС1СДокументооборотКлиент.ОткрытьСрокДействияДокумента(ЭтаФорма,
					Элементы.СрокДействияПредставление.ТолькоПросмотр);
			КонецЕсли;
				
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Реквизиты

///////////////////////////////////////////////////////////////////////////////////////////////////
// ВидДокумента

&НаКлиенте
Процедура ВидДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыбратьЗначениеИзСписка(
		"DMInternalDocumentType", "ВидДокумента", ЭтаФорма); 
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДокументаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
		"ВидДокумента", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
	ПриИзмененииВидаДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДокументаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание,
		СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMInternalDocumentType", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДокументаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMInternalDocumentType", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
				"ВидДокумента", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
			
			ПриИзмененииВидаДокумента();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Организация

&НаКлиенте
Процедура ОрганизацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыбратьЗначениеИзВыпадающегоСписка(
		"DMOrganization", "Организация", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
		"Организация", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMOrganization", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных,
		СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMOrganization", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
				"Организация", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Подписал

&НаКлиенте
Процедура ПодписалНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыбратьПользователяИзДереваПодразделений("Подписал", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписалОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
		"Подписал", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписалАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMUser", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписалОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMUser", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
				"Подписал", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Подготовил

&НаКлиенте
Процедура ПодготовилНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыбратьПользователяИзДереваПодразделений("Подготовил", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовилОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
		"Подготовил", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовилАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMUser", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовилОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMUser", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
				"Подготовил", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Подразделение

&НаКлиенте
Процедура ПодразделениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыбратьЗначениеИзСписка("DMSubdivision", "Подразделение", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
		"Подразделение", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание,
		СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMSubdivision", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMSubdivision", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
				"Подразделение", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Контрагент

&НаКлиенте
Процедура КонтрагентОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(КонтрагентID) Тогда
		ИнтеграцияС1СДокументооборотКлиент.ОткрытьОбъект("DMCorrespondent", КонтрагентID, Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыбратьЗначениеИзСписка("DMCorrespondent", "Контрагент", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
		"Контрагент", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMCorrespondent", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMCorrespondent", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
				"Контрагент", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// КонтактноеЛицо

&НаКлиенте
Процедура КонтактноеЛицоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(КонтрагентID) И ЗначениеЗаполнено(КонтрагентТип) Тогда 
		
		Correspondent = Новый Структура;
		Correspondent.Вставить("id",   КонтрагентID);
		Correspondent.Вставить("type", КонтрагентТип);
		
		Отбор = Новый Структура;
		Отбор.Вставить("correspondent", Correspondent);
		
		ИнтеграцияС1СДокументооборотКлиент.ВыбратьЗначениеИзСписка(
			"DMContactPerson", "КонтактноеЛицо", ЭтаФорма, Отбор);
		
	Иначе	
		ТекстСообщения = НСтр("ru = 'Не заполнено поле ""Контрагент""'");
		ПоказатьПредупреждение(, ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактноеЛицоОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
		"КонтактноеЛицо", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактноеЛицоАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMContactPerson", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактноеЛицоОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMContactPerson", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
				"КонтактноеЛицо", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Папка

&НаКлиенте
Процедура ПапкаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИнтеграцияС1СДокументооборотКлиент.ОткрытьОбъект(ПапкаТип, ПапкаID, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПапкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыбратьЗначениеИзСписка(
		"DMInternalDocumentFolder", "Папка", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПапкаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
		"Папка", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПапкаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMInternalDocumentFolder", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПапкаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMInternalDocumentFolder", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
				"Папка", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Валюта

&НаКлиенте
Процедура ВалютаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыбратьЗначениеИзВыпадающегоСписка("DMCurrency", "Валюта", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
		"Валюта", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMCurrency", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMCurrency", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
				"Валюта", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// СрокДействияПредставление

&НаКлиенте
Процедура СрокДействияПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИнтеграцияС1СДокументооборотКлиент.ОткрытьСрокДействияДокумента(ЭтаФорма);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// ГрифДоступа

&НаКлиенте
Процедура ГрифДоступаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыбратьЗначениеИзВыпадающегоСписка(
		"DMAccessLevel", "ГрифДоступа", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ГрифДоступаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
		"ГрифДоступа", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ГрифДоступаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMAccessLevel", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГрифДоступаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMAccessLevel", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
				"ГрифДоступа", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// ВопросДеятельности

&НаКлиенте
Процедура ВопросДеятельностиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыбратьЗначениеИзСписка(
		"DMActivityMatter", "ВопросДеятельности", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросДеятельностиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
		"ВопросДеятельности", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросДеятельностиАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание,
		СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMActivityMatter", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросДеятельностиОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMActivityMatter", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
				"ВопросДеятельности", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Проект

&НаКлиенте
Процедура ПроектНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыбратьЗначениеИзСписка("DMProject", "Проект", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроектОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
		"Проект", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроектАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMProject", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроектОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMProject", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
				"Проект", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Состояние

&НаКлиенте
Процедура СостояниеСписокНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИнтеграцияС1СДокументооборотКлиент.ОткрытьСостоянияДокумента(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыбратьЗначениеИзВыпадающегоСписка(
		"DMDocumentStatus", "Состояние", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
		"Состояние", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMDocumentStatus", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMDocumentStatus", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
				"Состояние", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Ответственный

&НаКлиенте
Процедура ОтветственныйНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыбратьПользователяИзДереваПодразделений("Ответственный", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныйОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
		"Ответственный", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныйАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMUser", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныйОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных,
	СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMUser", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		 Если ДанныеВыбора.Количество() = 1 Тогда 
			ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
				"Ответственный", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыФайлы

&НаКлиенте
Процедура ФайлыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФайлНаЧтение(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПриАктивизацииСтроки(Элемент)
	
	УстановитьДоступностьКомандСпискаФайлов();
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ОткрытьКарточку();
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	СоздатьФайл(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	Если Элементы.Файлы.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = "";
	
	Если Элементы.Файлы.ВыделенныеСтроки.Количество() = 1 Тогда
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Пометить ""%1"" на удаление?'"), Элементы.Файлы.ТекущиеДанные.Наименование);
	Иначе
		ТекстВопроса = НСтр("ru = 'Пометить выделенные файлы на удаление?'");
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("ВыделенныеСтроки", Элементы.Файлы.ВыделенныеСтроки);
	Оповещение = Новый ОписаниеОповещения("ФайлыПередУдалениемЗавершение", ЭтаФорма, ДополнительныеПараметры);
	
	ИнтеграцияС1СДокументооборотКлиент.ПоказатьВопросДаНет(Оповещение, ТекстВопроса);
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	Если ПустаяСтрока(ID) Тогда
		ЗаписатьОбъект();
		ИнтеграцияС1СДокументооборотКлиент.Оповестить_ЗаписьДокумента(ЭтаФорма);
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СписокФайловПослеДобавленияФайла", ЭтаФорма);
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Файл") 
		И ПараметрыПеретаскивания.Значение.ЭтоФайл() = Истина Тогда
		
		ИнтеграцияС1СДокументооборотКлиент.СоздатьФайлСДискаПеретаскиванием(
			ПараметрыПеретаскивания.Значение,
			УникальныйИдентификатор,
			ID,
			Тип,
			Представление,
			ОписаниеОповещения);
			
	ИначеЕсли ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") Тогда
			
		Для Каждого ФайлПринятый Из ПараметрыПеретаскивания.Значение Цикл
			
			Если ТипЗнч(ФайлПринятый) = Тип("Файл") Тогда
				ИнтеграцияС1СДокументооборотКлиент.СоздатьФайлСДискаПеретаскиванием(
					ФайлПринятый,
					УникальныйИдентификатор,
					ID,
					Тип,
					Представление,
					ОписаниеОповещения);
				КонецЕсли;
				
			КонецЦикла;
			
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСвойства

&НаКлиенте
Процедура СвойстваЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыбратьЗначениеДополнительногоРеквизита(
		ЭтаФорма, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.Свойства.ТекущиеДанные;
	Если ТекущиеДанные.СписокДоступныхТипов.Количество() <> 1 Тогда
		Возврат;
	КонецЕсли;
		
	Если ТипЗнч(ТекущиеДанные.Значение) = Тип("Строка") Тогда
		ТипXDTO =ТекущиеДанные.СписокДоступныхТипов[0].Значение.xdtoClassName;
		Если ТипXDTO = "integer" Тогда
			ТекущиеДанные.Значение = 0;
		ИначеЕсли ТипXDTO = "boolean" Тогда
			ТекущиеДанные.Значение = Ложь;
		ИначеЕсли ТипXDTO = "date" Тогда
			ТекущиеДанные.Значение = Дата(1, 1, 1);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СвойстваЗначениеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Текст) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Свойства.ТекущиеДанные;
	Если ТекущиеДанные.СписокДоступныхТипов.Количество() <> 1 Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
		
	ТипXDTO = ТекущиеДанные.СписокДоступныхТипов[0].Значение.xdtoClassName;
	
	Если ТипXDTO = "integer"
		Или ТипXDTO = "boolean"
		Или ТипXDTO = "date"
		Или ТипXDTO = "string" Тогда
		Возврат;
	КонецЕсли;
		
	Если ТипXDTO = "DMObjectPropertyValue" Тогда
		ДополнительноеСвойство = Новый Структура;
		ДополнительноеСвойство.Вставить("id", ТекущиеДанные.СвойствоID);
		ДополнительноеСвойство.Вставить("type", ТекущиеДанные.СвойствоТип);
		Отбор = Новый Структура("additionalProperty", ДополнительноеСвойство);
	Иначе
		Отбор = Неопределено;
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
		ТипXDTO, ДанныеВыбора, Текст, СтандартнаяОбработка, Отбор);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваЗначениеОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Текст) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Свойства.ТекущиеДанные;
	Если ТекущиеДанные.СписокДоступныхТипов.Количество() <> 1 Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
		
	ТипXDTO = ТекущиеДанные.СписокДоступныхТипов[0].Значение.xdtoClassName;
	
	Если ТипXDTO = "integer" 
		Или ТипXDTO = "boolean" 
		Или ТипXDTO = "string" 
		Или ТипXDTO = "date" Тогда
		Возврат;
	КонецЕсли;
		
	Если ТипXDTO = "DMObjectPropertyValue" Тогда
		ДополнительноеСвойство = Новый Структура;
		ДополнительноеСвойство.Вставить("id", ТекущиеДанные.СвойствоID);
		ДополнительноеСвойство.Вставить("type", ТекущиеДанные.СвойствоТип);
		Отбор = Новый Структура("additionalProperty", ДополнительноеСвойство);
	Иначе
		Отбор = Неопределено;
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
		ТипXDTO, ДанныеВыбора, Текст, СтандартнаяОбработка, Отбор);
	
	Если ДанныеВыбора.Количество() = 1 Тогда 
		ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
			"Значение", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма, Истина, Элемент);
		СтандартнаяОбработка = Истина;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура СвойстваЗначениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
		"Значение", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма, Истина, Элемент);
		
КонецПроцедуры

&НаКлиенте
Процедура СвойстваЗначениеОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Элементы.Свойства.ТекущиеДанные.Значение = "";
	Элементы.Свойства.ТекущиеДанные.ЗначениеID = "";
	Элементы.Свойства.ТекущиеДанные.ЗначениеТип = "";
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоСвязей

&НаКлиенте
Процедура ДеревоСвязейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элементы.ДеревоСвязей.ТекущиеДанные;
	ОткрытьСвязанныеДанныеСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвязейПриАктивизацииСтроки(Элемент)
	
	Строка = Элемент.ТекущиеДанные;
	
	ДоступностьОткрытия = 
		(Строка <> Неопределено)
		И (Строка.Тип <> "DMRelationType");
	ДоступностьУдаления = ДоступностьОткрытия
		И (Строка.Тип <> "DMFile");
		
	Элементы.ДеревоСвязейОткрыть.Доступность = ДоступностьОткрытия;
	Элементы.ДеревоСвязейОткрытьКонтекст.Доступность = ДоступностьОткрытия;
	Элементы.ДеревоСвязейУдалить.Доступность = ДоступностьУдаления;
	Элементы.ДеревоСвязейУдалитьКонтекст.Доступность = ДоступностьОткрытия;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвязейПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, 
	Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвязейПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвязейПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.ДеревоСвязей.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено
		Или ТекущиеДанные.Тип = "DMRelationType"
		Или ТекущиеДанные.Тип = "DMFile" Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"НачатьУдалениеСвязиЗавершение", ЭтаФорма);
		
	СтрокаТипаСвязи = ТекущиеДанные.ПолучитьРодителя();
	
	ИсходныйДокумент = Новый Структура("ID, Тип, Представление",
		ID, Тип, Представление);
	СвязанныйДокумент = Новый Структура("ID, Тип, Представление",
		ТекущиеДанные.ID, ТекущиеДанные.Тип, ТекущиеДанные.Заголовок);
	ТипСвязи = Новый Структура("ID, Тип, Представление",
		СтрокаТипаСвязи.ID, СтрокаТипаСвязи.Тип, СтрокаТипаСвязи.Заголовок);
	
	ИнтеграцияС1СДокументооборотКлиент.НачатьУдалениеСвязи(
		ИсходныйДокумент,
		СвязанныйДокумент,
		ТипСвязи,
		ОписаниеОповещения);
			
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвязейПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаПодписей

&НаКлиенте
Процедура ТаблицаПодписейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЭлектроннаяПодписьКлиент.ОткрытьПодпись(Элементы.ТаблицаПодписей.ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

///////////////////////////////////////////////////////////////////////////////////////////////////
// Общие

&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьИВозможноЗакрыть(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьИВозможноЗакрыть(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Зарегистрировать(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ЗарегистрироватьЗавершение", ЭтаФорма);
	
	Если ОграничиватьДоступностьПолей Тогда 
		
		ТекстВопроса = НСтр(
			"ru = 'После регистрации документ станет недоступным для изменения.
			|Продолжить?'");
		ИнтеграцияС1СДокументооборотКлиент.ПоказатьВопросДаНет(
			Оповещение, ТекстВопроса,,, КодВозвратаДиалога.Да);
		
	Иначе
		
		ВыполнитьОбработкуОповещения(Оповещение, КодВозвратаДиалога.Да);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УказатьТрудозатраты(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("Источник", Представление);
	ПараметрыФормы.Вставить("ИсточникID", ID);
	ПараметрыФормы.Вставить("ИсточникТип", Тип);
	
	Если ЗначениеЗаполнено(ВнешнийОбъект) Тогда
		ПараметрыФормы.Вставить("ВнешнийОбъект", ВнешнийОбъект);
	КонецЕсли;
	
	ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборот.Форма.ДобавлениеРаботы", ПараметрыФормы, ID);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьХронометраж(Команда)
	
	НуженДиалог = Истина;
	ДлительностьРаботы = 
		ИнтеграцияС1СДокументооборотКлиент.ПолучитьДлительностьРаботы(ДатаНачалаХронометража);
	Если ДлительностьРаботы < 60 Тогда // меньше 1 минуты - детали не спрашиваем
		НуженДиалог = Ложь;
	КонецЕсли;
	
	Если НуженДиалог Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Источник", Представление);
		ПараметрыФормы.Вставить("ИсточникID", ID);
		ПараметрыФормы.Вставить("ИсточникТип", Тип);
		
		ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборот.Форма.ДобавлениеРаботы", ПараметрыФормы, ID);
		
	Иначе // запишем молча
		
		Если ВключенХронометраж Тогда
			ПереключитьХронометражСервер();
			Если ВключенХронометраж Тогда
				Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Включен хронометраж по ""%1""'"), Строка(Представление));
				Состояние(Текст);
			КонецЕсли;
		Иначе 
			АктивныеЗаписи = ИнтеграцияС1СДокументооборотВызовСервера.АктивныеЗаписиХронометража();
			Если АктивныеЗаписи.Количество() = 0 Тогда
				ПереключитьХронометражСервер();
				Если ВключенХронометраж Тогда
					Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Включен хронометраж по ""%1""'"), Строка(Представление));
					Состояние(Текст);
				КонецЕсли;
			Иначе
				ПараметрыОповещения = Новый Структура;
				ПараметрыОповещения.Вставить("АктивныеЗаписи", АктивныеЗаписи);
				
				Оповещение = Новый ОписаниеОповещения(
					"ПереключитьХронометражЗавершение", ЭтаФорма, ПараметрыОповещения);
					
				СтрокаОбъектовХронометража = "";
				Для Каждого Запись Из АктивныеЗаписи Цикл 
					СтрокаОбъектовХронометража = СтрокаОбъектовХронометража + Символы.Таб 
						+ Запись.Источник + Символы.ПС;
				КонецЦикла;
				
				ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Включен хронометраж по другим объектам:
						|%1
						|Отключить хронометраж и зафиксировать трудозатраты
						|перед включением нового хронометража?'"),
						СтрокаОбъектовХронометража);
						
				Кнопки = Новый СписокЗначений;
				Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Да'"));
				Кнопки.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru = 'Отмена'"));
				
				ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки,, КодВозвратаДиалога.Отмена);
				
			КонецЕсли;
				
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьБизнесПроцесс(Команда)
	
	Если ЗначениеЗаполнено(ID) Тогда
		
		ИнтеграцияС1СДокументооборотКлиент.СоздатьБизнесПроцессПоОбъектуДО(ID, Тип, Наименование);
		
	Иначе
		
		Оповещение = Новый ОписаниеОповещения("СоздатьБизнесПроцессЗавершение", ЭтаФорма);
		ЗаписатьИВозможноЗакрыть(Ложь, Оповещение);

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПисьмо(Команда)
	
	ПараметрыФормы = Новый Структура("Предмет", новый Структура);
	
	ПараметрыФормы.Предмет.Вставить("name", Наименование);
	ПараметрыФормы.Предмет.Вставить("id", ID);
	ПараметрыФормы.Предмет.Вставить("type", Тип);
	
	ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборот.Форма.ИсходящееПисьмо", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ВизыСогласованияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИнтеграцияС1СДокументооборотКлиент.ОткрытьВизуСогласования(ЭтаФорма,
		Элементы.ВизыСогласования.ТекущиеДанные);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Файлы

&НаКлиенте
Процедура СоздатьФайл(Команда)
	
	Если ПустаяСтрока(ID) Тогда
		ЗаписатьОбъект();
		ИнтеграцияС1СДокументооборотКлиент.Оповестить_ЗаписьДокумента(ЭтаФорма);
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СписокФайловПослеДобавленияФайла", ЭтаФорма);
	ИнтеграцияС1СДокументооборотКлиент.СоздатьФайлСДиска(УникальныйИдентификатор, 
		ID, Тип, Представление, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПечатнуюФорму(Команда)
	
	Если Не ЗначениеЗаполнено(ВнешнийОбъект) Тогда
		ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Перед добавлением печатной формы нужно связать
				|внутренний документ с объектом %1'"), СокращенноеНаименованиеКонфигурации);
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(ID) Тогда
		ЗаписатьОбъект();
		ИнтеграцияС1СДокументооборотКлиент.Оповестить_ЗаписьДокумента(ЭтаФорма);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОбъектИС", ВнешнийОбъект);
	ПараметрыФормы.Вставить("ИдентификаторОбъектаДО", ID);
	ПараметрыФормы.Вставить("ТипОбъектаДО", Тип);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавлениеПечатнойФормыЗавершение", ЭтаФорма);
	
	ФормаДобавления = ОткрытьФорму(
		"Обработка.ИнтеграцияС1СДокументооборот.Форма.ДобавлениеПечатнойФормы",ПараметрыФормы,
		ЭтаФорма,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
		
	Если ФормаДобавления = Неопределено Тогда
		ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 не имеет печатных форм.'"), Строка(ВнешнийОбъект));
		ПоказатьПредупреждение(, ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьФайлыКопированием(Команда)
	
	Режим = Новый СписокЗначений;
	Режим.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Заполнить'"));
	Режим.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Не заполнять'"));
	
	Оповещение = Новый ОписаниеОповещения("ЗаполнитьФайлыКопированиемЗавершение", ЭтаФорма);
	ТекстВопроса =  НСтр("ru = 'Заполнить файлы копированием из присоединенных файлов?'");
	
	ПоказатьВопрос(Оповещение, ТекстВопроса, Режим,, КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайлНаЧтение(Команда)
	
	Если ПустаяСтрока(ID) Тогда
		ЗаписатьОбъект();
		ИнтеграцияС1СДокументооборотКлиент.Оповестить_ЗаписьДокумента(ЭтаФорма);
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ИнтеграцияС1СДокументооборотКлиент.ОткрытьФайл(ТекущиеДанные.ID, ТекущиеДанные.Наименование, 
			ТекущиеДанные.Расширение, УникальныйИдентификатор);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайлДляРедактирования(Команда)
	
	Если ПустаяСтрока(ID) Тогда
		ЗаписатьОбъект();
		ИнтеграцияС1СДокументооборотКлиент.Оповестить_ЗаписьДокумента(ЭтаФорма);
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("КомандыРедактированияЗавершение", ЭтотОбъект);
		ИнтеграцияС1СДокументооборотКлиент.ОткрытьФайл(
			ТекущиеДанные.ID,
			ТекущиеДанные.Наименование,
			ТекущиеДанные.Расширение,
			УникальныйИдентификатор,
			Ложь,
			ОписаниеОповещения);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьРедактированиеФайла(Команда)
	
	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("КомандыРедактированияЗавершение", ЭтотОбъект);
		ИнтеграцияС1СДокументооборотКлиент.ЗакончитьРедактированиеФайла(
			ТекущиеДанные.ID,
			ТекущиеДанные.Наименование,
			ТекущиеДанные.Расширение,
			УникальныйИдентификатор,
			ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьФайлНаДиск(Команда)
	
	Если ПустаяСтрока(ID) Тогда
		ЗаписатьОбъект();
		ИнтеграцияС1СДокументооборотКлиент.Оповестить_ЗаписьДокумента(ЭтаФорма);
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотКлиент.НачатьСохранениеВыделенныхФайлов(Файлы,
		Элементы.Файлы.ВыделенныеСтроки,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИзФайлаНаДиске(Команда)
	
	Если ПустаяСтрока(ID) Тогда
		ЗаписатьОбъект();
		ИнтеграцияС1СДокументооборотКлиент.Оповестить_ЗаписьДокумента(ЭтаФорма);
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("КомандыРедактированияЗавершение", ЭтаФорма);
		ИнтеграцияС1СДокументооборотКлиент.ОбновитьИзФайлаНаДиске(
			ТекущиеДанные.ID,
			ТекущиеДанные.Наименование,
			ТекущиеДанные.Расширение,
			УникальныйИдентификатор,
			ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзмененияФайла(Команда)
	
	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("КомандыРедактированияЗавершение", ЭтотОбъект);
		ИнтеграцияС1СДокументооборотКлиент.СохранитьИзмененияРедактируемогоФайла(
			ТекущиеДанные.ID,
			ТекущиеДанные.Наименование,
			ТекущиеДанные.Расширение,
			УникальныйИдентификатор,
			ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьРедактированиеФайла(Команда)
	
	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("КомандыРедактированияЗавершение", ЭтотОбъект);
		ИнтеграцияС1СДокументооборотКлиент.ОтменитьРедактированиеФайла(
			ТекущиеДанные.ID, ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьФайлДаннымиДокумента(Команда)
	
	Если ПустаяСтрока(ID) Тогда
		ЗаписатьОбъект();
		ИнтеграцияС1СДокументооборотКлиент.Оповестить_ЗаписьДокумента(ЭтаФорма);
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ЗаполнитьПоляФайлаДаннымиВладельца(ТекущиеДанные.ID);
		Текст = НСтр("ru = 'Поля в файле обновлены данными владельца.'");
		ПоказатьПредупреждение(, Текст);
	КонецЕсли;

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Файлы (ЭП)

&НаКлиенте
Процедура ПодписатьФайл(Команда)
	
	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторФайла = ТекущиеДанные.ID;
	ИмяФайла = ТекущиеДанные.Наименование;
	Описание = ТекущиеДанные.Описание;
	Редактируется = ТекущиеДанные.Редактируется;
	Зашифрован = ТекущиеДанные.Зашифрован;
	
	ДанныеПодписейФайла = ДанныеПодписейФайла(ИдентификаторФайла);
	
	ИнтеграцияС1СДокументооборотКлиент.ПодписатьФайл(
		ИдентификаторФайла,
		ИмяФайла,
		Редактируется,
		Зашифрован,
		Описание,
		ДанныеПодписейФайла);
		
	ПрочитатьИОбновитьСписокПодписей();
	УстановитьДоступностьКомандСпискаЭП();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЭПИзФайла(Команда)
	
	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеПодписейФайла = ДанныеПодписейФайла(ТекущиеДанные.ID);
	
	СвойстваФайла = Новый Структура;
	СвойстваФайла.Вставить("ИдентификаторФайла", ТекущиеДанные.ID);
	СвойстваФайла.Вставить("ИмяФайла", ТекущиеДанные.Наименование);
	СвойстваФайла.Вставить("ОписаниеФайла", ТекущиеДанные.Описание);
	СвойстваФайла.Вставить("Редактируется", ТекущиеДанные.Редактируется);
	СвойстваФайла.Вставить("Зашифрован", ТекущиеДанные.Зашифрован);
	СвойстваФайла.Вставить("ДанныеПодписейФайла", ДанныеПодписейФайла);
	СвойстваФайла.Вставить("УникальныйИдентификатор", ЭтаФорма.УникальныйИдентификатор);
	
	Оповещение = Новый ОписаниеОповещения("ДобавитьЭПИзФайлаЗавершение", ЭтаФорма);
	
	ИнтеграцияС1СДокументооборотКлиент.НачатьДобавлениеЭПИзФайла(Оповещение, СвойстваФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьФайлВместеСЭП(Команда)
	
	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторФайла = ТекущиеДанные.ID;
	Расширение = ТекущиеДанные.Расширение;
	Имя = ТекущиеДанные.Наименование;
	Размер = ТекущиеДанные.Размер * 1024; // преобразуем из КБ в байты
	ДатаМодификацииУниверсальная = ТекущиеДанные.ДатаМодификацииУниверсальная;
	
	ИнтеграцияС1СДокументооборотКлиент.НачатьСохранениеВместеСЭП(
		ИдентификаторФайла,
		Расширение,
		Имя,
		Размер,
		ДатаМодификацииУниверсальная,
		УникальныйИдентификатор);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Связи

// Начинает добавление связанного документа.
&НаКлиенте
Процедура ДобавитьСвязь(Команда)
	
	Если ПустаяСтрока(ID) Тогда
		ЗаписатьОбъект();
		ИнтеграцияС1СДокументооборотКлиент.Оповестить_ЗаписьДокумента(ЭтаФорма);
	КонецЕсли;
	
	Отбор = Новый Структура;
	Если ЗначениеЗаполнено(КонтрагентID) Тогда
		Условие = Новый Структура;
		Условие.Вставить("Значение", Контрагент);
		Условие.Вставить("ЗначениеID", КонтрагентID);
		Отбор.Вставить("correspondent", Условие);
	КонецЕсли;
	Если ЗначениеЗаполнено(ОрганизацияID) Тогда
		Условие = Новый Структура;
		Условие.Вставить("Значение", Организация);
		Условие.Вставить("ЗначениеID", ОрганизацияID);
		Отбор.Вставить("organization", Условие);
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"НачатьДобавлениеСвязиЗавершение", ЭтаФорма);
	ИнтеграцияС1СДокументооборотКлиент.НачатьДобавлениеСвязи(
		ID, Тип, Представление,, ОписаниеОповещения, Отбор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСвязанныеДанные(Команда)
	
	Строка = Элементы.ДеревоСвязей.ТекущиеДанные;
	Если Строка <> Неопределено Тогда
		ОткрытьСвязанныеДанныеСтроки(Строка);
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// ЭП

&НаКлиенте
Процедура ПодписатьДокумент(Команда)
	
	Если ПустаяСтрока(ID) Тогда 
		Оповещение = Новый ОписаниеОповещения("ПодписатьДокументЗавершение", ЭтаФорма);
		ЗаписатьИВозможноЗакрыть(Ложь, Оповещение);
	Иначе
		УстановитьПодписи();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодпись(Команда)
	
	ЭлектроннаяПодписьКлиент.ОткрытьПодпись(Элементы.ТаблицаПодписей.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодпись(Команда)
	
	НастройкиЭП = ЭлектроннаяПодписьКлиентСервер.ОбщиеНастройки();
	Если НастройкиЭП.ПроверятьЭлектронныеПодписиНаСервере Тогда
		ПроверитьНаСервере();
	Иначе
		ИнтеграцияС1СДокументооборотКлиент.ПроверитьПодписи(Элементы.ТаблицаПодписей.ВыделенныеСтроки,
			ТаблицаПодписей, УникальныйИдентификатор, АдресСлепкаДокумента);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВсеПодписи(Команда)
	
	НастройкиЭП = ЭлектроннаяПодписьКлиентСервер.ОбщиеНастройки();
	Если НастройкиЭП.ПроверятьЭлектронныеПодписиНаСервере Тогда
		ПроверитьВсеНаСервере();
		
	Иначе
		Подписи = ИнтеграцияС1СДокументооборотКлиент.ДанныеПодписей(ТаблицаПодписей);
		ИнтеграцияС1СДокументооборотКлиент.ПроверитьПодписи(
			Подписи, ТаблицаПодписей, УникальныйИдентификатор, АдресСлепкаДокумента);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьПодписьНаДиск(Команда)
	
	ТекущиеДанные = Элементы.ТаблицаПодписей.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено
		И Не ПустаяСтрока(ТекущиеДанные.Объект) Тогда
		ЭлектроннаяПодписьКлиент.СохранитьПодпись(ТекущиеДанные.АдресПодписи);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПодпись(Команда)
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Удалить'"));
	Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Не удалять'"));
	
	Оповещение = Новый ОписаниеОповещения("УдалитьПодписьЗавершение", ЭтаФорма);
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Удалить выделенные подписи?'"), Кнопки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

///////////////////////////////////////////////////////////////////////////////////////////////////
// Закрытие и запись

// Вызывается перед закрытием после ответа пользователя на вопрос "Записать?"
//
&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Ответ, ПараметрыОповещения) Экспорт
	
	ЗаписатьОбъект();
	ИнтеграцияС1СДокументооборотКлиент.Оповестить_ЗаписьДокумента(ЭтаФорма);
	ЗакрытьСПараметром();
	
КонецПроцедуры

// Вызывается перед записью и, если нужно, задает вопрос о последствиях регистрации.
//
// Параметры:
//   ЗакрытьПослеЗаписи - Булево - Истина, если форму после записи следует закрыть.
//   ОповещениеПослеЗаписи - ОписаниеОповещения - необязательный, обработчик, вызываемый после записи.
//
&НаКлиенте
Процедура ЗаписатьИВозможноЗакрыть(ЗакрытьПослеЗаписи, ОповещениеПослеЗаписи = Неопределено)
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ЗакрытьПослеЗаписи", ЗакрытьПослеЗаписи);
	ПараметрыОповещения.Вставить("ОповещениеПослеЗаписи", ОповещениеПослеЗаписи);
	
	Оповещение = Новый ОписаниеОповещения(
		"ЗаписатьИВозможноЗакрытьЗавершение", ЭтаФорма, ПараметрыОповещения);
	
	Если ОграничиватьДоступностьПолей 
		И ЗначениеЗаполнено(РегистрационныйНомер) 
		И Не ЗначениеЗаполнено(НачальныйРегистрационныйНомер) Тогда 
		
		ТекстВопроса = НСтр(
			"ru = 'При записи документ будет зарегистрирован и станет недоступным для изменения.
			|Продолжить?'");
		ИнтеграцияС1СДокументооборотКлиент.ПоказатьВопросДаНет(
			Оповещение, ТекстВопроса,,, КодВозвратаДиалога.Да);
		
	Иначе // вопрос не нужен
		
		ВыполнитьОбработкуОповещения(Оповещение, КодВозвратаДиалога.Да);
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывается перед записью и закрытием после ответа пользователя на вопрос "Зарегистрировать?"
//
&НаКлиенте
Процедура ЗаписатьИВозможноЗакрытьЗавершение(Ответ, ПараметрыОповещения) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьОбъект();
	ИнтеграцияС1СДокументооборотКлиент.Оповестить_ЗаписьДокумента(ЭтаФорма);
	Если ПараметрыОповещения.ЗакрытьПослеЗаписи Тогда
		Закрыть();
	КонецЕсли;
	Если ПараметрыОповещения.ОповещениеПослеЗаписи <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ПараметрыОповещения.ОповещениеПослеЗаписи);
	КонецЕсли;
	
КонецПроцедуры

// Безусловно закрывает документ с передачей его описания.
//
&НаКлиенте
Процедура ЗакрытьСПараметром()
	
	Если ЗначениеЗаполнено(ID) Тогда
		Результат = Новый Структура;
		Результат.Вставить("id", ID);
		Результат.Вставить("type", Тип);
		Результат.Вставить("name", Представление);
	Иначе
		Результат = Неопределено;
	КонецЕсли;
	
	ЗакрытиеСПараметром = Истина;
	Закрыть(Результат);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Связь с объектом ИС

// Вызывается после создания объекта ИС и фиксирует созданную связь.
//
&НаКлиенте
Процедура СвязьСоздатьНажатиеЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат <> Неопределено Тогда
		ИнтеграцияС1СДокументооборотКлиент.СоздатьИнтегрированныйОбъектПоДаннымФормы(
			ЭтаФорма, Результат.Значение);
	КонецЕсли;
	
КонецПроцедуры

// Вызывается после выбора типа объекта ИС и начинает выбор объекта.
//
&НаКлиенте
Процедура СвязьВыбратьНажатиеЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат <> Неопределено  Тогда
		ТипОбъектаПотребителя = ИнтеграцияС1СДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(Результат.Значение,
			"ТипОбъектаПотребителя");
		Оповещение = Новый ОписаниеОповещения("СвязьВыбратьНажатиеЗавершениеВыбора", ЭтаФорма);
		ОткрытьФорму(ТипОбъектаПотребителя + ".ФормаВыбора",, Элементы.СвязьВыбрать,,,, Оповещение);
	КонецЕсли;
	
КонецПроцедуры

// Вызывается после выбора объекта ИС и фиксирует созданную связь.
//
&НаКлиенте
Процедура СвязьВыбратьНажатиеЗавершениеВыбора(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат <> Неопределено Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДобавитьСвязь(ID, Тип, Результат);
		ИнтеграцияС1СДокументооборотКлиент.Оповестить_ДобавлениеСвязи(ID, Тип, Результат);
	КонецЕсли; 
	
КонецПроцедуры

// Вызывается после ответа на вопрос "Очистить связь?" и очищает ее.
//
&НаКлиенте
Процедура СвязьОчиститьНажатиеЗавершение(Ответ, ПараметрыОповещения) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда 
		КэшВнешнийОбъект = ВнешнийОбъект;
		УдалитьСвязьНаСервере();
		ИнтеграцияС1СДокументооборотКлиент.Оповестить_УдалениеСвязи(ID, Тип, КэшВнешнийОбъект); 
	КонецЕсли;
	
КонецПроцедуры

// Удаляет связь между объектом ДО и объектом ИС.
//
&НаСервере
Процедура УдалитьСвязьНаСервере()

	ИнтеграцияС1СДокументооборотВызовСервера.УдалитьСвязь(ID, Тип, ВнешнийОбъект);
	ВнешнийОбъект = Неопределено;
	ОбновитьДекорацииСвязи();

КонецПроцедуры

// Обновляет форму согласно состоянию связи с объектом ИС.
//
&НаСервере
Процедура ОбновитьДекорацииСвязи()
	
	ДокументЗаполнен = ЗначениеЗаполнено(ВнешнийОбъект);
	
	Если ДокументЗаполнен Тогда
	
		МетаданныеОбъекта = ВнешнийОбъект.Метаданные();
		Если ОбщегоНазначения.ЭтоСправочник(МетаданныеОбъекта)
			Или ОбщегоНазначения.ЭтоПланВидовРасчета(МетаданныеОбъекта) Тогда
			ВнешнийОбъектПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 (%2)",
				Строка(ВнешнийОбъект), МетаданныеОбъекта.Представление());
		Иначе
			ВнешнийОбъектПредставление = Строка(ВнешнийОбъект);
		КонецЕсли;
	
	КонецЕсли;
	
	Элементы.СвязьОбъект.Видимость = ДокументЗаполнен;
	Элементы.СвязьОчистить.Видимость = ДокументЗаполнен;
	
	Элементы.СвязьВыбрать.Видимость = Не ДокументЗаполнен;
	
	Если Не ДокументЗаполнен Тогда
	
		ВозможноСозданиеОбъекта = (ПравилаЗаполнения.Количество() > 0);
		
		Если ПравилаЗаполнения.Количество() = 1 Тогда
			ТекстЗаголовкаСоздать = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'создать %1'"), НРег(ПравилаЗаполнения[0].Представление));
		ИначеЕсли ПравилаЗаполнения.Количество() = 0 Тогда
			Элементы.ГруппаСвязь.Видимость = Ложь;
			Возврат;
		Иначе
			ТекстЗаголовкаСоздать = НСтр("ru = 'создать...'");
		КонецЕсли;
		
		Элементы.СвязьСоздать.Заголовок = ТекстЗаголовкаСоздать;
		
		Элементы.СтраницаСвязи.Заголовок = НСтр("ru = 'Связи'");
	
	КонецЕсли;
	
	Элементы.СвязьСоздать.Видимость = Не ДокументЗаполнен И ВозможноСозданиеОбъекта;
	Элементы.СвязьИли.Видимость = Не ДокументЗаполнен И ВозможноСозданиеОбъекта;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Хронометраж и трудозатраты

// Завершает переключение хронометража после ответа на вопрос "Выключить по другим объектам?".
//
&НаКлиенте
Процедура ПереключитьХронометражЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Записи = ИнтеграцияС1СДокументооборотВызовСервера.
			ПереключитьХронометражПоОбъектамДокументооборота(ПараметрыОповещения.АктивныеЗаписи);
		
		Для Каждого Запись Из Записи Цикл
			ПараметрыОповещения = Новый Структура;
			ПараметрыОповещения.Вставить("name", Запись.Источник);
			ПараметрыОповещения.Вставить("id", Запись.ИсточникID);
			ПараметрыОповещения.Вставить("type", Запись.ИсточникТип);
			Оповестить("Запись_ДокументооборотТрудозатраты", ПараметрыОповещения, Запись.ИсточникID);
		КонецЦикла;
	
		ПереключитьХронометражСервер();
		Если ВключенХронометраж Тогда
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Включен хронометраж по ""%1""'"), Строка(Представление));
			Состояние(Текст);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Изменяет элементы формы согласно текущему состоянию хронометража.
//
&НаСервере
Процедура УстановитьСвойстваЭлементовХронометражаСервер()
	
	Обработки.ИнтеграцияС1СДокументооборот.УстановитьСвойстваЭлементовХронометража(
		ВключенХронометраж, Команды.ПереключитьХронометраж, Элементы.ФормаПереключитьХронометраж);
	
КонецПроцедуры

// Переключает хронометраж по текущему объекту.
//
&НаСервере
Процедура ПереключитьХронометражСервер()
	
	Обработки.ИнтеграцияС1СДокументооборот.ПереключитьХронометраж(ЭтаФорма);
	
КонецПроцедуры

// Завершает регистрацию документа после подтверждения пользователя.
//
&НаКлиенте
Процедура ЗарегистрироватьЗавершение(Ответ, ПараметрыОповещения) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗарегистрироватьНаСервере();
		ИнтеграцияС1СДокументооборотКлиент.Оповестить_ЗаписьДокумента(ЭтаФорма);
		ОбновитьОбзор();
	КонецЕсли;
	
КонецПроцедуры

// Регистрирует документ и обновляет измененные этим реквизиты.
//
&НаСервере
Процедура ЗарегистрироватьНаСервере()
	
	ЗаписатьОбъект();
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	ОбъектXDTO = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMInternalDocument");
	ОбъектXDTO.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ID, Тип);
	ОбъектXDTO.name = Представление;
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMDocumentRegistrationRequest");
	Запрос.document = ОбъектXDTO;
		
	Результат = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	ЗаполнитьРеквизитФормыИзXDTO("ДатаРегистрации", Результат.document, "regDate");
	ЗаполнитьРеквизитФормыИзXDTO("РегистрационныйНомер", Результат.document, "regNumber");
	ЗаполнитьРеквизитФормыИзXDTO("Состояние", Результат.document, "status");
	Обработки.ИнтеграцияС1СДокументооборот.УстановитьСостоянияДокумента(ЭтаФорма, Результат.document);
	Представление = Результат.document.name;
	
	// доступность по состоянию
	УстановитьДоступностьПоСостоянию(Результат.document);
	УстановитьДоступностьПоЭП(Результат.document);
	
	НачальныйРегистрационныйНомер = РегистрационныйНомер;
	НачальнаяДатаРегистрации = ДатаРегистрации;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Файлы

// Завершает удаление файла после подтверждения пользователя.
//
&НаКлиенте
Процедура ФайлыПередУдалениемЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПометитьНаУдаление(ПараметрыОповещения.ВыделенныеСтроки);
		ОбновитьСписокФайловКлиент();
		УстановитьДоступностьКомандСпискаЭП();
	КонецЕсли; 
	
КонецПроцедуры

// Завершает копирование файлов из общей папки после подтверждения пользователя.
//
&НаКлиенте 
Процедура ЗаполнитьФайлыКопированиемЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда 
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(ID) Тогда
		ЗаписатьОбъект();
		ИнтеграцияС1СДокументооборотКлиент.Оповестить_ЗаписьДокумента(ЭтаФорма);
	КонецЕсли;
	
	Состояние(НСтр("ru = 'Идет заполнение файлов копированием из присоединенных файлов...'"));
	
	ЗаполнитьФайлыКопированиемСервер();
	
	Состояние();
	
КонецПроцедуры

// Копирует файлы из общей папки и обновляет список.
//
&НаСервере
Процедура ЗаполнитьФайлыКопированиемСервер()
	
	ИнтеграцияС1СДокументооборотВызовСервера.ЗаполнитьФайлыКопированием(
		ВнешнийОбъект, ID, Тип, Представление, УникальныйИдентификатор);
	ПрочитатьИОбновитьСписокФайлов();
	Элементы.ЗаполнитьФайлыКопированием.Доступность = (Файлы.Количество() = 0);
	
КонецПроцедуры

// Получает список файлов из ДО и обновляет форму.
//
&НаСервере
Процедура ПрочитатьИОбновитьСписокФайлов()
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	ОбъектИд = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ID, Тип);
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMRetrieveRequest");
	Запрос.objectIds.Добавить(ОбъектИд);
	Запрос.columnSet.Добавить("files");
	
	Результат = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);

	ОбъектXDTO = Результат.objects[0];
	ИнтеграцияС1СДокументооборотВызовСервера.ОбновитьСписокФайлов(
		ОбъектXDTO.files, Файлы, Элементы.ГруппаФайлы);
	
КонецПроцедуры

// Общее завершение команд редактирования. Обновляет список файлов.
//
&НаКлиенте
Процедура КомандыРедактированияЗавершение(Результат, Параметры) Экспорт
	
	ОбновитьСписокФайловКлиент();
	
КонецПроцедуры

// Завершение добавления печатной формы файла. Обновляет список.
//
&НаКлиенте
Процедура ДобавлениеПечатнойФормыЗавершение(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Массив") Тогда
		ОбновитьСписокФайловКлиент(Результат[0].Идентификатор);
	КонецЕсли;
	
КонецПроцедуры

// Открывает карточку текущего файла.
//
&НаКлиенте
Процедура ОткрытьКарточку()
	
	Если ПустаяСтрока(ID) Тогда
		ЗаписатьОбъект();
		ИнтеграцияС1СДокументооборотКлиент.Оповестить_ЗаписьДокумента(ЭтаФорма);
	КонецЕсли;
	
	Если Элементы.Файлы.ТекущиеДанные <> Неопределено Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("РазрешеноРедактирование",
			СостояниеРазрешаетРедактированиеФайла);
		ИнтеграцияС1СДокументооборотКлиент.ОткрытьОбъект("DMFile", Элементы.Файлы.ТекущиеДанные.ID,
			ЭтаФорма, ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

// Помечает выделенные файлы на удаление.
//
&НаСервере
Процедура ПометитьНаУдаление(Знач ВыделенныеСтроки)
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMDeleteRequest");
	
	Для Каждого НомерСтроки Из ВыделенныеСтроки Цикл
		Данные = Файлы.НайтиПоИдентификатору(НомерСтроки);
		ОбъектXDTO = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, Данные.ID, "DMFile");
		Запрос.objectIds.Добавить(ОбъектXDTO);
	КонецЦикла;
	
	Ответ = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
	ПрочитатьИОбновитьСписокПодписей();
	
КонецПроцедуры

// Получает подписи из ДО и обновляет список в форме.
//
&НаСервере
Процедура ЗаполнитьСписокПодписей(ПодписиXDTO, ФайлыXDTO)

	ИнтеграцияС1СДокументооборотВызовСервера.ЗаполнитьСписокПодписейСервер(
		ПодписиXDTO, ФайлыXDTO, ЭтаФорма);

КонецПроцедуры

// Получает список файлов из ДО и обновляет его, сохраняя выделенную строку.
//
&НаКлиенте
Процедура ОбновитьСписокФайловКлиент(ИдентификаторФайла = Неопределено)
	
	Если ИдентификаторФайла <> Неопределено Тогда
		ТекущийИдентификаторФайла = ИдентификаторФайла;
	ИначеЕсли Элементы.Файлы.ТекущиеДанные <> Неопределено Тогда
		ТекущийИдентификаторФайла = Элементы.Файлы.ТекущиеДанные.ID;
	КонецЕсли;
	
	ПрочитатьИОбновитьСписокФайлов();
	
	// Восстановим положение в списке.
	Для Каждого Строка Из Файлы Цикл
		Если Строка.ID = ТекущийИдентификаторФайла Тогда
			Элементы.Файлы.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ЗаполнитьФайлыКопированием.Доступность = (Файлы.Количество() = 0);
	
КонецПроцедуры

// Завершение добавления файла. Обновляет список.
//
&НаКлиенте
Процедура СписокФайловПослеДобавленияФайла(ОписанияФайлов, Параметры) Экспорт
	
	ОбновитьСписокФайловКлиент(ОписанияФайлов[0].Идентификатор);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// ЭП

// Завершает подписание документа после ответа на вопрос "Подписать?".
//
&НаКлиенте
Процедура ПодписатьДокументЗавершение(Ответ, ПараметрыОповещения) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.ОК Тогда 
		ЗаписатьОбъект();
		УстановитьПодписи();
	КонецЕсли;
	
КонецПроцедуры

// Завершает удаление подписи после подтверждения пользователя.
//
&НаКлиенте
Процедура УдалитьПодписьЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьПодписиИОбновитьСписок();
	УстановитьДоступностьКомандСпискаЭП();
	
КонецПроцедуры

// Подписывает документ.
//
&НаКлиенте
Процедура УстановитьПодписи()
	
	МассивДанныхДляЗанесенияВБазу = Новый Массив;
	МассивАдресов = Новый Массив;
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("МассивДанныхДляЗанесенияВБазу", МассивДанныхДляЗанесенияВБазу);
	ПараметрыОповещения.Вставить("МассивАдресов", МассивАдресов);
	
	Оповещение = Новый ОписаниеОповещения("УстановитьПодписиЗавершение", ЭтаФорма, ПараметрыОповещения);
	ИнтеграцияС1СДокументооборотКлиент.НачатьФормированиеПодписиОбъекта(ЭтаФорма, Оповещение);
	
КонецПроцедуры

// Вызывается после подписания документа и обновляет форму.
//
&НаКлиенте
Процедура УстановитьПодписиЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	МассивДанныхДляЗанесенияВБазу = ПараметрыОповещения.МассивДанныхДляЗанесенияВБазу;
	
	ИсходныеПодписи = ИнтеграцияС1СДокументооборотКлиент.ДанныеПодписей(ТаблицаПодписей);
	ПодписатьЭПСервер(МассивДанныхДляЗанесенияВБазу);
	Подписи = ИнтеграцияС1СДокументооборотКлиент.ДанныеПодписей(ТаблицаПодписей);
	
	Если ИсходныеПодписи.Количество() <> Подписи.Количество() Тогда
		ИнтеграцияС1СДокументооборотКлиент.ИнформироватьОПодписании(Представление);
		УстановитьДоступностьКомандСпискаЭП();
	КонецЕсли;
	
КонецПроцедуры

// Вызывается после добавления ЭП из файла и обновляет форму.
//
&НаКлиенте
Процедура ДобавитьЭПИзФайлаЗавершение(Успешно, ПараметрыОповещения) Экспорт
	
	Если Успешно Тогда
		ПрочитатьИОбновитьСписокПодписей();
		УстановитьДоступностьКомандСпискаЭП();
	КонецЕсли;
	
КонецПроцедуры

// В зависимости от наличия подписи меняет доступность команд ЭП.
//
&НаКлиенте
Процедура УстановитьДоступностьКомандСпискаЭП()
	
	ЭтоПодпись = Истина;
	ЕстьПодписи = (ТаблицаПодписей.ПолучитьЭлементы().Количество() <> 0);
	
	ТекущиеДанные = Элементы.ТаблицаПодписей.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ЭтоПодпись = Не ПустаяСтрока(ТекущиеДанные.Объект);
	КонецЕсли;
		
	Элементы.ТаблицаПодписейПроверить.Доступность = ЕстьПодписи И ЭтоПодпись;
	Элементы.ТаблицаПодписейПроверитьВсе.Доступность = ЕстьПодписи;
	Элементы.ТаблицаПодписейОткрытьПодпись.Доступность = ЕстьПодписи И ЭтоПодпись;
	Элементы.ТаблицаПодписейУдалить.Доступность = ЕстьПодписи И ЭтоПодпись;
	Элементы.ТаблицаПодписейСохранить.Доступность = ЕстьПодписи И ЭтоПодпись;
	
	Элементы.ТаблицаПодписейПроверить.Доступность = ЕстьПодписи;
	Элементы.ТаблицаПодписейПроверитьВсе.Доступность = ЕстьПодписи;
	Элементы.ТаблицаПодписейОткрытьПодпись.Доступность = ЕстьПодписи;
	Элементы.ТаблицаПодписейУдалить.Доступность = ЕстьПодписи;
	Элементы.ТаблицаПодписейСохранить.Доступность = ЕстьПодписи;
	
КонецПроцедуры

// Получает сведения об ЭП из ДО.
//
&НаСервере
Функция ДанныеПодписейФайла(ИдентификаторФайла)
	
	Возврат ИнтеграцияС1СДокументооборотВызовСервера.ДанныеПодписейФайла(
		ИдентификаторФайла, ТаблицаПодписей);
	
КонецФункции

// Проверяет выбранные подписи, обновляя статусы.
//
&НаСервере
Процедура ПроверитьНаСервере()
	
	ИнтеграцияС1СДокументооборотВызовСервера.ПроверитьПодписиНаСервере(
		Элементы.ТаблицаПодписей.ВыделенныеСтроки, ТаблицаПодписей,
		УникальныйИдентификатор, АдресСлепкаДокумента);
		
КонецПроцедуры

// Проверяет все подписи, обновляя статусы.
//
&НаСервере
Процедура ПроверитьВсеНаСервере()
	
	ИнтеграцияС1СДокументооборотВызовСервера.ПроверитьВсеПодписиНаСервере(
		ТаблицаПодписей, УникальныйИдентификатор, АдресСлепкаДокумента);
		
КонецПроцедуры

// Удаляет выбранные подписи и обновляет список подписей.
//
&НаСервере
Процедура УдалитьПодписиИОбновитьСписок()
	
	ТаблицаУдаленныеСтроки = ИнтеграцияС1СДокументооборотВызовСервера.ВыделенныеПодписи(
		Элементы.ТаблицаПодписей.ВыделенныеСтроки, ТаблицаПодписей);
		
	Если ТаблицаУдаленныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	Соответствие = ИнтеграцияС1СДокументооборотВызовСервера.
		УдалитьПодписиДокумента(ТаблицаУдаленныеСтроки, ТаблицаПодписей);
	
	МассивXDTOОбъектов = Новый Массив;
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Для Каждого ПараКлючЗначение Из Соответствие Цикл
		
		ИдОбъекта = ПараКлючЗначение.Ключ;
		ДанныеВладельца = ПараКлючЗначение.Значение;
		ТипОбъекта = ДанныеВладельца.Тип;
		
		Если ТипОбъекта <> "DMFile" Тогда // документ
			ОбъектXDTO = СоздатьXDTOДокумент(Прокси, ДанныеВладельца.МассивПодписей);
			МассивXDTOОбъектов.Добавить(ОбъектXDTO);
			
		Иначе // файл
			Для Каждого Файл Из Файлы Цикл
				Если Файл.ID = ИдОбъекта Тогда
					ОбъектXDTO = ИнтеграцияС1СДокументооборот.СоздатьXDTOФайл(
						Прокси, ДанныеВладельца.МассивПодписей, Файл);
					МассивXDTOОбъектов.Добавить(ОбъектXDTO);
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
	
	КонецЦикла;
	
	ИнтеграцияС1СДокументооборот.ЗаписатьОбъекты(Прокси, МассивXDTOОбъектов);
		
	ПрочитатьИОбновитьСписокПодписей();
	
КонецПроцедуры

// Получает подписи из ДО и обновляет форму.
//
&НаСервере
Процедура ПрочитатьИОбновитьСписокПодписей()
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	ОбъектИд = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ID, Тип);
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMRetrieveRequest");
	Запрос.objectIds.Добавить(ОбъектИд);
	Запрос.columnSet.Добавить("signatures");
	Запрос.columnSet.Добавить("files"); // также и файлы обновляем - чтобы иконку Подписан расставить
	Запрос.columnSet.Добавить("enabledProperties");
	Запрос.columnSet.Добавить("keyPropertiesValue");
	
	Результат = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);

	ОбъектXDTO = Результат.objects[0];
	
	ЗаполнитьСписокПодписей(ОбъектXDTO.signatures, ОбъектXDTO.files);
	ИнтеграцияС1СДокументооборотВызовСервера.ОбновитьСписокФайлов(
		ОбъектXDTO.files, Файлы, Элементы.ФайлыНаименование);
	
	УстановитьДоступностьПоСостоянию(ОбъектXDTO);// доступность по состоянию
	УстановитьДоступностьПоЭП(ОбъектXDTO);
	
КонецПроцедуры

// Создает и заполняет объект XDTO, соовтетствующий документу, для фиксации подписей в ДО.
//
&НаСервере
Функция СоздатьXDTOДокумент(Прокси, МассивПодписей)
	
	ОбъектXDTO = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMInternalDocument");
	
	Обработки.ИнтеграцияС1СДокументооборот.СформироватьДополнительныеСвойства(Прокси, ОбъектXDTO, ЭтаФорма);
	
	СоответствиеРеквизитов = Справочники.ПравилаИнтеграцииС1СДокументооборотом.
		СоответствиеСвойствXDTOиРеквизитовФормыДокумента(Тип);
	Для Каждого СтрокаСоответствия Из СоответствиеРеквизитов Цикл
		ИнтеграцияС1СДокументооборот.ЗаполнитьСвойствоXDTOизСтруктурыРеквизитов(
			Прокси,
			ОбъектXDTO,
			СтрокаСоответствия.Ключ,
			ЭтаФорма,
			СтрокаСоответствия.Значение);
	КонецЦикла;
	
	ОбъектXDTO.objectID = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ID, Тип);
	ОбъектXDTO.name = Представление;
	
	Для Каждого ДанныеПодписи Из МассивПодписей Цикл
		ПодписьXDTO = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMSignature");
		ИнтеграцияС1СДокументооборот.ЗаполнитьXDTOПодпись(Прокси, ПодписьXDTO, ДанныеПодписи);
		ОбъектXDTO.signatures.Добавить(ПодписьXDTO);
	КонецЦикла;
	
	Возврат ОбъектXDTO;
	
КонецФункции

// Добавляет переданнные подписи к документу.
//
&НаСервере
Процедура ПодписатьЭПСервер(ДобавленныеПодписи)
	
	Соответствие = ИнтеграцияС1СДокументооборотВызовСервера.ПодписатьДокумент(
		ДобавленныеПодписи, ТаблицаПодписей);
	
	МассивXDTOОбъектов = Новый Массив;
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Для Каждого ПараКлючЗначение Из Соответствие Цикл
		
		ИдОбъекта = ПараКлючЗначение.Ключ;
		ДанныеВладельца = ПараКлючЗначение.Значение;
		ТипОбъекта = ДанныеВладельца.Тип;
		
		Если ТипОбъекта <> "DMFile" Тогда // документ
			
			ОбъектXDTO = СоздатьXDTOДокумент(Прокси, ДанныеВладельца.МассивПодписей);
			МассивXDTOОбъектов.Добавить(ОбъектXDTO);
			
		Иначе // файл
			
			Для Каждого Файл Из Файлы Цикл
				
				Если Файл.ID = ИдОбъекта Тогда
					ОбъектXDTO = ИнтеграцияС1СДокументооборот.СоздатьXDTOФайл(
						Прокси, ДанныеВладельца.МассивПодписей, Файл);
					МассивXDTOОбъектов.Добавить(ОбъектXDTO);
					Прервать;
				КонецЕсли;
			
			КонецЦикла;
			
		КонецЕсли;
	
	КонецЦикла;
	
	Если МассивXDTOОбъектов.Количество() > 0 Тогда
		ИнтеграцияС1СДокументооборот.ЗаписатьОбъекты(Прокси, МассивXDTOОбъектов);
		ПрочитатьИОбновитьСписокПодписей();
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Связи

// Открывает текущий связанный документ или его файл.
//
&НаКлиенте
Процедура ОткрытьСвязанныеДанныеСтроки(Строка)
	
	Если Строка.Тип = "DMRelationType" Тогда // объект ИС
		Возврат;
		
	ИначеЕсли Строка.Тип = "DMFile" Тогда // файл
		ИнтеграцияС1СДокументооборотКлиент.ОткрытьФайл(Строка.ID, Строка.Заголовок,
			Строка.Расширение, УникальныйИдентификатор);
			
	ИначеЕсли ЗначениеЗаполнено(Строка.Ссылка) Тогда
		ПараметрыФормы = Новый Структура("Ключ", Строка.Ссылка);
		ПоказатьЗначение(, Строка.Ссылка);
		
	Иначе
		ИнтеграцияС1СДокументооборотКлиент.ОткрытьОбъект(Строка.Тип, Строка.ID, ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывается после добавления связи и обновляет дерево связей.
//
&НаКлиенте
Процедура НачатьДобавлениеСвязиЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьДеревоСвязей(, Результат.СвязанныйДокумент.ID);
		
	Если ЗначениеЗаполнено(АдресДляФайловСвязанныхДокументов) Тогда
		ПодключитьОбработчикОжидания("ОбновлениеФайловСвязанныхДокументов", 1, Истина);
	КонецЕсли;
		
КонецПроцедуры

// Вызывается после удаления связи и обновляет дерево связей.
//
&НаКлиенте
Процедура НачатьУдалениеСвязиЗавершение(Результат, Параметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат.ИсходныйДокумент.ID) Тогда
	
		ОбновитьДеревоСвязей();
		
		Если ЗначениеЗаполнено(АдресДляФайловСвязанныхДокументов) Тогда
			ПодключитьОбработчикОжидания("ОбновлениеФайловСвязанныхДокументов", 1, Истина);
		КонецЕсли;
		
	Иначе
		
		СтрокаТипаСвязи = Элементы.ДеревоСвязей.ТекущиеДанные.ПолучитьРодителя();
		СтрокиТипаСвязи = СтрокаТипаСвязи.ПолучитьЭлементы();
		СтрокаДокумента = ДеревоСвязей.НайтиПоИдентификатору(Элементы.ДеревоСвязей.ТекущаяСтрока);
		СтрокиТипаСвязи.Удалить(СтрокаДокумента);
		Если СтрокиТипаСвязи.Количество() = 0 Тогда
			ДеревоСвязей.ПолучитьЭлементы().Удалить(СтрокаТипаСвязи);
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

// Обновляет данные связей документа, получая их заново, если необходимо.
//
// Параметры:
//   ОбъектXDTO - ОбъектXDTO - полученный из ДО документ, или
//              - Неопределено - признак необходимости получить его.
//   ДобавленныйДокумент - Строка - идентификатор документа, чьи файлы нужно обновить, или
//                       - Неопределено - признак необходимости обновить все файлы.
//
&НаСервере
Процедура ОбновитьДеревоСвязей(Знач ОбъектXDTO = Неопределено, ДобавленныйДокумент = Неопределено)
	
	Если Не ЗначениеЗаполнено(ID) Тогда
		ДеревоСвязей.ПолучитьЭлементы().Очистить();
		Возврат;
	КонецЕсли;
	
	Если ОбъектXDTO = Неопределено Тогда
		Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
		Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(
			Прокси, "DMRetrieveRequest");
		ОбъектИд = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ID, Тип);
		Запрос.objectIds.Добавить(ОбъектИд);
		Запрос.columnSet.Добавить("relations");
		Результат = Прокси.execute(Запрос);
		ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
		ОбъектXDTO = Результат.objects[0];
	КонецЕсли;
	
	АдресДляФайловСвязанныхДокументов = Обработки.ИнтеграцияС1СДокументооборот.
		ЗаполнитьДеревоСвязейИНачатьПолучениеФайлов(
			ЭтаФорма,
			ОбъектXDTO,
			Элементы.СтраницаСвязи,
			ДобавленныйДокумент);
	
КонецПроцедуры

// Обновляет файлы связанных документов данными временного хранилища, заполняемыми
// в асинхронном задании, откладывая обновление, если данные еще не готовы.
//
&НаКлиенте
Процедура ОбновлениеФайловСвязанныхДокументов() Экспорт
	
	Если Не ЗначениеЗаполнено(АдресДляФайловСвязанныхДокументов) Тогда
		Возврат;
	КонецЕсли;
	
	// Сохраним текущее положение по ID объекта с учетом типа связи.
	ТекущиеДанные = Элементы.ДеревоСвязей.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ТекущийID = Неопределено;
		ТекущийТип = Неопределено;
		ТекущийТипСвязи = Неопределено;
	Иначе
		ТекущийID = ТекущиеДанные.ID;
		ТекущийТип = ТекущиеДанные.Тип;
		Если ТекущийТип = "DMRelationType" Тогда
			СтрокаТипаСвязи = ТекущиеДанные;
		ИначеЕсли ТекущийТип = "DMFile" Тогда
			СтрокаТипаСвязи = ТекущиеДанные.ПолучитьРодителя().ПолучитьРодителя();
		Иначе
			СтрокаТипаСвязи = ТекущиеДанные.ПолучитьРодителя();
		КонецЕсли;
		ТекущийТипСвязи = СтрокаТипаСвязи.ID;
	КонецЕсли;
	
	Если ОбновлениеФайловСвязанныхДокументовСервер() Тогда // данные еще не готовы
		
		ПодключитьОбработчикОжидания("ОбновлениеФайловСвязанныхДокументов", 1, Истина);
		
	Иначе // данные получены, развернем дерево и восстановим позицию в нем
		
		СтрокиТипСвязи = ДеревоСвязей.ПолучитьЭлементы();
		Для каждого СтрокаТипСвязи из СтрокиТипСвязи Цикл
			
			Идентификатор = СтрокаТипСвязи.ПолучитьИдентификатор();
			Элементы.ДеревоСвязей.Развернуть(Идентификатор, Истина);
			
			Если ТекущийТип = Неопределено Тогда
				Продолжить;
				
			ИначеЕсли ТекущийТип = "DMRelationType" Тогда
				Если СтрокаТипСвязи.ID = ТекущийID Тогда
					Элементы.ДеревоСвязей.ТекущаяСтрока = Идентификатор;
				КонецЕсли;
				
			ИначеЕсли СтрокаТипСвязи.ID = ТекущийТипСвязи Тогда
				
				СтрокиДокумент = СтрокаТипСвязи.ПолучитьЭлементы();
				Для каждого СтрокаДокумент из СтрокиДокумент Цикл
					
					Если ТекущийТип = "DMFile" Тогда
						СтрокиФайл = СтрокиДокумент.ПолучитьЭлементы();
						Для каждого СтрокаФайл из СтрокиФайл Цикл
							Если СтрокаФайл.ID = ТекущийID Тогда
								Элементы.ДеревоСвязей.ТекущаяСтрока = 
									СтрокаФайл.ПолучитьИдентификатор();
								Прервать;
							КонецЕсли;
						КонецЦикла;
						
					ИначеЕсли СтрокаДокумент.ID = ТекущийID Тогда
						Элементы.ДеревоСвязей.ТекущаяСтрока = 
							СтрокаДокумент.ПолучитьИдентификатор();
						Прервать;
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Обновляет файлы связанных документов данными временного хранилища, если они готовы.
//
// Возвращаемое значение:
//   Булево - Истина, если требуется повторное обновление, и Ложь, если не требуется.
//
&НаСервере
Функция ОбновлениеФайловСвязанныхДокументовСервер()
	
	Результат = ПолучитьИзВременногоХранилища(АдресДляФайловСвязанныхДокументов);
	Если Результат = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	Дерево = РеквизитФормыВЗначение("ДеревоСвязей");
	
	Для Каждого СвязанныйДокумент из Результат Цикл
		
		СтруктураПоиска = Новый Структура("ID, Тип",
			СвязанныйДокумент.ID,
			СвязанныйДокумент.Тип);
		СтрокиДокументов = Дерево.Строки.НайтиСтроки(СтруктураПоиска, Истина);
		Для каждого СтрокаДокумента из СтрокиДокументов Цикл
			СтрокаДокумента.Строки.Очистить();
			Для Каждого Файл из СвязанныйДокумент.Файлы Цикл
				СтрокаФайла = СтрокаДокумента.Строки.Добавить();
				СтрокаФайла.ID = Файл.ID;
				СтрокаФайла.Тип = Файл.Тип;
				СтрокаФайла.Заголовок = Файл.Наименование;
				СтрокаФайла.Расширение = Файл.Расширение;
				СтрокаФайла.Картинка = РаботаСФайламиСлужебныйКлиентСервер.
					ПолучитьИндексПиктограммыФайла(СтрокаФайла.Расширение);
			КонецЦикла;
		КонецЦикла;
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(Дерево, "ДеревоСвязей");
	
	Возврат Ложь;
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////////////////////////
// Прочее

// Обновляет HTML-представление документа.
//
&НаКлиенте
Процедура ОбновитьОбзор() Экспорт
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОбзор Тогда
		ПредставлениеHTML = ИнтеграцияС1СДокументооборотКлиентСервер.
			ПолучитьОбзорДокумента(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет форму по данным объекта XDTO, обновляя обзор и дерево связей, но не файлы.
//
&НаСервере
Процедура ПрочитатьОбъектВФорму(ОбъектXDTO)
	
	// Заполнение реквизитов.
	СоответствиеРеквизитов = Справочники.ПравилаИнтеграцииС1СДокументооборотом.
		СоответствиеСвойствXDTOиРеквизитовФормыДокумента(Тип);
	Для Каждого СтрокаСоответствия Из СоответствиеРеквизитов Цикл
		ЗаполнитьРеквизитФормыИзXDTO(СтрокаСоответствия.Значение, ОбъектXDTO, СтрокаСоответствия.Ключ);
	КонецЦикла;
	Представление = ОбъектXDTO.name;
	
	НачальныйРегистрационныйНомер = РегистрационныйНомер;
	НачальнаяДатаРегистрации = ДатаРегистрации;
	
	АвтоматическаяНумерация =
		(ОбъектXDTO.documentType <> Неопределено И ОбъектXDTO.documentType.automaticNumeration = Истина);
	Если АвтоматическаяНумерация Тогда
		Элементы.РегистрационныйНомер.ОтображениеПредупрежденияПриРедактировании =
			ОтображениеПредупрежденияПриРедактировании.Отображать;
		Элементы.ДатаРегистрации.ОтображениеПредупрежденияПриРедактировании =
			ОтображениеПредупрежденияПриРедактировании.Отображать;
	Иначе
		Элементы.РегистрационныйНомер.ОтображениеПредупрежденияПриРедактировании =
			ОтображениеПредупрежденияПриРедактировании.НеОтображать;
		Элементы.ДатаРегистрации.ОтображениеПредупрежденияПриРедактировании =
			ОтображениеПредупрежденияПриРедактировании.НеОтображать;
	КонецЕсли;
	
	// Доступность по состоянию.
	УстановитьДоступностьПоСостоянию(ОбъектXDTO);
	УстановитьДоступностьПоЭП(ОбъектXDTO);
	
	Элементы.Организация.Видимость = (ОбъектXDTO.organizationEnabled = Истина);
	Элементы.ВопросДеятельности.Видимость = (ОбъектXDTO.activityMatterEnabled = Истина);
	Элементы.ГрифДоступа.Видимость = (ОбъектXDTO.accessLevelEnabled = Истина);
	Элементы.Файлы.Видимость = (ОбъектXDTO.filesEnabled = Истина);
	Элементы.ВидДокумента.Видимость = (ОбъектXDTO.documentTypeEnabled = Истина);
	Элементы.Состояние.Видимость = (ОбъектXDTO.statusEnabled = Истина);
	Элементы.Состояние.ТолькоПросмотр = (ОбъектXDTO.statusChangeEnabled <> Истина);
	Элементы.СостояниеСписок.ТолькоПросмотр = (ОбъектXDTO.statusChangeEnabled <> Истина);
	ОграничиватьДоступностьПолей = (ОбъектXDTO.limitPropertiesAvailability = Истина);
	
	// Проекты и независимые состояния.
	Если ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3.CORP") Тогда
		Элементы.Проект.Видимость = (ОбъектXDTO.projectsEnabled = Истина);
		Элементы.СостояниеСписок.Видимость = (ОбъектXDTO.statusEnabled = Истина);
		Элементы.Состояние.Видимость = Ложь;
	Иначе
		Элементы.Проект.Видимость = Ложь;
		Элементы.СостояниеСписок.Видимость = Ложь;
		Элементы.Состояние.Видимость = (ОбъектXDTO.statusEnabled = Истина);
	КонецЕсли;
	
	// Видимость реквизитов по виду.
	Элементы.СрокИсполнения.Видимость = 
		(ОбъектXDTO.documentType <> Неопределено И ОбъектXDTO.documentType.performanceDateEnabled = Истина);
	Элементы.ГруппаКонтрагент.ОтображатьЗаголовок =
		(ОбъектXDTO.documentType <> Неопределено И ОбъектXDTO.documentType.correspondentEnabled = Истина);
	Элементы.Контрагент.Видимость =
		(ОбъектXDTO.documentType <> Неопределено И ОбъектXDTO.documentType.correspondentEnabled = Истина);
	Элементы.КонтактноеЛицо.Видимость =
		(ОбъектXDTO.documentType <> Неопределено И ОбъектXDTO.documentType.correspondentEnabled = Истина);
	Элементы.Сумма.Видимость =
		(ОбъектXDTO.documentType <> Неопределено И ОбъектXDTO.documentType.sumEnabled = Истина);
	Элементы.Валюта.Видимость =
		(ОбъектXDTO.documentType <> Неопределено И ОбъектXDTO.documentType.sumEnabled = Истина);
	Элементы.СрокДействияПредставление.Видимость =
		(ОбъектXDTO.documentType <> Неопределено И ОбъектXDTO.documentType.durationEnabled = Истина);
	
	СрокДействияПредставление = 
		ИнтеграцияС1СДокументооборотКлиентСервер.ПредставлениеСрокаДействияДокумента(
			ДатаНачалаДействия,
			Бессрочный,
			ДатаОкончанияДействия,
			ПорядокПродленияID,
			ПорядокПродления);
	
	Обработки.ИнтеграцияС1СДокументооборот.ПоместитьДополнительныеРеквизитыНаФорму(ЭтаФорма, ОбъектXDTO);
	Обработки.ИнтеграцияС1СДокументооборот.УстановитьНавигационнуюСсылку(ЭтаФорма, ОбъектXDTO);
	Обработки.ИнтеграцияС1СДокументооборот.ЗаполнитьПараметрыХронометража(ЭтаФорма, ОбъектXDTO);
	Обработки.ИнтеграцияС1СДокументооборот.УстановитьСостоянияДокумента(ЭтаФорма, ОбъектXDTO);
	Обработки.ИнтеграцияС1СДокументооборот.УстановитьЗаголовокДокумента(ЭтаФорма);
	
	Если ИспользоватьСвязанныеДокументы1СДокументооборота Тогда
		ОбновитьДеревоСвязей();
	КонецЕсли;
	
	ПредставлениеHTML = ИнтеграцияС1СДокументооборотКлиентСервер.ПолучитьОбзорДокумента(ЭтаФорма);
	
	Если ЗначениеЗаполнено(ID) Тогда
		ПравилаЗаполнения = ИнтеграцияС1СДокументооборотВызовСервера.
			ПравилаЗаполненияИнтегрированныхОбъектовСписком(ОбъектXDTO);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет указанный реквизит формы по свойству объекта XDTO.
//
&НаСервере
Процедура ЗаполнитьРеквизитФормыИзXDTO(ИмяРеквизитаФормы, ОбъектXDTO, ИмяСвойстваXDTO)
	
	Если ОбъектXDTO.Установлено(ИмяСвойстваXDTO) Тогда
		ЗначениеСвойстваXDTO = ОбъектXDTO[ИмяСвойстваXDTO];
		Если ТипЗнч(ЗначениеСвойстваXDTO) = Тип("ОбъектXDTO") Тогда 
			ЭтаФорма[ИмяРеквизитаФормы + "Тип"] = ЗначениеСвойстваXDTO.objectId.type;
			ЭтаФорма[ИмяРеквизитаФормы + "ID"] 	= ЗначениеСвойстваXDTO.objectId.id;
			ЭтаФорма[ИмяРеквизитаФормы] 		= ЗначениеСвойстваXDTO.name;
		Иначе	
			ЭтаФорма[ИмяРеквизитаФормы] = ЗначениеСвойстваXDTO;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Получает обновленное состояние из ДО и перезаполняет форму.
//
&НаСервере
Процедура ОбновитьСостояние()
	
	// Получим объект из ДО.
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMRetrieveRequest");
	
	ОбъектИд = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ID, Тип);
	Запрос.objectIds.Добавить(ОбъектИд);
	
	Запрос.columnSet.Добавить("status");
	Запрос.columnSet.Добавить("statusRegistration");
	Запрос.columnSet.Добавить("statusConsideration");
	Запрос.columnSet.Добавить("statusApproval");
	Запрос.columnSet.Добавить("statusConfirmation");
	Запрос.columnSet.Добавить("statusPerformance");
	Запрос.columnSet.Добавить("statusEnabled");
	Запрос.columnSet.Добавить("enabledProperties");
	
	Результат = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	ОбъектXDTO = Результат.objects[0];
	
	// Проекты и независимые состояния.
	Если ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3.CORP") Тогда
		Элементы.СостояниеСписок.Видимость = (ОбъектXDTO.statusEnabled = Истина);
		Элементы.Состояние.Видимость = Ложь;
	Иначе
		Элементы.Проект.Видимость = Ложь;
		Элементы.СостояниеСписок.Видимость = Ложь;
		Элементы.Состояние.Видимость = (ОбъектXDTO.statusEnabled = Истина);
	КонецЕсли;
	
	// Установим доступность по состоянию.
	Элементы.Состояние.ТолькоПросмотр = (ОбъектXDTO.statusChangeEnabled <> Истина);
	Элементы.СостояниеСписок.ТолькоПросмотр = (ОбъектXDTO.statusChangeEnabled <> Истина);
	УстановитьДоступностьПоСостоянию(ОбъектXDTO);
	
	// Заполним реквизиты формы, соответствующие состоянию.
	Обработки.ИнтеграцияС1СДокументооборот.УстановитьСостоянияДокумента(ЭтаФорма, ОбъектXDTO);
	
КонецПроцедуры

// Заполняет документ в ДО и обновляет форму.
//
&НаСервере
Процедура ЗаписатьОбъект()
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	ОбъектXDTO = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMInternalDocument");
	
	Обработки.ИнтеграцияС1СДокументооборот.СформироватьДополнительныеСвойства(Прокси, ОбъектXDTO, ЭтаФорма);
	
	// Заполним простые свойства по реквизитам формы.
	СоответствиеРеквизитов = Справочники.ПравилаИнтеграцииС1СДокументооборотом.
		СоответствиеСвойствXDTOиРеквизитовФормыДокумента(Тип);
	Для Каждого СтрокаСоответствия Из СоответствиеРеквизитов Цикл
		ИнтеграцияС1СДокументооборот.ЗаполнитьСвойствоXDTOизСтруктурыРеквизитов(
			Прокси,
			ОбъектXDTO,
			СтрокаСоответствия.Ключ,
			ЭтаФорма,
			СтрокаСоответствия.Значение);
	КонецЦикла;
	
	// Заполним независимые состояния.
	Если ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3.CORP") Тогда
		
		ВозможныеСостояния = Справочники.ПравилаИнтеграцииС1СДокументооборотом.
			СоответствиеСвойствXDTOиСостоянийДокумента();
		Для Каждого СтрокаСоответствия Из ВозможныеСостояния Цикл
			ИнтеграцияС1СДокументооборот.ЗаполнитьСвойствоXDTOизСтруктурыРеквизитов(
				Прокси,
				ОбъектXDTO,
				СтрокаСоответствия.Ключ,
				ЭтаФорма,
				СтрокаСоответствия.Значение);
		КонецЦикла;
		
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ID) И ЗначениеЗаполнено(Тип) Тогда // обновление
		
		ОбъектXDTO.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ID, Тип);
		ОбъектXDTO.name = Представление;
		
		МассивПодписей = ДанныеПодписейФайла(ID);
		Для Каждого ДанныеПодписи Из МассивПодписей Цикл
			
			ПодписьXDTO = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMSignature");
			ИнтеграцияС1СДокументооборот.ЗаполнитьXDTOПодпись(Прокси, ПодписьXDTO, ДанныеПодписи);
			ОбъектXDTO.signatures.Добавить(ПодписьXDTO);
			
		КонецЦикла;
		
		Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMUpdateRequest");
		Запрос.objects.Добавить(ОбъектXDTO);
		
		Результат = Прокси.execute(Запрос);
		ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
		
		ОбъектXDTO = Результат.objects[0];
		
		Если ИспользоватьСвязанныеДокументы1СДокументооборота
			И ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("2.1.0.1") Тогда
			
			Обработки.ИнтеграцияС1СДокументооборот.ЗаписатьСвязиНовогоДокумента(
				ДеревоСвязей,
				ID,
				Тип,
				Представление);
				
		КонецЕсли;
		
	Иначе // создание
		
		ОбъектXDTO.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, "", "");
		ОбъектXDTO.name = Представление;
		
		Если ЗначениеЗаполнено(ВнешнийОбъект) Тогда 
			
			ExternalObject = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "ExternalObject");
			ExternalObject.id = Строка(ВнешнийОбъект.УникальныйИдентификатор());
			ExternalObject.type = ВнешнийОбъект.Метаданные().ПолноеИмя();
			ExternalObject.name = Строка(ВнешнийОбъект);
			
			ОбъектXDTO.externalObject = ExternalObject;
			
		КонецЕсли;
		
		Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMCreateRequest");
		Запрос.object = ОбъектXDTO;
		
		Результат = Прокси.execute(Запрос);
		ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
		
		ОбъектXDTO = Результат.object;
		ID  = ОбъектXDTO.objectId.id;
		Тип = ОбъектXDTO.objectId.type;
		
		Обработки.ИнтеграцияС1СДокументооборот.ЗаписатьФайлыНовогоДокумента(
			Файлы,
			ID,
			Тип,
			Представление);
		
		Если ИспользоватьСвязанныеДокументы1СДокументооборота
			И ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("2.1.0.1") Тогда
			
			Обработки.ИнтеграцияС1СДокументооборот.ЗаписатьСвязиНовогоДокумента(
				ДеревоСвязей,
				ID,
				Тип,
				Представление);
				
		КонецЕсли;
		
		// Возможно, созданному объекту нужно присвоить штрихкод.
		ШтрихкодВнешнегоОбъекта = ИнтеграцияС1СДокументооборотПереопределяемый.
			ШтрихкодДокумента(ВнешнийОбъект);
		Если ЗначениеЗаполнено(ШтрихкодВнешнегоОбъекта) 
			и ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.4.3.1.CORP") Тогда
			
			Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMRetrieveBarcodesRequest");
			
			Запрос.objectId = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectID");
			Запрос.objectId.id = ID;
			Запрос.objectId.type = Тип;
			
			Результат = Прокси.execute(Запрос);
			ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
			
			Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMUpdateBarcodesRequest");
			
			Запрос.objectBarcodes = Результат.objectBarcodes;
			
			Штрихкод = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMBarcode");
			Штрихкод.internal = Ложь;
			Штрихкод.barcodeData = ШтрихкодВнешнегоОбъекта;
			Запрос.objectBarcodes.barcodes.Добавить(Штрихкод);
				
			Результат = Прокси.execute(Запрос);
			ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПрочитатьОбъектВФорму(ОбъектXDTO);
	
	Если ОбъектXDTO.Установлено("keyPropertiesValue") Тогда
		АдресСлепкаДокумента = ПоместитьВоВременноеХранилище(
			ОбъектXDTO.keyPropertiesValue, УникальныйИдентификатор);
	КонецЕсли;
	
	Модифицированность = Ложь;
	
КонецПроцедуры

// Обработка формы при изменении вида документа.
//
&НаСервере
Процедура ПриИзмененииВидаДокумента()
	
	Обработки.ИнтеграцияС1СДокументооборот.ПриИзмененииВидаНаФормеДокумента(ЭтаФорма);
	ОбновитьДекорацииСвязи();
	
КонецПроцедуры

// Устанавливает доступность полей и кнопок формы в зависимости от состояния документа.
//
&НаСервере
Процедура УстановитьДоступностьПоСостоянию(ОбъектXDTO)
	
	ДоступныеПоля = Новый Массив;
	Для Каждого ИмяПоля Из ОбъектXDTO.enabledProperties Цикл
		ДоступныеПоля.Добавить(НРег(ИмяПоля));
	КонецЦикла;
	
	СоответствиеРеквизитов = Справочники.ПравилаИнтеграцииС1СДокументооборотом.
		СоответствиеСвойствXDTOиРеквизитовФормыДокумента(Тип);
	Для Каждого СтрокаСоответствия Из СоответствиеРеквизитов Цикл
		
		ИмяРеквизита = СтрокаСоответствия.Значение;
		
		Если ИмяРеквизита = "ДатаНачалаДействия" 
			Или ИмяРеквизита = "ДатаОкончанияДействия"
			Или ИмяРеквизита = "ПорядокПродления"
			Или ИмяРеквизита = "Бессрочный" Тогда
			ИмяРеквизита = "СрокДействияПредставление";
		КонецЕсли;
		
		// Пропустим реквизиты, не выведенные на форму.
		Если Элементы.Найти(ИмяРеквизита) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ДоступныеПоля.Количество() = 0 
			Или ДоступныеПоля.Найти(НРег(СтрокаСоответствия.Ключ)) <> Неопределено Тогда 
			Элементы[ИмяРеквизита].ТолькоПросмотр = Ложь;
		Иначе
			Элементы[ИмяРеквизита].ТолькоПросмотр = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Если (ДоступныеПоля.Количество() = 0 Или ДоступныеПоля.Найти("register") <> Неопределено)
		И АвтоматическаяНумерация Тогда 
		Элементы.Зарегистрировать.Доступность = Истина;
	Иначе
		Элементы.Зарегистрировать.Доступность = Ложь;
	КонецЕсли;
	
	Если ДоступныеПоля.Количество() = 0 Или ДоступныеПоля.Найти("additionalproperties") <> Неопределено Тогда 
		Элементы.Свойства.Доступность = Истина;
	Иначе
		Элементы.Свойства.Доступность = Ложь;
	КонецЕсли;
	
	СостояниеРазрешаетРедактированиеФайла = (ДоступныеПоля.Количество() = 0 
		Или ДоступныеПоля.Найти("editfile") <> Неопределено);
	СостояниеРазрешаетДобавлениеФайла = (ДоступныеПоля.Количество() = 0 
		Или ДоступныеПоля.Найти("addfile") <> Неопределено);
		
	Элементы.Файлы.ИзменятьСоставСтрок = СостояниеРазрешаетДобавлениеФайла;
	Элементы.ДобавитьПечатнуюФорму.Доступность = ЗначениеЗаполнено(ВнешнийОбъект)
		И СостояниеРазрешаетДобавлениеФайла;
		
	Элементы.СоздатьФайл.Доступность = СостояниеРазрешаетДобавлениеФайла;
	Элементы.ФайлыКонтекстСоздатьФайл.Доступность = СостояниеРазрешаетДобавлениеФайла;
	
КонецПроцедуры

// Устанавливает доступность файловых команд согласно состоянию текущего файла.
//
&НаКлиенте
Процедура УстановитьДоступностьКомандСпискаФайлов()
	
	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		
		Элементы.ОткрытьНаЧтение.Доступность = Ложь;
		Элементы.ФайлыКонтекстОткрытьНаЧтение.Доступность = Ложь;
		
		Элементы.ОткрытьДляРедактирования.Доступность = Ложь;
		Элементы.ФайлыКонтекстОткрытьДляРедактирования.Доступность = Ложь;
		
		Элементы.ЗакончитьРедактирование.Доступность = Ложь;
		Элементы.ФайлыКонтекстЗакончитьРедактирование.Доступность = Ложь;
		
		Элементы.СохранитьНаДиск.Доступность = Ложь;
		Элементы.ОбновитьИзФайлаНаДиске.Доступность = Ложь;
		Элементы.СохранитьИзменения.Доступность = Ложь;
		Элементы.ОтменитьРедактирование.Доступность = Ложь;
		
		Элементы.ОткрытьКарточку.Доступность = Ложь;
		Элементы.ФайлыКонтекстОткрытьКарточку.Доступность = Ложь;
		
		Элементы.УдалитьФайл.Доступность = Ложь;
		Элементы.ФайлыКонтекстУдалитьФайл.Доступность = Ложь;
		
		Элементы.ЗаполнитьФайлДаннымиДокумента.Доступность = Ложь;
		
		Элементы.Подписать.Доступность = Ложь;
		Элементы.ДобавитьЭПИзФайла.Доступность = Ложь;
		Элементы.СохранитьВместеСЭП.Доступность = Ложь;
		
	Иначе
		
		Редактируется = ТекущиеДанные.Редактируется;
		РедактируетсяТекущимПользователем = ТекущиеДанные.РедактируетсяТекущимПользователем;
		РедактируетсяДругимПользователем = Редактируется И Не РедактируетсяТекущимПользователем;
		
		Элементы.ОткрытьНаЧтение.Доступность = Истина;
		Элементы.ФайлыКонтекстОткрытьНаЧтение.Доступность = Истина;
		
		Элементы.ОткрытьДляРедактирования.Доступность = СостояниеРазрешаетРедактированиеФайла
			И ДоступенЗахватФайлов И Не РедактируетсяДругимПользователем;
		Элементы.ФайлыКонтекстОткрытьДляРедактирования.Доступность = СостояниеРазрешаетРедактированиеФайла
			И ДоступенЗахватФайлов И Не РедактируетсяДругимПользователем;
			
		Элементы.ЗакончитьРедактирование.Доступность = СостояниеРазрешаетРедактированиеФайла
			И ДоступенЗахватФайлов И РедактируетсяТекущимПользователем;
		Элементы.ФайлыКонтекстЗакончитьРедактирование.Доступность = СостояниеРазрешаетРедактированиеФайла
			И ДоступенЗахватФайлов И РедактируетсяТекущимПользователем;
			
		Элементы.СохранитьИзменения.Доступность = СостояниеРазрешаетРедактированиеФайла
			И ДоступенЗахватФайлов И РедактируетсяТекущимПользователем;
		Элементы.ОтменитьРедактирование.Доступность = СостояниеРазрешаетРедактированиеФайла
			И ДоступенЗахватФайлов И РедактируетсяТекущимПользователем;
		Элементы.СохранитьНаДиск.Доступность = Истина;
		Элементы.ОбновитьИзФайлаНаДиске.Доступность = СостояниеРазрешаетРедактированиеФайла
			И Не РедактируетсяДругимПользователем;
			
		Элементы.ОткрытьКарточку.Доступность = Истина;
		Элементы.ФайлыКонтекстОткрытьКарточку.Доступность = Истина;
		
		Элементы.УдалитьФайл.Доступность = СостояниеРазрешаетРедактированиеФайла
			И Не РедактируетсяДругимПользователем;
		Элементы.ФайлыКонтекстУдалитьФайл.Доступность = СостояниеРазрешаетРедактированиеФайла
			И Не РедактируетсяДругимПользователем;
			
		Элементы.ЗаполнитьФайлДаннымиДокумента.Доступность = Не Редактируется;
			
		ПодписанЭП = ТекущиеДанные.ПодписанЭП;
		Зашифрован = ТекущиеДанные.Зашифрован;
		
		Элементы.Подписать.Доступность = Не Редактируется И Не Зашифрован;
		Элементы.ДобавитьЭПИзФайла.Доступность = Не Редактируется И Не Зашифрован;
		Элементы.СохранитьВместеСЭП.Доступность = ПодписанЭП;
		
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает доступность реквизитов документав зависимости от наличия подписи.
//
&НаСервере
Процедура УстановитьДоступностьПоЭП(ОбъектXDTO)
	
	Если ОбъектXDTO.signatures.Количество() <> 0 Тогда
		КлючевыеПоля = Новый Массив;
		КлючевыеПоля.Добавить("Наименование");
		КлючевыеПоля.Добавить("Описание");
		Для Каждого ИмяПоля Из КлючевыеПоля Цикл
			Элементы[ИмяПоля].ТолькоПросмотр = Истина;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Продолжает создание бизнес-процесса после ответа на вопрос "Записать документ?".
//
&НаКлиенте
Процедура СоздатьБизнесПроцессЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	ИнтеграцияС1СДокументооборотКлиент.СоздатьБизнесПроцессПоОбъектуДО(ID, Тип, Наименование);
		
КонецПроцедуры

// Вызывает обработчик команды, которая добавлена программно при создании формы на сервере.
// (см. ИнтеграцияС1СДокументооборотПереопределяемый.ДополнительнаяОбработкаФормыДокумента).
//
&НаКлиенте
Процедура Подключаемый_ВыполнитьПрограммноДобавленнуюКоманду(Команда)
	
	ИнтеграцияС1СДокументооборотКлиентПереопределяемый.ВыполнитьПрограммноДобавленнуюКоманду(
		Команда, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти 