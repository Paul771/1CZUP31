#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("ОрганизацияСсылка") Тогда
		ВызватьИсключение НСтр("ru='Обработка не предназначена для непосредственного использования.'");
	КонецЕсли;
	
	Объект.Организация = Параметры.ОрганизацияСсылка;
	Заголовок = Строка(Параметры.Сотрудник) + " (" + НСтр("ru='Документ удостоверяющий личность'") + ")";
	
	ВидДокумента = Параметры.ВидДокумента;
	Серия = Параметры.Серия;
	Номер = Параметры.Номер;
	ДатаВыдачи = Параметры.ДатаВыдачи;
	КемВыдан = Параметры.КемВыдан;
	КодПодразделения = Параметры.КодПодразделения;
	СрокДействия = Параметры.СрокДействия;
	
	ЦветСтиляПоясняющийОшибкуТекст 	= ЦветаСтиля.ПоясняющийОшибкуТекст;
	ЦветСтиляЦветТекстаПоля 			= ЦветаСтиля.ЦветТекстаПоля;
	
	СотрудникиКлиентСервер.ОбработатьОтображениеСерияДокументаФизическогоЛица(ВидДокумента, Серия, Элементы.ДокументыФизическихЛицСерия, ЭтотОбъект);
	СотрудникиКлиентСервер.ОбработатьОтображениеНомерДокументаФизическогоЛица(ВидДокумента, Номер, Элементы.ДокументыФизическихЛицНомер, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДокументыФизическихЛицВидДокументаПриИзменении(Элемент)
	
	СотрудникиКлиентСервер.ОбработатьОтображениеСерияДокументаФизическогоЛица(ВидДокумента, Серия, Элементы.ДокументыФизическихЛицСерия, ЭтотОбъект);
	СотрудникиКлиентСервер.ОбработатьОтображениеНомерДокументаФизическогоЛица(ВидДокумента, Номер, Элементы.ДокументыФизическихЛицНомер, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыФизическихЛицВидДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(ВидДокумента) Тогда
		ВидДокумента = ПредопределенноеЗначение("Справочник.ВидыДокументовФизическихЛиц.ПаспортРФ");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыФизическихЛицСерияПриИзменении(Элемент)
	СотрудникиКлиентСервер.ОбработатьОтображениеСерияДокументаФизическогоЛица(ВидДокумента, Серия, Элемент, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ДокументыФизическихЛицНомерПриИзменении(Элемент)
	СотрудникиКлиентСервер.ОбработатьОтображениеНомерДокументаФизическогоЛица(ВидДокумента, Номер, Элемент, ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)
	
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("ВидДокумента", ВидДокумента);
	ПараметрыЗакрытия.Вставить("Серия", Серия);
	ПараметрыЗакрытия.Вставить("Номер", Номер);
	ПараметрыЗакрытия.Вставить("ДатаВыдачи", ДатаВыдачи);
	ПараметрыЗакрытия.Вставить("КемВыдан", КемВыдан);
	ПараметрыЗакрытия.Вставить("КодПодразделения", КодПодразделения);
	ПараметрыЗакрытия.Вставить("СрокДействия", СрокДействия);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

#КонецОбласти

