#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ЖурналРегистрацииВводногоИнструктажа");
	НастройкиВарианта.Описание = НСтр("ru = 'Применяется для записи, учета и регистрации сведений о лицах прошедших вводный инструктаж'");
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ЖурналРегистрацииИнструктажаНаРабочемМесте");
	НастройкиВарианта.Описание = НСтр("ru = 'Регистрируется проведение определенного вида инструктажа (первичный на рабочем месте, внеплановый, целевой)'");
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область Печать

// Сформировать печатные формы объектов.
//
// ВХОДЯЩИЕ:
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать.
//
// ИСХОДЯЩИЕ:
//   ПараметрыПечати       - Структура  - Дополнительные параметры для печати.
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы.
//   ОбъектыПечати         - Список значений  - Объекты печати (значение - ссылка на объект, представление - имя
//                           области в которой был выведен объект).
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЖурналРегистрацииВводногоИнструктажа") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
				КоллекцияПечатныхФорм,
				"ЖурналРегистрацииВводногоИнструктажа",
				НСтр("ru = 'Журнал регистрации вводного инструктажа'"),
				ПечатьЖурналаРегистрацииВводногоИнструктажа(МассивОбъектов[0], ОбъектыПечати, "ЖурналРегистрацииВводногоИнструктажа"));
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЖурналРегистрацииИнструктажаНаРабочемМесте") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
				КоллекцияПечатныхФорм,
				"ЖурналРегистрацииИнструктажаНаРабочемМесте",
				НСтр("ru = 'Журнал регистрации инструктажа на рабочем месте'"),
				ПечатьЖурналаРегистрацииВводногоИнструктажа(МассивОбъектов[0], ОбъектыПечати, "ЖурналРегистрацииИнструктажаНаРабочемМесте"));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьЖурналаРегистрацииВводногоИнструктажа(Документ, ОбъектыПечати, КлючВарианта) 
	
	ОтчетОбъект = Отчеты.ЖурналРегистрацииИнструктажа.Создать();
	
	ВариантОтчета = ОтчетОбъект.СхемаКомпоновкиДанных.ВариантыНастроек.Найти(КлючВарианта);
	Если ВариантОтчета= Неопределено Тогда
		Возврат Новый ТабличныйДокумент;
	КонецЕсли;
	
	НастройкиОтчета = ВариантОтчета.Настройки;
	
	ЗначениеПараметраДокумент = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Документ"));
	ЗначениеПараметраДокумент.Значение = Документ;
	ЗначениеПараметраДокумент.Использование = Истина;
	
	ОтчетОбъект.КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиОтчета);
	
	ДокументРезультат = Новый ТабличныйДокумент;
	
	ОтчетОбъект.СкомпоноватьРезультат(ДокументРезультат);
	
	Возврат ДокументРезультат;
	
КонецФункции

#КонецОбласти

#КонецЕсли