#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Формирует отчет по расшифровке оценки кандидата.
//
// Параметры 
//	Кандидат - СправочникСсылка.Кандидаты - кандидат, для которого формируется отчет. 
//	ТаблицаОтчета - ТабличныйДокумент - табличный документ для вывода отчета.
//
Процедура СформироватьОтчет(Кандидат, ТаблицаОтчета) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВакансииХарактеристикиПерсонала.Характеристика КАК Характеристика,
		|	ВакансииХарактеристикиПерсонала.Значение КАК ЗначениеХарактеристики,
		|	ВакансииХарактеристикиПерсонала.Вес КАК Вес,
		|	ВакансииХарактеристикиПерсонала.ВесЗначения КАК ВесЗначения,
		|	0 КАК МаксимальныйБалл,
		|	0 КАК Балл,
		|	0 КАК ОтносительныйБалл,
		|	0 КАК Коэффициент
		|ИЗ
		|	Справочник.Вакансии.ХарактеристикиПерсонала КАК ВакансииХарактеристикиПерсонала
		|ГДЕ
		|	ВакансииХарактеристикиПерсонала.Ссылка = &Вакансия
		|	И ВакансииХарактеристикиПерсонала.ТребуетсяПроверка
		|ИТОГИ
		|	МАКСИМУМ(Вес)
		|ПО
		|	Характеристика";
	Запрос.УстановитьПараметр("Вакансия", Кандидат.Вакансия);
	ДеревоХарактеристик = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ФизическиеЛица = Новый СписокЗначений;
	ФизическиеЛица.Добавить(Кандидат.ФизическоеЛицо);
	ДанныеРасшифровки = ЭлектронноеИнтервью.РасшифровкаХарактеристикПоОтветам(Кандидат.Вакансия, ФизическиеЛица, Неопределено, Неопределено);
	
	Макет = Отчеты.ОценкиКандидатаРасшифровка.ПолучитьМакет("Макет");
	
	Шапка = Макет.ПолучитьОбласть("Шапка");
	Шапка.Параметры.Кандидат = Кандидат;
	ТаблицаОтчета.Вывести(Шапка);
	
	ОбщийВес = ДеревоХарактеристик.Строки.Итог("Вес");
	Формулировка = "";
	Для Каждого ТекущаяХарактеристика Из ДеревоХарактеристик.Строки Цикл
		ИтоговыйБалл = 0;
		ОсновноеЗначение = Неопределено;
		Для Каждого СтрокаЗначения Из ТекущаяХарактеристика.Строки Цикл
			СтрокаЗначения.Коэффициент = 1;
			Если ДанныеРасшифровки.Строки.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			ВеткаХарактеристики = ДанныеРасшифровки.Строки[0].Строки.Найти(ТекущаяХарактеристика.Характеристика);
			Если ВеткаХарактеристики = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ВеткаЗначения = ВеткаХарактеристики.Строки.Найти(СтрокаЗначения.ЗначениеХарактеристики);
			Если ВеткаЗначения = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			СтрокаЗначения.МаксимальныйБалл = ВеткаЗначения.МаксимальныйБалл;
			СтрокаЗначения.Балл = ВеткаЗначения.Балл;
	        СтрокаЗначения.ОтносительныйБалл = ВеткаЗначения.ИтоговыйБалл;
			СтрокаЗначения.Коэффициент = ВеткаЗначения.Вес;
			Если ВеткаЗначения.ИтоговыйБалл > ИтоговыйБалл Тогда
				ИтоговыйБалл = ВеткаЗначения.ИтоговыйБалл;
				ОсновноеЗначение = СтрокаЗначения.ЗначениеХарактеристики;
			КонецЕсли;
		КонецЦикла;
		ТаблицаХарактеристики = Новый ТабличныйДокумент;
		ЕстьВыделеннаяСтрока = Ложь;
		Для Каждого СтрокаЗначения Из ТекущаяХарактеристика.Строки Цикл
			Если СтрокаЗначения.ОтносительныйБалл = ИтоговыйБалл И Не ЕстьВыделеннаяСтрока Тогда
				ОбластьСтроки = Макет.ПолучитьОбласть("СтрокаВыделенная");
				ЕстьВыделеннаяСтрока = Истина;
			Иначе
				ОбластьСтроки = Макет.ПолучитьОбласть("Строка");
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(ОбластьСтроки.Параметры, СтрокаЗначения);
			ОбластьСтроки.Параметры.ОтносительныйБалл = Строка(СтрокаЗначения.Балл) + "/" + Строка(СтрокаЗначения.МаксимальныйБалл) + "*" + Формат(СтрокаЗначения.Коэффициент, "ЧДЦ=2; ЧН=0")
				+ " = " + Формат(СтрокаЗначения.ОтносительныйБалл, "ЧДЦ=2; ЧН=0") + "%";
			ОбластьСтроки.Параметры.ИтоговыйБалл = ИтоговыйБалл;
			СтруктураРасшифровки = Новый Структура;
			СтруктураРасшифровки.Вставить("ХарактеристикаПерсонала", ТекущаяХарактеристика.Характеристика);
			СтруктураРасшифровки.Вставить("ЗначениеХарактеристики", ОсновноеЗначение);
			ОбластьСтроки.Параметры.СтруктураРасшифровки = СтруктураРасшифровки;
			ТаблицаХарактеристики.Вывести(ОбластьСтроки);
		КонецЦикла;
		Область = ТаблицаХарактеристики.Область(1, 1, ТекущаяХарактеристика.Строки.Количество(), 1);
		Область.Объединить();
	    Область = ТаблицаХарактеристики.Область(1, 2, ТекущаяХарактеристика.Строки.Количество(), 2);
		Область.Объединить();
	    Область = ТаблицаХарактеристики.Область(1, 9, ТекущаяХарактеристика.Строки.Количество(), 9);
		Область.Объединить();
		ТаблицаОтчета.Вывести(ТаблицаХарактеристики);
		
		Формулировка = Формулировка + ?(ЗначениеЗаполнено(Формулировка), " + ", "") + Строка(ТекущаяХарактеристика.Вес) + "/" + Строка(ОбщийВес) + "*" + Формат(ИтоговыйБалл, "ЧДЦ=2; ЧН=0");
	КонецЦикла;
	
	ИтоговыйБалл = 0;
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОценкиКандидатовПоХарактеристикам.Оценка КАК Оценка
		|ИЗ
		|	РегистрСведений.ОценкиКандидатовПоХарактеристикам КАК ОценкиКандидатовПоХарактеристикам
		|ГДЕ
		|	ОценкиКандидатовПоХарактеристикам.Кандидат = &Кандидат";
	Запрос.УстановитьПараметр("Кандидат", Кандидат);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если ЗначениеЗаполнено(Выборка.Оценка) Тогда
			ИтоговыйБалл = Выборка.Оценка;
		КонецЕсли;
	КонецЕсли;
	
	ОбластьИтогов = Макет.ПолучитьОбласть("Итого");
	ОбластьИтогов.Параметры.Вес = ОбщийВес;
	ОбластьИтогов.Параметры.Формула = Формулировка + " = ";
	ОбластьИтогов.Параметры.ИтоговыйБалл = ИтоговыйБалл;
	ТаблицаОтчета.Вывести(ОбластьИтогов);	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли