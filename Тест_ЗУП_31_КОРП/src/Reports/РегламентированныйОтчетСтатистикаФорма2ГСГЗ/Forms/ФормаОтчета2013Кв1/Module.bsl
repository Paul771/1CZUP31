&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	// Обход ошибки платформы Параметр сеанса отсутствует или удален
	ПолеТабличногоДокументаФормаОтчета.Очистить();
	
	СформироватьСтруктуруРеквизитовФормы();
	
	СтруктураРеквизитовФормы.СписокПечатаемыхЛистов = Новый СписокЗначений;
	
	// ОПИСАНИЕ ПАРМЕТРОВ ФОРМЫ
	// Версия формы.
	СтруктураРеквизитовФормы.мВерсияФормы = "13/01/2014";
	// Код отчета, равен коду по ОКУД.
	СтруктураРеквизитовФормы.мКодОтчета = "0606014";
	
	СтруктураРеквизитовФормы.мПечатныеФормы = Новый СписокЗначений;
	
	ПолеТабличногоДокументаФормаОтчета.Вывести(Отчеты[Сред(Лев(ЭтаФорма.ИмяФормы, СтрНайти(
		ЭтаФорма.ИмяФормы, ".Форма.") - 1), 7)].ПолучитьМакет("ФормаОтчета2013Кв1_ФормаОтчета"));
	
	СтруктураРеквизитовФормы.мВыбраннаяФорма          = Параметры.мВыбраннаяФорма;
	СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета  = Параметры.мДатаКонцаПериодаОтчета;
	СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета = Параметры.мДатаНачалаПериодаОтчета;
	СтруктураРеквизитовФормы.мПериодичность           = Параметры.мПериодичность;
	СтруктураРеквизитовФормы.мСкопированаФорма        = Параметры.мСкопированаФорма;
	СтруктураРеквизитовФормы.мСохраненныйДок          = Параметры.мСохраненныйДок;
	СтруктураРеквизитовФормы.Организация              = Параметры.Организация;
	
	ДатаПодписи = ТекущаяДатаСеанса();
	
	СтруктураРеквизитовФормы.СправочникиВидыКонтактнойИнформацииПочтовыйАдресОрганизации
		= Справочники.ВидыКонтактнойИнформации.ПочтовыйАдресОрганизации;
	
	Инициализация(Параметры.БезОткрытияФормы);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСтруктуруРеквизитовФормы()
	
	СтруктураРеквизитовФормы = РегламентированнаяОтчетность.СформироватьСтруктуруОбязательныхРеквизитовСтатистическогоОтчета();
	
КонецПроцедуры

&НаСервере
Процедура Инициализация(БезОткрытияФормы = Ложь) Экспорт
	
	СтруктураРеквизитовФормы.мБезОткрытияФормы = БезОткрытияФормы;
	
	СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация = Ложь;
	
	ТекТабличноеПоле = ПолеТабличногоДокументаФормаОтчета;
	
	СтруктураРеквизитовФормы.ВидДокумента = 0;
	НомерКорректировки = 1;
	СтруктураРеквизитовФормы.мАдресвФорматеДляВыгрузки = "";
	
	ФормироватьСтруктуруСвойствСтраниц();
	Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
		
		Если СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено Тогда
			// Документ скопирован.
			ВосстановитьСохраненныеДанные();
		КонецЕсли;
		
		Модифицированность = Истина;
		
	Иначе
		
		// При открытии или при копировании сохраненного
		// отчета восстанавливаем сохраненные данные.
		ВосстановитьСохраненныеДанные();
		
		Если ЗначениеЗаполнено(СтруктураРеквизитовФормы.мСкопированаФорма) Тогда
			СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено;
		КонецЕсли;
		
		Если НЕ БезОткрытияФормы
           И НЕ СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
				   
        	ЗаблокироватьДанныеДляРедактирования(СтруктураРеквизитовФормы.мСохраненныйДок, , ЭтаФорма.УникальныйИдентификатор);

        КонецЕсли;
		
	КонецЕсли;
	
	Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
		НомерКорректировки = 0;
	Иначе
		НомерКорректировки = СтруктураРеквизитовФормы.мСохраненныйДок.Вид;
	КонецЕсли;
	
	ТекТабличноеПоле.Области.НомерКорректировки.Значение = НомерКорректировки;
	ПолеТабличногоДокументаФормаОтчета.Области.ДатаСоставленияОтчета.Значение = ДатаПодписи;
	
	ПоказатьПериод();
	
	Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Или СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация Тогда
		ЗаполнитьСведенияОбОрганизацииНаСервере();
	КонецЕсли;
	
	СтруктураРеквизитовФормы.НаимОрганизации = СтруктураРеквизитовФормы.Организация.Наименование;
	
	ТекущийЭлемент = Элементы["ПолеТабличногоДокументаФормаОтчета"];
	
	РегламентированнаяОтчетность.ДобавитьКнопкуПрисоединенныеФайлы(ЭтаФорма);
	
	РегламентированнаяОтчетностьКлиентСервер.ПриИнициализацииФормыРегламентированногоОтчета(ЭтаФорма, "ФСГС");
	
КонецПроцедуры

// Формирует дерево значений - структуру страниц отчета,
// содержащей настройки показа страниц, вывода на печать и выгрузки в ИФНС.
//
&НаСервере
Процедура ФормироватьСтруктуруСвойствСтраниц() Экспорт
	
	// 0 - нет
	// 1 - есть одна многострочная часть
	// 2 - и более, точное кол-во число многострочных частей на листе
	// Позволяет записывать многострочность в многостраничных документ либо как ТаблицуЗначений, либо как Структуру
	// В случае, если как Структура, то необходима еще одна процедура СформироватьСтруктуруДанныхДопСтрокСтраницы
	// Только для Многостраничных документов !
	// 3. Будет ли очищаться раздел по кнопки "Очистить"
	// 4. Если раздел многостраничный, параметр МногостраничностьВРазделе = Истина, тогда имя поля, по которому
	// будет осуществляться Представление доп. страницы
	// 5. Есть ли в разделе значения, отображаемые в квадратном представлении
	// 6. Обязательный раздел для выгрузки или нет
	// 7. Обязательный раздел для выгрузки или нет
	
	мСвойстваРазделовДекларации.ПолучитьЭлементы().Очистить();
	
	СтрокаУровня1 = мСвойстваРазделовДекларации.ПолучитьЭлементы().Добавить();
	
	// Добавим св-ва Титульного листа
	СтрокаУровня1.ИмяСтраницы                          = "ФормаОтчета";
	СтрокаУровня1.МногострочностьВРазделе              = 0;
	СтрокаУровня1.МногостраничностьВРазделе            = Ложь;
	СтрокаУровня1.ОчищатьРаздел                        = Ложь;
	СтрокаУровня1.ИмяПредставления                     = "";
	СтрокаУровня1.ПредставлениеДанных                  = Ложь;
	СтрокаУровня1.РазделОбязателенДляВыгрузки          = Неопределено;
	СтрокаУровня1.СохранятьМногострКакТЗ               = Неопределено;
	СтрокаУровня1.РазделЯвляетсяАвтозаполняемым        = Ложь;
	
КонецПроцедуры

// Восстанавливает сохраненные данные отчета.
//
&НаСервере
Процедура ВосстановитьСохраненныеДанные()
	
	Перем ДанныеВариановАвтоЗаполнения;
	Перем ПоказателиОтчета;
	Перем ВерсияФормы;

	// В случае, если формы была скопирована по F9, то проверим, не изменилась ли организация.
	Если СтруктураРеквизитовФормы.Организация <> СтруктураРеквизитовФормы.мСохраненныйДок.Организация
	   И СтруктураРеквизитовФормы.Организация <> РегламентированнаяОтчетностьКлиентСервер.ПустоеЗначениеТипа("СправочникСсылка.Организации")
	   И СтруктураРеквизитовФормы.Организация <> Неопределено Тогда
		// Меняем переменную, для того, чтобы запустилась процедура ЗаполнитьСведенияОбОрганизации.
		СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация = Истина;
	Иначе
		// восстанавливаем реквизиты отчета
		СтруктураРеквизитовФормы.Организация = СтруктураРеквизитовФормы.мСохраненныйДок.Организация;
	КонецЕсли;
	
	ДатаПодписи                    = СтруктураРеквизитовФормы.мСохраненныйДок.ДатаПодписи;
	СтруктураРеквизитовФормы.ЕдиницаИзмерения         = СтруктураРеквизитовФормы.мСохраненныйДок.ЕдиницаИзмерения;
	СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения = СтруктураРеквизитовФормы.мСохраненныйДок.ТочностьЕдиницыИзмерения;
	Комментарий                    = СтруктураРеквизитовФормы.мСохраненныйДок.Комментарий;
	
	// Восстанавливаем сохраненные данные отчета.
	СписокСохранения = СтруктураРеквизитовФормы.мСохраненныйДок.ДанныеОтчета.Получить();
	
	// Восстанавливаем сохраненные данные вариантов автозаполнения ячеек.
	Если СписокСохранения.Свойство("ДанныеВариантовАвтоЗаполнения", ДанныеВариановАвтоЗаполнения) Тогда
		
		Если НЕ ДанныеВариановАвтоЗаполнения = Неопределено Тогда
			
			мСтруктураВариантыЗаполнения.Очистить();
			
			Для Каждого ЭлементСтруктуры Из ДанныеВариановАвтоЗаполнения Цикл
				
				мСтруктураВариантыЗаполнения.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Ключ);
				
				ЭтаФорма["ТаблицаВариантыЗаполнения" + ЭлементСтруктуры.Ключ].Очистить();
				
				Для Каждого ЭлементМассива Из ЭлементСтруктуры.Значение Цикл
					
					ТаблВарЗаполнения = ЭтаФорма["ТаблицаВариантыЗаполнения" + ЭлементСтруктуры.Ключ].Добавить().ТаблицаВариантовЗаполнения;
					
					ЗначениеВДанныеФормы(ЭлементМассива, ТаблВарЗаполнения);
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Восстанавливаем версию формы.
	СписокСохранения.Свойство("ВерсияФормы", ВерсияФормы);
	
	// Восстановим сохраненные данные редактируемых ячеек.
	СписокСохранения.Свойство( "ПоказателиОтчета", ПоказателиОтчета );
	
	Для Каждого ПоказателиСтраницы Из ПоказателиОтчета Цикл
		ИмяТекТабличноеПоле = ПоказателиСтраницы.Ключ;
		ТекТабличноеПоле    = ЭтаФорма[ИмяТекТабличноеПоле];
		ПоказателиТаблПоле  = ПоказателиСтраницы.Значение;
		
		Для Каждого Показатель Из ПоказателиТаблПоле Цикл
			ИмяПоказателя       = Показатель.Ключ;
			ЗначениеПоказателя  = Показатель.Значение;
			
			// Установим значение в таблице.
			Попытка
				Если Лев(ИмяПоказателя, 2) = "ЧН" Тогда
					// Значение показателя с префиксом "ЧН" содержит формат представления нуля.
					ОбластьПоказателя = ТекТабличноеПоле.Области[Сред(ИмяПоказателя, 3)];
					ОбластьПоказателя.Формат = РегламентированнаяОтчетностьКлиентСервер.ЗаменитьПредставлениеНуляВСтрокеФормата(ОбластьПоказателя.Формат, ЗначениеПоказателя);
					Продолжить;
				КонецЕсли;
				ТекТабличноеПоле.Области[ИмяПоказателя].Значение = ЗначениеПоказателя;
			Исключение
			КонецПопытки;
		КонецЦикла;
	КонецЦикла;
	
	СтруктураРеквизитовФормы.мАдресвФорматеДляВыгрузки = ПолеТабличногоДокументаФормаОтчета.Области.ОргАдрес.Значение;
	ПолеТабличногоДокументаФормаОтчета.Области.ОргАдрес.Значение
		= РегламентированнаяОтчетностьКлиентСервер.ПредставлениеАдресаВФормате9Запятых(СтруктураРеквизитовФормы.мАдресвФорматеДляВыгрузки);
	
	РегламентированнаяОтчетность.ОперацииПриВосстановленииРегламентированногоОтчета(ЭтаФорма);
	
	Модифицированность = Ложь;
	
КонецПроцедуры

// Процедура управляет показом в форме периода построения отчета.
//
&НаСервере
Процедура ПоказатьПериод()
	
	// Период в шапке и заголовке отчета.
	ПолеТабличногоДокументаФормаОтчета.Области.ПериодОтчета.Значение
		= "за " + НРег(Формат(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета, "ДФ = 'гггг'")) + " год";
	СтруктураРеквизитовФормы.СтрПериодОтчета = НРег(Формат(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета, "ДФ = 'гггг'")) + " год";
	
	ПолеТабличногоДокументаФормаОтчета.Области.ГодОтчета1.Значение
		= Формат(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета, "ДФ = 'гггг'") + " году";
	ПолеТабличногоДокументаФормаОтчета.Области.ГодОтчета2.Значение
		= "в " + Формат(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета, "ДФ = 'гггг'") + " году";
	ПолеТабличногоДокументаФормаОтчета.Области.ГодОтчета3.Значение
		= Формат(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета, "ДФ = 'гггг'") + " году,";
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСведенияОбОрганизацииНаСервере(ВПрограммеИзмененаДатаПодписи = Ложь)
	
	РегламентированнаяОтчетностьКлиентСервер.ЗаполнитьСведенияОбОрганизацииДляОтчетаСтатистики(ЭтаФорма, ВПрограммеИзмененаДатаПодписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСведенияОбОрганизацииНаКлиенте(ВПрограммеИзмененаДатаПодписи = Ложь)
	
	РегламентированнаяОтчетностьКлиентСервер.ЗаполнитьСведенияОбОрганизацииДляОтчетаСтатистики(ЭтаФорма, ВПрограммеИзмененаДатаПодписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если СтрНайти(Заголовок, СтруктураРеквизитовФормы.СтрПериодОтчета) = 0 Тогда
		Заголовок = Заголовок + " за " + СтруктураРеквизитовФормы.СтрПериодОтчета;
	КонецЕсли;
	
	ОргСтр = " (" + СтруктураРеквизитовФормы.НаимОрганизации + ")";
	Если СтрНайти(Заголовок, ОргСтр) = 0 Тогда
		Заголовок = Заголовок + ОргСтр;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ВыполнитьПроверкуПередОткрытием", 0.1, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПроверкуПередОткрытием()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередОткрытиемФормыРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Модифицированность = Ложь;
		Закрыть();
		Возврат;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	РегламентированнаяОтчетностьКлиент.ПередЗакрытиемРегламентированногоОтчета(ЭтаФорма, Отказ, СтандартнаяОбработка, ЗавершениеРаботы, ТекстПредупреждения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
				
		Возврат;
		
	КонецЕсли;
	
	ПриЗакрытииНаСервере();
	
КонецПроцедуры 

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	РегламентированнаяОтчетность.ПриЗакрытииРегламентированногоОтчета(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеТабличногоДокументаФормаОтчетаВыбор(Элемент, Область, СтандартнаяОбработка)
	
	Если Область.Имя = "ОргАдрес" Тогда
		РегламентированнаяОтчетностьКлиент.ОбработкаАдресаВСтатистическомОтчете(ЭтаФорма, Область, СтандартнаяОбработка);
	ИначеЕсли Область.Имя = "ГипСсылкаУровеньГосОргана" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ЭтаФорма.ОткрытьСправкуФормы();
		
	ИначеЕсли Область.Имя = "ОргУровеньГосОргана" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Список = Новый СписокЗначений;
		Список.Добавить("Пустое значение");
		Список.Добавить("Федеральный");
		Список.Добавить("Региональный");
		
		НайденнаяСтрока = Список.НайтиПоЗначению(Область.Значение);
		
		ДополнительныеПараметры = Новый Структура("Область", Область);
		ОписаниеОповещения = Новый ОписаниеОповещения("ПолеТабличногоДокументаФормаОтчетаВыборЗавершение", ЭтотОбъект, ДополнительныеПараметры);
					
		// обход ошибок платформы: в тонком клиенте не работает ПоказатьВыборИзСписка (ПоказатьВыборИзМеню не работает в веб-клиенте 
		// и в событии ПриИзмененииСодержимогоОбласти табличного документа - где мы его и хотим использовать прежде всего)
		# Если ВебКлиент Тогда
		
			Если НайденнаяСтрока = Неопределено Тогда
				ПоказатьВыборИзСписка(ОписаниеОповещения, Список, Элемент);
			Иначе
				ПоказатьВыборИзСписка(ОписаниеОповещения, Список, Элемент, НайденнаяСтрока);
			КонецЕсли;
			
		#Иначе
			
			Список.ПоказатьВыборЭлемента(ОписаниеОповещения, "Выберите значение", Элемент);
				
		#КонецЕсли
		
		Возврат;
				
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеТабличногоДокументаФормаОтчетаВыборЗавершение(СписокЭлемент, ДополнительныеПараметры) Экспорт
	
	Область = ДополнительныеПараметры.Область;
	
	Если СписокЭлемент <> Неопределено  Тогда
		
		Область.Значение = СписокЭлемент.Значение;
		
		Если Область.Значение = "Пустое значение" Тогда
			Область.Значение = "";
		КонецЕсли;
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуКонтактнойИнформацииЗавершение(Результат, Параметры) Экспорт
	
	РегламентированнаяОтчетностьКлиент.ОбновитьАдресВТабличномДокументеСтатистическойОтчетности(Результат, Параметры.Область, СтруктураРеквизитовФормы);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеТабличногоДокументаФормаОтчетаПриИзмененииСодержимогоОбласти(Элемент, Область)
	
	Если Область.Имя = "ДатаСоставленияОтчета" Тогда 
		ДатаПодписи = Область.Значение;
		ЗаполнитьСведенияОбОрганизацииНаКлиенте(Истина);
		Модифицированность = Истина;
		Возврат;
	ИначеЕсли Область.Имя = "НомерКорректировки" Тогда 
		Если Область.Значение = 0 Тогда 
			СтруктураРеквизитовФормы.ВидДокумента = 0;
			НомерКорректировки = 0;
		Иначе 
			СтруктураРеквизитовФормы.ВидДокумента = 1;
			НомерКорректировки = Область.Значение;
		КонецЕсли;
		Модифицированность = Истина;
		Возврат;
	КонецЕсли;
	
	Если Область.СодержитЗначение И Область.ТипЗначения.СодержитТип(Тип("Число")) Тогда
		Если Область.Значение = 0 И Область.ТипЗначения.КвалификаторыЧисла.РазрядностьДробнойЧасти = 0 Тогда
			
			ФорматПредставленияНуля = РегламентированнаяОтчетностьКлиентСервер.ПолучитьПредставлениеНуляИзСтрокиФормата(Область.Формат);
			
			ОтображениеНуля = СокрЛП(Формат(0, РегламентированнаяОтчетностьКлиентСервер.ЗаменитьПредставлениеНуляВСтрокеФормата(Область.Формат, "ЧН=")));
			Если ПустаяСтрока(ОтображениеНуля) Тогда
				ОтображениеНуля = "0";
			КонецЕсли;
			ТипСтрокиЗаполнения = Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(СтрДлина(ОтображениеНуля), ДопустимаяДлина.Фиксированная));
			СтрокаЗаполнения = ТипСтрокиЗаполнения.ПривестиЗначение();
			
			Список = Новый СписокЗначений;
			Список.Добавить("ЧН=' '", "  <Пустое значение>    " + СтрокаЗаполнения);
			Список.Добавить("ЧН=" + ОтображениеНуля, "  Отображать как """ + ОтображениеНуля + """  ");
			
			НайденнаяСтрока = Список.НайтиПоЗначению(ФорматПредставленияНуля);
			
			ДополнительныеПараметры = Новый Структура("Область", Область);
			ОписаниеОповещения = Новый ОписаниеОповещения("ПолеТабличногоДокументаФормаОтчетаПриИзмененииСодержимогоОбластиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
						
			// обход ошибок платформы: в тонком клиенте не работает ПоказатьВыборИзСписка (ПоказатьВыборИзМеню не работает в веб-клиенте 
			// и в событии ПриИзмененииСодержимогоОбласти табличного документа - где мы его и хотим использовать прежде всего)
			# Если ВебКлиент Тогда
			
				Если НайденнаяСтрока = Неопределено Тогда
					ПоказатьВыборИзСписка(ОписаниеОповещения, Список, Элемент);
				Иначе
					ПоказатьВыборИзСписка(ОписаниеОповещения, Список, Элемент, НайденнаяСтрока);
				КонецЕсли;
				
			#Иначе
				
				Список.ПоказатьВыборЭлемента(ОписаниеОповещения, "Выберите значение", Элемент);
					
			#КонецЕсли
			
			Возврат;
						
		КонецЕсли;
	КонецЕсли;
	
	РасчетНаКлиенте(, Область.Имя);
	
	Модифицированность = Истина;
	
КонецПроцедуры 

&НаКлиенте
Процедура ПолеТабличногоДокументаФормаОтчетаПриИзмененииСодержимогоОбластиЗавершение(СписокЭлемент, ДополнительныеПараметры) Экспорт
	
	Область = ДополнительныеПараметры.Область;
	
	Если СписокЭлемент <> Неопределено  Тогда
		
		Область.Формат = РегламентированнаяОтчетностьКлиентСервер.ЗаменитьПредставлениеНуляВСтрокеФормата(Область.Формат, СписокЭлемент.Значение);
		
	КонецЕсли;
	
	РасчетНаКлиенте(, Область.Имя);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетНаКлиенте(ИмяСтраницыПанели = "", ОбластьИмя = "") Экспорт
	
	Расчет(ЭтаФорма, ИмяСтраницыПанели, ОбластьИмя);
	
КонецПроцедуры

&НаСервере
Процедура РасчетНаСервере(ИмяСтраницыПанели = "", ОбластьИмя = "") Экспорт
	
	Расчет(ЭтаФорма, ИмяСтраницыПанели, ОбластьИмя);
	
КонецПроцедуры

// Выполняет расчет вычисляемых показателей отчета
// (ячеек, выделенных зеленым цветом).
//
&НаКлиентеНаСервереБезКонтекста
Процедура Расчет(Форма, ИмяСтраницыПанели = "", ОбластьИмя = "")
	
	мФормаОтчета = Форма.ПолеТабличногоДокументаФормаОтчета;
	
	// Для Раздела 1.
	Для НомСтр = 101 По 124 Цикл
		// Гр.4 = сумме гр. с 5 по 8.
		ТекСумма = 0;
		Для НомКол = 5 По 8 Цикл
			ТекСумма = ТекСумма + мФормаОтчета.Области["П000001"
				+ Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=")
				+ Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение;
		КонецЦикла;
		мФормаОтчета.Области["П000001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "04"].Значение = ТекСумма;
		// Гр.9 = сумме гр. с 10 по 13.
		ТекСумма = 0;
		Для НомКол = 10 По 13 Цикл
			ТекСумма = ТекСумма + мФормаОтчета.Области["П000001"
				+ Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=")
				+ Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение;
		КонецЦикла;
		мФормаОтчета.Области["П000001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "09"].Значение = ТекСумма;
	КонецЦикла;
	Для НомКол = 3 По 13 Цикл
		// Стр.103 = сумме стр. с 104 по 106.
		ТекСумма = 0;
		Для НомСтр = 104 По 106 Цикл
			ТекСумма = ТекСумма + мФормаОтчета.Области["П000001"
				+ Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=")
				+ Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение;
		КонецЦикла;
		мФормаОтчета.Области["П0000010103" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение = ТекСумма;
		// Стр.107 = сумме стр. с 108 по 110.
		ТекСумма = 0;
		Для НомСтр = 108 По 110 Цикл
			ТекСумма = ТекСумма + мФормаОтчета.Области["П000001"
				+ Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=")
				+ Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение;
		КонецЦикла;
		мФормаОтчета.Области["П0000010107" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение = ТекСумма;
		// Стр.111 = сумме стр. с 112 по 115.
		ТекСумма = 0;
		Для НомСтр = 112 По 115 Цикл
			ТекСумма = ТекСумма + мФормаОтчета.Области["П000001"
				+ Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=")
				+ Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение;
		КонецЦикла;
		мФормаОтчета.Области["П0000010111" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение = ТекСумма;
		// Стр.116 = сумме стр. с 117 по 120.
		ТекСумма = 0;
		Для НомСтр = 117 По 120 Цикл
			ТекСумма = ТекСумма + мФормаОтчета.Области["П000001"
				+ Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=")
				+ Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение;
		КонецЦикла;
		мФормаОтчета.Области["П0000010116" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение = ТекСумма;
		// Стр.102 = сумме стр. 103, 107, 111, 116.
		мФормаОтчета.Области["П0000010102" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение
			= мФормаОтчета.Области["П0000010103" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение
			+ мФормаОтчета.Области["П0000010107" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение
			+ мФормаОтчета.Области["П0000010111" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение
			+ мФормаОтчета.Области["П0000010116" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение;
		// Стр.121 = сумме стр. 122, 123.
		мФормаОтчета.Области["П0000010121" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение
			= мФормаОтчета.Области["П0000010122" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение
			+ мФормаОтчета.Области["П0000010123" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение;
	КонецЦикла;
	
	// Для Справки 1 к разделу 1.
	// Стр.131 = сумме стр. с 132 по 135.
	ТекСумма = 0;
	Для НомСтр = 132 По 135 Цикл
		ТекСумма = ТекСумма + мФормаОтчета.Области["П000001"
			+ Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=")
			+ "03"].Значение;
	КонецЦикла;
	мФормаОтчета.Области["П000001013103"].Значение = ТекСумма;
	
	// Для Раздела 2.
	Для НомКол = 3 По 10 Цикл
		// Стр.203 = сумме стр. с 204 по 206.
		ТекСумма = 0;
		Для НомСтр = 204 По 206 Цикл
			ТекСумма = ТекСумма + мФормаОтчета.Области["П000002"
				+ Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=")
				+ Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение;
		КонецЦикла;
		мФормаОтчета.Области["П0000020203" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение = ТекСумма;
		// Стр.207 = сумме стр. с 208 по 210.
		ТекСумма = 0;
		Для НомСтр = 208 По 210 Цикл
			ТекСумма = ТекСумма + мФормаОтчета.Области["П000002"
				+ Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=")
				+ Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение;
		КонецЦикла;
		мФормаОтчета.Области["П0000020207" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение = ТекСумма;
		// Стр.211 = сумме стр. с 212 по 215.
		ТекСумма = 0;
		Для НомСтр = 212 По 215 Цикл
			ТекСумма = ТекСумма + мФормаОтчета.Области["П000002"
				+ Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=")
				+ Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение;
		КонецЦикла;
		мФормаОтчета.Области["П0000020211" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение = ТекСумма;
		// Стр.216 = сумме стр. с 217 по 220.
		ТекСумма = 0;
		Для НомСтр = 217 По 220 Цикл
			ТекСумма = ТекСумма + мФормаОтчета.Области["П000002"
				+ Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=")
				+ Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение;
		КонецЦикла;
		мФормаОтчета.Области["П0000020216" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение = ТекСумма;
		// Стр.202 = сумме стр. 203, 207, 211, 216.
		мФормаОтчета.Области["П0000020202" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение
			= мФормаОтчета.Области["П0000020203" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение
			+ мФормаОтчета.Области["П0000020207" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение
			+ мФормаОтчета.Области["П0000020211" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение
			+ мФормаОтчета.Области["П0000020216" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение;
		// Стр.221 = сумме стр. 201, 202.
		мФормаОтчета.Области["П0000020221" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение
			= мФормаОтчета.Области["П0000020201" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение
			+ мФормаОтчета.Области["П0000020202" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение;
	КонецЦикла;
	
	// Для Раздела 3.
	// Гр.3 = сумме гр. с 4 по 7.
	Для НомСтр = 301 По 307 Цикл
		ТекСумма = 0;
		Для НомКол = 4 По 7 Цикл
			ТекСумма = ТекСумма + мФормаОтчета.Области["П000003"
				+ Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=")
				+ Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")].Значение;
		КонецЦикла;
		мФормаОтчета.Области["П000003" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "03"].Значение = ТекСумма;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация = Истина;
	ЗаполнитьСведенияОбОрганизацииНаКлиенте();
	СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтчет(Команда)
	
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Внимание! Будут очищены все показатели отчета.%1Продолжить операцию?'"), Символы.ПС);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОчиститьОтчетЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтчетЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда        
		Возврат;        
	Иначе        
		Очистить();        
	КонецЕсли;
	
КонецПроцедуры

// Процедура очищает содержимое редактируемых и вычисляемых
// ячеек табличного документа.
//
&НаКлиенте
Процедура Очистить() Экспорт
	
	ОчиститьТабличноеПолеНаСервере();

	// Устанавливаем флаг модифицированности формы.
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьТабличноеПолеНаСервере()
	
	ОчиститьТабличноеПоле(ЭтаФорма);
	РасчетНаСервере();
	
КонецПроцедуры

// Процедура очищает содержимое редактируемыхи и вычисляемых ячеек
// поля табличного документа, переданного параметром ВыбТабличноеПоле.
//
// Параметры:
//  ВыбТабличноеПоле - поле табличного документа.
//
&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьТабличноеПоле(Форма)
	
	// Список ячеек, очищать которые не нужно.
	Перем СписокПоказателейНеПодлежащихОчистке;
	
	ВыбТабличноеПоле = Форма.ПолеТабличногоДокументаФормаОтчета;
	
	СписокПоказателейНеПодлежащихОчистке = Новый СписокЗначений;
	СписокПоказателейНеПодлежащихОчистке.Добавить("ОргНазв");
	СписокПоказателейНеПодлежащихОчистке.Добавить("ОргАдрес");
	СписокПоказателейНеПодлежащихОчистке.Добавить("ОргКодОКПО");
	СписокПоказателейНеПодлежащихОчистке.Добавить("Код1");
	СписокПоказателейНеПодлежащихОчистке.Добавить("Код2");
	СписокПоказателейНеПодлежащихОчистке.Добавить("Код3");
	СписокПоказателейНеПодлежащихОчистке.Добавить("Код4");
	СписокПоказателейНеПодлежащихОчистке.Добавить("ОргКодНазв");
	СписокПоказателейНеПодлежащихОчистке.Добавить("ОргКодЗнач");
	СписокПоказателейНеПодлежащихОчистке.Добавить("ОргУровеньГосОргана");
	СписокПоказателейНеПодлежащихОчистке.Добавить("ОргДолжностьИсп");
	СписокПоказателейНеПодлежащихОчистке.Добавить("ОргИсполнитель");
	СписокПоказателейНеПодлежащихОчистке.Добавить("ОргТелефонИсп");
	СписокПоказателейНеПодлежащихОчистке.Добавить("ОргАдресЭлектроннойПочты");
	СписокПоказателейНеПодлежащихОчистке.Добавить("ДатаСоставленияОтчета");
	СписокПоказателейНеПодлежащихОчистке.Добавить("НомерКорректировки");
	
	Для Инд = 0 По ВыбТабличноеПоле.Области.Количество() - 1 Цикл
		ТекущаяОбласть = ВыбТабличноеПоле.Области[Инд];
		
		// Ячейки, не подлежащие очистке.
		Если СписокПоказателейНеПодлежащихОчистке.НайтиПоЗначению(ТекущаяОбласть.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ТекущаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ТекущаяОбласть.СодержитЗначение Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекущаяОбласть.Защита Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущаяОбласть.Очистить();
		ТекущаяОбласть.Формат = РегламентированнаяОтчетностьКлиентСервер.ЗаменитьПредставлениеНуляВСтрокеФормата(ТекущаяОбласть.Формат, "ЧН=' '");
	КонецЦикла;
	
	Расчет(Форма, "", "");
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьОтчет(Команда)
	
	СохранитьНаКлиенте();
	
КонецПроцедуры

// СохранитьНаКлиенте()
&НаКлиенте
Процедура СохранитьНаКлиенте(Автосохранение = Ложь, ВыполняемоеОповещение = Неопределено) Экспорт
	
	Вариант = СтруктураРеквизитовФормы.ВидДокумента * НомерКорректировки;
	
	Если НЕ РегламентированнаяОтчетностьКлиент.ПриЗаписиРегламентированногоОтчетаНаКлиенте(ЭтаФорма, , Автосохранение, Вариант) Тогда
		Возврат;
	КонецЕсли;
	
	ПодобныйОтчетСуществует = Ложь;
	ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки = Ложь;
	
	// формируем данные редактируемых ячеек таблицы
	ПоказателиОтчета = Новый Структура();
	
	#Если ВебКлиент Тогда
		ЗаполнитьПоказателиОтчетаНаСервере(ПоказателиОтчета);
	#Иначе
		ЗаполнитьПоказателиОтчетаНаКлиенте(ПоказателиОтчета);
	#КонецЕсли
	
	РезультатСохранения = ПередСохранением(, ПодобныйОтчетСуществует, Вариант, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, Автосохранение, ПоказателиОтчета);
	
	ВидДокументаНомерКорректировкиИзменен = Неопределено;
	
	Если ПодобныйОтчетСуществует И Автосохранение Тогда
		Возврат;
	КонецЕсли;
	
	НуженВопросПередСохранением = (ПодобныйОтчетСуществует ИЛИ ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки);
	
	Если НуженВопросПередСохранением Тогда
		СохранитьНаКлиентеСВопросом(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, ПодобныйОтчетСуществует, ПоказателиОтчета);
	Иначе		
		ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаКлиентеСВопросом(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, ПодобныйОтчетСуществует, ПоказателиОтчета)
	
	Если ПодобныйОтчетСуществует Тогда
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, "Сохранить");
		Кнопки.Добавить(КодВозвратаДиалога.Нет, "Отмена");
		
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Отчет с видом %1 уже существует.
		|Сохранить отчет с таким же видом?'"), ?(Вариант = 0, """Первичный""", """Корректирующий/" + Вариант + """"));
		ДополнительныеПараметры = Новый Структура("Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, ПоказателиОтчета", Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, ПоказателиОтчета);
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросСохранитьОтчетСТакимЖеВидомЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки, , Кнопки.Получить(1).Значение);
		
	Иначе
		
		СохранитьНаКлиентеСВопросомПродолжение(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, ПоказателиОтчета);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросСохранитьОтчетСТакимЖеВидомЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Автосохранение = ДополнительныеПараметры.Автосохранение;
	Вариант = ДополнительныеПараметры.Вариант;
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки = ДополнительныеПараметры.ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки;
	ПоказателиОтчета = ДополнительныеПараметры.ПоказателиОтчета;
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		
		Возврат;
		
	КонецЕсли;
	
	СохранитьНаКлиентеСВопросомПродолжение(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, ПоказателиОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаКлиентеСВопросомПродолжение(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, ПоказателиОтчета)
	
	Если ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки Тогда
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ВидОтчета", Вариант);
		
		ФормаВопроса = РегламентированнаяОтчетностьКлиент.ПолучитьОбщуюФормуПоИмени("ВопросПриИзмененииВидаДокументаНомераКорректировки", ПараметрыФормы);
		
		ДополнительныеПараметры = Новый Структура("Автосохранение, Вариант, ВыполняемоеОповещение, ПоказателиОтчета", Автосохранение, Вариант, ВыполняемоеОповещение, ПоказателиОтчета);
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПриИзмененииВидаДокументаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		ФормаВопроса.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
		ФормаВопроса.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ФормаВопроса.Открыть();
		
	Иначе
		Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Сохраняется %1...'"), ЭтаФорма.Заголовок), , , БиблиотекаКартинок.Записать);
		РезультатСохранения = Сохранить(Автосохранение, , Вариант, Ложь, ПоказателиОтчета);
		ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПриИзмененииВидаДокументаЗавершение(КодВозврата, ДополнительныеПараметры) Экспорт
	
	Автосохранение = ДополнительныеПараметры.Автосохранение;
	Вариант = ДополнительныеПараметры.Вариант;
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	ПоказателиОтчета = ДополнительныеПараметры.ПоказателиОтчета;
	
	Если КодВозврата = КодВозвратаДиалога.Да
		ИЛИ КодВозврата = КодВозвратаДиалога.Нет Тогда
		
		ВидДокументаНомерКорректировкиИзменен = ?(КодВозврата = КодВозвратаДиалога.Да, Истина, Ложь);
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
	Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Сохраняется %1...'"), ЭтаФорма.Заголовок), , , БиблиотекаКартинок.Записать);
	РезультатСохранения = Сохранить(Автосохранение, , Вариант, ВидДокументаНомерКорректировкиИзменен, ПоказателиОтчета);
	ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения)
	
	КлючУникальности = СтруктураРеквизитовФормы.мСохраненныйДок;
	
	Если РезультатСохранения Тогда
		
		РегламентированнаяОтчетностьКлиент.ПослеЗаписиРегламентированногоОтчета(ЭтаФорма);
		
		Если ВыполняемоеОповещение <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ВыполняемоеОповещение);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
// СохранитьНаКлиенте

&НаСервере
Функция ПередСохранением(КодИФНС = "не применимо", ПодобныйОтчетСуществует="", Вариант="", ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки="", Автосохранение="", ПоказателиОтчета="")
	
	Если РегламентированнаяОтчетность.БылиИзмененыКлючевыеРеквизитыОтчета(ЭтаФорма, КодИФНС)
	   И РегламентированнаяОтчетность.СуществуетДокументСАналогичнымиРеквизитами(ЭтаФорма, КодИФНС) Тогда
		
		ПодобныйОтчетСуществует = Истина;
		
	КонецЕсли;
	
	СтруктураРеквизитовФормы.мЗаписываетсяНовыйДокумент = (СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено ИЛИ СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено);
	
	Если Вариант <> Неопределено И ((СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено) ИЛИ (СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено) ИЛИ (Вариант <> СтруктураРеквизитовФормы.мВариант)) Тогда
		
		Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено ИЛИ СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено Тогда
			
		ИначеЕсли Вариант <> СтруктураРеквизитовФормы.мВариант Тогда
			
			ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки = Истина;
			
		КонецЕсли;

	КонецЕсли;
	
	Если ПодобныйОтчетСуществует
	 ИЛИ ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Сохранить(Автосохранение, КодИФНС, Вариант, , ПоказателиОтчета);
	
КонецФункции

// Формирует структуру данных отчета и помещает в реквизит типа ХранилищеЗначения
// документа РегламентированныйОтчет.
// 
// При первом сохранении данных отчета создает новый документ этого вида.
// Структура данных отчета состоит из элементов:
//   - значения редактируемых реквизитов отчета;
//   - данные по многострочным разделам отчета;
//   - данные по  многостраничным разделам отчета;
//   - данные по вариантам заполнения реквизитов,
//     участвующих в автозаполнении.
//
&НаСервере
Функция Сохранить(Автосохранение = Ложь, КодИФНС = "не применимо", Вариант = Неопределено, ВидДокументаНомерКорректировкиИзменен = Неопределено, ПоказателиОтчета = Неопределено) Экспорт
	
	ДанныеДляРазблокирования = Неопределено;
	
	Если НЕ СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
		Если НЕ СтруктураРеквизитовФормы.мБезОткрытияФормы Тогда
			ДанныеДляРазблокирования = Новый Структура("Ключ,ИдФормы",
			СтруктураРеквизитовФормы.мСохраненныйДок, ЭтаФорма.УникальныйИдентификатор);
		КонецЕсли;
		СтруктураРеквизитовФормы.мСохраненныйДок = СтруктураРеквизитовФормы.мСохраненныйДок.ПолучитьОбъект();
	КонецЕсли;
	
	Если НЕ РегламентированнаяОтчетность.ПриЗаписиРегламентированногоОтчетаНаСервере(ЭтаФорма, КодИФНС, Автосохранение, Вариант, ВидДокументаНомерКорректировкиИзменен, СтруктураРеквизитовФормы.мСохраненныйДок) Тогда
		СтруктураРеквизитовФормы.мСохраненныйДок = СтруктураРеквизитовФормы.мСохраненныйДок.Ссылка;
		Возврат Ложь;
	КонецЕсли;
	
	// Установим текущие значения реквизитов документа.
	СтруктураРеквизитовФормы.мСохраненныйДок.ИсточникОтчета           = Метаданные.Отчеты[Сред(Лев(ЭтаФорма.ИмяФормы, СтрНайти(ЭтаФорма.ИмяФормы, ".Форма.") - 1), 7)].Имя;
	СтруктураРеквизитовФормы.мСохраненныйДок.НаименованиеОтчета       = Метаданные.Отчеты[Сред(Лев(ЭтаФорма.ИмяФормы, СтрНайти(ЭтаФорма.ИмяФормы, ".Форма.") - 1), 7)].ОсновнаяФорма.Синоним;
	СтруктураРеквизитовФормы.мСохраненныйДок.ДатаНачала               = СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета;
	СтруктураРеквизитовФормы.мСохраненныйДок.ДатаОкончания            = СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета;
	СтруктураРеквизитовФормы.мСохраненныйДок.Периодичность            = СтруктураРеквизитовФормы.мПериодичность;
	СтруктураРеквизитовФормы.мСохраненныйДок.ВыбраннаяФорма           = СтруктураРеквизитовФормы.мВыбраннаяФорма;
	СтруктураРеквизитовФормы.мСохраненныйДок.Организация              = СтруктураРеквизитовФормы.Организация;
	СтруктураРеквизитовФормы.мСохраненныйДок.ДатаПодписи              = ДатаПодписи;
	СтруктураРеквизитовФормы.мСохраненныйДок.ЕдиницаИзмерения         = Неопределено;
	СтруктураРеквизитовФормы.мСохраненныйДок.ТочностьЕдиницыИзмерения = СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения;
	СтруктураРеквизитовФормы.мСохраненныйДок.ВидОтчетности            = Перечисления.ВидыОтчетности.РегламентированнаяОтчетность;
	СтруктураРеквизитовФормы.мСохраненныйДок.Вид                      = Вариант;
	СтруктураРеквизитовФормы.мСохраненныйДок.ПредставлениеВида        = РегламентированнаяОтчетность.ПредставлениеВидаДокумента(СтруктураРеквизитовФормы.мСохраненныйДок.Вид);	
	СтруктураРеквизитовФормы.мСохраненныйДок.Комментарий              = Комментарий;
	
	СтруктураРеквизитовФормы.мВариант = Вариант;
	
	// Формируем список сохранения.
	СписокСохранения = Новый Структура();
	
	// Вставляем данные редактируемых ячеек таблицы.
	СписокСохранения.Вставить("ПоказателиОтчета", ПоказателиОтчета);
	
	// Вставляем данные многострочных разделов.
	СписокСохранения.Вставить("ДанныеМногострочныхРазделов", Неопределено);
	
	// Вставляем данные вариантов автозаполнения ячеек.
	СписокСохранения.Вставить("ДанныеВариантовАвтоЗаполнения", Неопределено);
	
	// Вставляем версию формы.
	СписокСохранения.Вставить("ВерсияФормы", СтруктураРеквизитовФормы.мВерсияФормы);
	
	ХранилищеДанных = Новый ХранилищеЗначения(СписокСохранения);
	СтруктураРеквизитовФормы.мСохраненныйДок.ДанныеОтчета = ХранилищеДанных;
	
	// Записываем документ, хранящий данные отчета.
	Попытка
		СтруктураРеквизитовФормы.мСохраненныйДок.Записать();
		РегламентированнаяОтчетность.СохранитьСтатусОтправкиРеглОтчета(ЭтаФорма);
	Исключение
		
		Если НЕ Автосохранение Тогда
			
			Сообщение = Новый СообщениеПользователю;
			
			Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1'"), РегламентированнаяОтчетностьКлиентСервер.СформироватьТекстСообщения(ОписаниеОшибки()));
			
			Сообщение.Сообщить();
			
		КонецЕсли;
		
		СтруктураРеквизитовФормы.мСохраненныйДок = СтруктураРеквизитовФормы.мСохраненныйДок.Ссылка;
		
		Возврат Ложь;
		
	КонецПопытки;
	
	Модифицированность = Ложь;
	
	СтруктураРеквизитовФормы.мСохраненныйДок = СтруктураРеквизитовФормы.мСохраненныйДок.Ссылка;
	
	Если НЕ СтруктураРеквизитовФормы.мБезОткрытияФормы
       И НЕ СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
				   
    	ЗаблокироватьДанныеДляРедактирования(СтруктураРеквизитовФормы.мСохраненныйДок, , ЭтаФорма.УникальныйИдентификатор);

    КонецЕсли;
	
	Если ДанныеДляРазблокирования <> Неопределено Тогда
		РазблокироватьДанныеДляРедактирования(ДанныеДляРазблокирования.Ключ, ДанныеДляРазблокирования.ИдФормы);
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПоказателиОтчетаНаСервере(ПоказателиОтчета)
	
	РегламентированнаяОтчетностьКлиентСервер.ЗаполнитьПоказателиОтчета(ЭтаФорма, ПоказателиОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоказателиОтчетаНаКлиенте(ПоказателиОтчета)
	
	РегламентированнаяОтчетностьКлиентСервер.ЗаполнитьПоказателиОтчета(ЭтаФорма, ПоказателиОтчета);
	
КонецПроцедуры

&НаКлиенте
Функция СобратьДанныеТекущегоТаблПоляНаКлиенте(ИмяТабличногоПоля) Экспорт
	
	Возврат РегламентированнаяОтчетностьКлиентСервер.СобратьДанныеТекущегоТаблПоля(ЭтаФорма, ИмяТабличногоПоля);
	
КонецФункции

&НаСервере
Функция СобратьДанныеТекущегоТаблПоляНаСервере(ИмяТабличногоПоля) Экспорт
	
	Возврат РегламентированнаяОтчетностьКлиентСервер.СобратьДанныеТекущегоТаблПоля(ЭтаФорма, ИмяТабличногоПоля);

КонецФункции

&НаКлиенте
Процедура АктивизироватьЯчейку(Ячейка) Экспорт
	
	ИмяПоляТабличногоДокумента = "ПолеТабличногоДокументаФормаОтчета";
	
	НайденнаяОбласть = ЭтаФорма[ИмяПоляТабличногоДокумента].Области.Найти(Ячейка.ИмяЯчейки);
	
	Если НЕ НайденнаяОбласть = Неопределено Тогда
		
		Элементы[ИмяПоляТабличногоДокумента].ТекущаяОбласть = НайденнаяОбласть;
		
	ИначеЕсли Ячейка.СтрокаПП <> Неопределено И ЗначениеЗаполнено(Ячейка.СтрокаПП) Тогда
		
		НайденнаяОбласть = ЭтаФорма[ИмяПоляТабличногоДокумента].Области.Найти(Ячейка.ИмяЯчейки + Ячейка.СтрокаПП);
		
		Если НЕ НайденнаяОбласть = Неопределено Тогда
			
			Элементы[ИмяПоляТабличногоДокумента].ТекущаяОбласть = НайденнаяОбласть;
			
		Иначе
			
			НайденнаяОбласть = ЭтаФорма[ИмяПоляТабличногоДокумента].Области.Найти(Ячейка.ИмяЯчейки + "_" + Ячейка.СтрокаПП);
			
			Если НЕ НайденнаяОбласть = Неопределено Тогда
				
				Элементы[ИмяПоляТабличногоДокумента].ТекущаяОбласть = НайденнаяОбласть;
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		НайденнаяОбласть = ЭтаФорма[ИмяПоляТабличногоДокумента].Области.Найти(Ячейка.ИмяЯчейки + "1");
		
		Если НЕ НайденнаяОбласть = Неопределено Тогда
			
			Элементы[ИмяПоляТабличногоДокумента].ТекущаяОбласть = НайденнаяОбласть;
			
		Иначе
			
			НайденнаяОбласть = ЭтаФорма[ИмяПоляТабличногоДокумента].Области.Найти(Ячейка.ИмяЯчейки + "_1");
			
			Если НЕ НайденнаяОбласть = Неопределено Тогда
				
				Элементы[ИмяПоляТабличногоДокумента].ТекущаяОбласть = НайденнаяОбласть;
				
			Иначе
				
				Элементы[ИмяПоляТабличногоДокумента].ТекущаяОбласть = ЭтаФорма[ИмяПоляТабличногоДокумента].Область(Ячейка.ИмяЯчейки);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьБланк(Команда)
	
	Отказ = Ложь;
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Команда", Команда);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПоказатьБланкЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередПечатьюРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьБланкЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	Команда = ДополнительныеПараметры.Команда;
		
	Если Команда <> Неопределено Тогда
		Печать(Команда.Имя);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(ВидПечати, НеИзФормыОтчета = Ложь) Экспорт
	
	Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1. Формируется печатная форма...'"), ЭтаФорма.Заголовок), , , БиблиотекаКартинок.Печать);
	
	Если НЕ ПечатьНаСервере(ВидПечати) Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ОткрытьФормуПредварительногоПросмотра(ЭтаФорма, ВидПечати, , СтруктураРеквизитовФормы.СписокПечатаемыхЛистов);
	
КонецПроцедуры

&НаСервере
Функция ПечатьНаСервере(ВидПечати)
	
	Если НЕ РегламентированнаяОтчетность.ПринтерДоступен() Тогда
		
		Сообщение = Новый СообщениеПользователю;
		
		Сообщение.Текст = НСтр("ru='Перед формированием печатных форм необходимо определить в системе принтер и задать его в качестве используемого по умолчанию!'");
		
		Сообщение.Сообщить();
		
		Возврат Ложь;
		
	КонецЕсли;
	
	ТабличныйДокумент = ЭтаФорма["ПолеТабличногоДокументаФормаОтчета"];
	
	СтруктураРеквизитовФормы.мПечатныеФормы.Очистить();
	
	ОбщиеРеквизитыРазделов = Новый Структура;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("НаименованиеЛиста",  "Форма 2-ГС (ГЗ)");
	СтруктураПараметров.Вставить("ОриентацияЛиста",    "Ландшафт");
	СтруктураПараметров.Вставить("ИмяСтраницы",        "2ГСГЗ");
	Структурапараметров.Вставить("ИмяОбластиТаблПоля", "ПолеТабличногоДокументаФормаОтчета");
	СтруктураПараметров.Вставить("ВидПечати",          ВидПечати);
	
	РегламентированнаяОтчетность.ВывестиНаПечатьЛистОбщая(ЭтаФорма.Заголовок, СтруктураПараметров, ТабличныйДокумент, СтруктураРеквизитовФормы.мПечатныеФормы);
	РегламентированнаяОтчетность.УдалитьНомерКорректировки(СтруктураРеквизитовФормы.мПечатныеФормы[0].Значение);
	РегламентированнаяОтчетностьКлиентСервер.ПроставлениеНомеровЛистов(ЭтаФорма, , СтруктураРеквизитовФормы.СписокПечатаемыхЛистов);
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДатуВЯчейкахНаСервере() Экспорт
	
	ЗаполнитьДатуВЯчейках(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДатуВЯчейкахНаКлиенте() Экспорт
	
	ЗаполнитьДатуВЯчейках(ЭтаФорма);
	
КонецПроцедуры

// Заполняет дату подписания отчета должностными лицами, в зависимости от
// наличия данных по этим лицам.
//
&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьДатуВЯчейках(Форма)
	
	// Процедура "заглушка", для роОчистить.
	Возврат;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьИзФайлаЗавершение", ЭтотОбъект);
	
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередЗагрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьЗавершение", ЭтотОбъект);

	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьИзФайлаОтчет();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаОтчет() Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьИзФайлаОтчетЗавершение", ЭтотОбъект);
	
	Если Модифицированность Тогда
		СохранитьНаКлиенте(,ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаОтчетЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	РегламентированнаяОтчетностьЗагрузкаКлиент.ЗагрузитьИзФайлаОтчет(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция ЗагрузкаОтчетаИзФайла(ДанныеДляЗагрузки) Экспорт
	
	Если Модифицированность Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон(НСтр("ru='Отчет ""%1"" не сохранен.'"), ЭтотОбъект.Заголовок);
		Сообщение.Сообщить();
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗагрузкаОтчетаИзФайлаНаСервере(ДанныеДляЗагрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
		
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ЗагрузкаОтчетаИзФайлаНаСервере(ДанныеДляЗагрузки)
	
	РегламентированнаяОтчетность.СформироватьСтруктуруПоказателей(ЭтаФорма);
	
	ПараметрыОтчета = СформироватьСтруктуруПараметров();
	
	ДеревоДляЗагрузки = РегламентированнаяОтчетность.ПолучитьДеревоВыгрузки(ЭтаФорма, "СхемаВыгрузкиXML2013Кв1");
	
	Если НЕ РегламентированнаяОтчетностьЗагрузка.ЗагрузкаОтчетаИзФайла(
		ЭтотОбъект, ПараметрыОтчета, ДанныеДляЗагрузки, ДеревоДляЗагрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ВыгрузитьЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьИзФормыПослеСохраненияЗавершение", ЭтотОбъект);
	Если Модифицированность Тогда
		СохранитьНаКлиенте(, ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьИзФормыПослеСохраненияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ПараметрыВыгрузки = Неопределено;
	ТекстВыгрузки = Неопределено;
	Если НЕ УниверсальнаяВыгрузкаXMLПроверкаВозможностиВыгрузкиНаСервере(ТекстВыгрузки, ПараметрыВыгрузки) Тогда
		ТекстВопроса = НСтр("ru='В ходе выгрузки были найдены ошибки.'");
		КнопкиВопроса = Новый СписокЗначений;
		КнопкиВопроса.Добавить(КодВозвратаДиалога.Да, "Продолжить выгрузку");
		КнопкиВопроса.Добавить(КодВозвратаДиалога.Нет, "Показать ошибки");
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьИзФормыЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, КнопкиВопроса, , КодВозвратаДиалога.Нет);
	Иначе
		ВыгрузитьОтчет();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьИзФормыЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ВыгрузитьОтчет(Истина);
	Иначе
		СпДокОсн = Новый СписокЗначений;
		СпДокОсн.Добавить(СтруктураРеквизитовФормы.мСохраненныйДок);
		ОшибкиОтчета = Новый Массив;
		РегламентированнаяОтчетностьКлиент.ПроверитьРегламентированныеОтчеты(СпДокОсн, ОшибкиОтчета);
		РегламентированнаяОтчетностьКлиент.ОткрытьФормуНавигацииПоОшибкамВыгрузки(ОшибкиОтчета);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузку(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьВыгрузкуЗавершение", ЭтотОбъект);

	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
		
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузкуЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьВыгрузкуОтчета();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузкуОтчета()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьВыгрузкуОтчетаЗавершение", ЭтотОбъект);
	
	Если НЕ ЗначениеЗаполнено(СтруктураРеквизитовФормы.Организация) Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Организация не выбрана! Выгрузка невозможна.'");
		Сообщение.Сообщить();
		Возврат;
		
	КонецЕсли;
	
	Если Модифицированность Тогда
		СохранитьНаКлиенте(,ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ПроверитьВыгрузкуОтчетаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ФормаВыгрузкиРеглОтчета = ПолучитьФорму("Документ.ВыгрузкаРегламентированныхОтчетов.Форма.ФормаДокумента", , ЭтаФорма);
	
	СпДокОсн = Новый СписокЗначений;
	СпДокОсн.Добавить(СтруктураРеквизитовФормы.мСохраненныйДок);
	
	ФормаВыгрузкиРеглОтчета.Проверить(СпДокОсн);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьОтчет(ВыгружатьСОшибками = Ложь) Экспорт
	
	ДополнительныеПараметры = Новый Структура("ВыгружатьСОшибками", ВыгружатьСОшибками);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьОтчетЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	Если НЕ ЗначениеЗаполнено(СтруктураРеквизитовФормы.Организация) Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Организация не выбрана! Выгрузка невозможна.'");
		Сообщение.Сообщить();
		Возврат;
		
	КонецЕсли;
	
	Если Модифицированность Тогда
		СохранитьНаКлиенте(, ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ВыгрузитьОтчетЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ФормаВыгрузкиРеглОтчета = ПолучитьФорму("Документ.ВыгрузкаРегламентированныхОтчетов.Форма.ФормаДокумента", , ЭтаФорма);
	
	СпДокОсн = Новый СписокЗначений;
	СпДокОсн.Добавить(СтруктураРеквизитовФормы.мСохраненныйДок);
	
	ФормаВыгрузкиРеглОтчета.СформироватьИЗаписать(СпДокОсн,,,,,,,,,ДополнительныеПараметры.ВыгружатьСОшибками);
	
КонецПроцедуры

&НаСервере
Функция ПроверитьПоказатели() Экспорт
	
	РегламентированнаяОтчетность.СформироватьСтруктуруПоказателей(ЭтаФорма);
	ДеревоВыгрузки = РегламентированнаяОтчетность.ПолучитьДеревоВыгрузки(ЭтаФорма, "СхемаВыгрузкиXML2013Кв1");
	ПараметрыВыгрузки = СформироватьСтруктуруПараметров();
	Если НЕ ПроверитьВозможностьВыгрузки(ПараметрыВыгрузки, ДеревоВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	ЗаполнитьДанными(ДеревоВыгрузки, ПараметрыВыгрузки);
	Если НЕ РегламентированнаяОтчетность.ПроверитьДанные(ЭтаФорма, ДеревоВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ТекущийФорматВыгрузки(Организация)
	
	Возврат РегламентированнаяОтчетность.ФорматВыгрузкиСтатистическойОтчетностиВЭлектронномВиде(Организация);
	
КонецФункции

&НаКлиенте
Функция УниверсальнаяВыгрузка(ТекстВыгрузки = Неопределено, ПроверятьБлокировки = Истина, ИмяФайлаВыгрузки = "", ВыгружатьСОшибками = Ложь) Экспорт
	
	Перем ПараметрыВыгрузки;
	
	Если ТекущийФорматВыгрузки(СтруктураРеквизитовФормы.Организация) = Неопределено Тогда
		РегламентированнаяОтчетностьКлиент.ПоказатьФормуНастроекФорматаВыгрузкиСтатистическойОтчетностиВЭлектронномВиде(
			СтруктураРеквизитовФормы.Организация, Ложь, Истина);
			
		Возврат "НеВыбранФормат";
	КонецЕсли;
	
	Если Модифицированность Тогда
		
		Если СтрНайти(Заголовок, СтруктураРеквизитовФормы.СтрПериодОтчета) = 0 Тогда
			ТекстЗаголовка = Заголовок + " за " + СтруктураРеквизитовФормы.СтрПериодОтчета;
		Иначе
			ТекстЗаголовка = Заголовок;
		КонецЕсли;
		
		ОргСтр = " (" + СтруктураРеквизитовФормы.НаимОрганизации + ")";
		Если СтрНайти(Заголовок, ОргСтр) = 0 Тогда
			ТекстЗаголовка = ТекстЗаголовка + ОргСтр;
		КонецЕсли;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Отчет """ + ТекстЗаголовка + """ не сохранен.";
		Сообщение.Сообщить();
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Если НЕ УниверсальнаяВыгрузкаXMLПроверкаВозможностиВыгрузкиНаСервере(ТекстВыгрузки, ПараметрыВыгрузки) Тогда
		Если НЕ ВыгружатьСОшибками Тогда
			Возврат Ложь;
		КонецЕсли;
		
		// В 2-ГС (ГЗ) обязательно указание "Уровня гос. органа",
		// согласно которому определяется шаблон выгрузки.
		Если мДанныеОтчета.ПолеТабличногоДокументаФормаОтчета.ОргУровеньГосОргана = "" Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ УниверсальнаяВыгрузкаXMLПроверкаДанныхНаСервере(ТекстВыгрузки, ПараметрыВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИмяФайлаВыгрузки = ПараметрыВыгрузки.ИмяФайла;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция УниверсальнаяВыгрузкаXMLПроверкаВозможностиВыгрузкиНаСервере(ТекстВыгрузки, ПараметрыВыгрузки) Экспорт
	
	РегламентированнаяОтчетность.СформироватьСтруктуруПоказателей(ЭтаФорма);
	ПараметрыВыгрузки = СформироватьСтруктуруПараметров();
	ДеревоВыгрузки = РегламентированнаяОтчетность.ПолучитьДеревоВыгрузки(ЭтаФорма, "СхемаВыгрузкиXML2013Кв1");
	Если НЕ ПроверитьВозможностьВыгрузки(ПараметрыВыгрузки, ДеревоВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция УниверсальнаяВыгрузкаXMLПроверкаДанныхНаСервере(ТекстВыгрузки, ПараметрыВыгрузки) Экспорт
	
	РегламентированнаяОтчетность.СформироватьСтруктуруПоказателей(ЭтаФорма);
	ПараметрыВыгрузки = СформироватьСтруктуруПараметров();
	ДеревоВыгрузки = РегламентированнаяОтчетность.ПолучитьДеревоВыгрузки(ЭтаФорма, "СхемаВыгрузкиXML2013Кв1");
	ЗаполнитьДанными(ДеревоВыгрузки, ПараметрыВыгрузки);
	Если НЕ РегламентированнаяОтчетность.ПроверитьДанные(ЭтаФорма, ДеревоВыгрузки) Тогда
		КоллекцияСписковВыбора = "";
		Возврат Ложь;
	КонецЕсли;
	
	ТекстВыгрузки = РегламентированнаяОтчетность.ВыгрузитьДеревоВXML(ДеревоВыгрузки, ПараметрыВыгрузки);
	
	КоллекцияСписковВыбора = "";
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ПроверитьВозможностьВыгрузки(ПараметрыВыгрузки, ДеревоВыгрузки)
	
	СтатусПроверки = Истина;
	
	ИмяПоляТаблДокумента = "ПолеТабличногоДокументаФормаОтчета";
	
	// Проверка показателей раздела.
	Если ПустаяСтрока(ПараметрыВыгрузки.ОргУровеньГосОргана) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма,
			"Не указан уровень органа власти, необходимый для определения шаблона выгрузки!",
			ИмяПоляТаблДокумента, "ОргУровеньГосОргана");
		СтатусПроверки = Ложь;
		Возврат СтатусПроверки;
	КонецЕсли;
	
	Если ПустаяСтрока(ПараметрыВыгрузки.ОКПО) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
			ЭтаФорма, "Не задан код ОКПО организации!",
			ИмяПоляТаблДокумента, "ОргКодОКПО");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если ПустаяСтрока(ПараметрыВыгрузки.ОргНазв) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
			ЭтаФорма, "Не задано наименование оганизации!",
			ИмяПоляТаблДокумента, "ОргНазв");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если ПустаяСтрока(ПараметрыВыгрузки.ОргИсполнитель) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
			ЭтаФорма, "Не указаны Ф.И.О. должностного лица, ответственного за предоставление статистической информации!",
			ИмяПоляТаблДокумента, "ОргИсполнитель");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если ПустаяСтрока(ПараметрыВыгрузки.ОргДолжностьИсп) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
			ЭтаФорма, "Не указана должность лица, ответственного за предоставление статистической информации!",
			ИмяПоляТаблДокумента, "ОргДолжностьИсп");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если ПустаяСтрока(ПараметрыВыгрузки.ОргТелефонИсп) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
			ЭтаФорма, "Не указан телефон должностного лица, ответственного за предоставление статистической информации!",
			ИмяПоляТаблДокумента, "ОргТелефонИсп");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	// Проверка на незаполненный отчет.
	Если НЕ РегламентированнаяОтчетность.ПроверитьРазделыНаНаличиеИнформации(ЭтаФорма, ДеревоВыгрузки, ИмяПоляТаблДокумента) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(ЭтаФорма, "Отчет не заполнен!", ИмяПоляТаблДокумента, "");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	// КОНТРОЛЬ показателей формы (Указания по заполнению Формы 2-ГС (ГЗ)).
	
	// Для Раздела 1.
	Для НомСтр = 101 По 124 Цикл
		// Гр.3 >= гр.4.
		Если Не (ПараметрыВыгрузки["ПР00001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "03"]
			>= ПараметрыВыгрузки["ПР00001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "04"]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
				ЭтаФорма, "Не выполнено условие контроля: в стр. " + Формат(НомСтр, "ЧГ=")
				+ " раздела 1 данные по графе 3 должны быть больше или равны данным по графе 4!",
				ИмяПоляТаблДокумента, "П000001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "03");
			СтатусПроверки = Ложь;
		КонецЕсли;
		// Гр.4 >= гр.9.
		Если Не (ПараметрыВыгрузки["ПР00001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "04"]
			>= ПараметрыВыгрузки["ПР00001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "09"]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
				ЭтаФорма, "Не выполнено условие контроля: в стр. " + Формат(НомСтр, "ЧГ=")
				+ " раздела 1 данные по графе 4 должны быть больше или равны данным по графе 9!",
				ИмяПоляТаблДокумента, "П000001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "04");
			СтатусПроверки = Ложь;
		КонецЕсли;
		// Гр.5 >= гр.10.
		Если Не (ПараметрыВыгрузки["ПР00001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "05"]
			>= ПараметрыВыгрузки["ПР00001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "10"]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
				ЭтаФорма, "Не выполнено условие контроля: в стр. " + Формат(НомСтр, "ЧГ=")
				+ " раздела 1 данные по графе 5 должны быть больше или равны данным по графе 10!",
				ИмяПоляТаблДокумента, "П000001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "05");
			СтатусПроверки = Ложь;
		КонецЕсли;
		// Гр.6 >= гр.11.
		Если Не (ПараметрыВыгрузки["ПР00001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "06"]
			>= ПараметрыВыгрузки["ПР00001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "11"]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
				ЭтаФорма, "Не выполнено условие контроля: в стр. " + Формат(НомСтр, "ЧГ=")
				+ " раздела 1 данные по графе 6 должны быть больше или равны данным по графе 11!",
				ИмяПоляТаблДокумента, "П000001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "06");
			СтатусПроверки = Ложь;
		КонецЕсли;
		// Гр.7 >= гр.12.
		Если Не (ПараметрыВыгрузки["ПР00001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "07"]
			>= ПараметрыВыгрузки["ПР00001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "12"]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
				ЭтаФорма, "Не выполнено условие контроля: в стр. " + Формат(НомСтр, "ЧГ=")
				+ " раздела 1 данные по графе 7 должны быть больше или равны данным по графе 12!",
				ИмяПоляТаблДокумента, "П000001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "07");
			СтатусПроверки = Ложь;
		КонецЕсли;
		// Гр.8 >= гр.13.
		Если Не (ПараметрыВыгрузки["ПР00001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "08"]
			>= ПараметрыВыгрузки["ПР00001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "13"]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
				ЭтаФорма, "Не выполнено условие контроля: в стр. " + Формат(НомСтр, "ЧГ=")
				+ " раздела 1 данные по графе 8 должны быть больше или равны данным по графе 13!",
				ИмяПоляТаблДокумента, "П000001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "08");
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЦикла;
	Для НомКол = 3 По 13 Цикл
		// Стр.121 = сумме стр.101, 102.
		Если Не (ПараметрыВыгрузки["ПР000010121" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")]
			= ПараметрыВыгрузки["ПР000010101" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")]
			+ ПараметрыВыгрузки["ПР000010102" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
				ЭтаФорма, "Не выполнено условие контроля: по графе " + Формат(НомКол, "ЧГ=")
				+ " раздела 1 данные в стр. 121 должны быть равны сумме данных в строках 101, 102!",
				ИмяПоляТаблДокумента, "П0000010121" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ="));
			СтатусПроверки = Ложь;
		КонецЕсли;
		// Стр.124 <= стр.102.
		Если Не (ПараметрыВыгрузки["ПР000010124" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")]
			<= ПараметрыВыгрузки["ПР000010102" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
				ЭтаФорма, "Не выполнено условие контроля: по графе " + Формат(НомКол, "ЧГ=")
				+ " раздела 1 данные в стр. 124 должны быть меньше или равны данным в стр. 102!",
				ИмяПоляТаблДокумента, "П0000010124" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ="));
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	// Для Справки 1 к разделу 1.
	// Стр. с 131 по 141, 148 <= стр.102, гр.3, разд.1.
	Для НомСтр = 131 По 141 Цикл
		Если НомСтр = 139 Тогда
			Продолжить;
		Конецесли;
		Если Не (ПараметрыВыгрузки["ПР00001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "03"]
			<= ПараметрыВыгрузки["ПР00001010203"]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
				ЭтаФорма, "Не выполнено условие контроля: в стр. " + Формат(НомСтр, "ЧГ=")
				+ " справки 1 к разделу 1 данные должны быть меньше или равны данным в стр. 102 по графе 3 раздела 1!",
				ИмяПоляТаблДокумента, "П000001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "03");
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЦикла;
	Если Не (ПараметрыВыгрузки["ПР00001014803"]
		<= ПараметрыВыгрузки["ПР00001010203"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
			ЭтаФорма, "Не выполнено условие контроля: в стр. 148 справки 1 к разделу 1 данные должны быть меньше или равны данным в стр. 102 по графе 3 раздела 1!",
			ИмяПоляТаблДокумента, "П000001014803");
		СтатусПроверки = Ложь;
	КонецЕсли;
	// Стр.136 ≥ стр.137.
	Если Не (ПараметрыВыгрузки["ПР00001013603"]
		>= ПараметрыВыгрузки["ПР00001013703"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
			ЭтаФорма, "Не выполнено условие контроля: в справке 1 к разделу 1 данные стр. 136 должны быть больше или равны данным в стр. 137!",
			ИмяПоляТаблДокумента, "П000001013603");
		СтатусПроверки = Ложь;
	КонецЕсли;
	// Стр.138 ≥ стр.140.
	Если Не (ПараметрыВыгрузки["ПР00001013803"]
		>= ПараметрыВыгрузки["ПР00001014003"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
			ЭтаФорма, "Не выполнено условие контроля: в справке 1 к разделу 1 данные стр. 138 должны быть больше или равны данным в стр. 140!",
			ИмяПоляТаблДокумента, "П000001013803");
		СтатусПроверки = Ложь;
	КонецЕсли;
	// Стр.142 ≥ стр. с 143 по 145.
	Для НомСтр = 143 По 145 Цикл
		Если Не (ПараметрыВыгрузки["ПР00001014203"]
			>= ПараметрыВыгрузки["ПР00001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "03"]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
				ЭтаФорма, "Не выполнено условие контроля: в справке 1 к разделу 1 данные стр. 142 должны быть больше или равны данным в стр. "
				+ Формат(НомСтр, "ЧГ=") + "!",
				ИмяПоляТаблДокумента, "П000001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "03");
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЦикла;
	// Стр. с 142 по 147 <= стр.121, гр.3, разд.1.
	Для НомСтр = 142 По 147 Цикл
		Если Не (ПараметрыВыгрузки["ПР00001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "03"]
			<= ПараметрыВыгрузки["ПР00001012103"]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
				ЭтаФорма, "Не выполнено условие контроля: в стр. " + Формат(НомСтр, "ЧГ=")
				+ " справки 1 к разделу 1 данные должны быть меньше или равны данным в стр. 121 по графе 3 раздела 1!",
				ИмяПоляТаблДокумента, "П000001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "03");
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЦикла;
	// Стр.146 ≥ суммы стр.147, 148.
	Если Не (ПараметрыВыгрузки["ПР00001014603"]
		>= ПараметрыВыгрузки["ПР00001014703"] + ПараметрыВыгрузки["ПР00001014803"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
			ЭтаФорма, "Не выполнено условие контроля: в справке 1 к разделу 1 данные стр. 146 должны быть больше или равны сумме данных в строках 147, 148!",
			ИмяПоляТаблДокумента, "П000001014603");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	// Для Справки 2 к разделу 1.
	// Сумма стр. с 151 по 153 = стр. 121, гр. 4, разд. 1;
	Если Не (ПараметрыВыгрузки["ПР00001015103"] + ПараметрыВыгрузки["ПР00001015203"] + ПараметрыВыгрузки["ПР00001015303"]
		= ПараметрыВыгрузки["ПР00001012104"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
			ЭтаФорма, "Не выполнено условие контроля: в справке 2 к разделу 1 сумма данных в строках с 151 по 153 должна быть равна данным в стр. 121 по графе 4 раздела 1!",
			ИмяПоляТаблДокумента, "П000001012104");
		СтатусПроверки = Ложь;
	КонецЕсли;
	// Стр.154 >= стр.155.
	Если Не (ПараметрыВыгрузки["ПР00001015403"]
		>= ПараметрыВыгрузки["ПР00001015503"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
			ЭтаФорма, "Не выполнено условие контроля: в справке 2 к разделу 1 данные стр. 154 должны быть больше или равны данным в стр. 155!",
			ИмяПоляТаблДокумента, "П000001015403");
		СтатусПроверки = Ложь;
	КонецЕсли;
	// Стр.154, 155 <= стр.121, гр. 4, разд. 1.
	Для НомСтр = 154 По 155 Цикл
		Если Не (ПараметрыВыгрузки["ПР00001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "03"]
			<= ПараметрыВыгрузки["ПР00001012104"]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
				ЭтаФорма, "Не выполнено условие контроля: в стр. " + Формат(НомСтр, "ЧГ=")
				+ " справки 2 к разделу 1 данные должны быть меньше или равны данным в стр. 121 по графе 4 раздела 1!",
				ИмяПоляТаблДокумента, "П000001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "03");
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЦикла;
	// Стр.161 >= стр.162.
	Если Не (ПараметрыВыгрузки["ПР00001016103"]
		>= ПараметрыВыгрузки["ПР00001016203"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
			ЭтаФорма, "Не выполнено условие контроля: в справке 2 к разделу 1 данные стр. 161 должны быть больше или равны данным в стр. 162!",
			ИмяПоляТаблДокумента, "П000001016103");
		СтатусПроверки = Ложь;
	КонецЕсли;
	// Стр. с 161 по 164 <= стр.121, гр.3, разд.1.
	Для НомСтр = 161 По 164 Цикл
		Если Не (ПараметрыВыгрузки["ПР00001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "03"]
			<= ПараметрыВыгрузки["ПР00001012103"]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
				ЭтаФорма, "Не выполнено условие контроля: в стр. " + Формат(НомСтр, "ЧГ=")
				+ " справки 2 к разделу 1 данные должны быть меньше или равны данным в стр. 121 по графе 3 раздела 1!",
				ИмяПоляТаблДокумента, "П000001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "03");
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЦикла;
	// Стр.165 <= стр.121, гр.4, разд.1, при стр.121, гр.4, разд.1 ≠ 0.
	Если ПараметрыВыгрузки["ПР00001012104"] <> 0 Тогда
		Если Не (ПараметрыВыгрузки["ПР00001016503"] <= ПараметрыВыгрузки["ПР00001012104"]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
				ЭтаФорма, "Не выполнено условие контроля: в справке 2 к разделу 1 данные стр. 165 должны быть меньше или равны данным в стр. 121 по графе 4 раздела 1, если данные в стр. 121 по графе 4 раздела 1 не равны нулю!",
				ИмяПоляТаблДокумента, "П000001016503");
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЕсли;
	// Стр.165, 166 < стр.121, гр.3, разд.1.
	Если ПараметрыВыгрузки["ПР00001010903"] > 0 Тогда
		Для НомСтр = 165 По 166 Цикл
			Если Не (ПараметрыВыгрузки["ПР00001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "03"]
				< ПараметрыВыгрузки["ПР00001012103"]) Тогда
				РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
					ЭтаФорма, "Не выполнено условие контроля: в стр. " + Формат(НомСтр, "ЧГ=")
					+ " справки 2 к разделу 1 данные должны быть меньше данных в стр. 121 по графе 3 раздела 1!",
					ИмяПоляТаблДокумента, "П000001" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "03");
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Для Раздела 2.
	Для НомСтр = 201 По 221 Цикл
		// Сумма гр. с 3 по 8 = гр.4, разд.1.
		ТекСумма = 0;
		Для НомКол = 3 По 8 Цикл
			ТекСумма = ТекСумма + ПараметрыВыгрузки["ПР00002"
				+ Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=")
				+ Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")];
		КонецЦикла;
		Если Не (ТекСумма = ПараметрыВыгрузки["ПР0000101" + Сред(Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ="), 3) + "04"]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
				ЭтаФорма, "Не выполнено условие контроля: в стр. " + Формат(НомСтр, "ЧГ=")
				+ " раздела 2 сумма данных по графам с 3 по 8 должна быть равна данным по графе 4 раздела 1!",
				ИмяПоляТаблДокумента, "П00000101" + Сред(Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ="), 3) + "04");
			СтатусПроверки = Ложь;
		КонецЕсли;
		// Сумма гр.9, 10 < гр. 4, разд.1 при гр.7, 8, разд.1 ≠ 0.
		Если ПараметрыВыгрузки["ПР0000101" + Сред(Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ="), 3) + "07"] <> 0
			И ПараметрыВыгрузки["ПР0000101" + Сред(Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ="), 3) + "08"] <> 0 Тогда
			Если Не (ПараметрыВыгрузки["ПР00002" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "09"]
				+ ПараметрыВыгрузки["ПР00002" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "10"]
				< ПараметрыВыгрузки["ПР0000101" + Сред(Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ="), 3) + "04"]) Тогда
				РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
					ЭтаФорма, "Не выполнено условие контроля: в стр. " + Формат(НомСтр, "ЧГ=")
					+ " раздела 2 сумма данных по графам 9, 10 должна быть меньше данных по графе 4 раздела 1, если данные по графам 7, 8 раздела 1 не равны нулю!",
					ИмяПоляТаблДокумента, "П00000101" + Сред(Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ="), 3) + "04");
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЕсли;
		// Сумма гр.9, 10 = гр.4 (гр.5 + гр.6), разд.1 при гр.7, 8, разд.1 = 0.
		Если ПараметрыВыгрузки["ПР0000101" + Сред(Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ="), 3) + "07"] = 0
			И ПараметрыВыгрузки["ПР0000101" + Сред(Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ="), 3) + "08"] = 0 Тогда
			Если Не (ПараметрыВыгрузки["ПР00002" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "09"]
				+ ПараметрыВыгрузки["ПР00002" + Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ=") + "10"]
				= ПараметрыВыгрузки["ПР0000101" + Сред(Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ="), 3) + "04"]) Тогда
				РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
					ЭтаФорма, "Не выполнено условие контроля: в стр. " + Формат(НомСтр, "ЧГ=")
					+ " раздела 2 сумма данных по графам 9, 10 должна быть равна данным по графе 4 раздела 1, если данные по графам 7, 8 раздела 1 равны нулю!",
					ИмяПоляТаблДокумента, "П00000101" + Сред(Формат(НомСтр, "ЧЦ=4; ЧВН=; ЧГ="), 3) + "04");
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Для Раздела 3.
	// Сумма стр.301, 303, 305, гр.3 = стр.121, гр.4, разд.1.
	Если Не (ПараметрыВыгрузки["ПР00003030103"]
		+ ПараметрыВыгрузки["ПР00003030303"]
		+ ПараметрыВыгрузки["ПР00003030503"]
		= ПараметрыВыгрузки["ПР00001012104"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
			ЭтаФорма, "Не выполнено условие контроля: сумма данных строк 301, 303, 305 раздела 3 по графе 3 должна быть равна данным в стр. 121 раздела 1 по графе 4!",
			ИмяПоляТаблДокумента, "П000001012104");
		СтатусПроверки = Ложь;
	КонецЕсли;
	// Сумма стр.301, 303, 305, гр.4 = стр.121, гр.5, разд.1.
	Если Не (ПараметрыВыгрузки["ПР00003030104"]
		+ ПараметрыВыгрузки["ПР00003030304"]
		+ ПараметрыВыгрузки["ПР00003030504"]
		= ПараметрыВыгрузки["ПР00001012105"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
			ЭтаФорма, "Не выполнено условие контроля: сумма данных строк 301, 303, 305 раздела 3 по графе 4 должна быть равна данным в стр. 121 раздела 1 по графе 5!",
			ИмяПоляТаблДокумента, "П000001012105");
		СтатусПроверки = Ложь;
	КонецЕсли;
	// Сумма стр.301, 303, 305, гр.5 = стр.121, гр.6, разд.1.
	Если Не (ПараметрыВыгрузки["ПР00003030105"]
		+ ПараметрыВыгрузки["ПР00003030305"]
		+ ПараметрыВыгрузки["ПР00003030505"]
		= ПараметрыВыгрузки["ПР00001012106"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
			ЭтаФорма, "Не выполнено условие контроля: сумма данных строк 301, 303, 305 раздела 3 по графе 5 должна быть равна данным в стр. 121 раздела 1 по графе 6!",
			ИмяПоляТаблДокумента, "П000001012106");
		СтатусПроверки = Ложь;
	КонецЕсли;
	// Сумма стр.301, 303, 305, гр.6 = стр.121, гр.7, разд.1.
	Если Не (ПараметрыВыгрузки["ПР00003030106"]
		+ ПараметрыВыгрузки["ПР00003030306"]
		+ ПараметрыВыгрузки["ПР00003030506"]
		= ПараметрыВыгрузки["ПР00001012107"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
			ЭтаФорма, "Не выполнено условие контроля: сумма данных строк 301, 303, 305 раздела 3 по графе 6 должна быть равна данным в стр. 121 раздела 1 по графе 7!",
			ИмяПоляТаблДокумента, "П000001012107");
		СтатусПроверки = Ложь;
	КонецЕсли;
	// Сумма стр.301, 303, 305, гр.7 = стр.121, гр.8, разд.1.
	Если Не (ПараметрыВыгрузки["ПР00003030107"]
		+ ПараметрыВыгрузки["ПР00003030307"]
		+ ПараметрыВыгрузки["ПР00003030507"]
		= ПараметрыВыгрузки["ПР00001012108"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
			ЭтаФорма, "Не выполнено условие контроля: сумма данных строк 301, 303, 305 раздела 3 по графе 7 должна быть равна данным в стр. 121 раздела 1 по графе 8!",
			ИмяПоляТаблДокумента, "П000001012108");
		СтатусПроверки = Ложь;
	КонецЕсли;
	// Сумма стр.302, 304, гр.3 = стр.121, гр.9, разд.1.
	Если Не (ПараметрыВыгрузки["ПР00003030203"]
		+ ПараметрыВыгрузки["ПР00003030403"]
		= ПараметрыВыгрузки["ПР00001012109"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
			ЭтаФорма, "Не выполнено условие контроля: сумма данных строк 302, 304 раздела 3 по графе 3 должна быть равна данным в стр. 121 раздела 1 по графе 9!",
			ИмяПоляТаблДокумента, "П000001012109");
		СтатусПроверки = Ложь;
	КонецЕсли;
	// Сумма стр.302, 304, гр.4 = стр.121, гр.10, разд.1.
	Если Не (ПараметрыВыгрузки["ПР00003030204"]
		+ ПараметрыВыгрузки["ПР00003030404"]
		= ПараметрыВыгрузки["ПР00001012110"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
			ЭтаФорма, "Не выполнено условие контроля: сумма данных строк 302, 304 раздела 3 по графе 4 должна быть равна данным в стр. 121 раздела 1 по графе 10!",
			ИмяПоляТаблДокумента, "П000001012110");
		СтатусПроверки = Ложь;
	КонецЕсли;
	// Сумма стр.302, 304, гр.5 = стр.121, гр.11, разд.1.
	Если Не (ПараметрыВыгрузки["ПР00003030205"]
		+ ПараметрыВыгрузки["ПР00003030405"]
		= ПараметрыВыгрузки["ПР00001012111"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
			ЭтаФорма, "Не выполнено условие контроля: сумма данных строк 302, 304 раздела 3 по графе 5 должна быть равна данным в стр. 121 раздела 1 по графе 11!",
			ИмяПоляТаблДокумента, "П000001012111");
		СтатусПроверки = Ложь;
	КонецЕсли;
	// Сумма стр.302, 304, гр.6 = стр.121, гр.12, разд.1.
	Если Не (ПараметрыВыгрузки["ПР00003030206"]
		+ ПараметрыВыгрузки["ПР00003030406"]
		= ПараметрыВыгрузки["ПР00001012112"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
			ЭтаФорма, "Не выполнено условие контроля: сумма данных строк 302, 304 раздела 3 по графе 6 должна быть равна данным в стр. 121 раздела 1 по графе 12!",
			ИмяПоляТаблДокумента, "П000001012112");
		СтатусПроверки = Ложь;
	КонецЕсли;
	// Сумма стр.302, 304, гр.7 = стр.121, гр.13, разд.1.
	Если Не (ПараметрыВыгрузки["ПР00003030207"]
		+ ПараметрыВыгрузки["ПР00003030407"]
		= ПараметрыВыгрузки["ПР00001012113"]) Тогда
		РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
			ЭтаФорма, "Не выполнено условие контроля: сумма данных строк 302, 304 раздела 3 по графе 7 должна быть равна данным в стр. 121 раздела 1 по графе 13!",
			ИмяПоляТаблДокумента, "П000001012113");
		СтатусПроверки = Ложь;
	КонецЕсли;
	Для НомКол = 3 По 7 Цикл
		// Стр.301 >= стр.302.
		Если Не (ПараметрыВыгрузки["ПР000030301" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")]
			>= ПараметрыВыгрузки["ПР000030302" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
				ЭтаФорма, "Не выполнено условие контроля: по графе " + Формат(НомКол, "ЧГ=")
				+ " раздела 3 данные в стр. 301 должны быть больше или равны данным в стр. 302!",
				ИмяПоляТаблДокумента, "П0000030301" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ="));
			СтатусПроверки = Ложь;
		КонецЕсли;
		// Стр.303 >= стр.304.
		Если Не (ПараметрыВыгрузки["ПР000030303" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")]
			>= ПараметрыВыгрузки["ПР000030304" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
				ЭтаФорма, "Не выполнено условие контроля: по графе " + Формат(НомКол, "ЧГ=")
				+ " раздела 3 данные в стр. 303 должны быть больше или равны данным в стр. 304!",
				ИмяПоляТаблДокумента, "П0000030303" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ="));
			СтатусПроверки = Ложь;
		КонецЕсли;
		// Стр.305 >= суммы стр.306, 307.
		Если Не (ПараметрыВыгрузки["ПР000030305" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")]
			>= ПараметрыВыгрузки["ПР000030306" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")]
			+ ПараметрыВыгрузки["ПР000030307" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ=")]) Тогда
			РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
				ЭтаФорма, "Не выполнено условие контроля: по графе " + Формат(НомКол, "ЧГ=")
				+ " раздела 3 данные в стр. 305 должны быть больше или равны сумме данных в строках 306, 307!",
				ИмяПоляТаблДокумента, "П0000030305" + Формат(НомКол, "ЧЦ=2; ЧВН=; ЧГ="));
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтатусПроверки;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДанными(ДеревоВыгрузки, ПараметрыВыгрузки)
	
	РегламентированнаяОтчетность.ОбработатьУсловныеЭлементы(ЭтаФорма, ПараметрыВыгрузки, ДеревоВыгрузки); // формируем точную структуру XML-макета
	ЗаполнитьДаннымиУзел(ПараметрыВыгрузки, ДеревоВыгрузки); // заполняем дерево данными
	РегламентированнаяОтчетность.ОтсечьНезаполненныеНеобязательныеУзлыСтатистики(ДеревоВыгрузки);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДаннымиУзел(ПараметрыВыгрузки, Узел, НомерСтроки = Неопределено)
	
	СтрокиУзла = Новый Массив;
	Для Каждого Стр Из Узел.Строки Цикл
		СтрокиУзла.Добавить(Стр);
	КонецЦикла;
	
	Для Каждого Стр из СтрокиУзла Цикл
		
		Если ПустаяСтрока(Стр.ЗначениеПоУмолчанию) Тогда
			Если НЕ ПустаяСтрока(Стр.Ключ) Тогда
				ПолныйКодПоказателя = Стр.Ключ + ?(ЗначениеЗаполнено(НомерСтроки), "_" + Формат(НомерСтроки, "ЧГ="), "");
				ЗначениеПоказателя = Неопределено;
				Если ПараметрыВыгрузки.Свойство(ПолныйКодПоказателя, ЗначениеПоказателя) Тогда
					ФорматПредставленияНуля = Неопределено;
					Если ПараметрыВыгрузки.Свойство("ЧН" + ПолныйКодПоказателя, ФорматПредставленияНуля) Тогда
						РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, ЗначениеПоказателя, ФорматПредставленияНуля);
					Иначе
						РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, ЗначениеПоказателя);
					КонецЕсли;
				Иначе
					РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, "");
				КонецЕсли;
			Иначе
				Стр.Значение = "";
			КонецЕсли;
		ИначеЕсли Лев(Стр.ЗначениеПоУмолчанию, 1) = "&" Тогда
			РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, ПараметрыВыгрузки[Сред(Стр.ЗначениеПоУмолчанию, 2)]);
		Иначе
			Стр.Значение = Стр.ЗначениеПоУмолчанию;
			Если НЕ ПустаяСтрока(Стр.Ключ) Тогда
				ПолныйКодПоказателя = Стр.Ключ + ?(ЗначениеЗаполнено(НомерСтроки), "_" + Формат(НомерСтроки, "ЧГ="), "");
				ЗначениеПоказателя = Неопределено;
				Если ПараметрыВыгрузки.Свойство(ПолныйКодПоказателя, ЗначениеПоказателя) Тогда
					ФорматПредставленияНуля = Неопределено;
					Если ПараметрыВыгрузки.Свойство("ЧН" + ПолныйКодПоказателя, ФорматПредставленияНуля) Тогда
						РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, ЗначениеПоказателя, ФорматПредставленияНуля);
					Иначе
						РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, ЗначениеПоказателя);
					КонецЕсли;
				Иначе
					РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, "");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если Стр.Тип = "С" ИЛИ Стр.Тип = "C" Тогда // учтем оба варианта: кириллицу и латиницу
			ЗаполнитьДаннымиУзел(ПараметрыВыгрузки, Стр, НомерСтроки);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СформироватьИмяФайла(ПараметрыВыгрузки, ДопАтрибуты)
	
	Перем ПолученноеЗначение;
	
	КодОКУД        = ДопАтрибуты.OKUD;
	КодФормы       = ДопАтрибуты.idf;
	Периодичность  = ДопАтрибуты.idp;
	КодОКПО        = ПараметрыВыгрузки.ОКПО;
	РасчетныйГод   = ПараметрыВыгрузки.РасчПериод;
	ОтчетныйПериод = ПараметрыВыгрузки.ОтчПериод;
	
	ПараметрыВыгрузки.Свойство("ОКФС", ПолученноеЗначение);
	КодОКФС = ПолученноеЗначение;
	ПараметрыВыгрузки.Свойство("ОКВЭД", ПолученноеЗначение);
	КодОКВЭД = СтрЗаменить(ПолученноеЗначение, ".", "-");
	
	// Преобразование кодов - составляющих имени файла.
	КодыИФ = Новый Структура;
	КодыИФ.Вставить("Документ",       СтруктураРеквизитовФормы.мСохраненныйДок);
	КодыИФ.Вставить("КодФормы",       КодФормы);
	КодыИФ.Вставить("Периодичность",  Периодичность);
	КодыИФ.Вставить("ОтчетныйПериод", ОтчетныйПериод);
	КодыИФ.Вставить("ДатаВыгрузки",   "");
	
	РегламентированнаяОтчетность.КодыДляИмениФайлаВФорматеВыгрузки(КодыИФ);
	
	// Формирование имени файла, в зависимости
	// от формата выгрузки статотчетности.
	Если ПустаяСтрока(КодыИФ.ДатаВыгрузки) Тогда // "старый" формат
		ИмяФайлаXML = КодОКУД + "_"
					+ КодФормы + "_"
					+ Периодичность + "_"
					+ КодОКПО + "_"
					+ РасчетныйГод + "_"
					+ ОтчетныйПериод
					+ ?(ПустаяСтрока(КодОКФС), "", "_" + КодОКФС)
					+ ?(ПустаяСтрока(КодОКВЭД), "", "_" + КодОКВЭД) + ".xml";
	Иначе										 // "новый" формат
		ИмяФайлаXML = КодОКУД + "_"
					+ КодыИФ.КодФормы + "_"
					+ КодыИФ.Периодичность + "_"
					+ КодОКПО + "_"
					+ РасчетныйГод + "_"
					+ КодыИФ.ОтчетныйПериод + "__"
					+ КодыИФ.ДатаВыгрузки + ".xml";
	КонецЕсли;
	
	Возврат ИмяФайлаXML;
	
КонецФункции

&НаСервере
Функция СформироватьСтруктуруПараметров()
	
	Перем ПолученноеЗначение;
	
	ТабДокумент = мДанныеОтчета.ПолеТабличногоДокументаФормаОтчета;
	
	ДопАтрибуты = СформироватьСтруктуруДопАтрибутов();
	
	СтрокаСведений = "ФИОРук";
	СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(
		СтруктураРеквизитовФормы.Организация, СтруктураРеквизитовФормы.мСохраненныйДок.ДатаПодписи, СтрокаСведений);
	
	ПараметрыВыгрузки = Новый Структура;
	
	ДопАтрибуты.Свойство("code", ПолученноеЗначение);
	ПараметрыВыгрузки.Вставить("КодШаблона", ПолученноеЗначение);
	ДопАтрибуты.Свойство("idf", ПолученноеЗначение);
	ПараметрыВыгрузки.Вставить("КодФормы", ПолученноеЗначение);
	ДопАтрибуты.Свойство("shifr", ПолученноеЗначение);
	ПараметрыВыгрузки.Вставить("ШифрФормы", ПолученноеЗначение);
	ДопАтрибуты.Свойство("version", ПолученноеЗначение);
	ПараметрыВыгрузки.Вставить("ВерсияШаблона", ПолученноеЗначение);
	ДопАтрибуты.Свойство("format_version", ПолученноеЗначение);
	ПараметрыВыгрузки.Вставить("ВерсияФормата", ПолученноеЗначение);
	
	ОтчПериод  = Месяц(СтруктураРеквизитовФормы.мСохраненныйДок.ДатаОкончания);
	РасчПериод = Формат(Год(СтруктураРеквизитовФормы.мСохраненныйДок.ДатаОкончания),"ЧГ=0");
	
	ПараметрыВыгрузки.Вставить("ОргУровеньГосОргана", СокрЛП(ТабДокумент.ОргУровеньГосОргана));
	
	ПараметрыВыгрузки.Вставить("ОКПО", СокрЛП(ТабДокумент.ОргКодОКПО));
	ПараметрыВыгрузки.Вставить("ОтчПериод", СокрЛП(ОтчПериод));
	ПараметрыВыгрузки.Вставить("РасчПериод", СокрЛП(РасчПериод));
	ПараметрыВыгрузки.Вставить("ОргНазв", СокрЛП(ТабДокумент.ОргНазв));
	ПараметрыВыгрузки.Вставить("ОргДиректор", СокрЛП(СведенияОбОрганизации.ФИОРук));
	ПараметрыВыгрузки.Вставить("ОргДолжностьИсп", СокрЛП(ТабДокумент.ОргДолжностьИсп));
	ПараметрыВыгрузки.Вставить("ОргИсполнитель", СокрЛП(ТабДокумент.ОргИсполнитель));
	ПараметрыВыгрузки.Вставить("ОргТелефонИсп", СокрЛП(ТабДокумент.ОргТелефонИсп));
	
	// Преобразование выгружаемых атрибутов, в соответствии
	// с форматом выгрузки статотчетности.
	ДопАтрибуты.Свойство("idp", ПолученноеЗначение);
	ПараметрыВыгрузки.Вставить("КодПериодичности", Число(СокрЛП(ПолученноеЗначение)));
	ПараметрыВыгрузки.Вставить("Документ", СтруктураРеквизитовФормы.мСохраненныйДок);
	
	РегламентированнаяОтчетность.АтрибутыВФорматеВыгрузки(ПараметрыВыгрузки);
	
	ПараметрыВыгрузки.Вставить("ИмяФайла", СформироватьИмяФайла(ПараметрыВыгрузки, ДопАтрибуты));
	
	ПараметрыВыгрузки.Вставить("ИмяКлючевогоУзлаСодержательнойЧасти", "row");
	
	Для Каждого ЭлемСтруктуры Из ТабДокумент Цикл
		ПараметрыВыгрузки.Вставить(СтрЗаменить(ЭлемСтруктуры.Ключ, "П0", "ПР"), ЭлемСтруктуры.Значение);
	КонецЦикла;
	
	Возврат ПараметрыВыгрузки;
	
КонецФункции

&НаСервере
Функция СформироватьСтруктуруДопАтрибутов()
	
	ДопАтрибуты = Новый Структура;
	
	Макет = Отчеты[Сред(Лев(ЭтаФорма.ИмяФормы, СтрНайти(ЭтаФорма.ИмяФормы, ".Форма.") - 1), 7)].ПолучитьМакет("АтрибВыгрузкиXML2013Кв1");
	
	Если мДанныеОтчета.ПолеТабличногоДокументаФормаОтчета.ОргУровеньГосОргана = "Федеральный" Тогда
		ИмяОбласти = "АтрФедеральныйУровень";
	Иначе // "Региональный"
		ИмяОбласти = "АтрРегиональныйУровень";
	КонецЕсли;

	МакетПериод = Макет.ПолучитьОбласть(ИмяОбласти);
	Для НомСтр = 1 По МакетПериод.ВысотаТаблицы Цикл
		АтрКлюч = СокрЛП(МакетПериод.Область(НомСтр, 1, НомСтр, 1).Текст);
		Если НЕ ПустаяСтрока(АтрКлюч) Тогда
			АтрКлюч = СтроковыеФункцииКлиентСервер.ЗаменитьОдниСимволыДругими(".- ", АтрКлюч, "___");
			ДопАтрибуты.Вставить(АтрКлюч, СокрЛП(МакетПериод.Область(НомСтр, 2, НомСтр, 2).Текст));
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДопАтрибуты;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ СТЫКОВКИ С СЕРВИСОМ СДАЧИ ОТЧЕТНОСТИ ЧЕРЕЗ ПРЕДСТАВИТЕЛЯ

&НаКлиенте
Процедура ОтправитьЧерезПредставителя(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьЧерезПредставителяЗавершение", ЭтотОбъект);

	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЧерезПредставителяЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиЧерезПредставителяВРегламентированномОтчете(ЭтаФорма, "ФСГС");
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернете(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьВИнтернетеЗавершение", ЭтотОбъект);

	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
		
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернетеЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ПроверитьВИнтернете(ЭтаФорма, "ФСГС");
	
КонецПроцедуры

#Область ПанельОтправкиВКонтролирующиеОрганы

&НаКлиенте
Процедура ОбновитьОтправку(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОбновитьОтправкуИзПанелиОтправки(ЭтаФорма, "ФСГС");
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаПротоколНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьПротоколИзПанелиОтправки(ЭтаФорма, "ФСГС");
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНеотправленноеИзвещение(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтправитьНеотправленноеИзвещениеИзПанелиОтправки(ЭтаФорма, "ФСГС");
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОтправкиНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьСостояниеОтправкиИзПанелиОтправки(ЭтаФорма, "ФСГС");
КонецПроцедуры

&НаКлиенте
Процедура КритическиеОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьКритическиеОшибкиИзПанелиОтправки(ЭтаФорма, "ФСГС");
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаНаименованиеЭтапаНажатие(Элемент)
	
	ПараметрыИзменения = Новый Структура;
	ПараметрыИзменения.Вставить("Форма", ЭтаФорма);
	ПараметрыИзменения.Вставить("Организация", СтруктураРеквизитовФормы.Организация);
	ПараметрыИзменения.Вставить("КонтролирующийОрган",
		ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФСГС"));
	
	РегламентированнаяОтчетностьКлиент.ИзменитьСтатусОтправки(ПараметрыИзменения);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ОтправитьВКонтролирующийОрган(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверкаВозможностиВыгрузкиРегламентированногоОтчетаПередОтправкой", ЭтотОбъект);
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередОтправкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаВозможностиВыгрузкиРегламентированногоОтчетаПередОтправкой(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьВКонтролирующийОрганЗавершение", ЭтотОбъект);
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВКонтролирующийОрганЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ЭтаФорма, "ФСГС");
	
КонецПроцедуры

&НаКлиенте
Процедура  ЗаписатьИЗакрыть(Команда)
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", ЭтаФорма);
	Оповещение = Новый ОписаниеОповещения("ПослеСохраненияФормыЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	СохранитьНаКлиенте(, Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияФормыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	ДополнительныеПараметры.Форма.Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьПрисоединенныеФайлы(Команда)
	
	РегламентированнаяОтчетностьКлиент.СохранитьОтчетИОткрытьФормуПрисоединенныеФайлы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры