#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Вакансия", Вакансия);
	Если Параметры.Свойство("ФизическиеЛица") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	Кандидаты.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Кандидаты КАК Кандидаты
			|ГДЕ
			|	Кандидаты.Вакансия = &Вакансия
			|	И Кандидаты.ФизическоеЛицо В(&ФизическиеЛица)";
		Запрос.УстановитьПараметр("Вакансия", Вакансия);
		Запрос.УстановитьПараметр("ФизическиеЛица", Параметры.ФизическиеЛица);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Кандидаты.Добавить(Выборка.Ссылка);
		КонецЦикла;
	КонецЕсли;
	Если Параметры.Свойство("Кандидат") Тогда
		Кандидаты.Добавить(Параметры.Кандидат);
	КонецЕсли;
	
	ОбновитьОтчетНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	
	ОбновитьОтчетНаСервере();	
	
КонецПроцедуры
 
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьОтчетНаСервере()
	
	ТаблицаОтчета = Новый ТабличныйДокумент;
	ТаблицаОтчета.АвтоМасштаб = Истина;
	ТаблицаОтчета.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ЭлектронноеИнтервью.ТабличныйДокументПодробногоОтчетаПоХарактеристикам(ТаблицаОтчета, Вакансия, Кандидаты);
	
КонецПроцедуры

#КонецОбласти

