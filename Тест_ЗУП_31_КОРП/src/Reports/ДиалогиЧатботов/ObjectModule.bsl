#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КлючВарианта = ЗарплатаКадрыОтчеты.КлючВарианта(КомпоновщикНастроек);
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных,
		НастройкиОтчета, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	
	ДанныеОтчета = Новый ДеревоЗначений;
	ПроцессорВывода.УстановитьОбъект(ДанныеОтчета);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	ОбластиМакета = Новый Структура;
	ИнициализироватьОбластиМакета(ОбластиМакета);
	Для Каждого СтрокаЧатбота Из ДанныеОтчета.Строки Цикл
		ОбластиМакета["Чатбот"].Параметры.Заполнить(СтрокаЧатбота);
		ДокументРезультат.Вывести(ОбластиМакета["Чатбот"]);
		Для Каждого СтрокаЧата Из СтрокаЧатбота.Строки Цикл
			ОбластиМакета["Чат"].Параметры.Заполнить(СтрокаЧата);
			ДокументРезультат.Вывести(ОбластиМакета["Чат"]);
			ДокументРезультат.НачатьГруппуСтрок();
			Отправитель = Неопределено;
			Для Каждого СтрокаСообщения Из СтрокаЧата.Строки Цикл
				Если Отправитель <> СтрокаСообщения.Отправитель Тогда
					Если Отправитель <> Неопределено Тогда
						ДокументРезультат.Вывести(ОбластиМакета["ПустаяСтрока"]);
					КонецЕсли;
					ОбластиМакета["Отправитель"].Параметры.Заполнить(СтрокаСообщения);
					ДокументРезультат.Вывести(ОбластиМакета["Отправитель"]);
					Отправитель = СтрокаСообщения.Отправитель;
				КонецЕсли;
				Если Отправитель = Неопределено Тогда
					Отправитель = СтрокаСообщения.Отправитель;
				КонецЕсли;
				ОбластиМакета["Сообщение"].Параметры.Заполнить(СтрокаСообщения);
				ДокументРезультат.Вывести(ОбластиМакета["Сообщение"]);
			КонецЦикла;
			ДокументРезультат.ЗакончитьГруппуСтрок();
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьОбластиМакета(ОбластиМакета)
	
	ОбластиМакета = Новый Структура;
	
	ИменаОбластей = Новый Массив;
	ИменаОбластей.Добавить("Чатбот");
	ИменаОбластей.Добавить("Чат");
	ИменаОбластей.Добавить("Отправитель");
	ИменаОбластей.Добавить("Сообщение");
	ИменаОбластей.Добавить("ПустаяСтрока");
	
	МакетДиалог = ПолучитьМакет("Диалог");
	Для Каждого ИмяОбласти Из ИменаОбластей Цикл
		ОбластиМакета.Вставить(ИмяОбласти, МакетДиалог.ПолучитьОбласть(ИмяОбласти));
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли