#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ЗаймыСотрудникам");
	НастройкиВарианта.Описание =
		НСтр("ru = 'Список займов с помесячной информацией о погашении основного долга и 
		|процентов по договору'");
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОтчетОПогашенииЗайма(Документ) Экспорт
	
	ОтчетОбъект = Отчеты.ЗаймыСотрудникам.Создать();
	
	НастройкиОтчета = ОтчетОбъект.КомпоновщикНастроек.Настройки;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(НастройкиОтчета.Отбор, "ДоговорЗайма", Документ,,, Истина);
	
	Заголовок = НастройкиОтчета.ПараметрыВывода.Элементы.Найти("Заголовок");
	Заголовок.Значение = НСтр("ru = 'Отчет о погашении займа'");
	
	ПустаяДата = Дата(1,1,1);
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Период",			Новый СтандартныйПериод(ПустаяДата, ПустаяДата));
	СтруктураПараметров.Вставить("НачалоПериода",	ПустаяДата);
	СтруктураПараметров.Вставить("КонецПериода",	ПустаяДата);
	
	Для каждого ПараметрЗаполнения Из СтруктураПараметров Цикл
		
		ПараметрКомпоновкиДанных = Новый ПараметрКомпоновкиДанных(ПараметрЗаполнения.Ключ);
		ЗначениеПараметра = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрКомпоновкиДанных);
		Если ЗначениеПараметра <> Неопределено Тогда
			ЗначениеПараметра.Значение = ПараметрЗаполнения.Значение;
			ЗначениеПараметра.Использование = Истина;
		Иначе
			НовыйПараметр = НастройкиОтчета.ПараметрыДанных.Элементы.Добавить();
			НовыйПараметр.Параметр = ПараметрКомпоновкиДанных;
			НовыйПараметр.Значение = ПараметрЗаполнения.Значение;
			НовыйПараметр.Использование = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	ОтчетОбъект.КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиОтчета);
	
	ДокументРезультат = Новый ТабличныйДокумент;
	
	ОтчетОбъект.СкомпоноватьРезультат(ДокументРезультат);
	
	Возврат ДокументРезультат;
	
КонецФункции

#КонецОбласти

#КонецЕсли