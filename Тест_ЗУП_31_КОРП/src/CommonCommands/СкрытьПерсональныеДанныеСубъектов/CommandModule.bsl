#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПараметрКоманды.Количество() = 1 Тогда
		
		ТекстВопроса = НСтр("ru = 'Персональные данные субъекта «%1» будут скрыты.
			|
			|Продолжить?'");
		ТекстВопроса = СтрШаблон(ТекстВопроса, ПараметрКоманды[0]);
		
	Иначе
		
		ТекстВопроса = НСтр("ru = 'Персональные данные субъектов будут скрыты.
			|
			|Продолжить?'");
		
	КонецЕсли;
				   
	Оповещение = Новый ОписаниеОповещения("ОбработкаКомандыЗавершение", ЭтотОбъект, ПараметрКоманды);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаКомандыЗавершение(Результат, Субъекты) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		СкрытьПерсональныеДанныеСубъектов(Субъекты);
		Оповестить("СкрытыПерсональныеДанные");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СкрытьПерсональныеДанныеСубъектов(Субъекты)
	
	ОтбираемыеСубъекты = Новый Массив;
	НастройкиСубъектов = РегистрыСведений.СубъектыДляСкрытияПерсональныхДанных.Прочитать(Субъекты);
	
	Если ЗначениеЗаполнено(НастройкиСубъектов) Тогда 
		
		Для Каждого НастройкиСкрытия Из НастройкиСубъектов Цикл
			
			Субъект = НастройкиСкрытия.Ключ;
			Настройки = НастройкиСкрытия.Значение;
			
			Если Настройки.Состояние = Перечисления.СостоянияСубъектовДляСкрытия.СкрытиеОтменено 
				Или Настройки.Состояние = Перечисления.СостоянияСубъектовДляСкрытия.СкрытиеВыполнено Тогда
				
				Если Настройки.Состояние = Перечисления.СостоянияСубъектовДляСкрытия.СкрытиеОтменено Тогда
					ТекстПричины = НСтр("ru = 'Скрытие ранее отменено по причине: %1.'");
					ТекстПричины = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПричины, Настройки.ПричинаОтменыСкрытия);
				Иначе
					ТекстПричины = НСтр("ru = 'Скрытие уже выполнено.'");
				КонецЕсли;
			
				ТекстСообщения = НСтр("ru = 'Персональные данные субъекта «%1» не могут быть скрыты.
					|%2'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Субъект, ТекстПричины);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);

				Продолжить;
				
			КонецЕсли;
			
			ОтбираемыеСубъекты.Добавить(Субъект);
			
		КонецЦикла;
		
	Иначе
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ОтбираемыеСубъекты, Субъекты);
	КонецЕсли;
	
	ЗащитаПерсональныхДанных.СкрытьПерсональныеДанныеСубъектов(ОтбираемыеСубъекты);
	
КонецПроцедуры

#КонецОбласти

