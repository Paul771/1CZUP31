#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Ведомость = ПараметрКоманды;
	Форма = ПараметрыВыполненияКоманды.Источник;
	
	ЗначенияЗаполнения = ЗначенияЗаполнения(Ведомость);
	
	Если ЗначенияЗаполнения = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ОткрытьФорму("Документ.СведенияОНезачисленнойЗарплате.ФормаОбъекта", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно); 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЗначенияЗаполнения(Ведомость)
	
	ЗначенияЗаполнения = Неопределено;
	
	ДокументыТребующиеПроведения = ОбщегоНазначения.ПроверитьПроведенностьДокументов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ведомость));
	Если ДокументыТребующиеПроведения.Количество() > 0 Тогда
		ТекстСообщения = НСтр("ru = 'Ввод сведений о незачисленной зарплате возможен только для проведенной ведомости'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат ЗначенияЗаполнения;
	КонецЕсли;
	
	Если Документы.СведенияОНезачисленнойЗарплате.ЕстьПоВедомости(Ведомость) Тогда
		ТекстСообщения = НСтр("ru = 'По ведомости уже введены сведения о незачисленной зарплате'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат ЗначенияЗаполнения;
	КонецЕсли;
	
	Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ведомость, "Организация");
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Организация", Организация);
	ЗначенияЗаполнения.Вставить("Ведомость", Ведомость);
	
	Возврат ЗначенияЗаполнения
	
КонецФункции

#КонецОбласти
