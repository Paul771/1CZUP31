#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ПервоначальноеЗаполнениеИОбновлениеИнформационнойБазы

Процедура ЗаполнитьДатыБлижайшейОценки() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РезультатыСпециальнойОценкиУсловийТруда.Период,
	|	РезультатыСпециальнойОценкиУсловийТруда.Регистратор,
	|	РезультатыСпециальнойОценкиУсловийТруда.РабочееМесто,
	|	РезультатыСпециальнойОценкиУсловийТруда.Организация,
	|	РезультатыСпециальнойОценкиУсловийТруда.УдалитьДатаБлижайшейОценки КАК ДатаБлижайшейОценки
	|ИЗ
	|	РегистрСведений.РезультатыСпециальнойОценкиУсловийТруда КАК РезультатыСпециальнойОценкиУсловийТруда
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеДатыСпециальнойОценкиУсловийТруда КАК ПлановыеДатыСпециальнойОценкиУсловийТруда
	|		ПО РезультатыСпециальнойОценкиУсловийТруда.Период = ПлановыеДатыСпециальнойОценкиУсловийТруда.Период
	|			И РезультатыСпециальнойОценкиУсловийТруда.РабочееМесто = ПлановыеДатыСпециальнойОценкиУсловийТруда.РабочееМесто
	|ГДЕ
	|	РезультатыСпециальнойОценкиУсловийТруда.УдалитьДатаБлижайшейОценки <> ПлановыеДатыСпециальнойОценкиУсловийТруда.ДатаБлижайшейОценки
	|
	|УПОРЯДОЧИТЬ ПО
	|	РезультатыСпециальнойОценкиУсловийТруда.Регистратор,
	|	РезультатыСпециальнойОценкиУсловийТруда.НомерСтроки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
		
		// Создать набор записей
		НаборЗаписей = РегистрыСведений.ПлановыеДатыСпециальнойОценкиУсловийТруда.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		
		Пока Выборка.Следующий() Цикл
			
			НоваяЗаписьНабора = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗаписьНабора, Выборка);
			
		КонецЦикла;
		
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли