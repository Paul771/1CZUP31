#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Получает все завершенные попытки тестов
//
Функция ПройденныеТесты(Учащийся, Контекст) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзученныеЭлементыЭлектронныхКурсов.Период КАК Дата,
		|	ИзученныеЭлементыЭлектронныхКурсов.Результат,
		|	ИзученныеЭлементыЭлектронныхКурсов.ВыставляетсяОценка,
		|	ИзученныеЭлементыЭлектронныхКурсов.ПроверяетсяПреподавателем,
		|	ИзученныеЭлементыЭлектронныхКурсов.ПроверенПреподавателем,
		|	ИзученныеЭлементыЭлектронныхКурсов.ДатаНачала,
		|	ИзученныеЭлементыЭлектронныхКурсов.ДатаОкончания,
		|	ИзученныеЭлементыЭлектронныхКурсов.Регистратор КАК СессияВыполненияТеста,
		|	ИзученныеЭлементыЭлектронныхКурсов.ЭлементСодержания,
		|	ИзученныеЭлементыЭлектронныхКурсов.ЭлементСодержания.Наименование КАК НаименованиеЭлементаСодержания,
		|	ИзученныеЭлементыЭлектронныхКурсов.ЭлектронныйКурс,
		|	ИзученныеЭлементыЭлектронныхКурсов.ЭлектронныйКурс.Наименование КАК НаименованиеЭлектронногоРесурса
		|ИЗ
		|	РегистрСведений.ИзученныеЭлементыЭлектронныхКурсов КАК ИзученныеЭлементыЭлектронныхКурсов
		|ГДЕ
		|	ИзученныеЭлементыЭлектронныхКурсов.Учащийся = &Учащийся
		|	И ИзученныеЭлементыЭлектронныхКурсов.Контекст = &Контекст
		|	И ИзученныеЭлементыЭлектронныхКурсов.ЭтоТест = ИСТИНА
		|	И ИзученныеЭлементыЭлектронныхКурсов.Завершено = ИСТИНА";
	
	Запрос.УстановитьПараметр("Учащийся", Учащийся);
	Запрос.УстановитьПараметр("Контекст", Контекст);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выбрать();
	
КонецФункции

Функция ЭлементСодержанияПройден(ЭлементСодержания, ИзучениеЭлектронногоКурса) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ЭлементСодержания)
		ИЛИ НЕ ЗначениеЗаполнено(ИзучениеЭлектронногоКурса) Тогда
		
		Возврат Ложь;

	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИзученныеЭлементыЭлектронныхКурсов.Регистратор
		|ИЗ
		|	РегистрСведений.ИзученныеЭлементыЭлектронныхКурсов КАК ИзученныеЭлементыЭлектронныхКурсов
		|ГДЕ
		|	ИзученныеЭлементыЭлектронныхКурсов.ИзучениеЭлектронногоКурса = &ИзучениеЭлектронногоКурса
		|	И ИзученныеЭлементыЭлектронныхКурсов.ЭлементСодержания = &ЭлементСодержания
		|	И ИзученныеЭлементыЭлектронныхКурсов.Завершено = ИСТИНА";
	
	Запрос.УстановитьПараметр("ИзучениеЭлектронногоКурса", ИзучениеЭлектронногоКурса);
	Запрос.УстановитьПараметр("ЭлементСодержания", ЭлементСодержания);
	
	УстановитьПривилегированныйРежим(Истина); // Для доступа учащимся
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли