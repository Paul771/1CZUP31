#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныеПроцедурыИФункции

// Функция проверяет, есть ли подтверждение выплаты по ведомости.
//
// Параметры:
//	Ведомость - документ-ссылка.
//
// Возвращаемое значение:
//	Булево
//
Функция ЕстьПоВедомости(Ведомость) Экспорт
	
	ЕстьПодтверждение = Ложь;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Поле1
	|ИЗ
	|	РегистрСведений.ДатыВыплатыДоходов КАК ДатыВыплатыДоходов
	|ГДЕ
	|	ДатыВыплатыДоходов.ОплаченнаяВедомость = &Ведомость";
	Запрос.УстановитьПараметр("Ведомость", Ведомость);
	
	ЕстьПодтверждение = Не Запрос.Выполнить().Пустой();
	
	Возврат ЕстьПодтверждение;
	
КонецФункции

// Осуществляет поиск документов подтверждения выплаты доходов по ведомости.
// 
// Параметры:
//	Ведомость - ДокументСсылка
//
// Возвращаемое значение:
//	ДокументСсылка - регистратор подтверждения выплаты дохода по ведомости (ДокументСсылка.ПодтверждениеВыплатыДоходов).
//
Функция НайтиПоВедомости(Ведомость) Экспорт
	
	ПодтверждениеВыплаты = Документы.ПодтверждениеВыплатыДоходов.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДатыВыплатыДоходов.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрСведений.ДатыВыплатыДоходов КАК ДатыВыплатыДоходов
	|ГДЕ
	|	ДатыВыплатыДоходов.ОплаченнаяВедомость = &Ведомость";
	Запрос.УстановитьПараметр("Ведомость", Ведомость);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ПодтверждениеВыплаты = Выборка.Регистратор;
	КонецЕсли;
	
	Возврат ПодтверждениеВыплаты;
	
КонецФункции

#КонецОбласти
	
#КонецЕсли