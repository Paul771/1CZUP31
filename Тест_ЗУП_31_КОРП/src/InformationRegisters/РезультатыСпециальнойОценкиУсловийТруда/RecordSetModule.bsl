#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	// Получаем предыдущее состояние набора
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеНабора.РабочееМесто,
	|	ДанныеНабора.Период,
	|	ДанныеНабора.КлассУсловийТруда
	|ПОМЕСТИТЬ ВТПредыдущийНаборРезультатовСпециальнойОценкиУсловийТруда
	|ИЗ
	|	РегистрСведений.РезультатыСпециальнойОценкиУсловийТруда КАК ДанныеНабора
	|ГДЕ
	|	ДанныеНабора.Регистратор = &Регистратор";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.Выполнить();
	
	ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	// Объединяем данные до и после записи набора
	// таблицу ВТПредыдущийНаборРезультатовСпециальнойОценкиУсловийТруда получаем перед записью набора.
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеНабора.РабочееМесто,
	|	ДанныеНабора.Период,
	|	ДанныеНабора.КлассУсловийТруда
	|ПОМЕСТИТЬ ВТТекущийНаборРезультатовСпециальнойОценкиУсловийТруда
	|ИЗ
	|	РегистрСведений.РезультатыСпециальнойОценкиУсловийТруда КАК ДанныеНабора
	|ГДЕ
	|	ДанныеНабора.Регистратор = &Регистратор";
	
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПредыдущийНабор.РабочееМесто КАК Должность,
	|	НАЧАЛОПЕРИОДА(ПредыдущийНабор.Период, МЕСЯЦ) КАК Период,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаРегистрацииИзменений,
	|	NULL КАК КлассУсловийТруда,
	|	ИСТИНА КАК УдалитьЗапись
	|ИЗ
	|	ВТПредыдущийНаборРезультатовСпециальнойОценкиУсловийТруда КАК ПредыдущийНабор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТекущийНаборРезультатовСпециальнойОценкиУсловийТруда КАК ТекущийНабор
	|		ПО ПредыдущийНабор.РабочееМесто = ТекущийНабор.РабочееМесто
	|			И ПредыдущийНабор.Период = ТекущийНабор.Период
	|ГДЕ
	|	ТекущийНабор.РабочееМесто ЕСТЬ NULL 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТекущийНабор.РабочееМесто,
	|	НАЧАЛОПЕРИОДА(ТекущийНабор.Период, МЕСЯЦ),
	|	НАЧАЛОПЕРИОДА(ТекущийНабор.Период, МЕСЯЦ),
	|	ТекущийНабор.КлассУсловийТруда,
	|	ЛОЖЬ
	|ИЗ
	|	ВТТекущийНаборРезультатовСпециальнойОценкиУсловийТруда КАК ТекущийНабор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПредыдущийНаборРезультатовСпециальнойОценкиУсловийТруда КАК ПредыдущийНабор
	|		ПО ТекущийНабор.РабочееМесто = ПредыдущийНабор.РабочееМесто
	|			И ТекущийНабор.Период = ПредыдущийНабор.Период
	|ГДЕ
	|	ПредыдущийНабор.РабочееМесто ЕСТЬ NULL "; 
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.КлассыУсловийТрудаПоДолжностям.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Должность.Установить(Выборка.Должность);
		НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
		
		Если Выборка.УдалитьЗапись Тогда
			НаборЗаписей.Записать();
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.КлассУсловийТруда) Тогда
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		КонецЕсли;
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

