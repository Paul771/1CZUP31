#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвиженияПоПериодамДействияДоговоров() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДоговорАвторскогоЗаказа.Ссылка КАК Регистратор,
		|	ДоговорАвторскогоЗаказа.Организация,
		|	ДоговорАвторскогоЗаказа.Сотрудник,
		|	ДоговорАвторскогоЗаказа.ФизическоеЛицо,
		|	ДоговорАвторскогоЗаказа.ДатаНачала,
		|	ДоговорАвторскогоЗаказа.ДатаОкончания,
		|	ДоговорАвторскогоЗаказа.Подразделение
		|ПОМЕСТИТЬ ВТДоговорыГПХ
		|ИЗ
		|	Документ.ДоговорАвторскогоЗаказа КАК ДоговорАвторскогоЗаказа
		|ГДЕ
		|	ДоговорАвторскогоЗаказа.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДоговорРаботыУслуги.Ссылка,
		|	ДоговорРаботыУслуги.Организация,
		|	ДоговорРаботыУслуги.Сотрудник,
		|	ДоговорРаботыУслуги.ФизическоеЛицо,
		|	ДоговорРаботыУслуги.ДатаНачала,
		|	ДоговорРаботыУслуги.ДатаОкончания,
		|	ДоговорРаботыУслуги.Подразделение
		|ИЗ
		|	Документ.ДоговорРаботыУслуги КАК ДоговорРаботыУслуги
		|ГДЕ
		|	ДоговорРаботыУслуги.Проведен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДоговорыГПХ.Регистратор КАК Регистратор,
		|	ДоговорыГПХ.Организация,
		|	ДоговорыГПХ.Сотрудник,
		|	ДоговорыГПХ.ФизическоеЛицо,
		|	ДоговорыГПХ.ДатаНачала,
		|	ДоговорыГПХ.ДатаОкончания,
		|	ДоговорыГПХ.Подразделение
		|ИЗ
		|	ВТДоговорыГПХ КАК ДоговорыГПХ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПериодыДействияДоговоровГражданскоПравовогоХарактера КАК ПериодыДействияДоговоровГражданскоПравовогоХарактера
		|		ПО ДоговорыГПХ.Регистратор = ПериодыДействияДоговоровГражданскоПравовогоХарактера.Регистратор
		|ГДЕ
		|	ПериодыДействияДоговоровГражданскоПравовогоХарактера.Регистратор ЕСТЬ NULL 
		|
		|УПОРЯДОЧИТЬ ПО
		|	Регистратор";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Набор = РегистрыСведений.ПериодыДействияДоговоровГражданскоПравовогоХарактера.СоздатьНаборЗаписей();
			Набор.Отбор.Регистратор.Установить(Выборка.Регистратор);
			
			ЗаполнитьЗначенияСвойств(Набор.Добавить(), Выборка);
			
			Набор.ОбменДанными.Загрузка = Истина;
			Набор.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
			
			Набор.Записать();
			
		КонецЦикла; 
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
