#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьГоловнуюОрганизацию() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПлановыйФОТ.Сотрудник
		|ПОМЕСТИТЬ ВТСотрудники
		|ИЗ
		|	РегистрСведений.ПлановыйФОТ КАК ПлановыйФОТ
		|ГДЕ
		|	ПлановыйФОТ.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Сотрудники.Сотрудник
		|ИЗ
		|	ВТСотрудники КАК Сотрудники";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПлановыйФОТ.Сотрудник КАК Сотрудник,
			|	ПлановыйФОТ.Сотрудник.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	ПлановыйФОТ.*
			|ИЗ
			|	РегистрСведений.ПлановыйФОТ КАК ПлановыйФОТ
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудники КАК Сотрудники
			|		ПО ПлановыйФОТ.Сотрудник = Сотрудники.Сотрудник
			|
			|УПОРЯДОЧИТЬ ПО
			|	Сотрудник";
			
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("Сотрудник") Цикл
			
			НаборЗаписей = РегистрыСведений.ПлановыйФОТ.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Сотрудник.Установить(Выборка.Сотрудник);
			
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
			КонецЦикла; 
			
			НаборЗаписей.ОбменДанными.Загрузка = Истина;
			НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
			НаборЗаписей.Записать();
			
		КонецЦикла; 
		
	КонецЕсли; 
	
КонецПроцедуры


#КонецОбласти

#КонецЕсли