#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьЦелевоеПодразделение(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ПланОбученияРазвитияМероприятия.Ссылка КАК Регистратор,
		|	ПланОбученияРазвитияМероприятия.Ссылка.Подразделение КАК Подразделение,
		|	ПланОбученияРазвитияМероприятия.Мероприятие КАК Мероприятие,
		|	ПланОбученияРазвитияМероприятия.ДатаНачала КАК ДатаНачала,
		|	ПланОбученияРазвитияМероприятия.ДатаОкончания КАК ДатаОкончания,
		|	ПланОбученияРазвитияМероприятия.КоличествоУчебныхЧасов КАК КоличествоУчебныхЧасов,
		|	ПланОбученияРазвитияМероприятия.СуммаРасходов КАК СуммаРасходов,
		|	ПланОбученияРазвитияМероприятия.КоличествоСотрудников КАК КоличествоСотрудников,
		|	ПланОбученияРазвитияМероприятия.Ссылка.ДатаНачалаПланирования КАК ДатаНачалаПланирования,
		|	ПланОбученияРазвитияМероприятия.Ссылка.ДатаОкончанияПланирования КАК ДатаОкончанияПланирования,
		|	ПланОбученияРазвитияМероприятия.Заявка КАК Заявка,
		|	ПланОбученияРазвитияМероприятия.Подразделение КАК ЦелевоеПодразделение
		|ИЗ
		|	Документ.ПланОбученияРазвития.Мероприятия КАК ПланОбученияРазвитияМероприятия
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПланОбученияПоМероприятиям КАК ПланОбученияПоМероприятиям
		|		ПО ПланОбученияРазвитияМероприятия.Ссылка = ПланОбученияПоМероприятиям.Регистратор
		|			И ПланОбученияРазвитияМероприятия.НомерСтроки = ПланОбученияПоМероприятиям.НомерСтроки
		|			И ПланОбученияРазвитияМероприятия.Подразделение <> ПланОбученияПоМероприятиям.ЦелевоеПодразделение
		|
		|УПОРЯДОЧИТЬ ПО
		|	Регистратор";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
		НаборЗаписей = РегистрыСведений.ПланОбученияПоМероприятиям.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
		КонецЦикла;
		
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Записать(Истина);
	КонецЦикла;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Выборка.Количество() = 0);

КонецПроцедуры

#КонецОбласти

#КонецЕсли
