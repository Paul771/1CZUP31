#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ВосстановитьДатыВозвратныхСобытий() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РазрядыКатегорииСотрудников.Регистратор
		|ПОМЕСТИТЬ ВТРегистраторы
		|ИЗ
		|	РегистрСведений.РазрядыКатегорииСотрудников КАК РазрядыКатегорииСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
		|		ПО РазрядыКатегорииСотрудников.Период = КадроваяИсторияСотрудников.Период
		|			И РазрядыКатегорииСотрудников.Регистратор = КадроваяИсторияСотрудников.Регистратор
		|			И РазрядыКатегорииСотрудников.Сотрудник = КадроваяИсторияСотрудников.Сотрудник
		|			И РазрядыКатегорииСотрудников.ДействуетДо <> КадроваяИсторияСотрудников.ДействуетДо
		|ГДЕ
		|	РазрядыКатегорииСотрудников.ДействуетДо <> ДАТАВРЕМЯ(1, 1, 1)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Регистраторы.Регистратор
		|ИЗ
		|	ВТРегистраторы КАК Регистраторы";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	РазрядыКатегорииСотрудников.Период,
			|	РазрядыКатегорииСотрудников.Регистратор КАК Регистратор,
			|	РазрядыКатегорииСотрудников.Сотрудник,
			|	РазрядыКатегорииСотрудников.РазрядКатегория,
			|	МАКСИМУМ(КадроваяИсторияСотрудников.ДействуетДо) КАК ДействуетДо,
			|	РазрядыКатегорииСотрудников.РазрядКатегорияПоОкончании
			|ИЗ
			|	ВТРегистраторы КАК Регистраторы
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РазрядыКатегорииСотрудников КАК РазрядыКатегорииСотрудников
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
			|			ПО РазрядыКатегорииСотрудников.Период = КадроваяИсторияСотрудников.Период
			|				И РазрядыКатегорииСотрудников.Регистратор = КадроваяИсторияСотрудников.Регистратор
			|				И РазрядыКатегорииСотрудников.Сотрудник = КадроваяИсторияСотрудников.Сотрудник
			|		ПО Регистраторы.Регистратор = РазрядыКатегорииСотрудников.Регистратор
			|
			|СГРУППИРОВАТЬ ПО
			|	РазрядыКатегорииСотрудников.Период,
			|	РазрядыКатегорииСотрудников.Регистратор,
			|	РазрядыКатегорииСотрудников.Сотрудник,
			|	РазрядыКатегорииСотрудников.РазрядКатегория,
			|	РазрядыКатегорииСотрудников.РазрядКатегорияПоОкончании
			|
			|УПОРЯДОЧИТЬ ПО
			|	Регистратор";
			
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
			
			НаборЗаписей = РегистрыСведений.РазрядыКатегорииСотрудников.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
			КонецЦикла; 
			
			НаборЗаписей.ОбменДанными.Загрузка = Истина;
			НаборЗаписей.Записать();
			
		КонецЦикла; 
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли