#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция ИспользуемыеВакансии(Чатбот) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Чатбот", Чатбот);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВакансииЧатботов.Чатбот КАК Чатбот,
		|	ВакансииЧатботов.Вакансия КАК Вакансия,
		|	ВакансииЧатботов.КраткоеНаименование КАК КраткоеНаименование,
		|	ВакансииЧатботов.Описание КАК Описание,
		|	ВакансииЧатботов.Используется КАК Используется
		|ИЗ
		|	РегистрСведений.ВакансииЧатботов КАК ВакансииЧатботов
		|ГДЕ
		|	ВакансииЧатботов.Чатбот = &Чатбот
		|	И ВакансииЧатботов.Используется = ИСТИНА
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВакансииЧатботов.Порядок";
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ЗаполнитьСведенияВакансии(СведенияВакансии, Вакансия, Чатбот = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Вакансия", Вакансия);
	Запрос.УстановитьПараметр("Чатбот", Чатбот);
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВакансииЧатботов.Чатбот КАК Чатбот,
		|	ВакансииЧатботов.Вакансия КАК Вакансия,
		|	ВакансииЧатботов.КраткоеНаименование КАК КраткоеНаименование,
		|	ВакансииЧатботов.Описание КАК Описание,
		|	ВакансииЧатботов.Используется КАК Используется,
		|	ВакансииЧатботов.Приоритет КАК Приоритет
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВакансииЧатботов.Чатбот КАК Чатбот,
		|		ВакансииЧатботов.Вакансия КАК Вакансия,
		|		ВакансииЧатботов.КраткоеНаименование КАК КраткоеНаименование,
		|		ВакансииЧатботов.Описание КАК Описание,
		|		ВакансииЧатботов.Используется КАК Используется,
		|		ВЫБОР
		|			КОГДА ВакансииЧатботов.Чатбот = &Чатбот
		|				ТОГДА 1
		|			ИНАЧЕ 2
		|		КОНЕЦ КАК Приоритет
		|	ИЗ
		|		РегистрСведений.ВакансииЧатботов КАК ВакансииЧатботов
		|	ГДЕ
		|		ВакансииЧатботов.Вакансия = &Вакансия) КАК ВакансииЧатботов
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(СведенияВакансии, Выборка);
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#КонецЕсли