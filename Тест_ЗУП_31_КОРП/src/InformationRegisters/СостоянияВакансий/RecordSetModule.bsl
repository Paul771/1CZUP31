#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	// Открытые - в начало дня, остальные - в конец
	Для Каждого ЭлементНабора Из ЭтотОбъект Цикл
		ЭлементНабора.Период = ?(ЭлементНабора.Состояние = Перечисления.СостоянияВакансии.Открыта,
			НачалоДня(ЭлементНабора.Период), КонецДня(ЭлементНабора.Период));
	КонецЦикла;
	
	// Переустановим отбор по периоду, если он указан
	Если Отбор.Период.Использование И ЭтотОбъект.Количество() > 0 И НЕ ЭтотОбъект.Отбор.Период.Значение = ЭтотОбъект[0].Период Тогда 
		ЭтотОбъект.Отбор.Период.Значение = ЭтотОбъект[0].Период;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	
	МассивВакансий = ЭтотОбъект.Выгрузить().ВыгрузитьКолонку("Вакансия");
	СоответствиеПозиций = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивВакансий, "Позиция");
	МассивПозиций = Новый Массив;
	Для Каждого ЭлементСоответствия Из СоответствиеПозиций Цикл
		Если МассивПозиций.Найти(ЭлементСоответствия.Значение) = Неопределено Тогда
			МассивПозиций.Добавить(ЭлементСоответствия.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОбменДаннымиЗарплатаКадрыРасширенный.ОграниченияРегистрацииПоПозициямШтатногоРасписания(ЭтотОбъект, МассивПозиций);
	
КонецФункции

#КонецОбласти 

#КонецЕсли

