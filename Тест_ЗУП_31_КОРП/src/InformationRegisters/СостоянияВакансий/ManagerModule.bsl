#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Процедура распределения состояний вакансий по эшелонам
//
Процедура РаспределениеСостоянийВакансийПоЭшелонам(ПараметрыОбновления) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СостоянияВакансий.Вакансия КАК Вакансия,
		|	СостоянияВакансий.Состояние КАК Состояние,
		|	СостоянияВакансий.Период КАК Период
		|ИЗ
		|	РегистрСведений.СостоянияВакансий КАК СостоянияВакансий
		|
		|УПОРЯДОЧИТЬ ПО
		|	Вакансия,
		|	Период");
	
	РезультатЗапроса = Запрос.Выполнить();
	ПараметрыОбновления.ОбработкаЗавершена = Истина;
	
	Открыта = Перечисления.СостоянияВакансии.Открыта;
	Приостановлена = Перечисления.СостоянияВакансии.Приостановлена;
	Отменена = Перечисления.СостоянияВакансии.Отменена;
	
	НаборЗаписей = РегистрыСведений.СостоянияВакансий.СоздатьНаборЗаписей();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Вакансия") Цикл
		НаборЗаписей.Отбор.Вакансия.Установить(Выборка.Вакансия);
		НоваяЗапись = Неопределено;
		ЕстьИзменения = Ложь;
		Пока Выборка.Следующий() Цикл 
			Если Не ЕстьИзменения 
				И ((Выборка.Состояние = Открыта И Не Выборка.Период = НачалоДня(Выборка.Период)) 
				Или	(Не Выборка.Состояние = Открыта И Не Выборка.Период = КонецДня(Выборка.Период))) Тогда 
				// Перезаписываем набор в случае наличии хоть одного статуса не в своем эшелоне
				// Установка эшелона произойдет автоматически при записи набора данных.
				ЕстьИзменения = Истина;
			КонецЕсли;		
			Если НоваяЗапись = Неопределено Тогда
				// Если первый статус Не Открыта - добавим Открыта в свой эшелон периода первого статуса.
				Если Не Выборка.Состояние = Открыта Тогда 
					НоваяЗапись = НаборЗаписей.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
					НоваяЗапись.Состояние = Открыта;
					ЕстьИзменения = Истина;
				КонецЕсли;
			ИначеЕсли (НоваяЗапись.Состояние = Открыта И Выборка.Состояние = Открыта) Тогда
				// Два статуса Открыта подряд - удалим младший
				ЕстьИзменения = Истина;
				Продолжить;
			ИначеЕсли Не НоваяЗапись.Состояние = Открыта И Не Выборка.Состояние = Открыта
				И Не (НоваяЗапись.Состояние = Приостановлена И Выборка.Состояние = Отменена) Тогда
				// Два статуса Не Открыта подряд - - добавим Открыта в свой эшелон периода второго статуса
				// Исключение - статус Отменена может идти после статуса Приостановлена.
				НоваяЗапись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
				НоваяЗапись.Состояние = Открыта;
				ЕстьИзменения = Истина;
			КонецЕсли;	
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		КонецЦикла;
		Если ЕстьИзменения Тогда 
			НаборЗаписей.Записать();
		КонецЕсли;
		НаборЗаписей.Очистить();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли