#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьИспользованиеТарифов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	УтверждениеТарифнойСеткиТарифы.Ссылка.ДатаВступленияВСилу,
		|	УтверждениеТарифнойСеткиТарифы.Ссылка.ТарифнаяСетка,
		|	УтверждениеТарифнойСеткиТарифы.РазрядКатегория,
		|	УтверждениеТарифнойСеткиТарифы.Ссылка
		|ПОМЕСТИТЬ ВТОтмененныеРазряды
		|ИЗ
		|	Документ.УтверждениеТарифнойСетки.Тарифы КАК УтверждениеТарифнойСеткиТарифы
		|ГДЕ
		|	УтверждениеТарифнойСеткиТарифы.Отменить
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗначенияТарифов.Период,
		|	ЗначенияТарифов.Регистратор,
		|	ЗначенияТарифов.ТарифнаяСетка,
		|	ЗначенияТарифов.РазрядКатегория,
		|	ЗначенияТарифов.РазрядныйКоэффициент,
		|	ЗначенияТарифов.Тариф,
		|	ВЫБОР
		|		КОГДА ОтмененныеРазряды.РазрядКатегория ЕСТЬ NULL 
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Используется
		|ПОМЕСТИТЬ ВТОжидаемоеСостояние
		|ИЗ
		|	РегистрСведений.ЗначенияТарифов КАК ЗначенияТарифов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОтмененныеРазряды КАК ОтмененныеРазряды
		|		ПО ЗначенияТарифов.Период = ОтмененныеРазряды.ДатаВступленияВСилу
		|			И ЗначенияТарифов.ТарифнаяСетка = ОтмененныеРазряды.ТарифнаяСетка
		|			И ЗначенияТарифов.РазрядКатегория = ОтмененныеРазряды.РазрядКатегория
		|			И ЗначенияТарифов.Регистратор = ОтмененныеРазряды.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОжидаемоеСостояние.Регистратор
		|ПОМЕСТИТЬ ВТОбновляемыеРегистраторы
		|ИЗ
		|	ВТОжидаемоеСостояние КАК ОжидаемоеСостояние
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияТарифов КАК ЗначенияТарифов
		|		ПО ОжидаемоеСостояние.Период = ЗначенияТарифов.Период
		|			И ОжидаемоеСостояние.Регистратор = ЗначенияТарифов.Регистратор
		|			И ОжидаемоеСостояние.ТарифнаяСетка = ЗначенияТарифов.ТарифнаяСетка
		|			И ОжидаемоеСостояние.РазрядКатегория = ЗначенияТарифов.РазрядКатегория
		|			И ОжидаемоеСостояние.Используется <> ЗначенияТарифов.Используется
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОжидаемоеСостояние.Период,
		|	ОжидаемоеСостояние.Регистратор КАК Регистратор,
		|	ОжидаемоеСостояние.ТарифнаяСетка,
		|	ОжидаемоеСостояние.РазрядКатегория,
		|	ОжидаемоеСостояние.РазрядныйКоэффициент,
		|	ОжидаемоеСостояние.Тариф,
		|	ОжидаемоеСостояние.Используется
		|ИЗ
		|	ВТОжидаемоеСостояние КАК ОжидаемоеСостояние
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОбновляемыеРегистраторы КАК ОбновляемыеРегистраторы
		|		ПО ОжидаемоеСостояние.Регистратор = ОбновляемыеРегистраторы.Регистратор
		|
		|УПОРЯДОЧИТЬ ПО
		|	Регистратор";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
			
			НаборЗаписей = РегистрыСведений.ЗначенияТарифов.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			
			Пока Выборка.Следующий() Цикл
				
				Запись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, Выборка);
				
			КонецЦикла; 
			
			НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
			НаборЗаписей.ОбменДанными.Загрузка = Истина;
			НаборЗаписей.Записать();
			
		КонецЦикла; 
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли