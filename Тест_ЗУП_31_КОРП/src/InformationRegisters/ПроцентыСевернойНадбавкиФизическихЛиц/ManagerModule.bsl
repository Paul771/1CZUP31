#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьДействуетДоРегистраСведений(ПараметрыОбновления = Неопределено) Экспорт
	
	ОбработкаЗавершена = Истина;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МаксимальнаяДата", ЗарплатаКадрыПериодическиеРегистры.МаксимальнаяДата());
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПроцентыСевернойНадбавкиФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТФизическиеЛицаКОбработке
		|ИЗ
		|	РегистрСведений.ПроцентыСевернойНадбавкиФизическихЛиц КАК ПроцентыСевернойНадбавкиФизическихЛиц
		|ГДЕ
		|	ПроцентыСевернойНадбавкиФизическихЛиц.ДействуетДо = ДАТАВРЕМЯ(1, 1, 1)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ФизическиеЛицаКОбработке.ФизическоеЛицо КАК ФизическоеЛицо
		|ИЗ
		|	ВТФизическиеЛицаКОбработке КАК ФизическиеЛицаКОбработке";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		ОбработкаЗавершена = Ложь;
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПроцентыСевернойНадбавкиФизическихЛиц.Период КАК Период,
			|	ПроцентыСевернойНадбавкиФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ПроцентыСевернойНадбавкиФизическихЛиц.ПроцентСевернойНадбавки КАК ПроцентСевернойНадбавки
			|ПОМЕСТИТЬ ВТНаборыФизическихЛиц
			|ИЗ
			|	ВТФизическиеЛицаКОбработке КАК ФизическиеЛицаКОбработке
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПроцентыСевернойНадбавкиФизическихЛиц КАК ПроцентыСевернойНадбавкиФизическихЛиц
			|		ПО ФизическиеЛицаКОбработке.ФизическоеЛицо = ПроцентыСевернойНадбавкиФизическихЛиц.ФизическоеЛицо
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НаборыФизическихЛиц.Период КАК Период,
			|	НаборыФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
			|	НаборыФизическихЛиц.ПроцентСевернойНадбавки КАК ПроцентСевернойНадбавки,
			|	МИНИМУМ(СледующиеПериоды.Период) КАК СледующийПериод
			|ПОМЕСТИТЬ ВТНаборыФизическихЛицСоСледующимиПериодами
			|ИЗ
			|	ВТНаборыФизическихЛиц КАК НаборыФизическихЛиц
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНаборыФизическихЛиц КАК СледующиеПериоды
			|		ПО НаборыФизическихЛиц.ФизическоеЛицо = СледующиеПериоды.ФизическоеЛицо
			|			И НаборыФизическихЛиц.Период < СледующиеПериоды.Период
			|
			|СГРУППИРОВАТЬ ПО
			|	НаборыФизическихЛиц.Период,
			|	НаборыФизическихЛиц.ФизическоеЛицо,
			|	НаборыФизическихЛиц.ПроцентСевернойНадбавки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НаборыФизическихЛицСоСледующимиПериодами.Период КАК Период,
			|	НаборыФизическихЛицСоСледующимиПериодами.ФизическоеЛицо КАК ФизическоеЛицо,
			|	НаборыФизическихЛицСоСледующимиПериодами.ПроцентСевернойНадбавки КАК ПроцентСевернойНадбавки,
			|	ЕСТЬNULL(ДОБАВИТЬКДАТЕ(НаборыФизическихЛицСоСледующимиПериодами.СледующийПериод, ДЕНЬ, -1), &МаксимальнаяДата) КАК ДействуетДо
			|ИЗ
			|	ВТНаборыФизическихЛицСоСледующимиПериодами КАК НаборыФизическихЛицСоСледующимиПериодами
			|
			|УПОРЯДОЧИТЬ ПО
			|	ФизическоеЛицо,
			|	Период";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("ФизическоеЛицо") Цикл
			
			Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(
				ПараметрыОбновления, "РегистрСведений.ПроцентыСевернойНадбавкиФизическихЛиц", "ФизическоеЛицо", Выборка.ФизическоеЛицо) Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			НаборЗаписей = РегистрыСведений.ПроцентыСевернойНадбавкиФизическихЛиц.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ФизическоеЛицо.Установить(Выборка.ФизическоеЛицо);
			
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", ОбработкаЗавершена);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
