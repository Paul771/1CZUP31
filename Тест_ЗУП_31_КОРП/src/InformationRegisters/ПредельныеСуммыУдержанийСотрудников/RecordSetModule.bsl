#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
    Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
        Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Регистратор", ЭтотОбъект.Отбор.Регистратор.Значение);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПредельныеСуммыУдержанийСотрудников.*
	               |ПОМЕСТИТЬ ВТСтарыйНаборЗаписей
	               |ИЗ
	               |	РегистрСведений.ПредельныеСуммыУдержанийСотрудников КАК ПредельныеСуммыУдержанийСотрудников
	               |ГДЕ
	               |	ПредельныеСуммыУдержанийСотрудников.Регистратор = &Регистратор";
				   
	Запрос.Выполнить();
	
	ДополнительныеСвойства.Вставить("МенеджерВременныхТаблицСтарыйНабор", Запрос.МенеджерВременныхТаблиц);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
    Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
        Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблицСтарыйНабор;
	
	Запрос.УстановитьПараметр("Регистратор", ЭтотОбъект.Отбор.Регистратор.Значение);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ПредельныеСуммыУдержанийСотрудников.Организация,
	               |	ПредельныеСуммыУдержанийСотрудников.ФизическоеЛицо,
	               |	ПредельныеСуммыУдержанийСотрудников.Удержание,
	               |	ПредельныеСуммыУдержанийСотрудников.ДокументОснование
	               |ПОМЕСТИТЬ ВТИзмененныеОснования
	               |ИЗ
	               |	РегистрСведений.ПредельныеСуммыУдержанийСотрудников КАК ПредельныеСуммыУдержанийСотрудников
	               |ГДЕ
	               |	ПредельныеСуммыУдержанийСотрудников.Регистратор = &Регистратор
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	СтарыйНаборЗаписей.Организация,
	               |	СтарыйНаборЗаписей.ФизическоеЛицо,
	               |	СтарыйНаборЗаписей.Удержание,
	               |	СтарыйНаборЗаписей.ДокументОснование
	               |ИЗ
	               |	ВТСтарыйНаборЗаписей КАК СтарыйНаборЗаписей
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПредельныеСуммыУдержанийСотрудников.Период КАК Период,
	               |	ПредельныеСуммыУдержанийСотрудников.Регистратор КАК Регистратор,
	               |	ПредельныеСуммыУдержанийСотрудников.НомерСтроки КАК НомерСтроки,
	               |	ПредельныеСуммыУдержанийСотрудников.Организация КАК Организация,
	               |	ПредельныеСуммыУдержанийСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	ПредельныеСуммыУдержанийСотрудников.Удержание КАК Удержание,
	               |	ПредельныеСуммыУдержанийСотрудников.ДокументОснование КАК ДокументОснование,
	               |	ПредельныеСуммыУдержанийСотрудников.ПрекратитьПоДостижениюПредела КАК ПрекратитьПоДостижениюПредела,
	               |	ПредельныеСуммыУдержанийСотрудников.Сумма КАК Сумма
	               |ПОМЕСТИТЬ ВТИзмененныеЗаписиПредварительно
	               |ИЗ
	               |	РегистрСведений.ПредельныеСуммыУдержанийСотрудников КАК ПредельныеСуммыУдержанийСотрудников
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИзмененныеОснования КАК ИзмененныеОснования
	               |		ПО ПредельныеСуммыУдержанийСотрудников.Организация = ИзмененныеОснования.Организация
	               |			И ПредельныеСуммыУдержанийСотрудников.ФизическоеЛицо = ИзмененныеОснования.ФизическоеЛицо
	               |			И ПредельныеСуммыУдержанийСотрудников.Удержание = ИзмененныеОснования.Удержание
	               |			И ПредельныеСуммыУдержанийСотрудников.ДокументОснование = ИзмененныеОснования.ДокументОснование
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	МАКСИМУМ(ИзмененныеЗаписи.Период) КАК Период,
	               |	ИзмененныеЗаписи.Организация,
	               |	ИзмененныеЗаписи.ФизическоеЛицо,
	               |	ИзмененныеЗаписи.Удержание,
	               |	ИзмененныеЗаписи.ДокументОснование
	               |ПОМЕСТИТЬ ВТИзмененныеЗаписиМаксимальныеДаты
	               |ИЗ
	               |	ВТИзмененныеЗаписиПредварительно КАК ИзмененныеЗаписи
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ИзмененныеЗаписи.Организация,
	               |	ИзмененныеЗаписи.ФизическоеЛицо,
	               |	ИзмененныеЗаписи.Удержание,
	               |	ИзмененныеЗаписи.ДокументОснование
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ИзмененныеЗаписи.Организация,
	               |	ИзмененныеЗаписи.ФизическоеЛицо,
	               |	ИзмененныеЗаписи.Удержание,
	               |	ИзмененныеЗаписи.ДокументОснование
	               |ПОМЕСТИТЬ ВТПределНеУстановленИзмерения
	               |ИЗ
	               |	ВТИзмененныеЗаписиПредварительно КАК ИзмененныеЗаписи
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИзмененныеЗаписиМаксимальныеДаты КАК ИзмененныеЗаписиМаксимальныеДаты
	               |		ПО ИзмененныеЗаписи.Период = ИзмененныеЗаписиМаксимальныеДаты.Период
	               |			И ИзмененныеЗаписи.Организация = ИзмененныеЗаписиМаксимальныеДаты.Организация
	               |			И ИзмененныеЗаписи.ФизическоеЛицо = ИзмененныеЗаписиМаксимальныеДаты.ФизическоеЛицо
	               |			И ИзмененныеЗаписи.Удержание = ИзмененныеЗаписиМаксимальныеДаты.Удержание
	               |			И ИзмененныеЗаписи.ДокументОснование = ИзмененныеЗаписиМаксимальныеДаты.ДокументОснование
	               |			И (НЕ ИзмененныеЗаписи.ПрекратитьПоДостижениюПредела)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ИзмененныеЗаписи.Организация,
	               |	ИзмененныеЗаписи.ФизическоеЛицо,
	               |	ИзмененныеЗаписи.Удержание,
	               |	ИзмененныеЗаписи.ДокументОснование
	               |ПОМЕСТИТЬ ВТПределУстановленИзмерения
	               |ИЗ
	               |	ВТИзмененныеЗаписиПредварительно КАК ИзмененныеЗаписи
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИзмененныеЗаписиМаксимальныеДаты КАК ИзмененныеЗаписиМаксимальныеДаты
	               |		ПО ИзмененныеЗаписи.Период = ИзмененныеЗаписиМаксимальныеДаты.Период
	               |			И ИзмененныеЗаписи.Организация = ИзмененныеЗаписиМаксимальныеДаты.Организация
	               |			И ИзмененныеЗаписи.ФизическоеЛицо = ИзмененныеЗаписиМаксимальныеДаты.ФизическоеЛицо
	               |			И ИзмененныеЗаписи.Удержание = ИзмененныеЗаписиМаксимальныеДаты.Удержание
	               |			И ИзмененныеЗаписи.ДокументОснование = ИзмененныеЗаписиМаксимальныеДаты.ДокументОснование
	               |			И (ИзмененныеЗаписи.ПрекратитьПоДостижениюПредела)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ИзмененныеЗаписи.Период,
	               |	ИзмененныеЗаписи.Регистратор,
	               |	ИзмененныеЗаписи.НомерСтроки,
	               |	ИзмененныеЗаписи.Организация,
	               |	ИзмененныеЗаписи.ФизическоеЛицо,
	               |	ИзмененныеЗаписи.Удержание,
	               |	ИзмененныеЗаписи.ДокументОснование,
	               |	ИзмененныеЗаписи.ПрекратитьПоДостижениюПредела,
	               |	ИзмененныеЗаписи.Сумма
	               |ПОМЕСТИТЬ ВТИзмененныеЗаписи
	               |ИЗ
	               |	ВТИзмененныеЗаписиПредварительно КАК ИзмененныеЗаписи
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПределУстановленИзмерения КАК ПределУстановленИзмерения
	               |		ПО ИзмененныеЗаписи.Организация = ПределУстановленИзмерения.Организация
	               |			И ИзмененныеЗаписи.ФизическоеЛицо = ПределУстановленИзмерения.ФизическоеЛицо
	               |			И ИзмененныеЗаписи.Удержание = ПределУстановленИзмерения.Удержание
	               |			И ИзмененныеЗаписи.ДокументОснование = ПределУстановленИзмерения.ДокументОснование
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ИзмененныеЗаписи.Период,
	               |	ИзмененныеЗаписи.Организация,
	               |	ИзмененныеЗаписи.ФизическоеЛицо,
	               |	ИзмененныеЗаписи.Удержание,
	               |	ИзмененныеЗаписи.ДокументОснование,
	               |	МАКСИМУМ(ИзмененныеЗаписиПредыдущие.Период) КАК ПериодПредыдущейЗаписи
	               |ПОМЕСТИТЬ ВТИзмененныеЗаписиДатыПредыдущихЗаписей
	               |ИЗ
	               |	ВТИзмененныеЗаписи КАК ИзмененныеЗаписи
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИзмененныеЗаписи КАК ИзмененныеЗаписиПредыдущие
	               |		ПО ИзмененныеЗаписи.Организация = ИзмененныеЗаписиПредыдущие.Организация
	               |			И ИзмененныеЗаписи.ФизическоеЛицо = ИзмененныеЗаписиПредыдущие.ФизическоеЛицо
	               |			И ИзмененныеЗаписи.Удержание = ИзмененныеЗаписиПредыдущие.Удержание
	               |			И ИзмененныеЗаписи.ДокументОснование = ИзмененныеЗаписиПредыдущие.ДокументОснование
	               |			И ИзмененныеЗаписи.Период > ИзмененныеЗаписиПредыдущие.Период
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ИзмененныеЗаписи.Период,
	               |	ИзмененныеЗаписи.Организация,
	               |	ИзмененныеЗаписи.ФизическоеЛицо,
	               |	ИзмененныеЗаписи.Удержание,
	               |	ИзмененныеЗаписи.ДокументОснование
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ИзмененныеЗаписи.Период,
	               |	ИзмененныеЗаписи.Регистратор,
	               |	ИзмененныеЗаписи.НомерСтроки,
	               |	ИзмененныеЗаписи.Организация,
	               |	ИзмененныеЗаписи.ФизическоеЛицо,
	               |	ИзмененныеЗаписи.Удержание,
	               |	ИзмененныеЗаписи.ДокументОснование,
	               |	ИзмененныеЗаписи.Сумма,
	               |	ИзмененныеЗаписи.Сумма - ЕСТЬNULL(ПредыдущиеЗаписи.Сумма, 0) КАК СуммаПрихода,
	               |	ИзмененныеЗаписи.ПрекратитьПоДостижениюПредела
	               |ПОМЕСТИТЬ ВТИсходныеДанные
	               |ИЗ
	               |	ВТИзмененныеЗаписи КАК ИзмененныеЗаписи
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТИзмененныеЗаписиДатыПредыдущихЗаписей КАК ДатыПредыдущихЗаписей
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИзмененныеЗаписи КАК ПредыдущиеЗаписи
	               |			ПО ДатыПредыдущихЗаписей.ПериодПредыдущейЗаписи = ПредыдущиеЗаписи.Период
	               |				И ДатыПредыдущихЗаписей.Организация = ПредыдущиеЗаписи.Организация
	               |				И ДатыПредыдущихЗаписей.ФизическоеЛицо = ПредыдущиеЗаписи.ФизическоеЛицо
	               |				И ДатыПредыдущихЗаписей.Удержание = ПредыдущиеЗаписи.Удержание
	               |				И ДатыПредыдущихЗаписей.ДокументОснование = ПредыдущиеЗаписи.ДокументОснование
	               |		ПО ИзмененныеЗаписи.Период = ДатыПредыдущихЗаписей.Период
	               |			И ИзмененныеЗаписи.Организация = ДатыПредыдущихЗаписей.Организация
	               |			И ИзмененныеЗаписи.ФизическоеЛицо = ДатыПредыдущихЗаписей.ФизическоеЛицо
	               |			И ИзмененныеЗаписи.Удержание = ДатыПредыдущихЗаписей.Удержание
	               |			И ИзмененныеЗаписи.ДокументОснование = ДатыПредыдущихЗаписей.ДокументОснование
	               |ГДЕ
	               |	ИзмененныеЗаписи.Сумма - ЕСТЬNULL(ПредыдущиеЗаписи.Сумма, 0) <> 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ИсходныеДанные.Регистратор
	               |ПОМЕСТИТЬ ВТРегистраторы
	               |ИЗ
	               |	ВТИсходныеДанные КАК ИсходныеДанные
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	УдержанияДоПределаПоСотрудникам.Период,
	               |	УдержанияДоПределаПоСотрудникам.Регистратор,
	               |	УдержанияДоПределаПоСотрудникам.ВидДвижения,
	               |	УдержанияДоПределаПоСотрудникам.Организация,
	               |	УдержанияДоПределаПоСотрудникам.ФизическоеЛицо,
	               |	УдержанияДоПределаПоСотрудникам.Удержание,
	               |	УдержанияДоПределаПоСотрудникам.ДокументОснование,
	               |	УдержанияДоПределаПоСотрудникам.Сумма
	               |ПОМЕСТИТЬ ВТТекущиеЗаписи
	               |ИЗ
	               |	РегистрНакопления.УдержанияДоПределаПоСотрудникам КАК УдержанияДоПределаПоСотрудникам
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПределУстановленИзмерения КАК ПределУстановленИзмерения
	               |		ПО УдержанияДоПределаПоСотрудникам.Организация = ПределУстановленИзмерения.Организация
	               |			И УдержанияДоПределаПоСотрудникам.ФизическоеЛицо = ПределУстановленИзмерения.ФизическоеЛицо
	               |			И УдержанияДоПределаПоСотрудникам.Удержание = ПределУстановленИзмерения.Удержание
	               |			И УдержанияДоПределаПоСотрудникам.ДокументОснование = ПределУстановленИзмерения.ДокументОснование
	               |			И (УдержанияДоПределаПоСотрудникам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))";
				   
	Запрос.Выполнить();
	
	// Удаление лишних записей прихода регистра УдержанияДоПределаПоСотрудникам.
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ТекущиеЗаписи.Регистратор
	               |ПОМЕСТИТЬ ВТРегистраторыУдаляемыхЗаписей
	               |ИЗ
	               |	ВТТекущиеЗаписи КАК ТекущиеЗаписи
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТРегистраторы КАК Регистраторы
	               |		ПО ТекущиеЗаписи.Регистратор = Регистраторы.Регистратор
	               |ГДЕ
	               |	Регистраторы.Регистратор ЕСТЬ NULL 
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ ПЕРВЫЕ 1
	               |	ИСТИНА КАК ЗначениеИстина
	               |ИЗ
	               |	ВТРегистраторыУдаляемыхЗаписей КАК РегистраторыУдаляемыхЗаписей";
				   
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда 
		
		Запрос.Текст = "ВЫБРАТЬ 
		               |	УдержанияДоПределаПоСотрудникам.Регистратор,
		               |	УдержанияДоПределаПоСотрудникам.Период,
		               |	УдержанияДоПределаПоСотрудникам.НомерСтроки,
		               |	УдержанияДоПределаПоСотрудникам.ВидДвижения,
		               |	УдержанияДоПределаПоСотрудникам.Организация,
		               |	УдержанияДоПределаПоСотрудникам.ФизическоеЛицо,
		               |	УдержанияДоПределаПоСотрудникам.Удержание,
		               |	УдержанияДоПределаПоСотрудникам.ДокументОснование,
		               |	УдержанияДоПределаПоСотрудникам.Сумма
		               |ПОМЕСТИТЬ ВТЗаписиРегистраторовУдаляемыхЗаписей
		               |ИЗ
		               |	РегистрНакопления.УдержанияДоПределаПоСотрудникам КАК УдержанияДоПределаПоСотрудникам
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистраторыУдаляемыхЗаписей КАК РегистраторыУдаляемыхЗаписей
		               |		ПО УдержанияДоПределаПоСотрудникам.Регистратор = РегистраторыУдаляемыхЗаписей.Регистратор
		               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТПределУстановленИзмерения КАК ПределУстановленИзмерения
		               |		ПО УдержанияДоПределаПоСотрудникам.Организация = ПределУстановленИзмерения.Организация
		               |			И УдержанияДоПределаПоСотрудникам.ФизическоеЛицо = ПределУстановленИзмерения.ФизическоеЛицо
		               |			И УдержанияДоПределаПоСотрудникам.Удержание = ПределУстановленИзмерения.Удержание
		               |			И УдержанияДоПределаПоСотрудникам.ДокументОснование = ПределУстановленИзмерения.ДокументОснование
		               |			И (УдержанияДоПределаПоСотрудникам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
		               |ГДЕ
		               |	ПределУстановленИзмерения.Организация ЕСТЬ NULL 
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	РегистраторыУдаляемыхЗаписей.Регистратор
		               |ИЗ
		               |	ВТРегистраторыУдаляемыхЗаписей КАК РегистраторыУдаляемыхЗаписей
		               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаписиРегистраторовУдаляемыхЗаписей КАК ЗаписиРегистраторовУдаляемыхЗаписей
		               |		ПО РегистраторыУдаляемыхЗаписей.Регистратор = ЗаписиРегистраторовУдаляемыхЗаписей.Регистратор
		               |ГДЕ
		               |	ЗаписиРегистраторовУдаляемыхЗаписей.Регистратор ЕСТЬ NULL 
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ЗаписиРегистраторовУдаляемыхЗаписей.Регистратор КАК Регистратор,
		               |	ЗаписиРегистраторовУдаляемыхЗаписей.Период,
		               |	ЗаписиРегистраторовУдаляемыхЗаписей.НомерСтроки,
		               |	ЗаписиРегистраторовУдаляемыхЗаписей.ВидДвижения,
		               |	ЗаписиРегистраторовУдаляемыхЗаписей.Организация,
		               |	ЗаписиРегистраторовУдаляемыхЗаписей.ФизическоеЛицо,
		               |	ЗаписиРегистраторовУдаляемыхЗаписей.Удержание,
		               |	ЗаписиРегистраторовУдаляемыхЗаписей.ДокументОснование,
		               |	ЗаписиРегистраторовУдаляемыхЗаписей.Сумма
		               |ИЗ
		               |	ВТЗаписиРегистраторовУдаляемыхЗаписей КАК ЗаписиРегистраторовУдаляемыхЗаписей
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Регистратор";
					   
		РезультатыЗапроса = Запрос.ВыполнитьПакет();
		
		Выборка = РезультатыЗапроса[1].Выбрать();
		Пока Выборка.Следующий() Цикл 
			НаборЗаписей = РегистрыНакопления.УдержанияДоПределаПоСотрудникам.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			НаборЗаписей.Записать();
		КонецЦикла;
		
		Выборка = РезультатыЗапроса[2].Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл 
			НаборЗаписей = РегистрыНакопления.УдержанияДоПределаПоСотрудникам.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			Пока Выборка.Следующий() Цикл 
				ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
			КонецЦикла;
			НаборЗаписей.Записать();
		КонецЦикла;
		
	КонецЕсли;
	
	// Добавление и изменение записей прихода регистра УдержанияДоПределаПоСотрудникам.
	Запрос.Текст = "ВЫБРАТЬ
	               |	ИсходныеДанные.Период,
	               |	ИсходныеДанные.Регистратор,
	               |	ИсходныеДанные.НомерСтроки,
	               |	ИсходныеДанные.Организация,
	               |	ИсходныеДанные.ФизическоеЛицо,
	               |	ИсходныеДанные.Удержание,
	               |	ИсходныеДанные.ДокументОснование,
	               |	ИсходныеДанные.Сумма,
	               |	ИсходныеДанные.СуммаПрихода,
	               |	ИсходныеДанные.ПрекратитьПоДостижениюПредела
	               |ПОМЕСТИТЬ ВТЗаписиДляИзменения
	               |ИЗ
	               |	ВТИсходныеДанные КАК ИсходныеДанные
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТТекущиеЗаписи КАК ТекущиеЗаписи
	               |		ПО ИсходныеДанные.Период = ТекущиеЗаписи.Период
	               |			И ИсходныеДанные.Регистратор = ТекущиеЗаписи.Регистратор
	               |			И ИсходныеДанные.Организация = ТекущиеЗаписи.Организация
	               |			И ИсходныеДанные.ФизическоеЛицо = ТекущиеЗаписи.ФизическоеЛицо
	               |			И ИсходныеДанные.Удержание = ТекущиеЗаписи.Удержание
	               |			И ИсходныеДанные.ДокументОснование = ТекущиеЗаписи.ДокументОснование
	               |			И ИсходныеДанные.СуммаПрихода = ТекущиеЗаписи.Сумма
	               |ГДЕ
	               |	ТекущиеЗаписи.Период ЕСТЬ NULL 
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ ПЕРВЫЕ 1
	               |	ИСТИНА КАК ЗначениеИстина
	               |ИЗ
	               |	ВТЗаписиДляИзменения КАК ЗаписиДляИзменения";
				   
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда 
		
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	ЗаписиДляИзменения.Регистратор
		               |ПОМЕСТИТЬ ВТРегистраторыДляИзменения
		               |ИЗ
		               |	ВТЗаписиДляИзменения КАК ЗаписиДляИзменения
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ЕСТЬNULL(УдержанияДоПределаПоСотрудникам.Период, ЗаписиДляИзменения.Период) КАК Период,
		               |	ЕСТЬNULL(УдержанияДоПределаПоСотрудникам.Регистратор, ЗаписиДляИзменения.Регистратор) КАК Регистратор,
		               |	ЕСТЬNULL(ЗаписиДляИзменения.НомерСтроки, УдержанияДоПределаПоСотрудникам.НомерСтроки) КАК НомерСтроки,
		               |	ЕСТЬNULL(УдержанияДоПределаПоСотрудникам.ВидДвижения, ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)) КАК ВидДвижения,
		               |	ЕСТЬNULL(УдержанияДоПределаПоСотрудникам.Организация, ЗаписиДляИзменения.Организация) КАК Организация,
		               |	ЕСТЬNULL(УдержанияДоПределаПоСотрудникам.ФизическоеЛицо, ЗаписиДляИзменения.ФизическоеЛицо) КАК ФизическоеЛицо,
		               |	ЕСТЬNULL(УдержанияДоПределаПоСотрудникам.Удержание, ЗаписиДляИзменения.Удержание) КАК Удержание,
		               |	ЕСТЬNULL(УдержанияДоПределаПоСотрудникам.ДокументОснование, ЗаписиДляИзменения.ДокументОснование) КАК ДокументОснование,
		               |	ЕСТЬNULL(ЗаписиДляИзменения.СуммаПрихода, УдержанияДоПределаПоСотрудникам.Сумма) КАК Сумма
		               |ИЗ
		               |	РегистрНакопления.УдержанияДоПределаПоСотрудникам КАК УдержанияДоПределаПоСотрудникам
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистраторыДляИзменения КАК РегистраторыДляИзменения
		               |		ПО УдержанияДоПределаПоСотрудникам.Регистратор = РегистраторыДляИзменения.Регистратор
		               |		ПОЛНОЕ СОЕДИНЕНИЕ ВТЗаписиДляИзменения КАК ЗаписиДляИзменения
		               |		ПО УдержанияДоПределаПоСотрудникам.Регистратор = ЗаписиДляИзменения.Регистратор
		               |			И УдержанияДоПределаПоСотрудникам.Период = ЗаписиДляИзменения.Период
		               |			И УдержанияДоПределаПоСотрудникам.Организация = ЗаписиДляИзменения.Организация
		               |			И УдержанияДоПределаПоСотрудникам.ФизическоеЛицо = ЗаписиДляИзменения.ФизическоеЛицо
		               |			И УдержанияДоПределаПоСотрудникам.Удержание = ЗаписиДляИзменения.Удержание
		               |			И УдержанияДоПределаПоСотрудникам.ДокументОснование = ЗаписиДляИзменения.ДокументОснование
		               |			И (УдержанияДоПределаПоСотрудникам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Регистратор";
					   
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл 
			НаборЗаписей = РегистрыНакопления.УдержанияДоПределаПоСотрудникам.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			Пока Выборка.Следующий() Цикл 
				ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
			КонецЦикла;
			НаборЗаписей.Записать();
		КонецЦикла;
		
	КонецЕсли;
	
	// Очистка записей регистра УдержанияДоПределаПоСотрудникам при отмене предела.
	Запрос.Текст = "ВЫБРАТЬ
	               |	УдержанияДоПределаПоСотрудникам.Период,
	               |	УдержанияДоПределаПоСотрудникам.Регистратор,
	               |	УдержанияДоПределаПоСотрудникам.Организация,
	               |	УдержанияДоПределаПоСотрудникам.ФизическоеЛицо,
	               |	УдержанияДоПределаПоСотрудникам.Удержание,
	               |	УдержанияДоПределаПоСотрудникам.ДокументОснование
	               |ПОМЕСТИТЬ ВТУдержанияДоПределаЗаписиДляУдаления
	               |ИЗ
	               |	РегистрНакопления.УдержанияДоПределаПоСотрудникам КАК УдержанияДоПределаПоСотрудникам
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПределНеУстановленИзмерения КАК ПределНеУстановленИзмерения
	               |		ПО УдержанияДоПределаПоСотрудникам.Организация = ПределНеУстановленИзмерения.Организация
	               |			И УдержанияДоПределаПоСотрудникам.ФизическоеЛицо = ПределНеУстановленИзмерения.ФизическоеЛицо
	               |			И УдержанияДоПределаПоСотрудникам.Удержание = ПределНеУстановленИзмерения.Удержание
	               |			И УдержанияДоПределаПоСотрудникам.ДокументОснование = ПределНеУстановленИзмерения.ДокументОснование
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ ПЕРВЫЕ 1
	               |	ИСТИНА КАК ЗначениеИстина
	               |ИЗ
	               |	ВТУдержанияДоПределаЗаписиДляУдаления КАК УдержанияДоПределаЗаписиДляУдаления";
				   
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда 
		
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	УдержанияДоПределаЗаписиДляУдаления.Регистратор
		               |ПОМЕСТИТЬ ВТУдержанияДоПределаРегистраторыЗаписейДляУдаления
		               |ИЗ
		               |	ВТУдержанияДоПределаЗаписиДляУдаления КАК УдержанияДоПределаЗаписиДляУдаления
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	УдержанияДоПределаПоСотрудникам.Период,
		               |	УдержанияДоПределаПоСотрудникам.Регистратор КАК Регистратор,
		               |	УдержанияДоПределаПоСотрудникам.НомерСтроки,
		               |	УдержанияДоПределаПоСотрудникам.ВидДвижения,
		               |	УдержанияДоПределаПоСотрудникам.Организация,
		               |	УдержанияДоПределаПоСотрудникам.ФизическоеЛицо,
		               |	УдержанияДоПределаПоСотрудникам.Удержание,
		               |	УдержанияДоПределаПоСотрудникам.ДокументОснование,
		               |	УдержанияДоПределаПоСотрудникам.Сумма
		               |ПОМЕСТИТЬ ВТУдержанияДоПределаДвиженияРегистраторовЗаписейДляУдаления
		               |ИЗ
		               |	РегистрНакопления.УдержанияДоПределаПоСотрудникам КАК УдержанияДоПределаПоСотрудникам
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТУдержанияДоПределаРегистраторыЗаписейДляУдаления КАК УдержанияДоПределаРегистраторыЗаписейДляУдаления
		               |		ПО УдержанияДоПределаПоСотрудникам.Регистратор = УдержанияДоПределаРегистраторыЗаписейДляУдаления.Регистратор
		               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТУдержанияДоПределаЗаписиДляУдаления КАК УдержанияДоПределаЗаписиДляУдаления
		               |		ПО УдержанияДоПределаПоСотрудникам.Период = УдержанияДоПределаЗаписиДляУдаления.Период
		               |			И УдержанияДоПределаПоСотрудникам.Регистратор = УдержанияДоПределаЗаписиДляУдаления.Регистратор
		               |			И УдержанияДоПределаПоСотрудникам.Организация = УдержанияДоПределаЗаписиДляУдаления.Организация
		               |			И УдержанияДоПределаПоСотрудникам.ФизическоеЛицо = УдержанияДоПределаЗаписиДляУдаления.ФизическоеЛицо
		               |			И УдержанияДоПределаПоСотрудникам.Удержание = УдержанияДоПределаЗаписиДляУдаления.Удержание
		               |			И УдержанияДоПределаПоСотрудникам.ДокументОснование = УдержанияДоПределаЗаписиДляУдаления.ДокументОснование
		               |ГДЕ
		               |	УдержанияДоПределаЗаписиДляУдаления.Период ЕСТЬ NULL 
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	УдержанияДоПределаРегистраторыЗаписейДляУдаления.Регистратор
		               |ИЗ
		               |	ВТУдержанияДоПределаРегистраторыЗаписейДляУдаления КАК УдержанияДоПределаРегистраторыЗаписейДляУдаления
		               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТУдержанияДоПределаДвиженияРегистраторовЗаписейДляУдаления КАК УдержанияДоПределаДвиженияРегистраторовЗаписейДляУдаления
		               |		ПО УдержанияДоПределаРегистраторыЗаписейДляУдаления.Регистратор = УдержанияДоПределаДвиженияРегистраторовЗаписейДляУдаления.Регистратор
		               |ГДЕ
		               |	УдержанияДоПределаДвиженияРегистраторовЗаписейДляУдаления.Регистратор ЕСТЬ NULL 
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	УдержанияДоПределаДвиженияРегистраторовЗаписейДляУдаления.Период,
		               |	УдержанияДоПределаДвиженияРегистраторовЗаписейДляУдаления.Регистратор,
		               |	УдержанияДоПределаДвиженияРегистраторовЗаписейДляУдаления.НомерСтроки,
		               |	УдержанияДоПределаДвиженияРегистраторовЗаписейДляУдаления.ВидДвижения,
		               |	УдержанияДоПределаДвиженияРегистраторовЗаписейДляУдаления.Организация,
		               |	УдержанияДоПределаДвиженияРегистраторовЗаписейДляУдаления.ФизическоеЛицо,
		               |	УдержанияДоПределаДвиженияРегистраторовЗаписейДляУдаления.Удержание,
		               |	УдержанияДоПределаДвиженияРегистраторовЗаписейДляУдаления.ДокументОснование,
		               |	УдержанияДоПределаДвиженияРегистраторовЗаписейДляУдаления.Сумма
		               |ИЗ
		               |	ВТУдержанияДоПределаДвиженияРегистраторовЗаписейДляУдаления КАК УдержанияДоПределаДвиженияРегистраторовЗаписейДляУдаления";
	
		РезультатыЗапроса = Запрос.ВыполнитьПакет();
		
		Выборка = РезультатыЗапроса[2].Выбрать();
		Пока Выборка.Следующий() Цикл 
			НаборЗаписей = РегистрыНакопления.УдержанияДоПределаПоСотрудникам.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			НаборЗаписей.Записать();
		КонецЦикла;
		
		Выборка = РезультатыЗапроса[3].Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл 
			НаборЗаписей = РегистрыНакопления.УдержанияДоПределаПоСотрудникам.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			Пока Выборка.Следующий() Цикл 
				ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
			КонецЦикла;
			НаборЗаписей.Записать();
		КонецЦикла;
		
	КонецЕсли;
	
	// Восстановление записей расхода регистра УдержанияДоПределаПоСотрудникам при назначении предела.
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	ИСТИНА КАК ЗначениеИстина
	               |ИЗ
	               |	РегистрНакопления.УдержанияДоПределаПоСотрудникам КАК УдержанияДоПределаПоСотрудникам
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПределУстановленИзмерения КАК ПределУстановленИзмерения
	               |		ПО УдержанияДоПределаПоСотрудникам.Организация = ПределУстановленИзмерения.Организация
	               |			И УдержанияДоПределаПоСотрудникам.ФизическоеЛицо = ПределУстановленИзмерения.ФизическоеЛицо
	               |			И УдержанияДоПределаПоСотрудникам.Удержание = ПределУстановленИзмерения.Удержание
	               |			И УдержанияДоПределаПоСотрудникам.ДокументОснование = ПределУстановленИзмерения.ДокументОснование
	               |			И (УдержанияДоПределаПоСотрудникам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))";
				   
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда 
	
		Запрос.Текст = "ВЫБРАТЬ
		               |	НачисленияУдержанияПоСотрудникам.Период КАК Период,
		               |	НачисленияУдержанияПоСотрудникам.Регистратор КАК Регистратор,
		               |	НачисленияУдержанияПоСотрудникам.Организация.ГоловнаяОрганизация КАК Организация,
		               |	НачисленияУдержанияПоСотрудникам.ФизическоеЛицо КАК ФизическоеЛицо,
		               |	НачисленияУдержанияПоСотрудникам.НачислениеУдержание КАК Удержание,
		               |	НачисленияУдержанияПоСотрудникам.ДокументОснование КАК ДокументОснование,
		               |	СУММА(НачисленияУдержанияПоСотрудникам.Сумма) КАК Сумма
		               |ПОМЕСТИТЬ ВТНачисленияУдержанияПоСотрудникам
		               |ИЗ
		               |	РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК НачисленияУдержанияПоСотрудникам
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПределУстановленИзмерения КАК ПределУстановленИзмерения
		               |		ПО НачисленияУдержанияПоСотрудникам.Организация = ПределУстановленИзмерения.Организация
		               |			И НачисленияУдержанияПоСотрудникам.ФизическоеЛицо = ПределУстановленИзмерения.ФизическоеЛицо
		               |			И НачисленияУдержанияПоСотрудникам.НачислениеУдержание = ПределУстановленИзмерения.Удержание
		               |			И НачисленияУдержанияПоСотрудникам.ДокументОснование = ПределУстановленИзмерения.ДокументОснование
		               |			И (НЕ НачисленияУдержанияПоСотрудникам.ОграничениеВзыскания)
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	НачисленияУдержанияПоСотрудникам.Период,
		               |	НачисленияУдержанияПоСотрудникам.Регистратор,
		               |	НачисленияУдержанияПоСотрудникам.Организация.ГоловнаяОрганизация,
		               |	НачисленияУдержанияПоСотрудникам.ФизическоеЛицо,
		               |	НачисленияУдержанияПоСотрудникам.НачислениеУдержание,
		               |	НачисленияУдержанияПоСотрудникам.ДокументОснование
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	УдержанияПоИсполнительнымДокументам.Период КАК Период,
		               |	УдержанияПоИсполнительнымДокументам.Регистратор КАК Регистратор,
		               |	УдержанияПоИсполнительнымДокументам.Организация.ГоловнаяОрганизация КАК Организация,
		               |	УдержанияПоИсполнительнымДокументам.ФизическоеЛицо КАК ФизическоеЛицо,
		               |	УдержанияПоИсполнительнымДокументам.ИсполнительныйДокумент.Удержание КАК Удержание,
		               |	УдержанияПоИсполнительнымДокументам.ИсполнительныйДокумент КАК ДокументОснование,
		               |	СУММА(УдержанияПоИсполнительнымДокументам.СуммаУдержания) КАК Сумма
		               |ПОМЕСТИТЬ ВТУдержанияПоИсполнительнымДокументам
		               |ИЗ
		               |	РегистрНакопления.УдержанияПоИсполнительнымДокументам КАК УдержанияПоИсполнительнымДокументам
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПределУстановленИзмерения КАК ПределУстановленИзмерения
		               |		ПО УдержанияПоИсполнительнымДокументам.Организация = ПределУстановленИзмерения.Организация
		               |			И УдержанияПоИсполнительнымДокументам.ФизическоеЛицо = ПределУстановленИзмерения.ФизическоеЛицо
		               |			И УдержанияПоИсполнительнымДокументам.ИсполнительныйДокумент = ПределУстановленИзмерения.ДокументОснование
		               |			И (НЕ УдержанияПоИсполнительнымДокументам.ОграничениеВзыскания)
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	УдержанияПоИсполнительнымДокументам.Период,
		               |	УдержанияПоИсполнительнымДокументам.Регистратор,
		               |	УдержанияПоИсполнительнымДокументам.Организация.ГоловнаяОрганизация,
		               |	УдержанияПоИсполнительнымДокументам.ФизическоеЛицо,
		               |	УдержанияПоИсполнительнымДокументам.ИсполнительныйДокумент.Удержание,
		               |	УдержанияПоИсполнительнымДокументам.ИсполнительныйДокумент
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	НачисленияУдержанияПоСотрудникам.Период КАК Период,
		               |	НачисленияУдержанияПоСотрудникам.Регистратор КАК Регистратор,
		               |	НачисленияУдержанияПоСотрудникам.Организация КАК Организация,
		               |	НачисленияУдержанияПоСотрудникам.ФизическоеЛицо КАК ФизическоеЛицо,
		               |	НачисленияУдержанияПоСотрудникам.Удержание КАК Удержание,
		               |	НачисленияУдержанияПоСотрудникам.ДокументОснование КАК ДокументОснование,
		               |	НачисленияУдержанияПоСотрудникам.Сумма КАК Сумма
		               |ПОМЕСТИТЬ ВТУдержанияДоПределаПоСотрудникамРасход
		               |ИЗ
		               |	ВТНачисленияУдержанияПоСотрудникам КАК НачисленияУдержанияПоСотрудникам
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	УдержанияПоИсполнительнымДокументам.Период,
		               |	УдержанияПоИсполнительнымДокументам.Регистратор,
		               |	УдержанияПоИсполнительнымДокументам.Организация,
		               |	УдержанияПоИсполнительнымДокументам.ФизическоеЛицо,
		               |	УдержанияПоИсполнительнымДокументам.Удержание,
		               |	УдержанияПоИсполнительнымДокументам.ДокументОснование,
		               |	УдержанияПоИсполнительнымДокументам.Сумма
		               |ИЗ
		               |	ВТУдержанияПоИсполнительнымДокументам КАК УдержанияПоИсполнительнымДокументам
		               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияУдержанияПоСотрудникам КАК НачисленияУдержанияПоСотрудникам
		               |		ПО УдержанияПоИсполнительнымДокументам.Регистратор = НачисленияУдержанияПоСотрудникам.Регистратор
		               |			И УдержанияПоИсполнительнымДокументам.Период = НачисленияУдержанияПоСотрудникам.Период
		               |			И УдержанияПоИсполнительнымДокументам.Организация = НачисленияУдержанияПоСотрудникам.Организация
		               |			И УдержанияПоИсполнительнымДокументам.ФизическоеЛицо = НачисленияУдержанияПоСотрудникам.ФизическоеЛицо
		               |			И УдержанияПоИсполнительнымДокументам.Удержание = НачисленияУдержанияПоСотрудникам.Удержание
		               |			И УдержанияПоИсполнительнымДокументам.ДокументОснование = НачисленияУдержанияПоСотрудникам.ДокументОснование
		               |ГДЕ
		               |	НачисленияУдержанияПоСотрудникам.Регистратор ЕСТЬ NULL
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	УдержанияДоПределаПоСотрудникамРасход.Регистратор КАК Регистратор
		               |ПОМЕСТИТЬ ВТУдержанияДоПределаПоСотрудникамРегистраторы
		               |ИЗ
		               |	ВТУдержанияДоПределаПоСотрудникамРасход КАК УдержанияДоПределаПоСотрудникамРасход
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	УдержанияДоПределаПоСотрудникам.Период КАК Период,
		               |	УдержанияДоПределаПоСотрудникам.Регистратор КАК Регистратор,
		               |	УдержанияДоПределаПоСотрудникам.НомерСтроки КАК НомерСтроки,
		               |	УдержанияДоПределаПоСотрудникам.ВидДвижения КАК ВидДвижения,
		               |	УдержанияДоПределаПоСотрудникам.Организация КАК Организация,
		               |	УдержанияДоПределаПоСотрудникам.ФизическоеЛицо КАК ФизическоеЛицо,
		               |	УдержанияДоПределаПоСотрудникам.Удержание КАК Удержание,
		               |	УдержанияДоПределаПоСотрудникам.ДокументОснование КАК ДокументОснование,
		               |	УдержанияДоПределаПоСотрудникам.Сумма КАК Сумма
		               |ИЗ
		               |	РегистрНакопления.УдержанияДоПределаПоСотрудникам КАК УдержанияДоПределаПоСотрудникам
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТУдержанияДоПределаПоСотрудникамРегистраторы КАК Регистраторы
		               |		ПО УдержанияДоПределаПоСотрудникам.Регистратор = Регистраторы.Регистратор
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	УдержанияДоПределаПоСотрудникамРасход.Период,
		               |	УдержанияДоПределаПоСотрудникамРасход.Регистратор,
		               |	NULL,
		               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
		               |	УдержанияДоПределаПоСотрудникамРасход.Организация,
		               |	УдержанияДоПределаПоСотрудникамРасход.ФизическоеЛицо,
		               |	УдержанияДоПределаПоСотрудникамРасход.Удержание,
		               |	УдержанияДоПределаПоСотрудникамРасход.ДокументОснование,
		               |	УдержанияДоПределаПоСотрудникамРасход.Сумма
		               |ИЗ
		               |	ВТУдержанияДоПределаПоСотрудникамРасход КАК УдержанияДоПределаПоСотрудникамРасход
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Регистратор";
					   
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл 
			НаборЗаписей = РегистрыНакопления.УдержанияДоПределаПоСотрудникам.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			Пока Выборка.Следующий() Цикл 
				ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
			КонецЦикла;
			НаборЗаписей.Записать();
		КонецЦикла;

	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти
	
#КонецЕсли
