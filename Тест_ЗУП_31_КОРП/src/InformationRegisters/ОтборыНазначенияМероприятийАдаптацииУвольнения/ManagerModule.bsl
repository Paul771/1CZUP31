#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ПервоначальногоЗаполненияИОбновленияИБ

Процедура ЗаполнитьОрганизацию(ПараметрыОбновления = НеОпределено) Экспорт
	
	Запрос = Новый Запрос();
	
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ОтборыНазначения.ОбъектНазначения КАК ОбъектНазначения,
	|	ОтборыНазначения.Мероприятие КАК Мероприятие
	|ПОМЕСТИТЬ ВТОтборыНазначения
	|ИЗ
	|	РегистрСведений.ОтборыНазначенияМероприятийАдаптацииУвольнения КАК ОтборыНазначения
	|ГДЕ
	|	ОтборыНазначения.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И НЕ ОтборыНазначения.ОбъектНазначения ССЫЛКА Справочник.Должности
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтборыНазначения.ОбъектНазначения КАК ОбъектНазначения,
	|	ОтборыНазначения.Мероприятие КАК Мероприятие,
	|	ОтборыНазначения.ОбъектНазначения КАК Организация
	|ПОМЕСТИТЬ ВТОтборыНазначенияПоОрганизациям
	|ИЗ
	|	ВТОтборыНазначения КАК ОтборыНазначения
	|ГДЕ
	|	ОтборыНазначения.ОбъектНазначения ССЫЛКА Справочник.Организации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтборыНазначения.ОбъектНазначения,
	|	ОтборыНазначения.Мероприятие,
	|	ОтборыНазначения.ОбъектНазначения.Владелец
	|ИЗ
	|	ВТОтборыНазначения КАК ОтборыНазначения
	|ГДЕ
	|	ОтборыНазначения.ОбъектНазначения ССЫЛКА Справочник.ПодразделенияОрганизаций
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтборыНазначения.ОбъектНазначения,
	|	ОтборыНазначения.Мероприятие,
	|	ОтборыНазначения.ОбъектНазначения.Владелец
	|ИЗ
	|	ВТОтборыНазначения КАК ОтборыНазначения
	|ГДЕ
	|	ОтборыНазначения.ОбъектНазначения ССЫЛКА Справочник.ШтатноеРасписание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТОтборыНазначения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтборыНазначения.ОбъектНазначения КАК ОбъектНазначения,
	|	ОтборыНазначения.Мероприятие КАК Мероприятие,
	|	ОтборыНазначения.Организация КАК Организация
	|ИЗ
	|	ВТОтборыНазначенияПоОрганизациям КАК ОтборыНазначения
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОбъектНазначения,
	|	Мероприятие";
	
	Если ПараметрыОбновления = НеОпределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, " ПЕРВЫЕ 1000", "");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
	Иначе
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.СледующийПоЗначениюПоля("ОбъектНазначения") Цикл
			
			Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.ОтборыНазначенияМероприятийАдаптацииУвольнения", "ОбъектНазначения", Выборка.ОбъектНазначения) Тогда
				Продолжить;
			КонецЕсли;
			
			НаборЗаписей = РегистрыСведений.ОтборыНазначенияМероприятийАдаптацииУвольнения.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ОбъектНазначения.Установить(Выборка.ОбъектНазначения);
			
			Пока Выборка.СледующийПоЗначениюПоля("Мероприятие") Цикл
				
				НаборЗаписей.Отбор.Мероприятие.Установить(Выборка.Мероприятие);
				НаборЗаписей.Прочитать();
				
				НаборЗаписей[0].Организация = Выборка.Организация;
				
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
				
			КонецЦикла;
			
			ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли