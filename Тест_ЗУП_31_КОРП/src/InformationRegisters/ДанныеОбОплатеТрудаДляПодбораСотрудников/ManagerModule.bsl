#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ПеренестиДанныеОбОплатеТруда() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДанныеОбОплатеТрудаДляПодбора.Сотрудник
		|ИЗ
		|	РегистрСведений.ДанныеОбОплатеТрудаДляПодбораСотрудников КАК ДанныеОбОплатеТрудаДляПодбора";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ДанныеДляПодбора.Сотрудник,
			|	ДанныеДляПодбора.Филиал,
			|	ДанныеДляПодбора.ФизическоеЛицо,
			|	ДанныеДляПодбора.ИдентификаторЗаписи,
			|	ДанныеДляПодбора.УдалитьТарифнаяСтавка КАК ТарифнаяСтавка,
			|	ДанныеДляПодбора.УдалитьФОТ КАК ФОТ,
			|	ДанныеДляПодбора.УдалитьНадбавка КАК Надбавка,
			|	ДанныеДляПодбора.УдалитьСпособРасчетаАванса КАК СпособРасчетаАванса,
			|	ДанныеДляПодбора.УдалитьАванс КАК Аванс
			|ИЗ
			|	РегистрСведений.ДанныеДляПодбораСотрудников КАК ДанныеДляПодбора
			|ГДЕ
			|	(ДанныеДляПодбора.УдалитьТарифнаяСтавка <> 0
			|			ИЛИ ДанныеДляПодбора.УдалитьФОТ <> 0
			|			ИЛИ ДанныеДляПодбора.УдалитьНадбавка <> 0
			|			ИЛИ ДанныеДляПодбора.УдалитьСпособРасчетаАванса <> ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаАванса.ПустаяСсылка)
			|			ИЛИ ДанныеДляПодбора.УдалитьАванс <> 0)";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			
			НаборЗаписей = РегистрыСведений.ДанныеОбОплатеТрудаДляПодбораСотрудников.СоздатьНаборЗаписей();
			НаборЗаписей.Загрузить(РезультатЗапроса.Выгрузить());
			
			НаборЗаписей.ОбменДанными.Загрузка = Истина;
			НаборЗаписей.Записать();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
