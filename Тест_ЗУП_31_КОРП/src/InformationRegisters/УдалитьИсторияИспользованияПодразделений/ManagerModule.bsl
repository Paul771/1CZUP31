#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ДополнитьНаборИспользованиемВИерархии(ТекущийНабор) Экспорт
	
	Регистратор = ТекущийНабор.Отбор.Регистратор.Значение;
	
	ТаблицаНабора = ТекущийНабор.Выгрузить();
	ТаблицаНабора.Колонки.Добавить("Уровень", Новый ОписаниеТипов("Число"));
	
	МаксимальныйУровень = -1;
	Для каждого СтрокаТаблицаНабора Из ТаблицаНабора Цикл
		СтрокаТаблицаНабора.Уровень = СтрокаТаблицаНабора.Подразделение.Уровень();
		Если МаксимальныйУровень < СтрокаТаблицаНабора.Уровень Тогда
			МаксимальныйУровень = СтрокаТаблицаНабора.Уровень;
		КонецЕсли; 
	КонецЦикла;
	
	Для СдвигУровня = 0 По МаксимальныйУровень - 1 Цикл
		
		ТаблицаУровней = ТаблицаНабора.Скопировать(Новый Структура("Уровень", МаксимальныйУровень - СдвигУровня));
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		Запрос.УстановитьПараметр("НаборЗаписей", ТаблицаУровней);
		Запрос.УстановитьПараметр("СсылкиДокумента", ТаблицаНабора.ВыгрузитьКолонку("Подразделение"));
		Запрос.УстановитьПараметр("Регистратор", Регистратор);
		Запрос.УстановитьПараметр("СледующийУровень", МаксимальныйУровень - СдвигУровня - 1);
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ИсторияИспользованияПодразделений.Период,
			|	ИсторияИспользованияПодразделений.Подразделение,
			|	ИсторияИспользованияПодразделений.Используется
			|ПОМЕСТИТЬ ВТДанныеНабораЗаписей
			|ИЗ
			|	&НаборЗаписей КАК ИсторияИспользованияПодразделений
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТДанныеНабораЗаписей.Период,
			|	ВТДанныеНабораЗаписей.Подразделение
			|ПОМЕСТИТЬ ВТНеиспользуемыеПодразделения
			|ИЗ
			|	ВТДанныеНабораЗаписей КАК ВТДанныеНабораЗаписей
			|ГДЕ
			|	НЕ ВТДанныеНабораЗаписей.Используется
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТНеиспользуемыеПодразделения.Период,
			|	ПодразделенияОрганизаций.Ссылка КАК Подразделение
			|ПОМЕСТИТЬ ВТПодчиненныеПодразделения
			|ИЗ
			|	ВТНеиспользуемыеПодразделения КАК ВТНеиспользуемыеПодразделения
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
			|		ПО ВТНеиспользуемыеПодразделения.Подразделение = ПодразделенияОрганизаций.Родитель
			|ГДЕ
			|	НЕ ПодразделенияОрганизаций.Ссылка В (&СсылкиДокумента)";
			
		Запрос.Выполнить();
		
		ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроения.Отборы, "Регистратор", "<>", Регистратор);
		
		ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
			"УдалитьИсторияИспользованияПодразделений",
			Запрос.МенеджерВременныхТаблиц,
			Ложь,
			ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
				"ВТПодчиненныеПодразделения",
				"Подразделение"),
			ПараметрыПостроения);
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ДанныеНабораЗаписей.Период,
			|	ДанныеНабораЗаписей.Подразделение КАК Подразделение,
			|	МИНИМУМ(ВЫБОР
			|			КОГДА ДанныеНабораЗаписей.Используется = ЛОЖЬ
			|				ТОГДА ЕСТЬNULL(ИсторияИспользованияПодразделений.Используется, ЛОЖЬ)
			|			ИНАЧЕ ИСТИНА
			|		КОНЕЦ) КАК Используется
			|ПОМЕСТИТЬ ВТИспользованиеПодразделенияСИспользуемостьюПоПодчиненным
			|ИЗ
			|	ВТДанныеНабораЗаписей КАК ДанныеНабораЗаписей
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУдалитьИсторияИспользованияПодразделенийСрезПоследних КАК ИсторияИспользованияПодразделений
			|		ПО (ДанныеНабораЗаписей.Подразделение = ВЫРАЗИТЬ(ИсторияИспользованияПодразделений.Подразделение КАК Справочник.ПодразделенияОрганизаций).Родитель)
			|			И (ИсторияИспользованияПодразделений.Используется)
			|
			|СГРУППИРОВАТЬ ПО
			|	ДанныеНабораЗаписей.Период,
			|	ДанныеНабораЗаписей.Подразделение
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИспользованиеПодразделенияСИспользуемостьюПоПодчиненным.Период,
			|	ИспользованиеПодразделенияСИспользуемостьюПоПодчиненным.Подразделение
			|ПОМЕСТИТЬ ВТНЕИспользованиеПодразделенияСИспользуемостьюПоПодчиненным
			|ИЗ
			|	ВТИспользованиеПодразделенияСИспользуемостьюПоПодчиненным КАК ИспользованиеПодразделенияСИспользуемостьюПоПодчиненным
			|ГДЕ
			|	НЕ ИспользованиеПодразделенияСИспользуемостьюПоПодчиненным.Используется
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НЕИспользованиеПодразделенияСИспользуемостьюПоПодчиненным.Период,
			|	НЕИспользованиеПодразделенияСИспользуемостьюПоПодчиненным.Подразделение,
			|	ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания,
			|	МАКСИМУМ(ИсторияИспользованияШтатногоРасписания.Дата) КАК Дата
			|ПОМЕСТИТЬ ВТДатыСведенийПоПозициям
			|ИЗ
			|	ВТНЕИспользованиеПодразделенияСИспользуемостьюПоПодчиненным КАК НЕИспользованиеПодразделенияСИспользуемостьюПоПодчиненным
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияИспользованияШтатногоРасписания КАК ИсторияИспользованияШтатногоРасписания
			|		ПО НЕИспользованиеПодразделенияСИспользуемостьюПоПодчиненным.Подразделение = ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания.Подразделение
			|			И НЕИспользованиеПодразделенияСИспользуемостьюПоПодчиненным.Период >= ИсторияИспользованияШтатногоРасписания.Дата
			|
			|СГРУППИРОВАТЬ ПО
			|	НЕИспользованиеПодразделенияСИспользуемостьюПоПодчиненным.Период,
			|	НЕИспользованиеПодразделенияСИспользуемостьюПоПодчиненным.Подразделение,
			|	ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДатыСведенийПоПозициям.Период,
			|	ДатыСведенийПоПозициям.Подразделение,
			|	МИНИМУМ(ИсторияИспользованияШтатногоРасписания.Используется) КАК Используется
			|ПОМЕСТИТЬ ВТИспользованиеПодразделенийВПозициях
			|ИЗ
			|	ВТДатыСведенийПоПозициям КАК ДатыСведенийПоПозициям
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияИспользованияШтатногоРасписания КАК ИсторияИспользованияШтатногоРасписания
			|		ПО ДатыСведенийПоПозициям.ПозицияШтатногоРасписания = ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания
			|			И ДатыСведенийПоПозициям.Дата = ИсторияИспользованияШтатногоРасписания.Дата
			|
			|СГРУППИРОВАТЬ ПО
			|	ДатыСведенийПоПозициям.Период,
			|	ДатыСведенийПоПозициям.Подразделение
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИспользованиеПодразделенияСИспользуемостьюПоПодчиненным.Период,
			|	ИспользованиеПодразделенияСИспользуемостьюПоПодчиненным.Подразделение,
			|	ВЫБОР
			|		КОГДА ИспользованиеПодразделенияСИспользуемостьюПоПодчиненным.Используется = ЛОЖЬ
			|			ТОГДА ЕСТЬNULL(ИспользованиеПодразделенийВПозициях.Используется, ЛОЖЬ)
			|		ИНАЧЕ ИспользованиеПодразделенияСИспользуемостьюПоПодчиненным.Используется
			|	КОНЕЦ КАК Используется
			|ПОМЕСТИТЬ ВТТекущийНаборСИспользуемостью
			|ИЗ
			|	ВТИспользованиеПодразделенияСИспользуемостьюПоПодчиненным КАК ИспользованиеПодразделенияСИспользуемостьюПоПодчиненным
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИспользованиеПодразделенийВПозициях КАК ИспользованиеПодразделенийВПозициях
			|		ПО ИспользованиеПодразделенияСИспользуемостьюПоПодчиненным.Период = ИспользованиеПодразделенийВПозициях.Период
			|			И ИспользованиеПодразделенияСИспользуемостьюПоПодчиненным.Подразделение = ИспользованиеПодразделенийВПозициях.Подразделение
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТекущийНаборСИспользуемостью.Период,
			|	ТекущийНаборСИспользуемостью.Подразделение,
			|	ТекущийНаборСИспользуемостью.Используется
			|ИЗ
			|	ВТТекущийНаборСИспользуемостью КАК ТекущийНаборСИспользуемостью";
			
		ВыборкаПоИспользуемости = Запрос.Выполнить().Выбрать();
		
		Пока ВыборкаПоИспользуемости.Следующий() Цикл
			
			Если ВыборкаПоИспользуемости.Используется Тогда
				
				НайденныеСтроки = ТаблицаНабора.НайтиСтроки(Новый Структура("Подразделение", ВыборкаПоИспользуемости.Подразделение));
				Если НайденныеСтроки.Количество() > 0 Тогда
					НайденныеСтроки[0].Используется = Истина;
				КонецЕсли; 
				
			КонецЕсли; 
			
		КонецЦикла; 
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ТекущийНаборСИспользуемостью.Период,
			|	ВЫРАЗИТЬ(ТекущийНаборСИспользуемостью.Подразделение КАК Справочник.ПодразделенияОрганизаций).Родитель КАК Подразделение,
			|	МИНИМУМ(ТекущийНаборСИспользуемостью.Используется) КАК Используется,
			|	&Регистратор КАК Регистратор,
			|	ИСТИНА КАК Активность,
			|	&СледующийУровень КАК Уровень
			|ИЗ
			|	ВТТекущийНаборСИспользуемостью КАК ТекущийНаборСИспользуемостью
			|ГДЕ
			|	ВЫРАЗИТЬ(ТекущийНаборСИспользуемостью.Подразделение КАК Справочник.ПодразделенияОрганизаций).Родитель <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
			|
			|СГРУППИРОВАТЬ ПО
			|	ТекущийНаборСИспользуемостью.Период,
			|	ТекущийНаборСИспользуемостью.Подразделение";
			
		ВыборкаПоРодителям = Запрос.Выполнить().Выбрать();
		Пока ВыборкаПоРодителям.Следующий() Цикл
		
			НайденныеСтроки = ТаблицаНабора.НайтиСтроки(Новый Структура("Подразделение", ВыборкаПоРодителям.Подразделение));
			Если НайденныеСтроки.Количество() > 0 Тогда
				Если ВыборкаПоРодителям.Используется Тогда
					НайденныеСтроки[0].Используется = Истина;
				КонецЕсли; 
			Иначе
				ЗаполнитьЗначенияСвойств(ТаблицаНабора.Добавить(), ВыборкаПоРодителям);
			КонецЕсли; 
			
		КонецЦикла; 
		
	КонецЦикла;
	
	ТекущийНабор.Загрузить(ТаблицаНабора);
	
КонецПроцедуры

Процедура СохранитьИзменениеИспользованияПодразделений(ТекущийНабор) Экспорт
	
	Регистратор = ТекущийНабор.Отбор.Регистратор.Значение;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ТекущийНабор", ТекущийНабор);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИсторияИспользованияПодразделений.Подразделение
	|ПОМЕСТИТЬ ВТПредыдущийНабор
	|ИЗ
	|	РегистрСведений.УдалитьИсторияИспользованияПодразделений КАК ИсторияИспользованияПодразделений
	|ГДЕ
	|	ИсторияИспользованияПодразделений.Регистратор = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТекущийНабор.Период,
	|	ТекущийНабор.Подразделение,
	|	ТекущийНабор.Используется
	|ПОМЕСТИТЬ ВТТекущийНабор
	|ИЗ
	|	&ТекущийНабор КАК ТекущийНабор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТекущийНабор.Период, ДАТАВРЕМЯ(1, 1, 1)) КАК Период,
	|	ЕСТЬNULL(ТекущийНабор.Подразделение, ПредыдущийНабор.Подразделение) КАК Подразделение,
	|	ЕСТЬNULL(ТекущийНабор.Используется, ЛОЖЬ) КАК Используется
	|ПОМЕСТИТЬ ВТПодразделенияНаборов
	|ИЗ
	|	ВТПредыдущийНабор КАК ПредыдущийНабор
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТТекущийНабор КАК ТекущийНабор
	|		ПО ПредыдущийНабор.Подразделение = ТекущийНабор.Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ИсторияИспользованияПодразделений.Период) КАК Период,
	|	ИсторияИспользованияПодразделений.Подразделение,
	|	ИсторияИспользованияПодразделений.Используется
	|ПОМЕСТИТЬ ВТРанниеПериодыИспользованияПодразделений
	|ИЗ
	|	РегистрСведений.УдалитьИсторияИспользованияПодразделений КАК ИсторияИспользованияПодразделений
	|ГДЕ
	|	ИсторияИспользованияПодразделений.Регистратор <> &Регистратор
	|	И ИсторияИспользованияПодразделений.Используется
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсторияИспользованияПодразделений.Подразделение,
	|	ИсторияИспользованияПодразделений.Используется
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ИсторияИспользованияПодразделений.Период) КАК Период,
	|	ИсторияИспользованияПодразделений.Подразделение
	|ПОМЕСТИТЬ ВТПоздниеПериодыПодразделений
	|ИЗ
	|	РегистрСведений.УдалитьИсторияИспользованияПодразделений КАК ИсторияИспользованияПодразделений
	|ГДЕ
	|	ИсторияИспользованияПодразделений.Регистратор <> &Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсторияИспользованияПодразделений.Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсторияИспользованияПодразделений.Период КАК Период,
	|	ИсторияИспользованияПодразделений.Подразделение,
	|	ИсторияИспользованияПодразделений.Используется
	|ПОМЕСТИТЬ ВТПоздниеПериодыИспользованияПодразделений
	|ИЗ
	|	ВТПоздниеПериодыПодразделений КАК ПоздниеПериодыПодразделений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьИсторияИспользованияПодразделений КАК ИсторияИспользованияПодразделений
	|		ПО ПоздниеПериодыПодразделений.Период = ИсторияИспользованияПодразделений.Период
	|			И ПоздниеПериодыПодразделений.Подразделение = ИсторияИспользованияПодразделений.Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодразделенияНаборов.Подразделение,
	|	ВЫБОР
	|		КОГДА РанниеПериодыИспользованияПодразделений.Период ЕСТЬ NULL 
	|				ИЛИ РанниеПериодыИспользованияПодразделений.Период > ПодразделенияНаборов.Период
	|					И ПодразделенияНаборов.Используется
	|			ТОГДА ВЫБОР
	|					КОГДА ПодразделенияНаборов.Используется
	|						ТОГДА ПодразделенияНаборов.Период
	|					ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|				КОНЕЦ
	|		ИНАЧЕ РанниеПериодыИспользованияПодразделений.Период
	|	КОНЕЦ КАК ДатаСоздания,
	|	ВЫБОР
	|		КОГДА ПоздниеПериодыИспользованияПодразделений.Период ЕСТЬ NULL 
	|				ИЛИ ПоздниеПериодыИспользованияПодразделений.Период < ПодразделенияНаборов.Период
	|			ТОГДА ВЫБОР
	|					КОГДА ПодразделенияНаборов.Используется
	|						ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|					ИНАЧЕ ПодразделенияНаборов.Период
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПоздниеПериодыИспользованияПодразделений.Используется
	|					ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|				ИНАЧЕ ПоздниеПериодыИспользованияПодразделений.Период
	|			КОНЕЦ
	|	КОНЕЦ КАК ДатаРасформирования
	|ПОМЕСТИТЬ ВТАктуальныеДанныеПодразделений
	|ИЗ
	|	ВТПодразделенияНаборов КАК ПодразделенияНаборов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРанниеПериодыИспользованияПодразделений КАК РанниеПериодыИспользованияПодразделений
	|		ПО ПодразделенияНаборов.Подразделение = РанниеПериодыИспользованияПодразделений.Подразделение
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоздниеПериодыИспользованияПодразделений КАК ПоздниеПериодыИспользованияПодразделений
	|		ПО ПодразделенияНаборов.Подразделение = ПоздниеПериодыИспользованияПодразделений.Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АктуальныеДанныеПодразделений.Подразделение,
	|	АктуальныеДанныеПодразделений.ДатаСоздания,
	|	ВЫБОР
	|		КОГДА АктуальныеДанныеПодразделений.ДатаСоздания = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|		ИНАЧЕ АктуальныеДанныеПодразделений.ДатаРасформирования
	|	КОНЕЦ КАК ДатаРасформирования,
	|	ВЫБОР
	|		КОГДА АктуальныеДанныеПодразделений.ДатаСоздания = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Сформировано,
	|	ВЫБОР
	|		КОГДА АктуальныеДанныеПодразделений.ДатаСоздания = ДАТАВРЕМЯ(1, 1, 1)
	|				ИЛИ АктуальныеДанныеПодразделений.ДатаРасформирования = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Расформировано
	|ПОМЕСТИТЬ ВТДанныеПодразделений
	|ИЗ
	|	ВТАктуальныеДанныеПодразделений КАК АктуальныеДанныеПодразделений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеПодразделений.Подразделение КАК Подразделение,
	|	ДанныеПодразделений.ДатаСоздания,
	|	ДанныеПодразделений.ДатаРасформирования,
	|	ДанныеПодразделений.Сформировано,
	|	ДанныеПодразделений.Расформировано
	|ИЗ
	|	ВТДанныеПодразделений КАК ДанныеПодразделений
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|		ПО ДанныеПодразделений.Подразделение = ПодразделенияОрганизаций.Ссылка
	|ГДЕ
	|	(ДанныеПодразделений.ДатаСоздания <> ПодразделенияОрганизаций.ДатаСоздания
	|			ИЛИ ДанныеПодразделений.ДатаРасформирования <> ПодразделенияОрганизаций.ДатаРасформирования
	|			ИЛИ ДанныеПодразделений.Сформировано <> ПодразделенияОрганизаций.Сформировано
	|			ИЛИ ДанныеПодразделений.Расформировано <> ПодразделенияОрганизаций.Расформировано)";
	
	ИзменившиесяДанные = Запрос.Выполнить().Выбрать();
	
	Пока ИзменившиесяДанные.Следующий() Цикл
		
		ПодразделениеОбъект = ИзменившиесяДанные.Подразделение.ПолучитьОбъект();
		
		Попытка 
			ПодразделениеОбъект.Заблокировать();
		Исключение
			ТекстИсключенияЗаписи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось изменить подразделение ""%1"".
			|Возможно, подразделение редактируется другим пользователем'"),
			ПодразделениеОбъект.Наименование);			
			ВызватьИсключение ТекстИсключенияЗаписи;
		КонецПопытки;
		
		ЗаполнитьЗначенияСвойств(ПодразделениеОбъект, ИзменившиесяДанные);
		
		Если НЕ ПодразделениеОбъект.Расформировано И ПодразделениеОбъект.ПометкаУдаления Тогда
			ПодразделениеОбъект.ПометкаУдаления = Ложь;
		КонецЕсли; 
		
		УправлениеШтатнымРасписанием.ОтключитьОбновлениеСтруктурыШтатногоРасписания(ПодразделениеОбъект);
		
		ПодразделениеОбъект.Записать();
		
	КонецЦикла;	
	
	Если ИзменившиесяДанные.Количество() > 0 Тогда
		
		Справочники.ШтатноеРасписание.ОбновитьСтруктуруШтатногоРасписанияПоПодразделениям();
		
	КонецЕсли; 
	
КонецПроцедуры	

#КонецОбласти

#КонецЕсли