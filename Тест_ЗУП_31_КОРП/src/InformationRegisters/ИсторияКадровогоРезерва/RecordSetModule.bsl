#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если УжеСуществуютЗаписиРезерваНаЭтуДату(ЭтотОбъект) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТекущийНабор.ПозицияРезерва,
		|	ТекущийНабор.ФизическоеЛицо
		|ПОМЕСТИТЬ ВТТекущийНабор
		|ИЗ
		|	&ТекущийНабор КАК ТекущийНабор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИсторияКадровогоРезерва.ПозицияРезерва,
		|	ИсторияКадровогоРезерва.ФизическоеЛицо
		|ИЗ
		|	РегистрСведений.ИсторияКадровогоРезерва КАК ИсторияКадровогоРезерва
		|ГДЕ
		|	ИсторияКадровогоРезерва.Регистратор = &Ссылка
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВТТекущийНабор.ПозицияРезерва,
		|	ВТТекущийНабор.ФизическоеЛицо
		|ИЗ
		|	ВТТекущийНабор КАК ВТТекущийНабор";
	
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ТекущийНабор",	ЭтотОбъект);
	ИзмененныеИзмерения = Запрос.Выполнить().Выгрузить();
	
	ЭтотОбъект.ДополнительныеСвойства.Вставить("ИзмененныеИзмерения", ИзмененныеИзмерения);

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект.ДополнительныеСвойства.Свойство("ИзмененныеИзмерения") Тогда
		ИзмененныеИзмерения = ЭтотОбъект.ДополнительныеСвойства.ИзмененныеИзмерения;
	Иначе
		ИзмененныеИзмерения = Неопределено;
	КонецЕсли;
	
	КадровыйРезерв.ЗаполнитьЗаписиКадровогоРезерва(ЭтотОбъект, ИзмененныеИзмерения);
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция УжеСуществуютЗаписиРезерваНаЭтуДату(НаборЗаписей)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТекущийНабор.Период,
		|	ТекущийНабор.ПозицияРезерва,
		|	ТекущийНабор.ФизическоеЛицо
		|ПОМЕСТИТЬ ВТТекущийНабор
		|ИЗ
		|	&ТекущийНабор КАК ТекущийНабор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ИсторияКадровогоРезерва.Регистратор) КАК Регистратор,
		|	ИсторияКадровогоРезерва.ФизическоеЛицо,
		|	ИсторияКадровогоРезерва.ПозицияРезерва
		|ИЗ
		|	РегистрСведений.ИсторияКадровогоРезерва КАК ИсторияКадровогоРезерва
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТТекущийНабор КАК ВТТекущийНабор
		|		ПО ИсторияКадровогоРезерва.ПозицияРезерва = ВТТекущийНабор.ПозицияРезерва
		|			И ИсторияКадровогоРезерва.ФизическоеЛицо = ВТТекущийНабор.ФизическоеЛицо
		|			И ИсторияКадровогоРезерва.Период = ВТТекущийНабор.Период
		|			И (ИсторияКадровогоРезерва.Регистратор <> &ИсключаяРегистратор)
		|
		|СГРУППИРОВАТЬ ПО
		|	ИсторияКадровогоРезерва.ФизическоеЛицо,
		|	ИсторияКадровогоРезерва.ПозицияРезерва";
	
	Запрос.УстановитьПараметр("ИсключаяРегистратор", ЭтотОбъект.Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ТекущийНабор",	НаборЗаписей);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	Иначе
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'За этот период для %1 по позиции %2 уже введены данные документом %3.'"),
				Выборка.ФизическоеЛицо,
				Выборка.ПозицияРезерва,
				Выборка.Регистратор);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЦикла;
		
		Возврат Истина;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли