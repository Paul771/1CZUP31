#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьИсторию() Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьИсториюИзмененияШтатногоРасписания") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИсторияНастройкиУправленческойЗарплатыПозиций.Регистратор
		|ИЗ
		|	РегистрСведений.ИсторияНастроекРасчетаУправленческойЗарплатыПозицийШтатногоРасписания КАК ИсторияНастройкиУправленческойЗарплатыПозиций";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ИсторияИспользованияШтатногоРасписания.Дата,
			|	ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
			|	ИсторияИспользованияШтатногоРасписания.Регистратор КАК Регистратор,
			|	ВложенныйЗапрос.ИспользоватьУправленческиеНачисления,
			|	ВложенныйЗапрос.ДоначислятьДоУправленческогоУчета
			|ИЗ
			|	РегистрСведений.ИсторияИспользованияШтатногоРасписания КАК ИсторияИспользованияШтатногоРасписания
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			|			ИзменениеШтатногоРасписанияПозиции.Ссылка.ДатаВступленияВСилу КАК Дата,
			|			ИзменениеШтатногоРасписанияПозиции.Позиция КАК ПозицияШтатногоРасписания,
			|			ИзменениеШтатногоРасписанияПозиции.ИспользоватьУправленческиеНачисления КАК ИспользоватьУправленческиеНачисления,
			|			ИзменениеШтатногоРасписанияПозиции.ДоначислятьДоУправленческогоУчета КАК ДоначислятьДоУправленческогоУчета,
			|			ИзменениеШтатногоРасписанияПозиции.Ссылка КАК Регистратор
			|		ИЗ
			|			Документ.ИзменениеШтатногоРасписания.Позиции КАК ИзменениеШтатногоРасписанияПозиции
			|		ГДЕ
			|			ИзменениеШтатногоРасписанияПозиции.Ссылка.Проведен
			|		
			|		ОБЪЕДИНИТЬ ВСЕ
			|		
			|		ВЫБРАТЬ
			|			УтверждениеШтатногоРасписанияПозиции.Ссылка.МесяцВступленияВСилу,
			|			УтверждениеШтатногоРасписанияПозиции.Позиция,
			|			УтверждениеШтатногоРасписанияПозиции.ИспользоватьУправленческиеНачисления,
			|			УтверждениеШтатногоРасписанияПозиции.ДоначислятьДоУправленческогоУчета,
			|			УтверждениеШтатногоРасписанияПозиции.Ссылка
			|		ИЗ
			|			Документ.УтверждениеШтатногоРасписания.Позиции КАК УтверждениеШтатногоРасписанияПозиции
			|		ГДЕ
			|			УтверждениеШтатногоРасписанияПозиции.Ссылка.Проведен) КАК ВложенныйЗапрос
			|		ПО ИсторияИспользованияШтатногоРасписания.Регистратор = ВложенныйЗапрос.Регистратор
			|			И ИсторияИспользованияШтатногоРасписания.Дата = ВложенныйЗапрос.Дата
			|			И ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания = ВложенныйЗапрос.ПозицияШтатногоРасписания
			|
			|УПОРЯДОЧИТЬ ПО
			|	Регистратор";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
				
				НаборЗаписей = РегистрыСведений.ИсторияНастроекРасчетаУправленческойЗарплатыПозицийШтатногоРасписания.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
				
				Пока Выборка.Следующий() Цикл
					ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
				КонецЦикла;
				
				НаборЗаписей.ОбменДанными.Загрузка = Истина;
				НаборЗаписей.Записать();
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли