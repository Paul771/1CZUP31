#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция ПредставлениеСведенийОбОбразованииФизическогоЛица(ФизическоеЛицоСсылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОбразованиеФЛ.ФизическоеЛицо,
		|	ОбразованиеФЛ.НомерПоПорядку,
		|	ОбразованиеФЛ.ВидОбразования,
		|	ОбразованиеФЛ.УчебноеЗаведение,
		|	ОбразованиеФЛ.Специальность,
		|	ОбразованиеФЛ.Диплом,
		|	ОбразованиеФЛ.ГодОкончания,
		|	ОбразованиеФЛ.Квалификация
		|ИЗ
		|	РегистрСведений.УдалитьОбразованиеФизическихЛиц КАК ОбразованиеФЛ
		|ГДЕ
		|	ОбразованиеФЛ.ФизическоеЛицо = &ФизическоеЛицоСсылка";
		
	Запрос.УстановитьПараметр("ФизическоеЛицоСсылка", ФизическоеЛицоСсылка);
	
	Возврат ПредставлениеСведенийОбОбразованииПоКоллекцииЗаписей(Запрос.Выполнить().Выгрузить());
	
КонецФункции

Функция ПредставлениеСведенийОбОбразованииПоКоллекцииЗаписей(НаборЗаписей) Экспорт
	
	ПредставлениеСведенийОбОбразовании = НСтр("ru='Нет сведений'");
	
	ПоследнеееПолученноеОбразование = Неопределено;
	Для Каждого СтрокаОбразование Из НаборЗаписей Цикл
		Если ПоследнеееПолученноеОбразование = Неопределено ИЛИ ПоследнеееПолученноеОбразование.ГодОкончания < СтрокаОбразование.ГодОкончания Тогда
			ПоследнеееПолученноеОбразование = СтрокаОбразование;
		КонецЕсли; 
	КонецЦикла;
	
	Если ПоследнеееПолученноеОбразование <> Неопределено Тогда
		ПредставлениеСведенийОбОбразовании = ?(ЗначениеЗаполнено(ПоследнеееПолученноеОбразование.ВидОбразования), Строка(ПоследнеееПолученноеОбразование.ВидОбразования), "")
									+ Символы.ПС
									+ ?(ЗначениеЗаполнено(ПоследнеееПолученноеОбразование.УчебноеЗаведение), Строка(ПоследнеееПолученноеОбразование.УчебноеЗаведение), "")
									+ ?(ЗначениеЗаполнено(ПоследнеееПолученноеОбразование.ГодОкончания), " (" + Формат(ПоследнеееПолученноеОбразование.ГодОкончания, "ЧГ=0") + ")", "") 
									+ Символы.ПС
									+ ?(ЗначениеЗаполнено(ПоследнеееПолученноеОбразование.Специальность), Строка(ПоследнеееПолученноеОбразование.Специальность), "")
									+ ?(ЗначениеЗаполнено(ПоследнеееПолученноеОбразование.Квалификация), ", " + Строка(ПоследнеееПолученноеОбразование.Квалификация), "");
	КонецЕсли; 
	
	Возврат ПредставлениеСведенийОбОбразовании;
	
КонецФункции

#КонецОбласти

#КонецЕсли