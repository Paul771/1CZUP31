#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("СотрудникСсылка") И ЗначениеЗаполнено(Параметры.СотрудникСсылка) Тогда
		
		ФизическоеЛицо = Параметры.СотрудникСсылка.ФизическоеЛицо;
		ЗаписьПоСотруднику = РегистрыСведений.ПричиныУвольненияСотрудников.СоздатьМенеджерЗаписи();
		ЗаписьПоСотруднику.Сотрудник = Параметры.СотрудникСсылка;
		ЗаписьПоСотруднику.ФизическоеЛицо = ФизическоеЛицо;
		ЗаписьПоСотруднику.Прочитать();
		
		Если ЗначениеЗаполнено(ЗаписьПоСотруднику.Сотрудник) Тогда
			ЗначениеВДанныеФормы(ЗаписьПоСотруднику, Запись);	
		Иначе
			Запись.Сотрудник = Параметры.СотрудникСсылка;		
			Запись.ФизическоеЛицо = ФизическоеЛицо;		
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Запись.Сотрудник) Тогда 
		ТекстСообщения = НСтр("ru = 'Поле Сотрудник не заполнено. Ввод причин увольнений возможен только в контексте сотрудника.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		Возврат;
	КонецЕсли;	
	
	ЗначенияДляЗаполнения = Новый Структура();
	ЗначенияДляЗаполнения.Вставить("Ответственный", "Запись.Ответственный");
	ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
	Запись.ДатаРегистрации = ТекущаяДатаСеанса();
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Запись.ФизическоеЛицо, "Наименование, Пол");
	СклонениеФИО = "";
	ФизическиеЛицаЗарплатаКадры.Просклонять(ЗначенияРеквизитов.Наименование, 2, СклонениеФИО, ЗначенияРеквизитов.Пол);
	ФамилияИнициалы = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(СклонениеФИО);
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Причина увольнения %1'"), 
		ФамилияИнициалы);

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ИзменениеПричинУвольненияСотрудника",, Запись.Сотрудник);
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Если ЗаписьУдалена(Запись.Сотрудник) Тогда 
		Оповестить("ИзменениеПричинУвольненияСотрудника",, Запись.Сотрудник);
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ЗаписьУдалена(Сотрудник)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ПричиныУвольненияСотрудников.Сотрудник КАК Сотрудник
		|ИЗ
		|	РегистрСведений.ПричиныУвольненияСотрудников КАК ПричиныУвольненияСотрудников
		|ГДЕ
		|	ПричиныУвольненияСотрудников.Сотрудник = &Сотрудник";
	
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
	Возврат Запрос.Выполнить().Выбрать().Количество() = 0;
	
КонецФункции	

#КонецОбласти


