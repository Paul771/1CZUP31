#Область ОписаниеПеременных

&НаКлиенте
Перем МассивУдаляемыхСотрудников;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Для Каждого ЭлементОтбора Из Параметры.Отбор Цикл
		ПолеСписка = Элементы.Список.ПодчиненныеЭлементы.Найти(ЭлементОтбора.Ключ);
		Если НЕ ПолеСписка = Неопределено Тогда 
			Элементы.Список.ПодчиненныеЭлементы[ЭлементОтбора.Ключ].Видимость = Ложь;
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийСписка

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
	
	ОбновитьМассивУдаляемыхСотрудников(Элементы.Список.ВыделенныеСтроки, МассивУдаляемыхСотрудников);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПослеУдаления(Элемент)
	
	Если ТипЗнч(МассивУдаляемыхСотрудников) = Тип("Массив") Тогда 
		
		Для Каждого УдаляемыйСотрудник Из МассивУдаляемыхСотрудников Цикл 
			Оповестить("ИзменениеПричинУвольненияСотрудника",, УдаляемыйСотрудник);
		КонецЦикла;
		
		МассивУдаляемыхСотрудников = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ОбновитьМассивУдаляемыхСотрудников(Знач ВыделенныеСтроки, МассивУдаляемыхСотрудников)
	
	МассивУдаляемыхСотрудников = Новый Массив;
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл 
		МассивУдаляемыхСотрудников.Добавить(ВыделеннаяСтрока.Сотрудник);
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти
