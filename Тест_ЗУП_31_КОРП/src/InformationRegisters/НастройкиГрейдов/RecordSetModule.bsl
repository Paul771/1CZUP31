#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект.Количество() > 0 Тогда
		
		Запись = ЭтотОбъект[0];
		
		ТекущаяЗапись = РегистрыСведений.НастройкиГрейдов.СоздатьМенеджерЗаписи();
		ТекущаяЗапись.Прочитать();
		
		Если Не ЗначениеЗаполнено(ТекущаяЗапись.ИспользоватьГрейды)
			Или ТекущаяЗапись.ИспользоватьГрейды <> Запись.ИспользоватьГрейды Тогда
			
			ДополнительныеСвойства.Вставить("ОбновитьСвойстваДолжностей", Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Грейды.УстановитьПараметрыНабораСвойствСправочников();
	
	Если ДополнительныеСвойства.Свойство("ОбновитьСвойстваДолжностей")
		И ДополнительныеСвойства.ОбновитьСвойстваДолжностей Тогда
		
		РегистрыСведений.ГрейдыПозиций.ОбновитьПодключаемыеХарактеристики(ЭтотОбъект[0].ИспользоватьГрейды);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
