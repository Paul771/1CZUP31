

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ВопросАнкеты = Параметры.Отбор.Вопрос;
	ХарактеристикаПерсонала = Параметры.Отбор.ХарактеристикаПерсонала;
	
	ПрочитатьВопросыАнкет();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Не Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей.Очистить();
	Для каждого Оценка Из Оценки Цикл
		НоваяОценка = НаборЗаписей.Добавить();
		НоваяОценка.ХарактеристикаПерсонала = ХарактеристикаПерсонала;
		НоваяОценка.Вопрос = ВопросАнкеты;
		НоваяОценка.ВариантОтвета = Оценка.Ответ;
		НоваяОценка.ОценкаОтвета = Оценка.Оценка;
	КонецЦикла; 
	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОценкиОценкаПриИзменении(Элемент)
	Оценки.Сортировать("Оценка Убыв");
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроставитьОбратные(Команда)
	
	Для СтрокаОценка=1 По Цел(Оценки.Количество()/2) Цикл
		ОценкаВерхняя = Оценки[СтрокаОценка-1].Оценка;
		Оценки[СтрокаОценка-1].Оценка = Оценки[Оценки.Количество()-СтрокаОценка].Оценка;
		Оценки[Оценки.Количество()-СтрокаОценка].Оценка = ОценкаВерхняя;
	КонецЦикла; 
	
	Оценки.Сортировать("Оценка Убыв");
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрочитатьВопросыАнкет()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОценкиВариантовОтвета.ВариантОтвета КАК ВариантОтвета,
	|	ОценкиВариантовОтвета.ОценкаОтвета КАК ОценкаОтвета
	|ПОМЕСТИТЬ ВТОценки
	|ИЗ
	|	РегистрСведений.ОценкиВариантовОтвета КАК ОценкиВариантовОтвета
	|ГДЕ
	|	ОценкиВариантовОтвета.ХарактеристикаПерсонала = &ХарактеристикаПерсонала
	|	И ОценкиВариантовОтвета.ВариантОтвета.Владелец = &ВопросАнкеты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВариантОтвета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВариантыОтветовАнкет.Ссылка КАК Ответ,
	|	ЕСТЬNULL(ВТОценки.ОценкаОтвета, ВариантыОтветовАнкет.РеквизитДопУпорядочивания) КАК Оценка
	|ИЗ
	|	Справочник.ВариантыОтветовАнкет КАК ВариантыОтветовАнкет
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОценки КАК ВТОценки
	|		ПО ВариантыОтветовАнкет.Ссылка = ВТОценки.ВариантОтвета
	|ГДЕ
	|	ВариантыОтветовАнкет.Владелец = &ВопросАнкеты
	|
	|УПОРЯДОЧИТЬ ПО
	|	Оценка УБЫВ";
	Запрос.УстановитьПараметр("ВопросАнкеты",ВопросАнкеты);
	Запрос.УстановитьПараметр("ХарактеристикаПерсонала", ХарактеристикаПерсонала);
	Оценки.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

#КонецОбласти
