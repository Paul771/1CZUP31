#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьФизическоеЛицо(ПараметрыОбновления) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	АнкетыКандидатов.Кандидат
		|ПОМЕСТИТЬ ВТКандидаты
		|ИЗ
		|	РегистрСведений.АнкетыКандидатов КАК АнкетыКандидатов
		|ГДЕ
		|	АнкетыКандидатов.ФизическоеЛицо = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1000
		|	Кандидаты.Кандидат
		|ИЗ
		|	ВТКандидаты КАК Кандидаты";
	РезультатЗапроса = Запрос.Выполнить();
	
	ПараметрыОбновления.ОбработкаЗавершена = Ложь;
	Если РезультатЗапроса.Пустой() Тогда
		ПараметрыОбновления.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;	
	
	Запрос.УстановитьПараметр("Кандидаты", РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Кандидат"));
	Запрос.Текст =
		"ВЫБРАТЬ
		|	АнкетыКандидатов.Кандидат.ФизическоеЛицо КАК ФизическоеЛицо,
		|	АнкетыКандидатов.Кандидат КАК Кандидат,
		|	АнкетыКандидатов.Вакансия КАК Вакансия,
		|	АнкетыКандидатов.ЭтапРаботыСКандидатом КАК ЭтапРаботыСКандидатом,
		|	АнкетыКандидатов.Анкета КАК Анкета
		|ИЗ
		|	РегистрСведений.АнкетыКандидатов КАК АнкетыКандидатов
		|ГДЕ
		|	АнкетыКандидатов.Кандидат В(&Кандидаты)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Кандидат";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Кандидат") Цикл
		НаборЗаписей = РегистрыСведений.АнкетыКандидатов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Кандидат.Установить(Выборка.Кандидат);
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
		КонецЦикла;
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли