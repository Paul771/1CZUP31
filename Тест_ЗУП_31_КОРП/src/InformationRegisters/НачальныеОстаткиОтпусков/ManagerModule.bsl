#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьНачальныеОстаткиОтпусков() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НачальныеОстаткиОтпусков.Регистратор
		|ИЗ
		|	РегистрСведений.НачальныеОстаткиОтпусков КАК НачальныеОстаткиОтпусков";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	УдалитьНачальныеОстаткиОтпусков.Сотрудник.ГоловнаяОрганизация КАК Организация,
			|	УдалитьНачальныеОстаткиОтпусков.Сотрудник КАК Сотрудник,
			|	УдалитьНачальныеОстаткиОтпусков.ВидЕжегодногоОтпуска,
			|	ДОБАВИТЬКДАТЕ(УдалитьНачальныеОстаткиОтпусков.ДатаНачальныхОстатков, ДЕНЬ, 1) КАК ДатаОстатка,
			|	ДОБАВИТЬКДАТЕ(ДОБАВИТЬКДАТЕ(УдалитьНачальныеОстаткиОтпусков.ДатаНачальныхОстатков, ГОД, -1), ДЕНЬ, 1) КАК РабочийГодНачало,
			|	УдалитьНачальныеОстаткиОтпусков.ДатаНачальныхОстатков КАК РабочийГодОкончание,
			|	УдалитьНачальныеОстаткиОтпусков.КоличествоДней КАК КоличествоДней
			|ИЗ
			|	РегистрСведений.УдалитьНачальныеОстаткиОтпусков КАК УдалитьНачальныеОстаткиОтпусков
			|
			|УПОРЯДОЧИТЬ ПО
			|	Организация,
			|	Сотрудник
			|ИТОГИ
			|	МИНИМУМ(РабочийГодНачало)
			|ПО
			|	Организация";
			
		ВыборкаПоОрганизациям = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоОрганизациям.Следующий() Цикл
			
			ДокументПереносДанных = Документы.ПереносДанных.СоздатьДокумент();
			ДокументПереносДанных.Организация = ВыборкаПоОрганизациям.Организация;
			ДокументПереносДанных.Дата = ВыборкаПоОрганизациям.РабочийГодНачало;
			
			ДокументПереносДанных.ТаблицаРегистров.Добавить().Имя ="НачальныеОстаткиОтпусков";
			
			ДвиженияНачальныхОстатковОтпусков = ДокументПереносДанных.Движения.НачальныеОстаткиОтпусков;
			
			ВыборкаПоСотрудникам = ВыборкаПоОрганизациям.Выбрать();
			Пока ВыборкаПоСотрудникам.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(ДвиженияНачальныхОстатковОтпусков.Добавить(), ВыборкаПоСотрудникам);
			КонецЦикла; 
			
			ДокументПереносДанных.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
			ДокументПереносДанных.ОбменДанными.Загрузка = Истина;
			ДокументПереносДанных.Записать(РежимЗаписиДокумента.Запись);
			
		КонецЦикла; 
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
