#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьНаборЗаписей(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	
	ИменаПолей = ОбменДаннымиЗарплатаКадры.ИменаПолейОграниченияРегистрацииРегистраСведений();
	ИменаПолей.Организации = "Организация";
	ИменаПолей.ФизическиеЛица = "ФизическоеЛицо";
	ИменаПолей.ДатыПолученияДанных = "Период";
	
	Возврат ОбменДаннымиЗарплатаКадры.ОграниченияРегистрацииРегистраСведений(ЭтотОбъект, ИменаПолей);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет номера лицевых счетов в наборе записей, зарегистрированы они или нет.
//
// Параметры:
//		НаборЗаписей - Набор записей регистра сведений "Лицевые счета сотрудников по зарплатным проектам".
//		Отказ - Если Истина, то запись набора не производится, иначе Ложь.
//
Процедура ПроверитьНаборЗаписей(НаборЗаписей, Отказ)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("НаборЗаписей", НаборЗаписей.Выгрузить());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НаборЗаписей.Организация КАК Организация,
	|	НаборЗаписей.ЗарплатныйПроект КАК ЗарплатныйПроект,
	|	НаборЗаписей.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НаборЗаписей.НомерЛицевогоСчета КАК НомерЛицевогоСчета,
	|	НаборЗаписей.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ВТНаборЗаписей
	|ИЗ
	|	&НаборЗаписей КАК НаборЗаписей
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЛицевыеСчета.Организация КАК Организация,
	|	ЛицевыеСчета.ЗарплатныйПроект КАК ЗарплатныйПроект,
	|	ЛицевыеСчета.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЛицевыеСчета.НомерЛицевогоСчета КАК НомерЛицевогоСчета
	|ИЗ
	|	РегистрСведений.ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам КАК ЛицевыеСчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНаборЗаписей КАК НаборЗаписей
	|		ПО ЛицевыеСчета.Организация = НаборЗаписей.Организация
	|			И ЛицевыеСчета.ЗарплатныйПроект = НаборЗаписей.ЗарплатныйПроект
	|			И ЛицевыеСчета.НомерЛицевогоСчета = НаборЗаписей.НомерЛицевогоСчета
	|			И ЛицевыеСчета.ФизическоеЛицо <> НаборЗаписей.ФизическоеЛицо
	|
	|СГРУППИРОВАТЬ ПО
	|	ЛицевыеСчета.Организация,
	|	ЛицевыеСчета.ЗарплатныйПроект,
	|	ЛицевыеСчета.ФизическоеЛицо,
	|	ЛицевыеСчета.НомерЛицевогоСчета";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Обнаружен сотрудник с точно таким же номером лицевого счета - %1.'"),
					Выборка.ФизическоеЛицо);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Выборка.ФизическоеЛицо);
		КонецЦикла;
		
	КонецЕсли;
	
	ОтменаРегистрацииЛицевыхСчетов = Ложь;
	Если НаборЗаписей.ДополнительныеСвойства.Свойство("ОтменаРегистрацииЛицевыхСчетов", ОтменаРегистрацииЛицевыхСчетов) Тогда
		Если ОтменаРегистрацииЛицевыхСчетов Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Период", НаборЗаписей.Отбор.Период.Значение);
	Запрос.УстановитьПараметр("Организация", НаборЗаписей.Отбор.Организация.Значение);
	Запрос.УстановитьПараметр("ФизическоеЛицо", НаборЗаписей.Отбор.ФизическоеЛицо.Значение);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЛицевыеСчета.Организация КАК Организация,
	|	ЛицевыеСчета.ЗарплатныйПроект КАК ЗарплатныйПроект,
	|	ЛицевыеСчета.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЛицевыеСчета.НомерЛицевогоСчета КАК НомерЛицевогоСчета,
	|	ЛицевыеСчета.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	РегистрСведений.ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам КАК ЛицевыеСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНаборЗаписей КАК НаборЗаписей
	|		ПО ЛицевыеСчета.Организация = НаборЗаписей.Организация
	|			И ЛицевыеСчета.ФизическоеЛицо = НаборЗаписей.ФизическоеЛицо
	|			И (ЛицевыеСчета.ДокументОснование = НаборЗаписей.ДокументОснование
	|					И ЛицевыеСчета.ЗарплатныйПроект = НаборЗаписей.ЗарплатныйПроект
	|					И ЛицевыеСчета.НомерЛицевогоСчета = НаборЗаписей.НомерЛицевогоСчета
	|				ИЛИ НаборЗаписей.ДокументОснование <> ЗНАЧЕНИЕ(Документ.ПодтверждениеОткрытияЛицевыхСчетовСотрудников.ПустаяСсылка))
	|ГДЕ
	|	ЛицевыеСчета.Период = &Период
	|	И ЛицевыеСчета.Организация = &Организация
	|	И ЛицевыеСчета.ФизическоеЛицо = &ФизическоеЛицо
	|	И ЛицевыеСчета.ДокументОснование <> ЗНАЧЕНИЕ(Документ.ПодтверждениеОткрытияЛицевыхСчетовСотрудников.ПустаяСсылка)
	|	И НаборЗаписей.ДокументОснование ЕСТЬ NULL ";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Лицевые счета, введенные документом %1 нельзя удалять.'"),
					Выборка.ДокументОснование);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Выборка.ДокументОснование, , , Отказ);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли