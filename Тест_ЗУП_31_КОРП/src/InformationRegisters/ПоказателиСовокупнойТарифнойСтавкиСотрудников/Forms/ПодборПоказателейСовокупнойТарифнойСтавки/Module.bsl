
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьТаблицуПоказателей();
	
	УстановитьОтборИспользуемых(ЭтаФорма, ПоказыватьНеиспользуемыеПоказатели)
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоказыватьНеиспользуемыеПоказателиПриИзменении(Элемент)
	
	УстановитьОтборИспользуемых(ЭтаФорма, ПоказыватьНеиспользуемыеПоказатели)
	
КонецПроцедуры

#Область ОбработчикиСобытийТаблицыФормыПоказатели

&НаКлиенте
Процедура ПоказателиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.Показатели.ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(, Элементы.Показатели.ТекущиеДанные.Показатель);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ЗаписатьПоказателиСовокупнойТарифнойСтавкиСотрудников();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуПоказателей()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПоказателиРасчетаЗарплаты.Ссылка КАК Показатель,
	               |	ПоказателиРасчетаЗарплаты.КраткоеНаименование,
	               |	ПоказателиРасчетаЗарплаты.Идентификатор,
	               |	ПоказателиРасчетаЗарплаты.НеИспользуется,
	               |	ВЫБОР
	               |		КОГДА ПоказателиСовокупнойТарифнойСтавкиСотрудников.Показатель ЕСТЬ NULL 
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ КАК Выбран
	               |ИЗ
	               |	Справочник.ПоказателиРасчетаЗарплаты КАК ПоказателиРасчетаЗарплаты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПоказателиСовокупнойТарифнойСтавкиСотрудников КАК ПоказателиСовокупнойТарифнойСтавкиСотрудников
	               |		ПО (ПоказателиСовокупнойТарифнойСтавкиСотрудников.Показатель = ПоказателиРасчетаЗарплаты.Ссылка)
	               |ГДЕ
	               |	ПоказателиРасчетаЗарплаты.НазначениеПоказателя = ЗНАЧЕНИЕ(Перечисление.НазначенияПоказателейРасчетаЗарплаты.ДляСотрудника)
	               |	И ПоказателиРасчетаЗарплаты.СпособПримененияЗначений = ЗНАЧЕНИЕ(Перечисление.СпособыПримененияЗначенийПоказателейРасчетаЗарплаты.Постоянное)";
				   
	Показатели.Загрузить(Запрос.Выполнить().Выгрузить());			   
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьПоказателиСовокупнойТарифнойСтавкиСотрудников()
	
	ПоказателиСовокупнойТарифнойСтавки = Показатели.НайтиСтроки(Новый Структура("Выбран", Истина));
	
	НаборЗаписей = РегистрыСведений.ПоказателиСовокупнойТарифнойСтавкиСотрудников.СоздатьНаборЗаписей();
	
	Для Каждого ДанныеПоказателя Из ПоказателиСовокупнойТарифнойСтавки Цикл 
		НаборЗаписей.Добавить().Показатель = ДанныеПоказателя.Показатель;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборИспользуемых(Форма, ПоказыватьНеиспользуемые)
	
	Если ПоказыватьНеиспользуемые Тогда
		Форма.Элементы.Показатели.ОтборСтрок = Неопределено
	Иначе 
		Форма.Элементы.Показатели.ОтборСтрок = Новый ФиксированнаяСтруктура("НеИспользуется", Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
