#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("ДокументСреднегоЗаработка") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "ДокументСреднегоЗаработка", Параметры.Отбор.ДокументСреднегоЗаработка, , , , РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
			
		УстановитьГруппировкуПоДокументуСреднегоЗаработка(Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"СписокКомпоновщикНастроекПользовательскиеНастройки",
			"Видимость",
			Ложь);
		
	Иначе
		УстановитьГруппировкуПоДокументуСреднегоЗаработка(Истина);
	КонецЕсли; 
	
	ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "Список", , , , "ДокументСреднегоЗаработка");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Если ТекущиеДанные.Свойство("ГруппировкаСтроки") Тогда
			Если ЗначениеЗаполнено(ТекущиеДанные.РодительскаяГруппировкаСтроки) Тогда
				ТекущееЗначение = ТекущиеДанные[Поле.Имя];
			Иначе
				ТекущееЗначение = ТекущиеДанные.ГруппировкаСтроки.Ключ;
			КонецЕсли;
		Иначе
			ТекущееЗначение = ТекущиеДанные[Поле.Имя];
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекущееЗначение) Тогда
			ПоказатьЗначение(, ТекущееЗначение);
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, Настройки);
	
КонецПроцедуры

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, , СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьГруппировкуПоДокументуСреднегоЗаработка(Использование)
	
	ГруппировкаПоДокументСреднегоЗаработка = Неопределено;
	Для каждого ЭлементГруппировки Из Список.Группировка.Элементы Цикл
		Если ЭлементГруппировки.Поле = Новый ПолеКомпоновкиДанных("ДокументСреднегоЗаработка") Тогда
			ГруппировкаПоДокументСреднегоЗаработка = ЭлементГруппировки;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ГруппировкаПоДокументСреднегоЗаработка = Неопределено Тогда
		ГруппировкаПоДокументСреднегоЗаработка = Список.Группировка.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
		ГруппировкаПоДокументСреднегоЗаработка.Поле = Новый ПолеКомпоновкиДанных("ДокументСреднегоЗаработка");
	КонецЕсли;
	
	ГруппировкаПоДокументСреднегоЗаработка.Использование = Использование;
	
КонецПроцедуры

#КонецОбласти
