#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	
	ИменаПолей = ОбменДаннымиЗарплатаКадры.ИменаПолейОграниченияРегистрацииРегистраСведений();
	ИменаПолей.Организации = "Организация";
	
	Возврат ОбменДаннымиЗарплатаКадры.ОграниченияРегистрацииРегистраСведений(ЭтотОбъект, ИменаПолей);
	
КонецФункции

#КонецОбласти 

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	СоздатьВТПоЗаписываемомуНаборуЗаписей(МенеджерВременныхТаблиц, "ВТДатыПередачиВыплатыПособий");
	
	ПрямыеВыплатыПособийСоциальногоСтрахования.ЗаполнитьПризнакВыплачиваетсяФСССуществующихДокументов(МенеджерВременныхТаблиц);
	
КонецПроцедуры

Процедура СоздатьВТПоЗаписываемомуНаборуЗаписей(МенеджерВременныхТаблиц, ИмяВТ)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НаборЗаписей.ДатаПередачиФССВыплатыПособий КАК Дата,
	|	НаборЗаписей.Организация
	|ПОМЕСТИТЬ ИмяВТ
	|ИЗ
	|	&НаборЗаписей КАК НаборЗаписей";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИмяВТ", ИмяВТ);
	
	Запрос.УстановитьПараметр("НаборЗаписей", ЭтотОбъект.Выгрузить());
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли