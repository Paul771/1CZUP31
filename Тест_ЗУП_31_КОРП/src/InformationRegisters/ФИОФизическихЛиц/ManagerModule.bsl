#Область СлужебныеПроцедурыИФункции

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ЗаполнитьИнициалыИмени(Параметры) Экспорт
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	Запрос = Новый Запрос;
	
	Если Параметры.ПрогрессВыполнения.ВсегоОбъектов = 0 Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ФИОФизическихЛиц.Период,
			|	ФИОФизическихЛиц.ФизическоеЛицо
			|ИЗ
			|	РегистрСведений.ФИОФизическихЛиц КАК ФИОФизическихЛиц
			|ГДЕ
			|	ФИОФизическихЛиц.ИнициалыИмени = """"
			|	И ФИОФизическихЛиц.Имя <> """"";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			
			Параметры.ОбработкаЗавершена = Истина;
			Возврат ;
			
		КонецЕсли;
		
		Выборка = РезультатЗапроса.Выбрать();
		Параметры.ПрогрессВыполнения.ВсегоОбъектов = Выборка.Количество();
		
	КонецЕсли;
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ФИОФизическихЛиц.Период,
		|	ФИОФизическихЛиц.ФизическоеЛицо
		|ИЗ
		|	РегистрСведений.ФИОФизическихЛиц КАК ФИОФизическихЛиц
		|ГДЕ
		|	ФИОФизическихЛиц.ИнициалыИмени = """"
		|	И ФИОФизическихЛиц.Имя <> """"";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НачатьТранзакцию();
			
			Попытка
				
				Блокировка = Новый БлокировкаДанных;
				
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ФИОФизическихЛиц");
				ЭлементБлокировки.УстановитьЗначение("Период", Выборка.Период);
				ЭлементБлокировки.УстановитьЗначение("ФизическоеЛицо", Выборка.ФизическоеЛицо);
				
				Блокировка.Заблокировать();
				
				НаборЗаписей = РегистрыСведений.ФИОФизическихЛиц.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
				НаборЗаписей.Отбор.ФизическоеЛицо.Установить(Выборка.ФизическоеЛицо);
				
				НаборЗаписей.Прочитать();
				
				Для каждого Запись Из НаборЗаписей Цикл
					
					// Заполнение реквизита ИнициалыИмени
					Если Не ПустаяСтрока(Запись.Имя) Тогда
						ИнициалыИмени = ФизическиеЛицаЗарплатаКадрыКлиентСервер.ИнициалыИмени(Запись.Имя);
					Иначе
						ИнициалыИмени = "";
					КонецЕсли;
					
					Запись.ИнициалыИмени = ИнициалыИмени;
					
				КонецЦикла;
				
				// Сохранение объекта
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей, Истина, Ложь, Ложь);
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				
				ОтменитьТранзакцию();
				
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось обновить инициалы %1 за %2 по причине: %3'"),
					Выборка.ФизическоеЛицо,
					Формат(Выборка.Период, "ДЛФ=D"),
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
					УровеньЖурналаРегистрации.Предупреждение,
					Метаданные.Справочники.ФизическиеЛица,
					Выборка.ФизическоеЛицо,
					ТекстСообщения);
				
			КонецПопытки;
			
		КонецЦикла;
		
		Параметры.ПрогрессВыполнения.ОбработаноОбъектов = Параметры.ПрогрессВыполнения.ОбработаноОбъектов + 1000;
		
	Иначе
		Параметры.ОбработкаЗавершена = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли

#КонецОбласти
