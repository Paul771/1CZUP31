#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет связь объекта ИС и объекта ДО.
//
// Параметры:
//   ID - Строка - идентификатор объекта ДО.
//   Тип - Строка - тип объекта ДО.
//   Объект - ЛюбаяСсылка - объект ИС.
//
Процедура ДобавитьСвязь(ID, Тип, Объект) Экспорт
	
	Если Не ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3.CORP")  Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборот.ПроверитьОбновитьДанныеСвязанныхОбъектов();
	
	НаборЗаписей = РегистрыСведений.ОбъектыИнтегрированныеС1СДокументооборотом.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.Объект.Установить(Объект);
	НаборЗаписей.Отбор.ТипОбъектаДокументооборота.Установить(Тип);
	НаборЗаписей.Отбор.ИдентификаторОбъектаДокументооборота.Установить(ID);
	
	Запись = НаборЗаписей.Добавить();
	
	Запись.Объект = Объект;
	Запись.ТипОбъектаДокументооборота = Тип;
	Запись.ИдентификаторОбъектаДокументооборота = ID;
	
	НаборЗаписей.Записать();

КонецПроцедуры

// Удаляет связь объекта ИС и объекта ДО.
//
// Параметры:
//   ID - Строка - идентификатор объекта ДО.
//   Тип - Строка - тип объекта ДО.
//   Объект - ЛюбаяСсылка - объект ИС.
//
Процедура УдалитьСвязь(ID, Тип, Объект) Экспорт
	
	// синхронизация
	Если Не ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3.CORP")  Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ОбъектыИнтегрированныеС1СДокументооборотом.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.Объект.Установить(Объект);
	НаборЗаписей.Отбор.ТипОбъектаДокументооборота.Установить(Тип);
	НаборЗаписей.Отбор.ИдентификаторОбъектаДокументооборота.Установить(ID);
	
	НаборЗаписей.Записать();

	ИнтеграцияС1СДокументооборотПереопределяемый.ПриУдаленииПрисоединенныхФайловДокументооборота(Объект);
	
КонецПроцедуры

// Получает связанный объект ИС по объекту ДО.
//
// Параметры:
//   ID - Строка - идентификатор объекта ДО.
//   Тип - Строка - тип объекта ДО.
//   ТипОбъекта - Строка - необязательный, тип объекта ИС.
//
Функция СсылкаНаОбъектПоДаннымДокументооборота(ID, Тип, ТипОбъекта = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ОбъектыИнтегрированныеС1СДокументооборотом.Объект
		|ИЗ
		|	РегистрСведений.ОбъектыИнтегрированныеС1СДокументооборотом КАК ОбъектыИнтегрированныеС1СДокументооборотом
		|ГДЕ
		|	ОбъектыИнтегрированныеС1СДокументооборотом.ТипОбъектаДокументооборота = &ТипОбъектаДокументооборота
		|	И ОбъектыИнтегрированныеС1СДокументооборотом.ИдентификаторОбъектаДокументооборота = &ИдентификаторОбъектаДокументооборота
		|	И &Условие";
		
	Запрос.УстановитьПараметр("ТипОбъектаДокументооборота", Тип);
	Запрос.УстановитьПараметр("ИдентификаторОбъектаДокументооборота", ID);
	
	Если ТипОбъекта = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", "ИСТИНА");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&Условие", 
			"ТИПЗНАЧЕНИЯ(ОбъектыИнтегрированныеС1СДокументооборотом.Объект) = &ТипОбъекта");
		Запрос.УстановитьПараметр("ТипОбъекта", ТипОбъекта);
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
		
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		Возврат Выборка.Объект;
	
	КонецЕсли; 
	
КонецФункции 

#КонецОбласти

#КонецЕсли