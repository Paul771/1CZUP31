#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПодразделение(ПараметрыОбновления) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НастройкаВопросовДляСобеседования.Объект КАК Объект
		|ПОМЕСТИТЬ ВТОбъекты
		|ИЗ
		|	РегистрСведений.НастройкаВопросовДляСобеседования КАК НастройкаВопросовДляСобеседования
		|ГДЕ
		|	НастройкаВопросовДляСобеседования.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|	И НастройкаВопросовДляСобеседования.Объект.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1000
		|	Объекты.Объект КАК Объект
		|ИЗ
		|	ВТОбъекты КАК Объекты";
	РезультатЗапроса = Запрос.Выполнить();
	
	ПараметрыОбновления.ОбработкаЗавершена = Ложь;
	Если РезультатЗапроса.Пустой() Тогда
		ПараметрыОбновления.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;	
	
	Запрос.УстановитьПараметр("Объекты", РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Объект"));
	Запрос.Текст =
		"ВЫБРАТЬ
		|	НастройкаВопросовДляСобеседования.Объект.Подразделение КАК Подразделение,
		|	НастройкаВопросовДляСобеседования.Объект КАК Объект,
		|	НастройкаВопросовДляСобеседования.ВопросДляСобеседования КАК ВопросДляСобеседования,
		|	НастройкаВопросовДляСобеседования.ЭлементарныйВопрос КАК ЭлементарныйВопрос,
		|	НастройкаВопросовДляСобеседования.ОтветНаВопрос КАК ОтветНаВопрос,
		|	НастройкаВопросовДляСобеседования.ХарактеристикаПерсонала КАК ХарактеристикаПерсонала,
		|	НастройкаВопросовДляСобеседования.ЗначениеХарактеристики КАК ЗначениеХарактеристики,
		|	НастройкаВопросовДляСобеседования.Балл КАК Балл,
		|	НастройкаВопросовДляСобеседования.ЭлементарныйВопросВопросаДляСобеседования КАК ЭлементарныйВопросВопросаДляСобеседования,
		|	НастройкаВопросовДляСобеседования.ОтветНаВопросВопросаДляСобеседования КАК ОтветНаВопросВопросаДляСобеседования
		|ИЗ
		|	РегистрСведений.НастройкаВопросовДляСобеседования КАК НастройкаВопросовДляСобеседования
		|ГДЕ
		|	НастройкаВопросовДляСобеседования.Объект В(&Объекты)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Объект";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Объект") Цикл
		НаборЗаписей = РегистрыСведений.НастройкаВопросовДляСобеседования.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Объект.Установить(Выборка.Объект);
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
		КонецЦикла;
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли