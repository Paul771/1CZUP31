#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьПодчиненностьПодразделений(СписокПодразделений) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПодразделенияОрганизаций.Ссылка,
		|	ПодразделенияОрганизаций.Родитель,
		|	ПодразделенияОрганизаций.Владелец КАК Организация
		|ИЗ
		|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
		|ГДЕ
		|	ПодразделенияОрганизаций.Родитель <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)";
	
	РодителиПодразделений = Новый Соответствие;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РодителиПодразделений.Вставить(Выборка.Ссылка, Выборка.Родитель);
	КонецЦикла;
	
	ОрганизацииПодразделений = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СписокПодразделений, "Владелец");
	Для каждого Подразделение Из СписокПодразделений Цикл
		
		НаборЗаписей = РегистрыСведений.ПодчиненностьПодразделенийОрганизаций.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Подразделение.Установить(Подразделение);
		
		Запись = НаборЗаписей.Добавить();
		Запись.ВышестоящееПодразделение = Подразделение;
		Запись.Подразделение = Подразделение;
		Запись.Организация = ОрганизацииПодразделений.Получить(Запись.Подразделение);
		
		Родитель = РодителиПодразделений.Получить(Запись.ВышестоящееПодразделение);
		Пока Родитель <> Неопределено Цикл
			
			Запись = НаборЗаписей.Добавить();
			Запись.ВышестоящееПодразделение = Родитель;
			Запись.Подразделение = Подразделение;
			Запись.Организация = ОрганизацииПодразделений.Получить(Запись.Подразделение);
			
			Родитель = РодителиПодразделений.Получить(Запись.ВышестоящееПодразделение);
			
		КонецЦикла;
		
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ОбновитьПодчиненностьПодразделенийПриСменеРодителя(СписокПодразделений) Экспорт
	
	СписокРодителей = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(СписокПодразделений);
	Пока СписокРодителей.Количество() > 0 Цикл
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СписокРодителей", СписокРодителей);
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПодразделенияОрганизаций.Ссылка
			|ИЗ
			|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
			|ГДЕ
			|	ПодразделенияОрганизаций.Родитель В(&СписокРодителей)";
		
		УстановитьПривилегированныйРежим(Истина);
		СписокПодразделенийКОбновлению = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		УстановитьПривилегированныйРежим(Ложь);
		
		Если СписокПодразделенийКОбновлению.Количество() = 0 Тогда
			Прервать;
		КонецЕсли;
		
		ОбновитьПодчиненностьПодразделений(СписокПодразделенийКОбновлению);
		СписокРодителей = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(СписокПодразделенийКОбновлению);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПодчиненностьПодразделений() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПодчиненностьПодразделенийОрганизаций.ВышестоящееПодразделение
		|ИЗ
		|	РегистрСведений.ПодчиненностьПодразделенийОрганизаций КАК ПодчиненностьПодразделенийОрганизаций";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПодразделенияОрганизаций.Ссылка
			|ИЗ
			|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций";
		
		ОбновитьПодчиненностьПодразделений(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьОрганизацию() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПодчиненностьПодразделенийОрганизаций.ВышестоящееПодразделение,
		|	ПодчиненностьПодразделенийОрганизаций.ВышестоящееПодразделение.Владелец КАК Организация
		|ИЗ
		|	РегистрСведений.ПодчиненностьПодразделенийОрганизаций КАК ПодчиненностьПодразделенийОрганизаций
		|ГДЕ
		|	ПодчиненностьПодразделенийОрганизаций.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НаборЗаписей = РегистрыСведений.ПодчиненностьПодразделенийОрганизаций.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ВышестоящееПодразделение.Установить(Выборка.ВышестоящееПодразделение);
			
			НаборЗаписей.Прочитать();
			Для каждого Запись Из НаборЗаписей Цикл
				Запись.Организация = Выборка.Организация;
			КонецЦикла;
			
			НаборЗаписей.ОбменДанными.Загрузка = Истина;
			НаборЗаписей.Записать();
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли