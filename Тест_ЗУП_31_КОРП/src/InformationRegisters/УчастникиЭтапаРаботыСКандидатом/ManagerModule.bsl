#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает участников этапа работы с кандидатом.
//
// Параметры:
//	Кандидат	- СправочникСсылка.Кандидаты - кандидат, приглашенный на этап.
//	ЭтапРаботы	- СправочникСсылка.ЭтапыРаботыСКандидатами - этап, по которому запрашиваются участники.
//
// Возвращаемое значение:
//	Массив - коллекция ссылок типа СправочникСсылка.Пользователи.
//
Функция УчастникиЭтапа(Кандидат, ЭтапРаботы) Экспорт
	
	Участники = Новый Массив;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Кандидат", Кандидат);
	Запрос.УстановитьПараметр("ЭтапРаботы", ЭтапРаботы);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	УчастникиЭтапаРаботыСКандидатом.Участник КАК Участник
	|ИЗ
	|	РегистрСведений.УчастникиЭтапаРаботыСКандидатом КАК УчастникиЭтапаРаботыСКандидатом
	|ГДЕ
	|	УчастникиЭтапаРаботыСКандидатом.Кандидат = &Кандидат
	|	И УчастникиЭтапаРаботыСКандидатом.ЭтапРаботы = &ЭтапРаботы";
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Участники = Результат.Выгрузить().ВыгрузитьКолонку("Участник");
	КонецЕсли;
	
	Возврат Участники;
	
КонецФункции

// Записывает участников этапа работы с кандидатом.
//
// Параметры:
//	Кандидат	- СправочникСсылка.Кандидаты - кандидат, приглашенный на этап.
//	ЭтапРаботы	- СправочникСсылка.ЭтапыРаботыСКандидатами - этап, по которому записываются участники.
//	Участники	- Массив - коллекция ссылок типа СправочникСсылка.Пользователи.
//
Процедура ЗаписатьУчастниковЭтапа(Кандидат, ЭтапРаботы, Участники) Экспорт
	
	НаборЗаписей = РегистрыСведений.УчастникиЭтапаРаботыСКандидатом.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Кандидат.Установить(Кандидат);
	НаборЗаписей.Отбор.ЭтапРаботы.Установить(ЭтапРаботы);
	
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	
	Если ЗначениеЗаполнено(Участники) Тогда
		
		Для Каждого Участник Из Участники Цикл
			
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Кандидат = Кандидат;
			НоваяЗапись.ЭтапРаботы = ЭтапРаботы;
			НоваяЗапись.Участник = Участник;
			
		КонецЦикла;
		
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
