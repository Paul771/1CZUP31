#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
		
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	// Получаем таблицу сотрудников и месяцев предыдущего состояния набора, 
	// т.к. по ним также нужно пересчитать корректировки.
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеНабора.Сотрудник,
	|	НАЧАЛОПЕРИОДА(ДанныеНабора.Период, МЕСЯЦ) КАК Месяц
	|ПОМЕСТИТЬ ВТСотрудникиМесяцыПредыдущегоНабора
	|ИЗ
	|	РегистрНакопления.ДанныеОВремениДляРасчетаСреднегоОбщий КАК ДанныеНабора
	|ГДЕ
	|	ДанныеНабора.Регистратор = &Регистратор";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.Выполнить();
	
	ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	// Объединяем данные ключевых измерений (сотрудник и месяц) до и после записи набора
	// таблицу ВТСотрудникиМесяцыПредыдущегоНабора получаем перед записью набора.
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПредыдущийНабор.Сотрудник,
	|	ПредыдущийНабор.Месяц
	|ПОМЕСТИТЬ ВТСотрудникиМесяцы
	|ИЗ
	|	ВТСотрудникиМесяцыПредыдущегоНабора КАК ПредыдущийНабор
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ДанныеНабора.Сотрудник,
	|	НАЧАЛОПЕРИОДА(ДанныеНабора.Период, МЕСЯЦ)
	|ИЗ
	|	РегистрНакопления.ДанныеОВремениДляРасчетаСреднегоОбщий КАК ДанныеНабора
	|ГДЕ
	|	ДанныеНабора.Регистратор = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСотрудникиМесяцыПредыдущегоНабора";
	
	МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.Выполнить();
	
	// Если пишется пустой набор записей, то игнорируем движения текущего регистратора.
	// Такое правило применяем для обновления корректировок при отмене проведения, 
	// т.к. на момент обновления корректировок пустые наборы по другим регистрам могут быть пока не записаны.
	
	ИсключаемыйРегистратор = Неопределено;
	Если ЭтотОбъект.Количество() = 0 Тогда
		ИсключаемыйРегистратор = Отбор.Регистратор.Значение;
	КонецЕсли;
	
	// Кроме того, исключаемый регистратор может быть определен при временной записи набора в форме проведенного документа.
	// Например, при увольнении последним днем месяца.
	
	Если ЭтотОбъект.ДополнительныеСвойства.Свойство("ИсключаемыйРегистратор") Тогда
		ИсключаемыйРегистратор = ЭтотОбъект.ДополнительныеСвойства.ИсключаемыйРегистратор;
	КонецЕсли;
	
	УчетСреднегоЗаработка.ОбновитьДанныеКорректировкиДнейДляРасчетаОбщегоСреднегоЗаработка(МенеджерВременныхТаблиц, ИсключаемыйРегистратор);
	
КонецПроцедуры

#КонецОбласти
	
#КонецЕсли