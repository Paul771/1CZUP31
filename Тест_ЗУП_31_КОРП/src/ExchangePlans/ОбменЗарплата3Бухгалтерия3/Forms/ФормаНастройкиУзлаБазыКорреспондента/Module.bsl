
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Данные = Новый Соответствие;
	Данные.Вставить("Справочник.Организации");
	
	ОбменДаннымиСервер.ФормаНастройкиУзлаБазыКорреспондентаПриСозданииНаСервере(ЭтотОбъект, Метаданные.ПланыОбмена.ОбменЗарплата3Бухгалтерия3.Имя, Данные);
	
	ВерсияКорреспондентаБезСборки = ОбщегоНазначенияКлиентСервер.ВерсияКонфигурацииБезНомераСборки(ВерсияКорреспондента);
	РезультатСравнения            = ОбщегоНазначенияКлиентСервер.СравнитьВерсииБезНомераСборки(ВерсияКорреспондентаБезСборки, "3.0.43");
	Если РезультатСравнения < 0 Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаНастройкиДатаИРежимВыгрузки", "Видимость", Ложь);
	КонецЕсли;
	
	РежимСинхронизацииОрганизаций = ?(ИспользоватьОтборПоОрганизациям, "СинхронизироватьДанныеТолькоПоВыбраннымОрганизациям", "СинхронизироватьДанныеПоВсемОрганизациям");
	РежимВыгрузкиСотрудников = ?(НеВыгружатьПерсональныеДанныеФизическихЛиц,1,0);
	
	ЗагрузитьДанныеВТаблицуФормы(Данные["Справочник.Организации"], Организации, "Организация");
	
	ОтметитьВыбранныеЭлементыТаблицы("Организации", "Организации", "Организация_Ключ");
	
	ЗначениеВыгружатьВсегда = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьВсегда;
	ЗначениеВыгружатьПриНеобходимости = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПриНеобходимости;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РежимСинхронизацииОрганизацийПриИзмененииЗначения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ОбменДаннымиКлиент.ФормаНастройкиПередЗакрытием(Отказ, ЭтаФорма, ЗавершениеРаботы);
	
КонецПроцедуры

&НаКлиенте
Процедура РежимВыгрузкиСотрудниковПриИзменении(Элемент)
	
	НеВыгружатьПерсональныеДанныеФизическихЛиц = ?(РежимВыгрузкиСотрудников = 0, Ложь, Истина);
	
	Если НеВыгружатьПерсональныеДанныеФизическихЛиц Тогда
		РежимВыгрузкиПерсональныеДанные = ЗначениеВыгружатьПриНеобходимости;
	Иначе
		РежимВыгрузкиПерсональныеДанные = ЗначениеВыгружатьВсегда;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВключитьВсеОрганизации(Команда)
	
	ВключитьОтключитьВсеЭлементыВТаблице(Истина, "Организации");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьВсеОрганизации(Команда)
	
	ВключитьОтключитьВсеЭлементыВТаблице(Ложь, "Организации");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьИЗакрытьСервер();
	
	ОбменДаннымиКлиент.ФормаНастройкиУзлаКомандаЗакрытьФорму(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура РежимСинхронизацииОрганизацийПриИзменении(Элемент)
	
	РежимСинхронизацииОрганизацийПриИзмененииЗначения();
	
КонецПроцедуры

&НаКлиенте
Процедура РежимСинхронизацииОрганизацийПриИзмененииЗначения()
	
	Элементы.Организации.Доступность =
		(РежимСинхронизацииОрганизаций = "СинхронизироватьДанныеТолькоПоВыбраннымОрганизациям")
	;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВключитьОтключитьВсеЭлементыВТаблице(Включить, ИмяТаблицы)
	
	Для Каждого ЭлементКоллекции Из ЭтаФорма[ИмяТаблицы] Цикл
		
		ЭлементКоллекции.Использовать = Включить;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОтметитьВыбранныеЭлементыТаблицы(ИмяТаблицы, ИмяВспомогательнойТаблицы, ИмяРеквизита)
	
	Для Каждого СтрокаТаблицы Из ЭтотОбъект[ИмяТаблицы] Цикл
		
		Строки = ЭтотОбъект[ИмяВспомогательнойТаблицы].НайтиСтроки(Новый Структура(ИмяРеквизита, СтрокаТаблицы[ИмяРеквизита]));
		
		Если Строки.Количество() > 0 Тогда
			
			Строки[0].Использовать = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеВТаблицуФормы(Источник, Приемник, ИмяРеквизита)
	
	Приемник.Очистить();
	
	Для Каждого СтрокаИсточника Из Источник Цикл
		
		СтрокаПриемника = Приемник.Добавить();
		СтрокаПриемника[ИмяРеквизита] = СтрокаИсточника.Представление;
		СтрокаПриемника[ИмяРеквизита + "_Ключ"] = СтрокаИсточника.Идентификатор;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИЗакрытьСервер()
	
	ИспользоватьОтборПоОрганизациям =
		(РежимСинхронизацииОрганизаций = "СинхронизироватьДанныеТолькоПоВыбраннымОрганизациям")
	;
	
	Если ИспользоватьОтборПоОрганизациям Тогда
		
		Организации.Загрузить(Организации.Выгрузить(Новый Структура("Использовать", Истина), "Организация, Организация_Ключ"));
		
	Иначе
		
		Организации.Очистить();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
