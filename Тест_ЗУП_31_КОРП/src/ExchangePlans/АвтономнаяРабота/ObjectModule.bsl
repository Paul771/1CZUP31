#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриПолученииДанныхОтПодчиненного(ЭлементДанных, ПолучениеЭлемента, ОтправкаНазад)
	ПриПолученииДанныхФайла(ЭлементДанных);
КонецПроцедуры

Процедура ПриПолученииДанныхОтГлавного(ЭлементДанных, ПолучениеЭлемента, ОтправкаНазад)
	
	Если ТипЗнч(ЭлементДанных) = Тип("УдалениеОбъекта") Тогда
		ДанныеВБазе = ЭлементДанных.Ссылка.ПолучитьОбъект();
		
		Если ДанныеВБазе = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ДанныеВБазе.ПометкаУдаления = Истина;
		
		Если ОбщегоНазначения.ЭтоДокумент(ДанныеВБазе.Метаданные()) Тогда
			ДанныеВБазе.Проведен = Ложь;
		КонецЕсли;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДанныеВБазе);
		
		ПолучениеЭлемента = ПолучениеЭлементаДанных.Игнорировать;
	КонецЕсли;
	
	ПриПолученииДанныхФайла(ЭлементДанных);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		
		Если НЕ ИспользоватьОтборПоОрганизациям И Организации.Количество() <> 0 Тогда
			Организации.Очистить();
		ИначеЕсли Организации.Количество() = 0 И ИспользоватьОтборПоОрганизациям Тогда
			ИспользоватьОтборПоОрганизациям = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЭтотОбъект.Ссылка <> ПланыОбмена.АвтономнаяРабота.ЭтотУзел() Тогда
		
		Если ОбменДаннымиСервер.НадоВыполнитьОбработчикПослеЗагрузкиДанных(ЭтотОбъект, Ссылка) Тогда
			ПослеЗагрузкиДанных();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Переопределяет стандартное поведение при загрузке данных;
//
Процедура ПриПолученииДанныхФайла(ЭлементДанных)
	
	// СтандартныеПодсистемы.Взаимодействия
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.СинхронизацияДанныхПриложения.ОбменДаннымиЗарплатаКадрыВзаимодействия") Тогда
		МодульВзаимодействия = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиЗарплатаКадрыВзаимодействия");
		МодульВзаимодействия.ПриПолученииДанныхФайла(ЭтотОбъект, ЭлементДанных);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Взаимодействия
	
КонецПроцедуры

Процедура ПослеЗагрузкиДанных()
	
	// СтандартныеПодсистемы.Взаимодействия
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.СинхронизацияДанныхПриложения.ОбменДаннымиЗарплатаКадрыВзаимодействия") Тогда
		МодульВзаимодействия = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиЗарплатаКадрыВзаимодействия");
		МодульВзаимодействия.ПослеЗагрузкиДанных(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Взаимодействия
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
