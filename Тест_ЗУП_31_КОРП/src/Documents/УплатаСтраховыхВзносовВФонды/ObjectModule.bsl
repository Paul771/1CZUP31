#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	УчетСтраховыхВзносов.СформироватьУплатуВзносовВФонд(Движения, Отказ, Организация, ДанныеДляПроведения());
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, ПлатежноеПоручениеДата, "Объект.ПлатежноеПоручениеДата", Отказ, НСтр("ru='Дата поручения'"), , , Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	Возврат ОбменДаннымиЗарплатаКадры.ОграниченияРегистрацииПоОрганизации(ЭтотОбъект, Организация);
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведения()
	
	РасчетыСФондами = Новый ТаблицаЗначений;
	РасчетыСФондами.Колонки.Добавить("Период");
	РасчетыСФондами.Колонки.Добавить("ВидОбязательногоСтрахованияСотрудников");
	РасчетыСФондами.Колонки.Добавить("ЭтоСтраховыеВзносы");
	РасчетыСФондами.Колонки.Добавить("ГодЗадолженностиПоЕСН");
	РасчетыСФондами.Колонки.Добавить("Сумма");
	РасчетыСФондами.Колонки.Добавить("ПлатежноеПоручениеДата");
	РасчетыСФондами.Колонки.Добавить("ПлатежноеПоручениеНомер");

	ДанныеДокумента = РасчетыСФондами.Добавить();
	ДанныеДокумента.Период = Дата;
	ДанныеДокумента.ВидОбязательногоСтрахованияСотрудников = ВидПлатежа;
	ДанныеДокумента.ЭтоСтраховыеВзносы = Истина;
	ДанныеДокумента.Сумма = СуммаПлатежа;
	ДанныеДокумента.ПлатежноеПоручениеДата = ПлатежноеПоручениеДата;
	ДанныеДокумента.ПлатежноеПоручениеНомер = ПлатежноеПоручениеНомер;
	
	Возврат РасчетыСФондами;
	
КонецФункции

#КонецОбласти

#КонецЕсли
