#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	
	Если ДополнительныеСвойства.Свойство("ОграниченияРегистрации") Тогда
		// Ограничения регистрации уже были получены
		Возврат ДополнительныеСвойства.ОграниченияРегистрации;
	КонецЕсли;
	
	МассивСотрудников = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСотрудников, Сотрудники.ВыгрузитьКолонку("Сотрудник"), Истина);
	
	ОграниченияРегистрации = ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации();
	ОграниченияРегистрации.Сотрудники = МассивСотрудников;
	ОграниченияРегистрации.ДатаПолученияДанных = ДатаПеремещения;
	МассивОрганизаций = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
	МассивОрганизаций.Добавить(ОрганизацияНовая);
	ОграниченияРегистрации.Организации = МассивОрганизаций;
	
	Возврат ОграниченияРегистрации;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("Действие") И ДанныеЗаполнения.Действие = "Исправить" Тогда
			ИсправлениеДокументовЗарплатаКадры.СкопироватьДокумент(ЭтотОбъект, ДанныеЗаполнения.Ссылка);
			ИсправленныйДокумент = ДанныеЗаполнения.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Подготовка к регистрации перерасчетов
	ДанныеДляРегистрацииПерерасчетов = Новый МенеджерВременныхТаблиц;
	
	СоздатьВТДанныеДокументов(ДанныеДляРегистрацииПерерасчетов);
	ЕстьПерерасчеты = ПерерасчетЗарплаты.СборДанныхДляРегистрацииПерерасчетов(Ссылка, ДанныеДляРегистрацииПерерасчетов, Организация);
	
	// Проведение документа
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект, , , ЗначениеЗаполнено(ИсправленныйДокумент));
	
	ДанныеДляПроведения = ДанныеДляПроведенияДокумента();
	
	ЗарплатаКадрыРасширенный.УстановитьВремяРегистрацииДокумента(Движения, ДанныеДляПроведения.СотрудникиДаты, Ссылка);
	
	ИсправлениеДокументовЗарплатаКадры.ПриПроведенииИсправления(Ссылка, Движения, РежимПроведения, Отказ,,, ЭтотОбъект, "ДатаПеремещения");
	
	КадровыйУчет.СформироватьКадровыеДвижения(ЭтотОбъект, Движения, ДанныеДляПроведения.КадровыйУчет);
	
	УчетСтажаПФР.ЗарегистрироватьПериодыВУчетеСтажаПФР(Движения, ДанныеДляРегистрацииВУчетаСтажаПФР());
	
	КадровыйУчетРасширенный.СформироватьДвиженияПоТерриториям(Движения, ДанныеДляПроведения.ТерриторииСотрудников, ОрганизацияНовая);
	
	Если ДанныеДляПроведения.Свойство("ПоказателиРасчетаЗарплаты") Тогда
		РасчетЗарплатыРасширенный.СформироватьДвиженияЗначенийПериодическихПоказателейСотрудников(Движения, ДанныеДляПроведения.ПоказателиРасчетаЗарплаты);
	КонецЕсли; 
	
	// Регистрация перерасчетов
	Если ЕстьПерерасчеты Тогда
		ПерерасчетЗарплаты.РегистрацияПерерасчетов(Движения, ДанныеДляРегистрацииПерерасчетов, Организация);
	КонецЕсли; 
	
	КадровыйУчетРасширенный.ЗарегистрироватьВРеестреКадровыхПриказов(Движения, ДанныеДляПроведения.ДанныеРеестраКадровыхПриказов, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, ДатаПеремещения, "Объект.ДатаПеремещения", Отказ, НСтр("ru='Дата перемещения'"), , , Ложь);
	
	ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияРабочихМестВОрганизацийПоВременнойТаблице();
	ПараметрыПолученияСотрудниковОрганизаций.Организация 				= Организация;
	
	Если ЗначениеЗаполнено(ПодразделениеПрежнее) Тогда
		ПараметрыПолученияСотрудниковОрганизаций.Подразделение 			= ПодразделениеПрежнее;
	КонецЕсли; 
	
	ПараметрыПолученияСотрудниковОрганизаций.НачалоПериода				= ДатаПеремещения;
	ПараметрыПолученияСотрудниковОрганизаций.ОкончаниеПериода			= ДатаПеремещения;
	ПараметрыПолученияСотрудниковОрганизаций.РаботникиПоДоговорамГПХ 	= Неопределено;
	
	ПараметрыПолученияСотрудниковОрганизаций.ИсключаемыйРегистратор = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ссылка);
	Если ЗначениеЗаполнено(ИсправленныйДокумент) Тогда
		ПараметрыПолученияСотрудниковОрганизаций.ИсключаемыйРегистратор.Добавить(ИсправленныйДокумент);
	КонецЕсли;
	
	КадровыйУчет.ПроверитьРаботающихСотрудников(
		Сотрудники.ВыгрузитьКолонку("Сотрудник"),
		ПараметрыПолученияСотрудниковОрганизаций,
		Отказ,
		Новый Структура("ИмяПоляСотрудник, ИмяОбъекта", "Сотрудник", "Объект.Сотрудники"));
	
	ПроверитьСоответствиеПозицииШРПодразделению(Отказ);
	
	СписокСотрудников = ОбщегоНазначения.ВыгрузитьКолонку(Сотрудники, "Сотрудник", Истина);
	ВремяРегистрацииСотрудников = ЗарплатаКадрыРасширенный.ВремяРегистрацииСотрудниковДокумента(Ссылка, СписокСотрудников, ДатаПеремещения);
	
	ДокументыДляИсключения = Новый Массив;
	ДокументыДляИсключения.Добавить(ЭтотОбъект.Ссылка);
	ДокументыДляИсключения.Добавить(ЭтотОбъект.ИсправленныйДокумент);
	
	СотрудникиДаты = Новый ТаблицаЗначений;
	СотрудникиДаты.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	СотрудникиДаты.Колонки.Добавить("ДатаСобытия", Новый ОписаниеТипов("Дата"));
	СотрудникиДаты.Колонки.Добавить("ВидСобытия", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыКадровыхСобытий")); 
	
	Для Каждого Сотрудник Из СписокСотрудников Цикл
		
		СтрокаСотрудникиДаты = СотрудникиДаты.Добавить();
		СтрокаСотрудникиДаты.ДатаСобытия = ВремяРегистрацииСотрудников.Получить(Сотрудник);
		СтрокаСотрудникиДаты.Сотрудник = Сотрудник;
		СтрокаСотрудникиДаты.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Перемещение;
		
	КонецЦикла;
	
	КадровыйУчет.ПроверитьВозможностьПроведенияПоКадровомуУчетуТаблицыСотрудников(СотрудникиДаты, ДокументыДляИсключения, Отказ);

	ИсправлениеДокументовЗарплатаКадры.ПроверитьЗаполнение(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ, "ПериодическиеСведения", "ДатаПеремещения");
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Подготовка к регистрации перерасчетов
	ДанныеДляРегистрацииПерерасчетов = Новый МенеджерВременныхТаблиц;
	
	СоздатьВТДанныеДокументов(ДанныеДляРегистрацииПерерасчетов);
	ЕстьПерерасчеты = ПерерасчетЗарплаты.СборДанныхДляРегистрацииПерерасчетов(Ссылка, ДанныеДляРегистрацииПерерасчетов, Организация);
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКУдалениюПроведения(ЭтотОбъект, ЗначениеЗаполнено(ИсправленныйДокумент));
	
	// Регистрация перерасчетов
	Если ЕстьПерерасчеты Тогда
		ПерерасчетЗарплаты.РегистрацияПерерасчетовПриОтменеПроведения(Ссылка, ДанныеДляРегистрацииПерерасчетов, Организация);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьШтатноеРасписание") Тогда
		ДолжностиПозиции = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Сотрудники.Выгрузить().ВыгрузитьКолонку("ДолжностьПоШтатномуРасписанию"), "Должность");
		Для каждого СтрокаСотрудника Из Сотрудники Цикл
			Если СтрокаСотрудника.Должность <> ДолжностиПозиции[СтрокаСотрудника.ДолжностьПоШтатномуРасписанию] Тогда
				СтрокаСотрудника.Должность = ДолжностиПозиции[СтрокаСотрудника.ДолжностьПоШтатномуРасписанию];
			КонецЕсли;
		КонецЦикла;
	Иначе
		Для каждого СтрокаСотрудника Из Сотрудники Цикл
			Если ЗначениеЗаполнено(СтрокаСотрудника.ДолжностьПоШтатномуРасписанию) Тогда
				СтрокаСотрудника.ДолжностьПоШтатномуРасписанию = Справочники.ШтатноеРасписание.ПустаяСсылка();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
		
	НеРегистрироватьБухучет = Ложь;
	ИмяТаблицы 			= "Документ.ПеремещениеВДругоеПодразделение.Сотрудники";
	ИмяПоляПериод 		= "Таблица.Ссылка.ДатаПеремещения";
	ИмяПоляДействуетДо 	= Неопределено;
	ОтражениеЗарплатыВБухучетеРасширенный.ОбновитьСведенияОБухучетеЗарплатыСотрудников(ЭтотОбъект,НеРегистрироватьБухучет,ИмяТаблицы,ИмяПоляПериод,ИмяПоляДействуетДо);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведенияДокумента()Экспорт
	
	ДанныеДляПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КадровыйПеревод.Ссылка.ДатаПеремещения КАК ДатаСобытия,
	|	КадровыйПеревод.Сотрудник,
	|	КадровыйПеревод.ДолжностьПоШтатномуРасписанию КАК Позиция,
	|	КадровыйПеревод.Ссылка.ПодразделениеНовое КАК Подразделение,
	|	КадровыйПеревод.Должность КАК Должность,
	|	0 КАК КоличествоСтавок,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Перемещение) КАК ВидСобытия,
	|	КадровыйПеревод.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	Документ.ПеремещениеВДругоеПодразделение.Сотрудники КАК КадровыйПеревод
	|ГДЕ
	|	КадровыйПеревод.Ссылка = &Ссылка";
	
	ДанныеДляПроведения.Вставить("КадровыйУчет", Запрос.Выполнить().Выгрузить());
	
	Если Организация <> ОрганизацияНовая Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПеремещениеВДругоеПодразделениеСотрудники.Ссылка.ДатаПеремещения КАК Период,
			|	ПеремещениеВДругоеПодразделениеСотрудники.Ссылка.Организация КАК Организация,
			|	ПеремещениеВДругоеПодразделениеСотрудники.Сотрудник КАК Сотрудник
			|ПОМЕСТИТЬ ВТСотрудникиПериоды
			|ИЗ
			|	Документ.ПеремещениеВДругоеПодразделение.Сотрудники КАК ПеремещениеВДругоеПодразделениеСотрудники
			|ГДЕ
			|	ПеремещениеВДругоеПодразделениеСотрудники.Ссылка = &Ссылка";
			
		Запрос.Выполнить();
		
		ПараметрыСреза = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыСреза.Отборы, "Регистратор", "<>", Ссылка);
		
		ОписаниеИсточника = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
			"ВТСотрудникиПериоды", "Организация,Сотрудник");
			
		ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
			"ЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудников",
			Запрос.МенеджерВременныхТаблиц,
			Ложь,
			ОписаниеИсточника,
			ПараметрыСреза);
			
		Запрос.УстановитьПараметр("ОрганизацияНовая", ОрганизацияНовая);
			
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудниковСрезПоследних.Период КАК ДатаСобытия,
			|	&ОрганизацияНовая КАК Организация,
			|	ЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудниковСрезПоследних.Сотрудник КАК Сотрудник,
			|	ЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудниковСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудниковСрезПоследних.Показатель КАК Показатель,
			|	ЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудниковСрезПоследних.ДокументОснование КАК ДокументОснование,
			|	ЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудниковСрезПоследних.Значение КАК Значение
			|ИЗ
			|	ВТЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудниковСрезПоследних КАК ЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудниковСрезПоследних";
		
		ДанныеДляПроведения.Вставить("ПоказателиРасчетаЗарплаты", Запрос.Выполнить().Выгрузить());
		
	КонецЕсли; 
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПеремещениеВДругоеПодразделениеСотрудники.Ссылка.ДатаПеремещения КАК ДатаСобытия,
	|	ПеремещениеВДругоеПодразделениеСотрудники.Сотрудник КАК Сотрудник
	|ИЗ
	|	Документ.ПеремещениеВДругоеПодразделение.Сотрудники КАК ПеремещениеВДругоеПодразделениеСотрудники
	|ГДЕ
	|	ПеремещениеВДругоеПодразделениеСотрудники.Ссылка = &Ссылка";
	
	ДанныеДляПроведения.Вставить("СотрудникиДаты", Запрос.Выполнить().Выгрузить());
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПеремещениеВДругоеПодразделениеСотрудники.Ссылка.ДатаПеремещения КАК Период,
		|	ПеремещениеВДругоеПодразделениеСотрудники.Сотрудник,
		|	ПеремещениеВДругоеПодразделениеСотрудники.Сотрудник.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	ПеремещениеВДругоеПодразделениеСотрудники.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ПеремещениеВДругоеПодразделениеСотрудники.Ссылка.Территория
		|ИЗ
		|	Документ.ПеремещениеВДругоеПодразделение.Сотрудники КАК ПеремещениеВДругоеПодразделениеСотрудники
		|ГДЕ
		|	ПеремещениеВДругоеПодразделениеСотрудники.Ссылка = &Ссылка";
	
	// Набор сведений для проведения по территориям сотрудников
	СведенияОКонтрактахДоговорах = Запрос.Выполнить().Выгрузить();
	ДанныеДляПроведения.Вставить("ТерриторииСотрудников", СведенияОКонтрактахДоговорах);
	
	// Данные для Реестра кадровых приказов
	ДанныеРеестраКадровыхПриказов = КадровыйУчетРасширенный.ТаблицаРеестраКадровыхПриказов();
	НомерПриказа = "";
	ДатаПриказа = Дата(1, 1, 1);
			
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПеремещениеВДругоеПодразделениеСотрудники.Сотрудник
	|ПОМЕСТИТЬ ВТСотрудники
	|ИЗ
	|	Документ.ПеремещениеВДругоеПодразделение.Сотрудники КАК ПеремещениеВДругоеПодразделениеСотрудники
	|ГДЕ
	|	ПеремещениеВДругоеПодразделениеСотрудники.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеестрКадровыхПриказов.Сотрудник,
	|	МАКСИМУМ(РеестрКадровыхПриказов.Дата) КАК Дата
	|ПОМЕСТИТЬ ВТСотрудникиМаксимальныеПериодыРеестра
	|ИЗ
	|	РегистрСведений.РеестрКадровыхПриказов КАК РеестрКадровыхПриказов
	|ГДЕ
	|	РеестрКадровыхПриказов.Сотрудник В
	|			(ВЫБРАТЬ
	|				ВТСотрудники.Сотрудник
	|			ИЗ
	|				ВТСотрудники КАК ВТСотрудники)
	|	И РеестрКадровыхПриказов.Дата < &ДатаПеремещения
	|
	|СГРУППИРОВАТЬ ПО
	|	РеестрКадровыхПриказов.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТСотрудникиМаксимальныеПериодыРеестра.Сотрудник,
	|	ВТСотрудникиМаксимальныеПериодыРеестра.Дата,
	|	ВЫРАЗИТЬ(РеестрКадровыхПриказов.ТарифнаяСтавкаНадбавка КАК СТРОКА(1000)) КАК ТарифнаяСтавкаНадбавка,
	|	РеестрКадровыхПриказов.ВидДоговора,
	|	РеестрКадровыхПриказов.Разряд
	|ПОМЕСТИТЬ ВТСотрудникиТарСтавкаВидДоговора
	|ИЗ
	|	ВТСотрудникиМаксимальныеПериодыРеестра КАК ВТСотрудникиМаксимальныеПериодыРеестра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрКадровыхПриказов КАК РеестрКадровыхПриказов
	|		ПО ВТСотрудникиМаксимальныеПериодыРеестра.Сотрудник = РеестрКадровыхПриказов.Сотрудник
	|			И ВТСотрудникиМаксимальныеПериодыРеестра.Дата = РеестрКадровыхПриказов.Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТСотрудникиТарСтавкаВидДоговора.ТарифнаяСтавкаНадбавка) КАК ТарифнаяСтавкаНадбавка,
	|	МАКСИМУМ(ВТСотрудникиТарСтавкаВидДоговора.ВидДоговора) КАК ВидДоговора,
	|	МАКСИМУМ(ВТСотрудникиТарСтавкаВидДоговора.Разряд) КАК Разряд,
	|	ВТСотрудникиТарСтавкаВидДоговора.Сотрудник
	|ПОМЕСТИТЬ ВТДанныеСотрудникаСГруппировкой
	|ИЗ
	|	ВТСотрудникиТарСтавкаВидДоговора КАК ВТСотрудникиТарСтавкаВидДоговора
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТСотрудникиТарСтавкаВидДоговора.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПеремещениеВДругоеПодразделениеСотрудники.Сотрудник,
	|	ПеремещениеВДругоеПодразделениеСотрудники.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ПеремещениеВДругоеПодразделениеСотрудники.Ссылка КАК ДокументОснование,
	|	ПеремещениеВДругоеПодразделениеСотрудники.Ссылка.Номер КАК НомерПриказа,
	|	ПеремещениеВДругоеПодразделениеСотрудники.Ссылка.Дата КАК ДатаПриказа,
	|	ПеремещениеВДругоеПодразделениеСотрудники.Ссылка.ПодразделениеНовое КАК Подразделение,
	|	ПеремещениеВДругоеПодразделениеСотрудники.Должность,
	|	ПеремещениеВДругоеПодразделениеСотрудники.Ссылка.ДатаПеремещения КАК Дата,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Перемещение) КАК ВидСобытия,
	|	ВТДанныеСотрудникаСГруппировкой.ТарифнаяСтавкаНадбавка,
	|	ВТДанныеСотрудникаСГруппировкой.ВидДоговора,
	|	ВТДанныеСотрудникаСГруппировкой.Разряд,
	|	ПеремещениеВДругоеПодразделениеСотрудники.НомерСтроки КАК Номер,
	|	ПеремещениеВДругоеПодразделениеСотрудники.Ссылка.Организация
	|ИЗ
	|	Документ.ПеремещениеВДругоеПодразделение.Сотрудники КАК ПеремещениеВДругоеПодразделениеСотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеСотрудникаСГруппировкой КАК ВТДанныеСотрудникаСГруппировкой
	|		ПО ПеремещениеВДругоеПодразделениеСотрудники.Сотрудник = ВТДанныеСотрудникаСГруппировкой.Сотрудник
	|ГДЕ
	|	ПеремещениеВДругоеПодразделениеСотрудники.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("ДатаПеремещения", ДатаПеремещения);
	
	Выборка = Запрос.Выполнить().Выбрать();
		
	Пока Выборка.Следующий() Цикл
						
		НоваяСтрока = ДанныеРеестраКадровыхПриказов.Добавить();
				
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			
		НомерПриказа = Выборка.НомерПриказа;
		ДатаПриказа = Выборка.ДатаПриказа;
					
	КонецЦикла;  
	
	Основание = КадровыйУчетРасширенный.ОснованиеДляРеестра(ДатаПриказа, НомерПриказа);
	
	ДанныеРеестраКадровыхПриказов.ЗаполнитьЗначения(Основание, "Основание");
	
	ДанныеДляПроведения.Вставить("ДанныеРеестраКадровыхПриказов", ДанныеРеестраКадровыхПриказов);
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

Функция СформироватьЗапросШРДляПроверки()
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Сотрудники.НомерСтроки,
	|	Сотрудники.Сотрудник,
	|	Сотрудники.ДолжностьПоШтатномуРасписанию
	|ПОМЕСТИТЬ ВТПозицииШР
	|ИЗ
	|	&Сотрудники КАК Сотрудники
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТПозицииШР.ДолжностьПоШтатномуРасписанию.Подразделение КАК ПодразделениеШР,
	|   ВТПозицииШР.ДолжностьПоШтатномуРасписанию КАК ПозицияШР,
	|	ВТПозицииШР.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ВТПозицииШР КАК ВТПозицииШР
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат Запрос.Выполнить();
КонецФункции	

Процедура ПроверитьСоответствиеПозицииШРПодразделению(Отказ)
	
	ВыборкаПодразделенияШР = СформироватьЗапросШРДляПроверки().Выбрать();
	
	Пока ВыборкаПодразделенияШР.Следующий() Цикл
		Если ЗначениеЗаполнено(ВыборкаПодразделенияШР.ПозицияШР) И ВыборкаПодразделенияШР.ПодразделениеШР <> ПодразделениеНовое Тогда
			ТекстСообщения = Нстр("ru = 'Выбранная позиция штатного расписания не соответствует подразделению.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, "Объект.Сотрудники[" + Строка(ВыборкаПодразделенияШР.НомерСтроки - 1) + "].ДолжностьПоШтатномуРасписанию",  , Отказ);
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры	

Функция ДанныеДляРегистрацииВУчетаСтажаПФР()
	МассивСсылок = Новый Массив;
	МассивСсылок.Добавить(Ссылка);
	
	ДанныеДляРегистрацииВУчете = Документы.ПеремещениеВДругоеПодразделение.ДанныеДляРегистрацииВУчетаСтажаПФР(МассивСсылок);
	
	Возврат ДанныеДляРегистрацииВУчете[Ссылка];
														
КонецФункции	

Процедура СоздатьВТДанныеДокументов(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка.Организация КАК Организация,
		|	ТаблицаДокумента.Сотрудник,
		|	ТаблицаДокумента.Ссылка.ДатаПеремещения КАК ПериодДействия,
		|	ТаблицаДокумента.Ссылка КАК ДокументОснование
		|ПОМЕСТИТЬ ВТДанныеДокументов
		|ИЗ
		|	Документ.ПеремещениеВДругоеПодразделение.Сотрудники КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Регистратор";
		
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
