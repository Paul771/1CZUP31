
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Пострадавший = Параметры.Пострадавший;
	Организация = Параметры.Организация;
	ФИОПострадавшийСклоняемое = Строка(Пострадавший);
	ФизическиеЛицаЗарплатаКадры.Просклонять(Строка(ФИОПострадавшийСклоняемое), 2, ФИОПострадавшийСклоняемое, ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Пострадавший, "Пол"));
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Пройденные инструктажи: %1'"), ФИОПострадавшийСклоняемое);
	
	Инструктажи.Очистить();
	Для каждого СтрокаИнструктажа Из Параметры.Инструктажи Цикл
		НоваяСтрока = Инструктажи.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаИнструктажа);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)

	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("Пострадавший", Пострадавший);
	
	МассивИнструктажей = Новый Массив;
	Для каждого СтрокаТаблицы Из Инструктажи Цикл
		СтрокаИнструктажа = Новый Структура;
		СтрокаИнструктажа.Вставить("ВидИнструктажа", СтрокаТаблицы.ВидИнструктажа);
		СтрокаИнструктажа.Вставить("ДатаПрохождения", СтрокаТаблицы.ДатаПрохождения);
		МассивИнструктажей.Добавить(СтрокаИнструктажа);
	КонецЦикла;
	
	ПараметрыЗакрытия.Вставить("Инструктажи", МассивИнструктажей);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	ИспользоватьИнструктажиПоОхранеТруда = ПолучитьФункциональнуюОпцию("ИспользоватьИнструктажиПоОхранеТруда");
	Если Не ИспользоватьИнструктажиПоОхранеТруда Тогда
		Возврат;
	КонецЕсли;
	
	Инструктажи.Очистить();
	МассивФизическихЛиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Пострадавший);
	СотрудникиФизическихЛиц = КадровыйУчетРасширенный.СотрудникиФизическихЛиц(МассивФизическихЛиц, Организация, Истина, Истина);
	ИнструктажиПоОхранеТруда = ОхранаТруда.ИнструктажиСотрудниковПоОхранеТруда(СотрудникиФизическихЛиц);
	Для Каждого ИнструктажСотрудника Из ИнструктажиПоОхранеТруда Цикл
		НоваяСтрока = Инструктажи.Добавить();
		НоваяСтрока.ВидИнструктажа = ИнструктажСотрудника.ВидИнструктажа;
		НоваяСтрока.ДатаПрохождения = ИнструктажСотрудника.ДатаПроведения;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры

#КонецОбласти
