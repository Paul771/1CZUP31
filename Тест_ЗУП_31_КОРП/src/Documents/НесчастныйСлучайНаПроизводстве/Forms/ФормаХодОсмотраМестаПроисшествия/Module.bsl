#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбстановкаИСостояниеМестаПроисшествия = Параметры.ОбстановкаИСостояниеМестаПроисшествия;
	ОписаниеРабочегоМеста = Параметры.ОписаниеРабочегоМеста;
	СостояниеЗащитныхОграждений = Параметры.СостояниеЗащитныхОграждений;
	СостояниеОбщеобменнойВентиляции = Параметры.СостояниеОбщеобменнойВентиляции;
	ОписаниеОборудования = Параметры.ОписаниеОборудования;
	СостояниеЗащитныхСредств = Параметры.СостояниеЗащитныхСредств;
	СостояниеОсвещенности = Параметры.СостояниеОсвещенности;
	ТехническиеСредстваОсмотра = Параметры.ТехническиеСредстваОсмотра;
	ИзъятыеПредметы = Параметры.ИзъятыеПредметы;
	ПрилагаемыеДокументы = Параметры.ПрилагаемыеДокументы;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЭлементРедактированияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОповещения = Новый Структура("ИмяЭлемента", Элемент.Имя);
	Оповещение = Новый ОписаниеОповещения("ЭлементРедактированияЗавершениеВвода", ЭтотОбъект, ПараметрыОповещения);
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияМногострочногоТекста(
		Оповещение,
		Элемент.ТекстРедактирования,
		Элемент.Заголовок);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)

	Если ПустаяСтрока(ЭтотОбъект.ОбстановкаИСостояниеМестаПроисшествия) Тогда
		ТекстОшибки = НСтр("ru = 'Не заполнена обстановка и состояние места происшествия на момент осмотра.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , "ОбстановкаИСостояниеМестаПроисшествия");
		Возврат;
	КонецЕсли;
	
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("ОбстановкаИСостояниеМестаПроисшествия", ОбстановкаИСостояниеМестаПроисшествия);
	ПараметрыЗакрытия.Вставить("ОписаниеРабочегоМеста", ОписаниеРабочегоМеста);
	ПараметрыЗакрытия.Вставить("СостояниеЗащитныхОграждений", СостояниеЗащитныхОграждений);
	ПараметрыЗакрытия.Вставить("СостояниеОбщеобменнойВентиляции", СостояниеОбщеобменнойВентиляции);
	ПараметрыЗакрытия.Вставить("ОписаниеОборудования", ОписаниеОборудования);
	ПараметрыЗакрытия.Вставить("СостояниеЗащитныхСредств", СостояниеЗащитныхСредств);
	ПараметрыЗакрытия.Вставить("СостояниеОсвещенности", СостояниеОсвещенности);
	ПараметрыЗакрытия.Вставить("ТехническиеСредстваОсмотра", ТехническиеСредстваОсмотра);
	ПараметрыЗакрытия.Вставить("ИзъятыеПредметы", ИзъятыеПредметы);
	ПараметрыЗакрытия.Вставить("ПрилагаемыеДокументы", ПрилагаемыеДокументы);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЭлементРедактированияЗавершениеВвода(Знач ВведенныйТекст, Знач ДополнительныеПараметры) Экспорт
	
	Если ВведенныйТекст = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект[ДополнительныеПараметры.ИмяЭлемента] = ВведенныйТекст;
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти
