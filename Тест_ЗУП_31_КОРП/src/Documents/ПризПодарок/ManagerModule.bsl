#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов


// Проводит документ по учетам. Если в параметре ВидыУчетов передано Неопределено, то документ проводится по всем учетам.
// Процедура вызывается из обработки проведения и может вызываться из вне.
// 
// Параметры:
//  ДокументСсылка	- ДокументСсылка.ПризПодарок - Ссылка на документ
//  РежимПроведения - РежимПроведенияДокумента - Режим проведения документа (оперативный, неоперативный)
//  Отказ 			- Булево - Признак отказа от выполнения проведения
//  ВидыУчетов 		- Строка - Список видов учета, по которым необходимо провести документ. Если параметр пустой или Неопределено, то документ проведется по всем учетам
//  Движения 		- Коллекция движений документа - Передается только при вызове из обработки проведения документа
//  Объект			- ДокументОбъект.ПризПодарок - Передается только при вызове из обработки проведения документа
//  ДополнительныеПараметры - Структура - Дополнительные параметры, необходимые для проведения документа.
//
Процедура ПровестиПоУчетам(ДокументСсылка, РежимПроведения, Отказ, ВидыУчетов = Неопределено, Движения = Неопределено, Объект = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	СтруктураВидовУчета = ПроведениеРасширенныйСервер.СтруктураВидовУчета();
	ПроведениеРасширенныйСервер.ПодготовитьНаборыЗаписейКРегистрацииДвиженийПоВидамУчета(РежимПроведения, ДокументСсылка, СтруктураВидовУчета, ВидыУчетов, Движения, Объект, Отказ); 
	
	РеквизитыДляПроведения = РеквизитыДляПроведения(ДокументСсылка);
	ДанныеДляПроведения = ДанныеДляПроведения(РеквизитыДляПроведения, СтруктураВидовУчета);
	
	ИсправлениеДокументовЗарплатаКадры.ПриПроведенииИсправления(ДокументСсылка, Движения, РежимПроведения, Отказ, РеквизитыДляПроведения, СтруктураВидовУчета, Объект);
	
	Если СтруктураВидовУчета.ОстальныеВидыУчета Тогда
		
		ДатаОперацииПоНалогам = УчетНДФЛРасширенный.ДатаОперацииПоДокументу(РеквизитыДляПроведения.Дата, НачалоМесяца(РеквизитыДляПроведения.ПериодРегистрации));
		УчетНДФЛ.СформироватьДоходыНДФЛПоКодамДоходовИзТаблицыЗначений(Движения, Отказ, РеквизитыДляПроведения.Организация, ДатаОперацииПоНалогам, ДанныеДляПроведения.ДанныеДляНДФЛ, Ложь, Ложь, ДокументСсылка);
		УчетНДФЛ.СформироватьИсчисленныйНалогПоТаблицеЗначений(Движения, Отказ, РеквизитыДляПроведения.Организация, ДатаОперацииПоНалогам, ДанныеДляПроведения.ДанныеДляРасчетовСБюджетомПоНДФЛ, , , , Ложь, РеквизитыДляПроведения.ДатаПолученияДохода);
		
		УчетНачисленнойЗарплаты.ЗарегистрироватьНачисленияУдержания(Движения, Отказ, РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.ПериодРегистрации,
			Неопределено, Неопределено, Неопределено, ДанныеДляПроведения.Начисления);
		
		// Учет исчисленного налога в "зарплате".
		УчетНачисленнойЗарплаты.ПодготовитьДанныеНДФЛКРегистрации(ДанныеДляПроведения.НДФЛ, РеквизитыДляПроведения.Организация,ДатаОперацииПоНалогам);
		УчетНачисленнойЗарплаты.ЗарегистрироватьНДФЛ(Движения, Отказ, РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.ПериодРегистрации, ДанныеДляПроведения.НДФЛ, ДанныеДляПроведения.МенеджерВременныхТаблиц, Перечисления.ХарактерВыплатыЗарплаты.Межрасчет);
		
		// - Регистрация начислений и удержаний.
		ОтражениеЗарплатыВБухучетеРасширенный.СформироватьДвиженияБухучетНачисленияУдержанияПоСотрудникам(
					Движения, Отказ, РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.ПериодРегистрации,
					ДанныеДляПроведения.Начисления, Неопределено, ДанныеДляПроведения.НДФЛ, Истина);
					
		УчетСтраховыхВзносов.СформироватьДоходыСтраховыеВзносы(Движения, Отказ, РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.ПериодРегистрации, ДанныеДляПроведения.ДанныеДляВзносов, Истина);
		
	КонецЕсли;
	
	Если СтруктураВидовУчета.ДанныеДляРасчетаСреднего Тогда

		// Средний заработок ФСС.
		УчетПособийСоциальногоСтрахованияРасширенный.ЗарегистрироватьДанныеСреднегоЗаработкаФСС(Движения, Отказ, ДанныеДляПроведения.МенеджерВременныхТаблиц, , "ВТНачисленияДокумента");
		
	КонецЕсли;
	
	ПроведениеРасширенныйСервер.ЗаписьДвиженийПоУчетам(Движения, СтруктураВидовУчета);
	
КонецПроцедуры

// Сторнирует документ по учетам. Используется подсистемой исправления документов.
//
// Параметры:
//  Движения				 - КоллекцияДвижений, Структура	 - Коллекция движений исправляющего документа в которую будут добавлены сторно стоки.
//  Регистратор				 - ДокументСсылка				 - Документ регистратор исправления (документ исправление).
//  ИсправленныйДокумент	 - ДокументСсылка				 - Исправленный документ движения которого будут сторнированы.
//  СтруктураВидовУчета		 - Структура					 - Виды учета, по которым будет выполнено сторнирование исправленного документа.
//  					Состав полей см. в ПроведениеРасширенныйСервер.СтруктураВидовУчета().
//  ДополнительныеПараметры	 - Структура					 - Структура со свойствами:
//  					* ИсправлениеВТекущемПериоде - Булево - Истина когда исправление выполняется в периоде регистрации исправленного документа.
//						* ОтменаДокумента - Булево - Истина когда исправление вызвано документом СторнированиеНачислений.
//  					* ПериодРегистрации	- Дата - Период регистрации документа регистратора исправления.
// 
// Возвращаемое значение:
//  Булево - "Истина" если сторнирование выполнено этой функцией, "Ложь" если специальной процедуры не предусмотрено.
//
Функция СторнироватьПоУчетам(Движения, Регистратор, ИсправленныйДокумент, СтруктураВидовУчета, ДополнительныеПараметры) Экспорт
	
	Если ДополнительныеПараметры.ОтменаДокумента Тогда
		
		Если СтруктураВидовУчета.ДанныеДляРасчетаСреднего Тогда
			УчетПособийСоциальногоСтрахованияРасширенный.СторнироватьДвиженияДокумента(Движения, ИсправленныйДокумент);
		КонецЕсли;
		
		Если СтруктураВидовУчета.ОстальныеВидыУчета Тогда
			
			УчетСтраховыхВзносовРасширенный.СторнироватьДвиженияДокумента(Движения, ИсправленныйДокумент, ДополнительныеПараметры);
			УчетНДФЛРасширенный.СторнироватьДвиженияДокумента(Движения, ИсправленныйДокумент, ДополнительныеПараметры);
			ОтражениеЗарплатыВБухучетеРасширенный.СторнироватьДвиженияДокумента(Движения, ИсправленныйДокумент);
			УчетНачисленнойЗарплатыРасширенный.СторнироватьДвиженияДокумента(Движения, ИсправленныйДокумент);
			
			ИсправлениеДокументовЗарплатаКадры.СторнироватьДвиженияБезСпецификиУчетов(Движения, ИсправленныйДокумент, Ложь);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "ЗарплатаКадрыКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьКадровыхПриказовРасширенная";
	КомандаПечати.Идентификатор = "ПФ_MXL_Т11а";
	КомандаПечати.Представление = НСтр("ru = 'Приказ о поощрении сотрудников (Т-11а)'");
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "ЗарплатаКадрыКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьКадровыхПриказовРасширенная";
	КомандаПечати.Идентификатор = "ПФ_MXL_Т11";
	КомандаПечати.СписокФорм = "ФормаДокумента";
	КомандаПечати.ДополнительныеПараметры.Вставить("ПечатьВсехПриказов", Истина);
	КомандаПечати.Представление = НСтр("ru = 'Приказы на каждого сотрудника в отдельности (Т-11)'");
	
КонецПроцедуры

Функция ДанныеДляБухучетаЗарплатыПервичныхДокументов(Объект) Экспорт

	ДанныеДляБухучета = Новый Структура;
	ДанныеДляБухучета.Вставить("ДокументОснование", Объект.Ссылка);
	
	ТаблицаБухучетЗарплаты = ОтражениеЗарплатыВБухучетеРасширенный.НоваяТаблицаБухучетЗарплатыПервичныхДокументов();
	НоваяСтрока = ТаблицаБухучетЗарплаты.Добавить();
	НоваяСтрока.ДокументОснование = Объект.Ссылка;
	НоваяСтрока.НачислениеУдержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.СтоимостьПодарковПризов;
	НоваяСтрока.СпособОтраженияЗарплатыВБухучете = Объект.СпособОтраженияЗарплатыВБухучете;
	НоваяСтрока.ОтношениеКЕНВД = Объект.ОтношениеКЕНВД;
	НоваяСтрока.СтатьяФинансирования = Объект.СтатьяФинансирования;
	НоваяСтрока.СтатьяРасходов = Объект.СтатьяРасходов;
	
	ДанныеДляБухучета.Вставить("ТаблицаБухучетЗарплаты", ТаблицаБухучетЗарплаты);
	
	Возврат ДанныеДляБухучета;
	
КонецФункции

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаДокумента.
Функция ОписаниеСоставаДокумента() Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.ПризПодарок;
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаДокументаПоМетаданнымФизическиеЛицаВТабличныхЧастях(МетаданныеДокумента);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведения(РеквизитыДляПроведения, СтруктураВидовУчета) 

	ДанныеДляПроведения = РасчетЗарплаты.СоздатьДанныеДляПроведенияНачисленияЗарплаты();
		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", РеквизитыДляПроведения.Ссылка);
	Запрос.УстановитьПараметр("ИспользоватьШтатноеРасписание", ПолучитьФункциональнуюОпцию("ИспользоватьШтатноеРасписание"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПризПодарок.Ссылка КАК Ссылка,
	|	ПризПодарок.Ссылка.Дата КАК Дата,
	|	ПризПодарок.Ссылка.ДатаПолученияДохода КАК ДатаДействия,
	|	НАЧАЛОПЕРИОДА(ПризПодарок.Ссылка.ДатаПолученияДохода, МЕСЯЦ) КАК ПериодДействия,
	|	ПризПодарок.Ссылка.Организация КАК Организация,
	|	ПризПодарок.Сотрудник КАК Сотрудник,
	|	ПризПодарок.Подразделение КАК Подразделение,
	|	ПризПодарок.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ПризПодарок.Ссылка.ДатаПолученияДохода КАК ПланируемаяДатаВыплаты,
	|	НАЧАЛОПЕРИОДА(ПризПодарок.Ссылка.ДатаПолученияДохода, МЕСЯЦ) КАК МесяцНалоговогоПериода,
	|	НАЧАЛОПЕРИОДА(ПризПодарок.Ссылка.ДатаПолученияДохода, МЕСЯЦ) КАК ПериодРегистрации,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.СтоимостьПодарковПризов) КАК Начисление,
	|	ПризПодарок.Результат КАК Сумма,
	|	ПризПодарок.Ссылка.КодДоходаНДФЛ КАК КодДохода,
	|	ПризПодарок.Ссылка.КодДоходаНДФЛ.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	ПризПодарок.Ссылка.ВидДоходаСтраховыеВзносы КАК ВидДохода,
	|	ПризПодарок.КодВычета КАК КодВычета,
	|	ПризПодарок.СуммаНДФЛ КАК НДФЛ,
	|	ПризПодарок.СуммаВычета КАК СуммаВычета,
	|	ПризПодарок.Ссылка.СтатьяФинансирования КАК СтатьяФинансирования,
	|	ПризПодарок.Ссылка.СтатьяРасходов КАК СтатьяРасходов,
	|	ПризПодарок.Ссылка.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	ПризПодарок.Ссылка.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	ПризПодарок.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ВТДанныеДокументаПредварительно
	|ИЗ
	|	Документ.ПризПодарок.Начисления КАК ПризПодарок
	|ГДЕ
	|	ПризПодарок.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПризПодарок.Ссылка,
	|	ПризПодарок.Ссылка.Дата,
	|	ПризПодарок.Ссылка.ДатаПолученияДохода,
	|	НАЧАЛОПЕРИОДА(ПризПодарок.Ссылка.ДатаПолученияДохода, МЕСЯЦ),
	|	ПризПодарок.Ссылка.Организация,
	|	ПризПодарок.Сотрудник,
	|	ПризПодарок.Подразделение,
	|	ПризПодарок.Сотрудник.ФизическоеЛицо,
	|	ПризПодарок.Ссылка.ДатаПолученияДохода,
	|	НАЧАЛОПЕРИОДА(ПризПодарок.Ссылка.ДатаПолученияДохода, МЕСЯЦ),
	|	НАЧАЛОПЕРИОДА(ПризПодарок.Ссылка.ДатаПолученияДохода, МЕСЯЦ),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.СтоимостьПодарковПризов),
	|	ВЫБОР
	|		КОГДА ПризПодарок.Сторно
	|			ТОГДА -ПризПодарок.Результат
	|		ИНАЧЕ ПризПодарок.Результат
	|	КОНЕЦ,
	|	ПризПодарок.Ссылка.КодДоходаНДФЛ,
	|	ПризПодарок.Ссылка.КодДоходаНДФЛ.СтавкаНалогообложенияРезидента,
	|	ПризПодарок.Ссылка.ВидДоходаСтраховыеВзносы,
	|	ПризПодарок.КодВычета,
	|	ВЫБОР
	|		КОГДА ПризПодарок.Сторно
	|			ТОГДА -ПризПодарок.СуммаНДФЛ
	|		ИНАЧЕ ПризПодарок.СуммаНДФЛ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ПризПодарок.Сторно
	|			ТОГДА -ПризПодарок.СуммаВычета
	|		ИНАЧЕ ПризПодарок.СуммаВычета
	|	КОНЕЦ,
	|	ПризПодарок.Ссылка.СтатьяФинансирования,
	|	ПризПодарок.Ссылка.СтатьяРасходов,
	|	ПризПодарок.Ссылка.СпособОтраженияЗарплатыВБухучете,
	|	ПризПодарок.Ссылка.ОтношениеКЕНВД,
	|	ПризПодарок.НомерСтроки
	|ИЗ
	|	Документ.ПризПодарок.НачисленияПерерасчет КАК ПризПодарок
	|ГДЕ
	|	ПризПодарок.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.ФизическоеЛицо
	|ПОМЕСТИТЬ ВТФизическиеЛица
	|ИЗ
	|	ВТДанныеДокументаПредварительно КАК ДанныеДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.ФизическоеЛицо,
	|	ДанныеДокумента.КодДохода,
	|	ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеНатуральныеДоходы) КАК КатегорияДохода,
	|	ДанныеДокумента.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	ДанныеДокумента.ПланируемаяДатаВыплаты КАК Период
	|ПОМЕСТИТЬ ВТДоходыФизическихЛиц
	|ИЗ
	|	ВТДанныеДокументаПредварительно КАК ДанныеДокумента";
	
	Запрос.Выполнить();
	
	// Территория Сотрудников
	Запрос.УстановитьПараметр("ИспользоватьРаспределениеПоТерриториямУсловиямТруда", ЗарплатаКадрыРасширенный.ИспользоватьРаспределениеПоТерриториямУсловиямТруда(РеквизитыДляПроведения.Организация));
	
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц, "ВТДанныеДокументаПредварительно", "Сотрудник,ДатаДействия");
	ОписательВременныхТаблиц.ИмяВТКадровыеДанныеСотрудников = "ВТТерриторииСотрудников";
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, Ложь, "Территория");
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Дата,
	|	ДанныеДокумента.ДатаДействия,
	|	ДанныеДокумента.ПериодДействия,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.Сотрудник,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА &ИспользоватьРаспределениеПоТерриториямУсловиямТруда = ИСТИНА
	|			ТОГДА ЕСТЬNULL(ТерриторииСотрудников.Территория, ДанныеДокумента.Подразделение)
	|		ИНАЧЕ ДанныеДокумента.Подразделение
	|	КОНЕЦ КАК ТерриторияВыполненияРаботВОрганизации,
	|	ДанныеДокумента.Подразделение КАК ПодразделениеСотрудника,
	|	ДанныеДокумента.ФизическоеЛицо,
	|	ДанныеДокумента.ПланируемаяДатаВыплаты,
	|	ДанныеДокумента.МесяцНалоговогоПериода,
	|	ДанныеДокумента.ПериодРегистрации,
	|	ДанныеДокумента.Начисление,
	|	ДанныеДокумента.Сумма,
	|	ДанныеДокумента.КодДохода,
	|	ДанныеДокумента.СтавкаНалогообложенияРезидента,
	|	ДанныеДокумента.ВидДохода,
	|	ДанныеДокумента.КодВычета,
	|	ДанныеДокумента.НДФЛ,
	|	ДанныеДокумента.СуммаВычета,
	|	ДанныеДокумента.СтатьяФинансирования,
	|	ДанныеДокумента.СтатьяРасходов,
	|	ДанныеДокумента.СпособОтраженияЗарплатыВБухучете,
	|	ДанныеДокумента.ОтношениеКЕНВД,
	|	ДанныеДокумента.НомерСтроки
	|ПОМЕСТИТЬ ВТДанныеДокумента
	|ИЗ
	|	ВТДанныеДокументаПредварительно КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТерриторииСотрудников КАК ТерриторииСотрудников
	|		ПО ДанныеДокумента.Сотрудник = ТерриторииСотрудников.Сотрудник
	|			И ДанныеДокумента.ДатаДействия = ТерриторииСотрудников.Период";
	Запрос.Выполнить();
	
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеДокумента.ПериодРегистрации КАК ПериодРегистрации,
		|	ДанныеДокумента.ДатаДействия,
		|	ДанныеДокумента.ПериодДействия,
		|	ДанныеДокумента.Организация,
		|	ДанныеДокумента.Сотрудник КАК Сотрудник,
		|	ДанныеДокумента.ФизическоеЛицо,
		|	ДанныеДокумента.Подразделение,
		|	ДанныеДокумента.ПодразделениеСотрудника,
		|	ДанныеДокумента.ТерриторияВыполненияРаботВОрганизации,
		|	ДанныеДокумента.Начисление,
		|	ДанныеДокумента.ВидДохода,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ПустаяСсылка) КАК ВидОперации,
		|	НАЧАЛОПЕРИОДА(ДанныеДокумента.ПериодРегистрации, МЕСЯЦ) КАК ДатаНачала,
		|	КОНЕЦПЕРИОДА(ДанныеДокумента.ПериодРегистрации, МЕСЯЦ) КАК ДатаОкончания,
		|	ДанныеДокумента.НомерСтроки КАК ИдентификаторСтроки,
		|	ДанныеДокумента.Сумма,
		|	ДанныеДокумента.СуммаВычета,
		|	ДанныеДокумента.Ссылка КАК ДокументОснование
		|ПОМЕСТИТЬ ВТНачисленияДляРаспределения
		|ИЗ
		|	ВТДанныеДокумента КАК ДанныеДокумента";
		
		Запрос.Выполнить();
		
		ИсходныеДанные = ОтражениеЗарплатыВБухучетеРасширенный.ОписаниеИсходныхДанныхДляОтраженияНачисленийВБухучете();
		ИсходныеДанные.МенеджерВременныхТаблиц    = Запрос.МенеджерВременныхТаблиц;
		ИсходныеДанные.Организация    			  = РеквизитыДляПроведения.Организация;
		ИсходныеДанные.МесяцНачисления 			  = РеквизитыДляПроведения.ПериодРегистрации;  
		ИсходныеДанные.ИсключаемыйРегистратор     = РеквизитыДляПроведения.Ссылка;
		ИсходныеДанные.БухучетПервичногоДокумента = Документы.ПризПодарок.ДанныеДляБухучетаЗарплатыПервичныхДокументов(РеквизитыДляПроведения).ТаблицаБухучетЗарплаты;
		ОтражениеЗарплатыВБухучетеРасширенный.СоздатьВТБухучетНачисленийБезДоговоровГПХ(ИсходныеДанные, "ВТБухучетНачислений");
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Начисления.ПериодРегистрации,
		|	Начисления.ПериодРегистрации КАК МесяцНачисления,
		|	Начисления.ДатаДействия,
		|	Начисления.ПериодДействия,
		|	Начисления.Организация,
		|	Начисления.Сотрудник,
		|	Начисления.ФизическоеЛицо,
		|	Начисления.Подразделение,
		|	Начисления.ПодразделениеСотрудника,
		|	Начисления.ТерриторияВыполненияРаботВОрганизации,
		|	Начисления.Начисление,
		|	Начисления.ВидДохода,
		|	Начисления.ДатаНачала,
		|	Начисления.ДатаОкончания,
		|	Начисления.ДокументОснование,
		|	Начисления.ДокументОснование КАК ДокументСсылка,
		|	БухучетНачислений.СтатьяФинансирования,
		|	БухучетНачислений.СтатьяРасходов,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.ОблагаетсяЕНВД,
		|	БухучетНачислений.Сумма,
		|	ВЫБОР
		|		КОГДА Начисления.Сумма = 0
		|			ТОГДА Начисления.СуммаВычета
		|		КОГДА БухучетНачислений.ОблагаетсяЕНВД
		|			ТОГДА ВЫРАЗИТЬ(Начисления.СуммаВычета * БухучетНачислений.Сумма / Начисления.Сумма КАК ЧИСЛО(15, 2))
		|		ИНАЧЕ Начисления.СуммаВычета - (ВЫРАЗИТЬ(Начисления.СуммаВычета * (Начисления.Сумма - БухучетНачислений.Сумма) / Начисления.Сумма КАК ЧИСЛО(15, 2)))
		|	КОНЕЦ КАК СуммаВычета
		|ПОМЕСТИТЬ ВТНачисленияДокумента
		|ИЗ
		|	ВТНачисленияДляРаспределения КАК Начисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетНачислений КАК БухучетНачислений
		|		ПО Начисления.ИдентификаторСтроки = БухучетНачислений.ИдентификаторСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТНачисленияДляРаспределения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТБухучетНачислений";
		Запрос.Выполнить();
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Начисления.ПериодРегистрации,
		|	Начисления.ПериодРегистрации КАК МесяцНачисления,
		|	Начисления.ДатаДействия,
		|	Начисления.ПериодДействия,
		|	Начисления.Организация,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.ФизическоеЛицо,
		|	Начисления.Подразделение,
		|	Начисления.ПодразделениеСотрудника,
		|	Начисления.ТерриторияВыполненияРаботВОрганизации,
		|	Начисления.Начисление,
		|	Начисления.ВидДохода,
		|	НАЧАЛОПЕРИОДА(Начисления.ПериодРегистрации, МЕСЯЦ) КАК ДатаНачала,
		|	КОНЕЦПЕРИОДА(Начисления.ПериодРегистрации, МЕСЯЦ) КАК ДатаОкончания,
		|	Начисления.Ссылка КАК ДокументОснование,
		|	Начисления.Ссылка КАК ДокументСсылка,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка) КАК СпособОтраженияЗарплатыВБухучете,
		|	ЛОЖЬ КАК ОблагаетсяЕНВД,
		|	Начисления.Сумма,
		|	Начисления.СуммаВычета
		|ПОМЕСТИТЬ ВТНачисленияДокумента
		|ИЗ
		|	ВТДанныеДокумента КАК Начисления";
		Запрос.Выполнить();
	КонецЕсли;
	
	УчетНДФЛ.СоздатьВТСтавкаНДФЛПоСтавкеРезидента(Запрос.МенеджерВременныхТаблиц,"ВТДоходыФизическихЛиц");
	
	КадровыеДанные = "Должность,
	|ДолжностьПоШтатномуРасписанию, 
	|ЯвляетсяРаботникомСДосрочнойПенсией,
	|ЯвляетсяФармацевтом,
	|ЯвляетсяЧленомЭкипажаСуднаПодФлагомРФ,
	|ЯвляетсяРаботникомСДосрочнойПенсией,
	|ЯвляетсяПрокурором,
	|ЯвляетсяВоеннослужащим,
	|РаботаетВСтуденческомОтряде,
	|ЯвляетсяЧленомЛетногоЭкипажа,
	|ЯвляетсяШахтером";
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц, "ВТНачисленияДокумента", "Сотрудник,ДатаНачала");
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, Ложь, КадровыеДанные);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КадровыеДанныеСотрудников.Сотрудник КАК Сотрудник,
		|	ВЫБОР
		|		КОГДА &ИспользоватьШтатноеРасписание
		|			ТОГДА КадровыеДанныеСотрудников.ДолжностьПоШтатномуРасписанию
		|		ИНАЧЕ КадровыеДанныеСотрудников.Должность
		|	КОНЕЦ КАК Должность,
		|	КадровыеДанныеСотрудников.ЯвляетсяРаботникомСДосрочнойПенсией КАК ЯвляетсяРаботникомСДосрочнойПенсией,
		|	КадровыеДанныеСотрудников.ЯвляетсяФармацевтом,
		|	КадровыеДанныеСотрудников.ЯвляетсяЧленомЭкипажаСуднаПодФлагомРФ,
		|	КадровыеДанныеСотрудников.ЯвляетсяПрокурором,
		|	КадровыеДанныеСотрудников.ЯвляетсяВоеннослужащим,
		|	КадровыеДанныеСотрудников.РаботаетВСтуденческомОтряде,
		|	КадровыеДанныеСотрудников.ЯвляетсяЧленомЛетногоЭкипажа,
		|	КадровыеДанныеСотрудников.ЯвляетсяШахтером,
		|	КадровыеДанныеСотрудников.Период КАК Период
		|ПОМЕСТИТЬ ВТКадровыеДанные
		|ИЗ
		|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КадровыеДанные.Должность КАК Должность,
		|	КадровыеДанные.Период КАК Период
		|ПОМЕСТИТЬ ВТДолжности
		|ИЗ
		|	ВТКадровыеДанные КАК КадровыеДанные";
		
	Запрос.Выполнить();
	
	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	ПараметрыПостроения.ВсеЗаписи = Истина;
	УчетСтраховыхВзносов.ДобавитьОтборПриЧтенииПериодическихДанных(ПараметрыПостроения, КонецМесяца(РеквизитыДляПроведения.ДатаПолученияДохода), Ложь);

	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
		"КлассыУсловийТрудаПоДолжностям",
		Запрос.МенеджерВременныхТаблиц,
		Ложь,
		ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
			"ВТДолжности",
			"Должность"),
		ПараметрыПостроения);
		
		
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НачисленияДокумента.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НачисленияДокумента.СтатьяФинансирования КАК СтатьяФинансирования,
		|	НачисленияДокумента.СтатьяРасходов КАК СтатьяРасходов
		|ПОМЕСТИТЬ ВТБухучетФизическихЛиц
		|ИЗ
		|	ВТНачисленияДокумента КАК НачисленияДокумента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокумента.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ДанныеДокумента.Сотрудник КАК Сотрудник,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеНатуральныеДоходы) КАК КатегорияДохода,
		|	ДанныеДокумента.КодДохода КАК КодДохода,
		|	ДанныеДокумента.КодВычета КАК КодВычета,
		|	ДанныеДокумента.ПланируемаяДатаВыплаты КАК ДатаПолученияДохода,
		|	ДанныеДокумента.Сумма КАК СуммаДохода,
		|	ДанныеДокумента.СуммаВычета КАК СуммаВычета,
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|			ТОГДА ДанныеДокумента.НДФЛ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК НалогПоСтавке13,
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
		|			ТОГДА ДанныеДокумента.НДФЛ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК НалогПоСтавке09,
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
		|			ТОГДА ДанныеДокумента.НДФЛ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК НалогПоСтавке35,
		|	ДанныеДокумента.НДФЛ КАК Сумма,
		|	ДанныеДокумента.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
		|	ДанныеДокумента.ТерриторияВыполненияРаботВОрганизации КАК Подразделение,
		|	ДанныеДокумента.ПодразделениеСотрудника КАК ПодразделениеСотрудника,
		|	ДанныеДокумента.Начисление КАК Начисление,
		|	ДанныеДокумента.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
		|	СтавкаНДФЛПоСтавкеРезидента.СтавкаНДФЛ КАК Ставка
		|ИЗ
		|	ВТДанныеДокумента КАК ДанныеДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкаНДФЛПоСтавкеРезидента КАК СтавкаНДФЛПоСтавкеРезидента
		|		ПО ДанныеДокумента.ФизическоеЛицо = СтавкаНДФЛПоСтавкеРезидента.ФизическоеЛицо
		|			И ДанныеДокумента.ПланируемаяДатаВыплаты = СтавкаНДФЛПоСтавкеРезидента.Период
		|ГДЕ
		|	ДанныеДокумента.КодДохода <> ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокумента.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ДанныеДокумента.Сотрудник КАК Сотрудник,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ДанныеДокумента.КодДохода КАК КодДохода,
		|	ДанныеДокумента.КодВычета КАК КодВычета,
		|	ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеНатуральныеДоходы) КАК КатегорияДохода,
		|	ДанныеДокумента.ПланируемаяДатаВыплаты КАК МесяцНалоговогоПериода,
		|	ДанныеДокумента.НДФЛ КАК Сумма,
		|	ДанныеДокумента.ТерриторияВыполненияРаботВОрганизации КАК Подразделение,
		|	ДанныеДокумента.ПодразделениеСотрудника КАК ПодразделениеСотрудника,
		|	ДанныеДокумента.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента
		|ИЗ
		|	ВТДанныеДокумента КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.КодДохода <> ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокумента.Организация КАК Организация,
		|	ДанныеДокумента.Сотрудник КАК Сотрудник,
		|	ДанныеДокумента.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ДанныеДокумента.ПодразделениеСотрудника КАК Подразделение,
		|	ДанныеДокумента.ВидДохода КАК ВидДохода,
		|	ДанныеДокумента.Начисление КАК Начисление,
		|	ДанныеДокумента.ДатаНачала КАК ДатаНачала,
		|	ДанныеДокумента.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	ДанныеДокумента.Сумма КАК Сумма,
		|	КадровыеДанные.ЯвляетсяРаботникомСДосрочнойПенсией КАК ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	КадровыеДанные.ЯвляетсяФармацевтом КАК ЯвляетсяДоходомФармацевта,
		|	КадровыеДанные.ЯвляетсяЧленомЭкипажаСуднаПодФлагомРФ КАК ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
		|	КадровыеДанные.ЯвляетсяЧленомЛетногоЭкипажа КАК ОблагаетсяВзносамиНаДоплатуЛетчикам,
		|	КадровыеДанные.ЯвляетсяШахтером КАК ОблагаетсяВзносамиНаДоплатуШахтерам,
		|	КлассыУсловийТрудаПоДолжностям.КлассУсловийТруда КАК КлассУсловийТруда
		|ИЗ
		|	ВТНачисленияДокумента КАК ДанныеДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанные КАК КадровыеДанные
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТКлассыУсловийТрудаПоДолжностямСрезПоследних КАК КлассыУсловийТрудаПоДолжностям
		|			ПО КадровыеДанные.Должность = КлассыУсловийТрудаПоДолжностям.Должность
		|				И КадровыеДанные.Период = КлассыУсловийТрудаПоДолжностям.Период
		|		ПО ДанныеДокумента.Сотрудник = КадровыеДанные.Сотрудник
		|			И ДанныеДокумента.ДатаНачала = КадровыеДанные.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокумента.Организация КАК Организация,
		|	ДанныеДокумента.Сотрудник КАК Сотрудник,
		|	ДанныеДокумента.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ДанныеДокумента.ПодразделениеСотрудника КАК Подразделение,
		|	ДанныеДокумента.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	ДанныеДокумента.Сумма КАК Сумма,
		|	ДанныеДокумента.Начисление КАК Начисление,
		|	ДанныеДокумента.ДатаНачала КАК ДатаНачала,
		|	ДанныеДокумента.ДатаОкончания КАК ДатаОкончания,
		|	ДанныеДокумента.ДокументОснование КАК ДокументОснование,
		|	ДанныеДокумента.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	ДанныеДокумента.СтатьяФинансирования КАК СтатьяФинансирования,
		|	ДанныеДокумента.СтатьяРасходов КАК СтатьяРасходов,
		|	ДанныеДокумента.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете
		|ИЗ
		|	ВТНачисленияДокумента КАК ДанныеДокумента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокумента.Организация КАК Организация,
		|	ДанныеДокумента.Сотрудник КАК Сотрудник,
		|	ДанныеДокумента.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ДанныеДокумента.ТерриторияВыполненияРаботВОрганизации КАК Подразделение,
		|	ДанныеДокумента.Подразделение КАК ПодразделениеСотрудника,
		|	ДанныеДокумента.НДФЛ КАК Сумма,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.НДФЛРасчетыСБывшимиСотрудниками) КАК НачислениеУдержание,
		|	ДанныеДокумента.Ссылка КАК ДокументОснование,
		|	БухучетФизическихЛиц.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетФизическихЛиц.СтатьяРасходов КАК СтатьяРасходов,
		|	ДанныеДокумента.ПланируемаяДатаВыплаты КАК МесяцНалоговогоПериода
		|ИЗ
		|	ВТДанныеДокумента КАК ДанныеДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетФизическихЛиц КАК БухучетФизическихЛиц
		|		ПО ДанныеДокумента.ФизическоеЛицо = БухучетФизическихЛиц.ФизическоеЛицо";
	
	Результат = Запрос.ВыполнитьПакет();
	КоличествоРезультатов = Результат.ВГраница();
	
	ДанныеДляПроведения.Вставить("ДанныеДляНДФЛ", Результат[КоличествоРезультатов-4].Выгрузить());
	ДанныеДляПроведения.Вставить("ДанныеДляРасчетовСБюджетомПоНДФЛ", Результат[КоличествоРезультатов-3].Выгрузить());
	ДанныеДляПроведения.Вставить("ДанныеДляВзносов", Результат[КоличествоРезультатов-2].Выгрузить());
	ДанныеДляПроведения.Вставить("Начисления", Результат[КоличествоРезультатов-1].Выгрузить());
	ДанныеДляПроведения.Вставить("НДФЛ", Результат[КоличествоРезультатов].Выгрузить());
	ДанныеДляПроведения.Вставить("МенеджерВременныхТаблиц", Запрос.МенеджерВременныхТаблиц);
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

Функция РеквизитыДляПроведения(ДокументСсылка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПризПодарок.Ссылка,
	|	ПризПодарок.Организация,
	|	ПризПодарок.ПериодРегистрации,
	|	ПризПодарок.ДатаПолученияДохода,
	|	ПризПодарок.ИсправленныйДокумент,
	|	ПризПодарок.Дата,
	|	ПризПодарок.СтатьяФинансирования,
	|	ПризПодарок.СтатьяРасходов,
	|	ПризПодарок.СпособОтраженияЗарплатыВБухучете,
	|	ПризПодарок.ОтношениеКЕНВД
	|ИЗ
	|	Документ.ПризПодарок КАК ПризПодарок
	|ГДЕ
	|	ПризПодарок.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Результаты = Запрос.ВыполнитьПакет();
	
	РеквизитыДляПроведения = РеквизитыДляПроведенияПустаяСтруктура();
	
	ВыборкаРеквизиты = Результаты[0].Выбрать();
	
	Пока ВыборкаРеквизиты.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(РеквизитыДляПроведения, ВыборкаРеквизиты);
		
	КонецЦикла;
	
	Возврат РеквизитыДляПроведения;
	
КонецФункции

Функция РеквизитыДляПроведенияПустаяСтруктура()
	
	РеквизитыДляПроведенияПустаяСтруктура = Новый Структура(
		"Ссылка,
		|Организация,
		|ПериодРегистрации,
		|ДатаПолученияДохода,
		|ИсправленныйДокумент,
		|Дата,
		|СтатьяФинансирования,
		|СтатьяРасходов,
		|СпособОтраженияЗарплатыВБухучете,
		|ОтношениеКЕНВД");	
	
	Возврат РеквизитыДляПроведенияПустаяСтруктура;
	
КонецФункции

#КонецОбласти

#КонецЕсли
