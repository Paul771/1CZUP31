#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаДокумента.
Функция ОписаниеСоставаДокумента() Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.РегистрацияПрочихДоходов;
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаДокументаПоМетаданнымФизическиеЛицаВТабличныхЧастях(МетаданныеДокумента);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Справка о списании депонированной зарплаты.
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "ЗарплатаКадрыКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.МенеджерПечати = "Документ.РегистрацияПрочихДоходов";
	КомандаПечати.Идентификатор = "ПФ_MXL_СправкаОРегистрацииПрочихДоходов";
	КомандаПечати.Представление = НСтр("ru = 'Справка о регистрации прочих доходов'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнитьКомплектВнешнимиПечатнымиФормами = Истина;

КонецПроцедуры

// Формирует печатные формы
//
// Параметры:
//  (входные)
//    МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//    ПараметрыПечати - Структура - дополнительные настройки печати;
//  (выходные)
//   КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы.
//   ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                             представление - имя области в которой был выведен объект;
//   ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	НужноПечататьСправку = УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_СправкаОРегистрацииПрочихДоходов");
	
	Если НужноПечататьСправку Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПФ_MXL_СправкаОРегистрацииПрочихДоходов",
			НСтр("ru = 'Справка о выплате бывшим сотрудникам'"), ПечатьСправки(МассивОбъектов, ОбъектыПечати), ,
			"Документ.РегистрацияПрочихДоходов.ПФ_MXL_СправкаОРегистрацииПрочихДоходов");
	КонецЕсли;
						
КонецПроцедуры								

Функция ПечатьСправки(МассивОбъектов, ОбъектыПечати)
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РегистрацияПрочихДоходов_СправкаОРегистрацииПрочихДоходов";
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ДокументРезультат.АвтоМасштаб = Истина;
	
	ДанныеПечатиОбъектов = ДанныеПечатиДокументов(МассивОбъектов);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РегистрацияПрочихДоходов.ПФ_MXL_СправкаОРегистрацииПрочихДоходов");
		
	ОбластьМакетаШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакетаШапкаПовторятьПриПечати = Макет.ПолучитьОбласть("ШапкаПовторятьПриПечати");
	ОбластьМакетаСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал");
	
	ПервыйДокумент = Истина;
	
	Для Каждого ДанныеПечати Из ДанныеПечатиОбъектов Цикл
		
		ДанныеДокумента = ДанныеПечати.Значение;
		
		// Документы нужно выводить на разных страницах.
		Если Не ПервыйДокумент Тогда
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		Иначе
			ПервыйДокумент = Ложь;
		КонецЕсли;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ДокументРезультат.ВысотаТаблицы + 1;
		
		ОбластьМакетаШапка.Параметры.Заполнить(ДанныеДокумента);
		ДокументРезультат.Вывести(ОбластьМакетаШапка);
		
		Для каждого ДанныеСотрудника Из ДанныеДокумента.ТабличнаяЧастьДокумента Цикл
			
			МассивВыводимыхОбластей = Новый Массив;
			МассивВыводимыхОбластей.Добавить(ОбластьМакетаСтрока);
			МассивВыводимыхОбластей.Добавить(ОбластьМакетаПодвал);
			Если НЕ ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, МассивВыводимыхОбластей) Тогда
				ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
				ДокументРезультат.Вывести(ОбластьМакетаШапкаПовторятьПриПечати);
			КонецЕсли;
			
			ОбластьМакетаСтрока.Параметры.Заполнить(ДанныеСотрудника);
			
			ДокументРезультат.Вывести(ОбластьМакетаСтрока);
			
		КонецЦикла;
		
		ОбластьМакетаПодвал.Параметры.Заполнить(ДанныеДокумента);
		ДокументРезультат.Вывести(ОбластьМакетаПодвал);
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ДокументРезультат, НомерСтрокиНачало, ОбъектыПечати, ДанныеДокумента.Ссылка);
				
	КонецЦикла;
	
	Возврат ДокументРезультат;
		
КонецФункции

Функция ДанныеПечатиДокументов(МассивОбъектов)
	
	ДанныеПечатиОбъектов = Новый Соответствие;
	
	ВалютаУчета = ЗарплатаКадры.ВалютаУчетаЗаработнойПлаты();
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РегистрацияПрочихДоходов.Ссылка.Организация.НаименованиеПолное КАК Организация,
	|	РегистрацияПрочихДоходов.Ссылка.Подразделение.Наименование КАК Подразделение,
	|	РегистрацияПрочихДоходов.Ссылка КАК Ссылка,
	|	РегистрацияПрочихДоходов.Ссылка.Номер,
	|	НАЧАЛОПЕРИОДА(РегистрацияПрочихДоходов.Ссылка.Дата, ДЕНЬ) КАК Дата,
	|	РегистрацияПрочихДоходов.Ссылка.ВидПрочегоДохода,
	|	РегистрацияПрочихДоходов.ФизическоеЛицо,
	|	РегистрацияПрочихДоходов.НДФЛ КАК НДФЛ,
	|	РегистрацияПрочихДоходов.Начислено КАК СуммаДохода,
	|	РегистрацияПрочихДоходов.СуммаВычета КАК СуммаВычета,
	|	РегистрацияПрочихДоходов.Удержано КАК Удержано,
	|	РегистрацияПрочихДоходов.КВыплате КАК СуммаКВыплате,
	|	РегистрацияПрочихДоходов.Ссылка.Исполнитель.ФИО КАК Исполнитель,
	|	РегистрацияПрочихДоходов.Ссылка.ДолжностьИсполнителя КАК ДолжностьИсполнителя
	|ИЗ
	|	Документ.РегистрацияПрочихДоходов.НачисленияУдержанияВзносы КАК РегистрацияПрочихДоходов
	|ГДЕ
	|	РегистрацияПрочихДоходов.Ссылка В(&МассивОбъектов)
	|ИТОГИ
	|	СУММА(СуммаДохода),
	|	СУММА(СуммаВычета),
	|	СУММА(Удержано)
	|ПО
	|	Ссылка";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	РезультатЗапроса = Запрос.Выполнить();
	ГруппировкаПоДокументу = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ГруппировкаПоДокументу.Следующий() Цикл
		
		ДанныеПечати = Новый Структура;
		ДанныеПечати.Вставить("Ссылка", ГруппировкаПоДокументу.Ссылка);
		ДанныеПечати.Вставить("НазваниеОрганизации", ГруппировкаПоДокументу.Организация);
		ДанныеПечати.Вставить("НазваниеПодразделения", ГруппировкаПоДокументу.Подразделение);
		ДанныеПечати.Вставить("ЕдиницаИзмерения", ВалютаУчета.НаименованиеПолное);
		ДанныеПечати.Вставить("ВидПрочегоДохода", ГруппировкаПоДокументу.ВидПрочегоДохода);
		ДанныеПечати.Вставить("ДатаДокумента", Формат(ГруппировкаПоДокументу.Дата, "ДЛФ=D"));
		ДанныеПечати.Вставить("НомерДокумента", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ГруппировкаПоДокументу.Номер, Истина, Истина));
		ДанныеПечати.Вставить("ДолжностьИсполнителя", ГруппировкаПоДокументу.ДолжностьИсполнителя);
		ДанныеПечати.Вставить("ИсполнительРасшифровкаПодписи", ФизическиеЛицаЗарплатаКадры.РасшифровкаПодписи(Строка(ГруппировкаПоДокументу.Исполнитель)));
		ДанныеПечати.Вставить("ТабличнаяЧастьДокумента", Новый Массив);
		
		ГруппировкаПоСотрудникам = ГруппировкаПоДокументу.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
		Пока ГруппировкаПоСотрудникам.Следующий() Цикл
			
			СтрокаДанныхПечати = Новый Структура;
			СтрокаДанныхПечати.Вставить("Получатель", "");
			СтрокаДанныхПечати.Вставить("СуммаДохода", ГруппировкаПоСотрудникам.СуммаДохода);
			СтрокаДанныхПечати.Вставить("СуммаВычета", ГруппировкаПоСотрудникам.СуммаВычета);
			СтрокаДанныхПечати.Вставить("СуммаКВыплате", ГруппировкаПоСотрудникам.СуммаКВыплате);
			СтрокаДанныхПечати.Вставить("НДФЛ", ГруппировкаПоСотрудникам.НДФЛ);
			СтрокаДанныхПечати.Вставить("Удержано", ГруппировкаПоСотрудникам.Удержано);
			
			ДанныеФизическогоЛица = КадровыйУчет.КадровыеДанныеФизическихЛиц(
				Истина, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ГруппировкаПоСотрудникам.ФизическоеЛицо), 
				"ФИОПолные,Пол", ГруппировкаПоДокументу.Дата);
			
			Если ДанныеФизическогоЛица.Количество() > 0 Тогда
				СтрокаДанныхПечати.Получатель = ДанныеФизическогоЛица[0].ФИОПолные;
			КонецЕсли;			
			
			ДанныеПечати.ТабличнаяЧастьДокумента.Добавить(СтрокаДанныхПечати);
			
		КонецЦикла;		
		
		ДанныеПечатиОбъектов.Вставить(ГруппировкаПоДокументу.Ссылка, ДанныеПечати);
		
	КонецЦикла;
	
	Возврат ДанныеПечатиОбъектов;
		
КонецФункции

#КонецОбласти

Процедура ЗаполнитьРеквизитыКВыплате() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегистрацияПрочихДоходовНачисления.Ссылка,
	|	СУММА(РегистрацияПрочихДоходовНачисления.КВыплате) КАК КВыплате,
	|	РегистрацияПрочихДоходовНачисления.Ссылка.ВсегоВыплачено
	|ПОМЕСТИТЬ ВТДокументыСИтогами
	|ИЗ
	|	Документ.РегистрацияПрочихДоходов.НачисленияУдержанияВзносы КАК РегистрацияПрочихДоходовНачисления
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрацияПрочихДоходовНачисления.Ссылка,
	|	РегистрацияПрочихДоходовНачисления.Ссылка.ВсегоВыплачено
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДокументыСИтогами.Ссылка
	|ИЗ
	|	ВТДокументыСИтогами КАК ВТДокументыСИтогами
	|ГДЕ
	|	ВТДокументыСИтогами.КВыплате <> ВТДокументыСИтогами.ВсегоВыплачено";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ДокументОбъект.ВсегоВыплачено = ДокументОбъект.НачисленияУдержанияВзносы.Итог("КВыплате");
			
			ДокументОбъект.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
			ДокументОбъект.ОбменДанными.Загрузка = Истина;
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			
		КонецЦикла; 
		
	КонецЕсли; 
		
КонецПроцедуры

Функция ДанныеДляБухучетаЗарплатыПервичныхДокументов(Объект) Экспорт

	ДанныеДляБухучета = Новый Структура;
	ДанныеДляБухучета.Вставить("ДокументОснование", Объект.Ссылка);
	
	ТаблицаБухучетЗарплаты = ОтражениеЗарплатыВБухучетеРасширенный.НоваяТаблицаБухучетЗарплатыПервичныхДокументов();
	НоваяСтрока = ТаблицаБухучетЗарплаты.Добавить();
	НоваяСтрока.ДокументОснование = Объект.Ссылка;
	НоваяСтрока.НачислениеУдержание = Объект.ВидПрочегоДохода;
	НоваяСтрока.СпособОтраженияЗарплатыВБухучете = Объект.СпособОтраженияЗарплатыВБухучете;
	НоваяСтрока.ОтношениеКЕНВД = Объект.ОтношениеКЕНВД;
	НоваяСтрока.СтатьяФинансирования = Объект.СтатьяФинансирования;
	НоваяСтрока.СтатьяРасходов = Объект.СтатьяРасходов;
	
	ДанныеДляБухучета.Вставить("ТаблицаБухучетЗарплаты", ТаблицаБухучетЗарплаты);
	
	Возврат ДанныеДляБухучета;
	
КонецФункции

Процедура СоздатьВТДанныеДокумента(МенеджерВременныхТаблиц, ДокументСсылка, СписокФизическихЛиц = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.Ссылка.Организация,
	|	Начисления.Ссылка.ВидПрочегоДохода.КодДоходаНДФЛ КАК КодДохода,
	|	Начисления.Ссылка.ВидПрочегоДохода.КодДоходаНДФЛ.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	Начисления.Ссылка.КодВычетаНДФЛ КАК КодВычета,
	|	Начисления.Начислено КАК СуммаДохода,
	|	Начисления.Ссылка.ПланируемаяДатаВыплаты КАК ПланируемаяДатаВыплаты,
	|	НАЧАЛОПЕРИОДА(Начисления.Ссылка.ПланируемаяДатаВыплаты, МЕСЯЦ) КАК МесяцНалоговогоПериода,
	|	НАЧАЛОПЕРИОДА(Начисления.Ссылка.ПериодРегистрации, МЕСЯЦ) КАК ПериодРегистрации,
	|	Начисления.Ссылка.Подразделение КАК Подразделение,
	|	Начисления.Ссылка.ВидПрочегоДохода.КодДоходаСтраховыеВзносы КАК ВидДохода,
	|	Начисления.Ссылка.ОтношениеКЕНВД,
	|	Начисления.Начислено КАК Сумма,
	|	Начисления.НДФЛ КАК НДФЛ,
	|	Начисления.СуммаВычета КАК СуммаВычета,
	|	Начисления.Ссылка.ВидПрочегоДохода КАК Начисление,
	|	Начисления.Ссылка,
	|	Начисления.Ссылка.СтатьяФинансирования,
	|	Начисления.Ссылка.СтатьяРасходов,
	|	Начисления.Ссылка.СпособОтраженияЗарплатыВБухучете,
	|	Начисления.НомерСтроки,
	|	Начисления.Удержано
	|ПОМЕСТИТЬ ВТДанныеДокумента
	|ИЗ
	|	Документ.РегистрацияПрочихДоходов.НачисленияУдержанияВзносы КАК Начисления
	|ГДЕ
	|	Начисления.Ссылка = &Ссылка
	|	И Начисления.ФизическоеЛицо В(&СписокФизическихЛиц)";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Если СписокФизическихЛиц = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Начисления.ФизическоеЛицо В(&СписокФизическихЛиц)", "");
	Иначе
		Запрос.УстановитьПараметр("СписокФизическихЛиц", СписокФизическихЛиц);
	КонецЕсли;
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьВТНачисленияУдержанияДокумента(МенеджерВременныхТаблиц, ДокументОбъект, Организация, ПериодРегистрации) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеДокумента.ПериодРегистрации КАК ПериодРегистрации,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
		|	ДанныеДокумента.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ДанныеДокумента.Подразделение КАК Подразделение,
		|	ДанныеДокумента.Подразделение КАК ТерриторияВыполненияРаботВОрганизации,
		|	ДанныеДокумента.Начисление КАК Начисление,
		|	ДанныеДокумента.НомерСтроки КАК НомерСтроки,
		|	ДанныеДокумента.ВидДохода КАК ВидДохода,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ПустаяСсылка) КАК ВидОперации,
		|	НАЧАЛОПЕРИОДА(ДанныеДокумента.ПериодРегистрации, МЕСЯЦ) КАК ДатаНачала,
		|	КОНЕЦПЕРИОДА(ДанныеДокумента.ПериодРегистрации, МЕСЯЦ) КАК ДатаОкончания,
		|	ДанныеДокумента.НомерСтроки КАК ИдентификаторСтроки,
		|	ДанныеДокумента.Сумма КАК Сумма,
		|	ДанныеДокумента.Удержано КАК Удержано,
		|	ДанныеДокумента.СуммаВычета КАК СуммаВычета,
		|	ДанныеДокумента.Ссылка КАК ДокументОснование
		|ПОМЕСТИТЬ ВТНачисленияДляРаспределения
		|ИЗ
		|	ВТДанныеДокумента КАК ДанныеДокумента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокумента.ПериодРегистрации КАК ПериодРегистрации,
		|	ДанныеДокумента.Организация КАК Организация,
		|	Удержания.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ДанныеДокумента.Подразделение КАК Подразделение,
		|	ДанныеДокумента.Подразделение КАК ТерриторияВыполненияРаботВОрганизации,
		|	Удержания.Удержание КАК Удержание,
		|	Удержания.НомерСтроки КАК НомерСтроки,
		|	НАЧАЛОПЕРИОДА(ДанныеДокумента.ПериодРегистрации, МЕСЯЦ) КАК ДатаНачала,
		|	КОНЕЦПЕРИОДА(ДанныеДокумента.ПериодРегистрации, МЕСЯЦ) КАК ДатаОкончания,
		|	Удержания.НомерСтроки КАК ИдентификаторСтроки,
		|	Удержания.Результат КАК Удержано,
		|	ДанныеДокумента.Ссылка КАК ДокументОснование,
		|	Удержания.Получатель КАК Получатель,
		|	Удержания.ПлатежныйАгент КАК ПлатежныйАгент,
		|	Удержания.ДокументОснование КАК ИсполнительныйДокумент
		|ПОМЕСТИТЬ ВТУдержанияДляРаспределения
		|ИЗ
		|	Документ.РегистрацияПрочихДоходов.Удержания КАК Удержания
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеДокумента КАК ДанныеДокумента
		|		ПО Удержания.Ссылка = ДанныеДокумента.Ссылка
		|			И Удержания.ФизическоеЛицо = ДанныеДокумента.ФизическоеЛицо
		|ГДЕ
		|	Удержания.Ссылка В
		|			(ВЫБРАТЬ
		|				ДанныеДокумента.Ссылка
		|			ИЗ
		|				ВТДанныеДокумента КАК ДанныеДокумента)";
		
		Запрос.Выполнить();
		
		ИсходныеДанные = ОтражениеЗарплатыВБухучетеРасширенный.ОписаниеИсходныхДанныхДляОтраженияНачисленийВБухучете();
		ИсходныеДанные.МенеджерВременныхТаблиц    = Запрос.МенеджерВременныхТаблиц;
		ИсходныеДанные.Организация    			  = Организация;
		ИсходныеДанные.МесяцНачисления 			  = НачалоМесяца(ПериодРегистрации);  
		ИсходныеДанные.ИсключаемыйРегистратор     = ДокументОбъект.Ссылка;
		ИсходныеДанные.БухучетПервичногоДокумента = Документы.РегистрацияПрочихДоходов.ДанныеДляБухучетаЗарплатыПервичныхДокументов(ДокументОбъект).ТаблицаБухучетЗарплаты;
		ОтражениеЗарплатыВБухучетеРасширенный.СоздатьВТБухучетНачисленийБезДоговоровГПХ(ИсходныеДанные, "ВТБухучетНачислений");
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Начисления.ПериодРегистрации,
		|	Начисления.Организация,
		|	Начисления.Сотрудник,
		|	Начисления.ФизическоеЛицо,
		|	Начисления.Подразделение,
		|	Начисления.Начисление,
		|	Начисления.НомерСтроки,
		|	Начисления.ВидДохода,
		|	Начисления.ДатаНачала,
		|	Начисления.ДатаОкончания,
		|	Начисления.ДокументОснование,
		|	БухучетНачислений.СтатьяФинансирования,
		|	БухучетНачислений.СтатьяРасходов,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.ОблагаетсяЕНВД,
		|	БухучетНачислений.Сумма,
		|	ВЫБОР
		|		КОГДА Начисления.Сумма = 0
		|			ТОГДА Начисления.СуммаВычета
		|		КОГДА БухучетНачислений.ОблагаетсяЕНВД
		|			ТОГДА ВЫРАЗИТЬ(Начисления.СуммаВычета * БухучетНачислений.Сумма / Начисления.Сумма КАК ЧИСЛО(15, 2))
		|		ИНАЧЕ Начисления.СуммаВычета - (ВЫРАЗИТЬ(Начисления.СуммаВычета * (Начисления.Сумма - БухучетНачислений.Сумма) / Начисления.Сумма КАК ЧИСЛО(15, 2)))
		|	КОНЕЦ КАК СуммаВычета
		|ПОМЕСТИТЬ ВТНачисленияДокумента
		|ИЗ
		|	ВТНачисленияДляРаспределения КАК Начисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетНачислений КАК БухучетНачислений
		|		ПО Начисления.ИдентификаторСтроки = БухучетНачислений.ИдентификаторСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Удержания.ПериодРегистрации,
		|	Удержания.Организация,
		|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
		|	Удержания.ФизическоеЛицо,
		|	Удержания.Подразделение,
		|	Удержания.Удержание КАК НачислениеУдержание,
		|	ВЫРАЗИТЬ(Удержания.Удержание КАК ПланВидовРасчета.Удержания).КатегорияУдержания КАК КатегорияУдержания,
		|	Удержания.НомерСтроки,
		|	Удержания.ДатаНачала,
		|	Удержания.ДатаОкончания,
		|	БухучетНачислений.СтатьяФинансирования,
		|	БухучетНачислений.СтатьяРасходов,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.ОблагаетсяЕНВД,
		|	ВЫРАЗИТЬ(Удержания.Удержано * БухучетНачислений.Сумма / Начисления.Сумма КАК ЧИСЛО(15, 2)) КАК Сумма,
		|	Удержания.Получатель,
		|	Удержания.ПлатежныйАгент,
		|	Удержания.ИсполнительныйДокумент
		|ПОМЕСТИТЬ ВТУдержанияДокумента
		|ИЗ
		|	ВТУдержанияДляРаспределения КАК Удержания
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетНачислений КАК БухучетНачислений
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисленияДляРаспределения КАК Начисления
		|			ПО (Начисления.ИдентификаторСтроки = БухучетНачислений.ИдентификаторСтроки)
		|		ПО Удержания.ФизическоеЛицо = БухучетНачислений.ФизическоеЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТНачисленияДляРаспределения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТУдержанияДляРаспределения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТБухучетНачислений";
		
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Начисления.ПериодРегистрации КАК ПериодРегистрации,
		|	Начисления.Организация КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
		|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Начисления.Подразделение КАК Подразделение,
		|	Начисления.Начисление КАК Начисление,
		|	Начисления.НомерСтроки КАК НомерСтроки,
		|	Начисления.ВидДохода КАК ВидДохода,
		|	НАЧАЛОПЕРИОДА(Начисления.ПериодРегистрации, МЕСЯЦ) КАК ДатаНачала,
		|	КОНЕЦПЕРИОДА(Начисления.ПериодРегистрации, МЕСЯЦ) КАК ДатаОкончания,
		|	Начисления.Ссылка КАК ДокументОснование,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка) КАК СпособОтраженияЗарплатыВБухучете,
		|	ВЫБОР
		|		КОГДА Начисления.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ОблагаетсяЕНВД,
		|	Начисления.Сумма КАК Сумма,
		|	Начисления.СуммаВычета КАК СуммаВычета
		|ПОМЕСТИТЬ ВТНачисленияДокумента
		|ИЗ
		|	ВТДанныеДокумента КАК Начисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокумента.ПериодРегистрации КАК ПериодРегистрации,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
		|	Удержания.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ДанныеДокумента.Подразделение КАК Подразделение,
		|	ДанныеДокумента.Подразделение КАК ТерриторияВыполненияРаботВОрганизации,
		|	Удержания.Удержание КАК НачислениеУдержание,
		|	ВЫРАЗИТЬ(Удержания.Удержание КАК ПланВидовРасчета.Удержания).КатегорияУдержания КАК КатегорияУдержания,
		|	Удержания.НомерСтроки КАК НомерСтроки,
		|	НАЧАЛОПЕРИОДА(ДанныеДокумента.ПериодРегистрации, МЕСЯЦ) КАК ДатаНачала,
		|	КОНЕЦПЕРИОДА(ДанныеДокумента.ПериодРегистрации, МЕСЯЦ) КАК ДатаОкончания,
		|	Удержания.НомерСтроки КАК ИдентификаторСтроки,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка) КАК СпособОтраженияЗарплатыВБухучете,
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ОблагаетсяЕНВД,
		|	Удержания.Результат КАК Сумма,
		|	Удержания.Получатель КАК Получатель,
		|	Удержания.ПлатежныйАгент КАК ПлатежныйАгент,
		|	Удержания.ДокументОснование КАК ИсполнительныйДокумент
		|ПОМЕСТИТЬ ВТУдержанияДокумента
		|ИЗ
		|	Документ.РегистрацияПрочихДоходов.Удержания КАК Удержания
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеДокумента КАК ДанныеДокумента
		|		ПО Удержания.Ссылка = ДанныеДокумента.Ссылка
		|			И Удержания.ФизическоеЛицо = ДанныеДокумента.ФизическоеЛицо
		|ГДЕ
		|	Удержания.Ссылка В
		|			(ВЫБРАТЬ
		|				ДанныеДокумента.Ссылка
		|			ИЗ
		|				ВТДанныеДокумента КАК ДанныеДокумента)";
		
	КонецЕсли;
	
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ДанныеДляПроведениеНДФЛ(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.ФизическоеЛицо,
	|	ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеДоходы) КАК КатегорияДохода,
	|	ДанныеДокумента.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	ДанныеДокумента.ПланируемаяДатаВыплаты КАК Период
	|ПОМЕСТИТЬ ВТДоходыФизическихЛиц
	|ИЗ
	|	ВТДанныеДокумента КАК ДанныеДокумента";
	
	Запрос.Выполнить();
	
	УчетНДФЛ.СоздатьВТСтавкаНДФЛПоСтавкеРезидента(МенеджерВременныхТаблиц,"ВТДоходыФизическихЛиц");
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.ФизическоеЛицо,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.КодДохода,
	|	ДанныеДокумента.КодВычета,
	|	ДанныеДокумента.ПланируемаяДатаВыплаты КАК ДатаПолученияДохода,
	|	ДанныеДокумента.Сумма КАК СуммаДохода,
	|	ДанныеДокумента.СуммаВычета КАК СуммаВычета,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|			ТОГДА ДанныеДокумента.НДФЛ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НалогПоСтавке13,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|			ТОГДА ДанныеДокумента.НДФЛ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НалогПоСтавке09,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|			ТОГДА ДанныеДокумента.НДФЛ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НалогПоСтавке35,
	|	ДанныеДокумента.НДФЛ КАК Сумма,
	|	ДанныеДокумента.МесяцНалоговогоПериода,
	|	ДанныеДокумента.Начисление,
	|	ДанныеДокумента.Подразделение,
	|	ДанныеДокумента.Подразделение КАК ПодразделениеСотрудника,
	|	ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеДоходы) КАК КатегорияДохода,
	|	ДанныеДокумента.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	СтавкаНДФЛПоСтавкеРезидента.СтавкаНДФЛ КАК Ставка
	|ИЗ
	|	ВТДанныеДокумента КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкаНДФЛПоСтавкеРезидента КАК СтавкаНДФЛПоСтавкеРезидента
	|		ПО ДанныеДокумента.ФизическоеЛицо = СтавкаНДФЛПоСтавкеРезидента.ФизическоеЛицо
	|			И ДанныеДокумента.ПланируемаяДатаВыплаты = СтавкаНДФЛПоСтавкеРезидента.Период
	|ГДЕ
	|	ДанныеДокумента.КодДохода <> ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДоходыФизическихЛиц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСтавкаНДФЛПоСтавкеРезидента";
	
	ДанныеУчетаНДФЛ = Запрос.ВыполнитьПакет()[0].Выгрузить();
	
	Возврат ДанныеУчетаНДФЛ;
	
КонецФункции

Функция ДанныеДляПроведенияНДФЛИВычетов(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента
	|ИЗ
	|	ВТДанныеДокумента КАК ДанныеДокумента";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() И Выборка.СтавкаНалогообложенияРезидента = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13 Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДокумента.ФизическоеЛицо
		|ИЗ
		|	ВТДанныеДокумента КАК ДанныеДокумента";
		
		ФизическиеЛица = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ФизическоеЛицо");
		
		ДанныеДляПроведения = УчетНДФЛ.ДанныеДокументаОНалогеПоОсновнойСтавкеИВычетах(Выборка.Ссылка, , , ФизическиеЛица);
		
	Иначе
		ДанныеДляПроведения = Неопределено;
	КонецЕсли;
	
	Возврат ДанныеДляПроведения;
	
	
КонецФункции

Функция ДанныеДляПроведенияВзносы(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.ФизическоеЛицо,
	|	ДанныеДокумента.Подразделение,
	|	ДанныеДокумента.ВидДохода,
	|	ДанныеДокумента.Начисление,
	|	ДанныеДокумента.НомерСтроки,
	|	ДанныеДокумента.ДатаНачала,
	|	ДанныеДокумента.ОблагаетсяЕНВД,
	|	ДанныеДокумента.Сумма,
	|	ДанныеДокумента.СуммаВычета КАК Скидка
	|ИЗ
	|	ВТНачисленияДокумента КАК ДанныеДокумента";
	
	Возврат Запрос.Выполнить().Выгрузить();;
	
КонецФункции

Функция СпособВыплатыПрочихДоходов() Экспорт
	Возврат Перечисления.СпособыВыплатыПрочихДоходов.ВыплатаПрочихДоходов;
КонецФункции

#КонецОбласти

#КонецЕсли