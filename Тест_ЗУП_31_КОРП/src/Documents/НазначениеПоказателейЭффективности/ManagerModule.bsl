#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс		

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДанныеДляПроведения(Ссылка)Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НазначениеПоказателейЭффективностиПодразделения.Ссылка.Период КАК Период,
		|	НазначениеПоказателейЭффективностиПодразделения.Подразделение КАК Подразделение,
		|	НазначениеПоказателейЭффективностиПоказателиПодразделений.Показатель КАК Показатель,
		|	НазначениеПоказателейЭффективностиПоказателиПодразделений.МестоВводаЗначений КАК МестоВводаЗначений,
		|	НазначениеПоказателейЭффективностиПоказателиПодразделений.Вес КАК Вес
		|ИЗ
		|	Документ.НазначениеПоказателейЭффективности.Подразделения КАК НазначениеПоказателейЭффективностиПодразделения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НазначениеПоказателейЭффективности.ПоказателиПодразделений КАК НазначениеПоказателейЭффективностиПоказателиПодразделений
		|		ПО НазначениеПоказателейЭффективностиПодразделения.Ссылка = НазначениеПоказателейЭффективностиПоказателиПодразделений.Ссылка
		|			И НазначениеПоказателейЭффективностиПодразделения.ИдентификаторСтрокиОбъектаНазначения = НазначениеПоказателейЭффективностиПоказателиПодразделений.ИдентификаторСтрокиОбъектаНазначения
		|ГДЕ
		|	НазначениеПоказателейЭффективностиПодразделения.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НазначениеПоказателейЭффективностиПозиции.Ссылка.Период КАК Период,
		|	НазначениеПоказателейЭффективностиПозиции.Позиция КАК Позиция,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.Показатель КАК Показатель,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.МестоВводаЗначений КАК МестоВводаЗначений,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.Вес КАК Вес
		|ИЗ
		|	Документ.НазначениеПоказателейЭффективности.Позиции КАК НазначениеПоказателейЭффективностиПозиции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НазначениеПоказателейЭффективности.ПоказателиПозиций КАК НазначениеПоказателейЭффективностиПоказателиПозиций
		|		ПО НазначениеПоказателейЭффективностиПозиции.Ссылка = НазначениеПоказателейЭффективностиПоказателиПозиций.Ссылка
		|			И НазначениеПоказателейЭффективностиПозиции.ИдентификаторСтрокиОбъектаНазначения = НазначениеПоказателейЭффективностиПоказателиПозиций.ИдентификаторСтрокиОбъектаНазначения
		|ГДЕ
		|	НазначениеПоказателейЭффективностиПозиции.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НазначениеПоказателейЭффективностиПодразделения.Ссылка.Период КАК Период,
		|	НазначениеПоказателейЭффективностиПоказателиПодразделений.Показатель КАК Показатель,
		|	НазначениеПоказателейЭффективностиПоказателиПодразделений.МестоВводаЗначений КАК Подразделение,
		|	НазначениеПоказателейЭффективностиПоказателиПодразделений.ОтветственныйЗаВводПлана КАК ОтветственныйЗаВводПлана,
		|	НазначениеПоказателейЭффективностиПоказателиПодразделений.ОтветственныйЗаВводФакта КАК ОтветственныйЗаВводФакта
		|ИЗ
		|	Документ.НазначениеПоказателейЭффективности.Подразделения КАК НазначениеПоказателейЭффективностиПодразделения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НазначениеПоказателейЭффективности.ПоказателиПодразделений КАК НазначениеПоказателейЭффективностиПоказателиПодразделений
		|		ПО НазначениеПоказателейЭффективностиПодразделения.Ссылка = НазначениеПоказателейЭффективностиПоказателиПодразделений.Ссылка
		|			И НазначениеПоказателейЭффективностиПодразделения.ИдентификаторСтрокиОбъектаНазначения = НазначениеПоказателейЭффективностиПоказателиПодразделений.ИдентификаторСтрокиОбъектаНазначения
		|ГДЕ
		|	НазначениеПоказателейЭффективностиПодразделения.Ссылка = &Ссылка
		|	И НазначениеПоказателейЭффективностиПоказателиПодразделений.МестоВводаЗначений ССЫЛКА Справочник.СтруктураПредприятия
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НазначениеПоказателейЭффективностиПозиции.Ссылка.Период,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.Показатель,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.МестоВводаЗначений,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.ОтветственныйЗаВводПлана,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.ОтветственныйЗаВводФакта
		|ИЗ
		|	Документ.НазначениеПоказателейЭффективности.Позиции КАК НазначениеПоказателейЭффективностиПозиции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НазначениеПоказателейЭффективности.ПоказателиПозиций КАК НазначениеПоказателейЭффективностиПоказателиПозиций
		|		ПО НазначениеПоказателейЭффективностиПозиции.Ссылка = НазначениеПоказателейЭффективностиПоказателиПозиций.Ссылка
		|			И НазначениеПоказателейЭффективностиПозиции.ИдентификаторСтрокиОбъектаНазначения = НазначениеПоказателейЭффективностиПоказателиПозиций.ИдентификаторСтрокиОбъектаНазначения
		|ГДЕ
		|	НазначениеПоказателейЭффективностиПозиции.Ссылка = &Ссылка
		|	И НазначениеПоказателейЭффективностиПоказателиПозиций.МестоВводаЗначений ССЫЛКА Справочник.СтруктураПредприятия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НазначениеПоказателейЭффективностиПозиции.Ссылка.Период КАК Период,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.Показатель КАК Показатель,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.МестоВводаЗначений КАК Позиция,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.ОтветственныйЗаВводПлана КАК ОтветственныйЗаВводПлана,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.ОтветственныйЗаВводФакта КАК ОтветственныйЗаВводФакта
		|ИЗ
		|	Документ.НазначениеПоказателейЭффективности.Позиции КАК НазначениеПоказателейЭффективностиПозиции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НазначениеПоказателейЭффективности.ПоказателиПозиций КАК НазначениеПоказателейЭффективностиПоказателиПозиций
		|		ПО НазначениеПоказателейЭффективностиПозиции.Ссылка = НазначениеПоказателейЭффективностиПоказателиПозиций.Ссылка
		|			И НазначениеПоказателейЭффективностиПозиции.ИдентификаторСтрокиОбъектаНазначения = НазначениеПоказателейЭффективностиПоказателиПозиций.ИдентификаторСтрокиОбъектаНазначения
		|ГДЕ
		|	НазначениеПоказателейЭффективностиПозиции.Ссылка = &Ссылка
		|	И НазначениеПоказателейЭффективностиПоказателиПозиций.МестоВводаЗначений ССЫЛКА Справочник.ШтатноеРасписание
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НазначениеПоказателейЭффективностиПодразделения.Ссылка.Период КАК Период,
		|	НазначениеПоказателейЭффективностиПодразделения.Подразделение КАК Подразделение,
		|	НазначениеПоказателейЭффективностиПоказателиПодразделений.Показатель КАК Показатель,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.ЗначениеДо КАК ЗначениеДо,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.ОценкаЗадаетсяИнтервалом КАК ОценкаЗадаетсяИнтервалом,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.ИнтервалОценкиОт КАК ИнтервалОценкиОт,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.ИнтервалОценкиДо КАК ИнтервалОценкиДо,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.Оценка КАК Оценка,
		|	НазначениеПоказателейЭффективностиПоказателиПодразделений.Показатель.ОбратнаяШкала КАК ОбратнаяШкала
		|ИЗ
		|	Документ.НазначениеПоказателейЭффективности.Подразделения КАК НазначениеПоказателейЭффективностиПодразделения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НазначениеПоказателейЭффективности.ПоказателиПодразделений КАК НазначениеПоказателейЭффективностиПоказателиПодразделений
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НазначениеПоказателейЭффективности.ШкалыЗначенийПодразделений КАК НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений
		|			ПО НазначениеПоказателейЭффективностиПоказателиПодразделений.Ссылка = НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.Ссылка
		|				И НазначениеПоказателейЭффективностиПоказателиПодразделений.ИдентификаторСтрокиПоказателя = НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.ИдентификаторСтрокиПоказателя
		|		ПО НазначениеПоказателейЭффективностиПодразделения.Ссылка = НазначениеПоказателейЭффективностиПоказателиПодразделений.Ссылка
		|			И НазначениеПоказателейЭффективностиПодразделения.ИдентификаторСтрокиОбъектаНазначения = НазначениеПоказателейЭффективностиПоказателиПодразделений.ИдентификаторСтрокиОбъектаНазначения
		|ГДЕ
		|	НазначениеПоказателейЭффективностиПодразделения.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Подразделение,
		|	Показатель,
		|	ЗначениеДо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НазначениеПоказателейЭффективностиПозиции.Ссылка.Период КАК Период,
		|	НазначениеПоказателейЭффективностиПозиции.Позиция КАК Позиция,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.Показатель КАК Показатель,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.ЗначениеДо КАК ЗначениеДо,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.ОценкаЗадаетсяИнтервалом КАК ОценкаЗадаетсяИнтервалом,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.ИнтервалОценкиОт КАК ИнтервалОценкиОт,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.ИнтервалОценкиДо КАК ИнтервалОценкиДо,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.Оценка КАК Оценка,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.Показатель.ОбратнаяШкала КАК ОбратнаяШкала
		|ИЗ
		|	Документ.НазначениеПоказателейЭффективности.Позиции КАК НазначениеПоказателейЭффективностиПозиции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НазначениеПоказателейЭффективности.ПоказателиПозиций КАК НазначениеПоказателейЭффективностиПоказателиПозиций
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НазначениеПоказателейЭффективности.ШкалыЗначенийПозиций КАК НазначениеПоказателейЭффективностиШкалыЗначенийПозиций
		|			ПО НазначениеПоказателейЭффективностиПоказателиПозиций.Ссылка = НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.Ссылка
		|				И НазначениеПоказателейЭффективностиПоказателиПозиций.ИдентификаторСтрокиПоказателя = НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.ИдентификаторСтрокиПоказателя
		|		ПО НазначениеПоказателейЭффективностиПозиции.Ссылка = НазначениеПоказателейЭффективностиПоказателиПозиций.Ссылка
		|			И НазначениеПоказателейЭффективностиПозиции.ИдентификаторСтрокиОбъектаНазначения = НазначениеПоказателейЭффективностиПоказателиПозиций.ИдентификаторСтрокиОбъектаНазначения
		|ГДЕ
		|	НазначениеПоказателейЭффективностиПозиции.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Позиция,
		|	Показатель,
		|	ЗначениеДо";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ДанныеДляПроведения = Новый Структура;
	
	ДанныеДляПроведения.Вставить("ПоказателиПодразделений", РезультатыЗапроса[РезультатыЗапроса.Количество() - 6].Выгрузить());
	ДанныеДляПроведения.Вставить("ПоказателиПозиций", РезультатыЗапроса[РезультатыЗапроса.Количество() - 5].Выгрузить());
	ДанныеДляПроведения.Вставить("ПодразделенияВводаЗначений", РезультатыЗапроса[РезультатыЗапроса.Количество() - 4].Выгрузить());
	ДанныеДляПроведения.Вставить("ПозицииВводаЗначений", РезультатыЗапроса[РезультатыЗапроса.Количество() - 3].Выгрузить());
	
	// Формируем вторичные признаки шкал.
	РезультатШкалыПодразделений = РезультатыЗапроса[РезультатыЗапроса.Количество() - 2];
	Выборка = РезультатШкалыПодразделений.Выбрать();
	ДанныеДляПроведения.Вставить("ШкалыПодразделений", ИтоговаяТаблицаШкалы(Выборка, "Подразделение"));
	
	РезультатШкалыПозиций = РезультатыЗапроса[РезультатыЗапроса.Количество() - 1];
	Выборка = РезультатШкалыПозиций.Выбрать();
	ДанныеДляПроведения.Вставить("ШкалыПозиций", ИтоговаяТаблицаШкалы(Выборка, "Позиция"));
	
	Возврат ДанныеДляПроведения;

КонецФункции

Функция ИтоговаяТаблицаШкалы(Выборка, ИмяВладельца)

	ИтоговаяТаблица = ТаблицаШкалы();
	ИтоговаяТаблица.Колонки.Добавить(ИмяВладельца);
	
	Пока Выборка.СледующийПоЗначениюПоля(ИмяВладельца) Цикл
		Пока Выборка.СледующийПоЗначениюПоля("Показатель") Цикл
			ТаблицаШкалы = ТаблицаШкалы();
			ТаблицаШкалы.Колонки.Добавить(ИмяВладельца);
			
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(ТаблицаШкалы.Добавить(), Выборка);
			КонецЦикла;
			КлючевыеПоказателиЭффективностиКлиентСервер.ОтсортироватьШкалуЗначений(ТаблицаШкалы, Выборка.ОбратнаяШкала);
			
			ПредыдущееЗначение = КлючевыеПоказателиЭффективностиКлиентСервер.ЗначениеПоследнегоПорогаШкалы(Не Выборка.ОбратнаяШкала);
			Для каждого СтрокаШкалы Из ТаблицаШкалы Цикл
				СтрокаШкалы.ЗначениеОт = ПредыдущееЗначение;
				ПредыдущееЗначение =  СтрокаШкалы.ЗначениеДо;
			КонецЦикла; 
			
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаШкалы, ИтоговаяТаблица);
		КонецЦикла;
	КонецЦикла;

	Возврат ИтоговаяТаблица;
	
КонецФункции

Функция ТаблицаШкалы()
	
	ТаблицаШкалы = Новый ТаблицаЗначений;
	
	ТаблицаШкалы.Колонки.Добавить("Период");
	ТаблицаШкалы.Колонки.Добавить("Показатель");
	ТаблицаШкалы.Колонки.Добавить("ЗначениеДо");
	
	ТаблицаШкалы.Колонки.Добавить("ОценкаЗадаетсяИнтервалом");
	ТаблицаШкалы.Колонки.Добавить("ИнтервалОценкиОт");
	ТаблицаШкалы.Колонки.Добавить("ИнтервалОценкиДо");
	ТаблицаШкалы.Колонки.Добавить("Оценка");
	
	ТаблицаШкалы.Колонки.Добавить("ЗначениеОт");
	
	Возврат ТаблицаШкалы;
	
КонецФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли