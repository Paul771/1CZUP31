#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ДанныеДляПроведения = Документы.НазначениеПоказателейЭффективности.ПолучитьДанныеДляПроведения(Ссылка);
	
	КлючевыеПоказателиЭффективности.СформироватьДвиженияПоказателейПодразделений(Движения, ДанныеДляПроведения.ПоказателиПодразделений, ДанныеДляПроведения.ШкалыПодразделений);
	КлючевыеПоказателиЭффективности.СформироватьДвиженияПоказателейПозиций(Движения, ДанныеДляПроведения.ПоказателиПозиций, ДанныеДляПроведения.ШкалыПозиций);
	КлючевыеПоказателиЭффективности.СформироватьДвиженияМестВводаЗначенийПоказателейЭффективности(Движения, ДанныеДляПроведения.ПодразделенияВводаЗначений, ДанныеДляПроведения.ПозицииВводаЗначений);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Не Утвержден Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Утвердил");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	
	Возврат ОбменДаннымиЗарплатаКадрыРасширенный.ОграниченияРегистрацииПоОрганизацииИПодразделению(
				ЭтотОбъект,, Подразделения.ВыгрузитьКолонку("Подразделение"), Позиции.ВыгрузитьКолонку("Позиция"));
	
КонецФункции

#КонецОбласти

#КонецЕсли
