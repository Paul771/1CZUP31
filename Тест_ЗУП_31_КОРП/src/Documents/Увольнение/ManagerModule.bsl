#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Проводит документ по учетам. Если в параметре ВидыУчетов передано Неопределено, то документ проводится по всем учетам.
// Процедура вызывается из обработки проведения и может вызываться из вне.
// 
// Параметры:
//  ДокументСсылка	- ДокументСсылка.увольнение - Ссылка на документ
//  РежимПроведения - РежимПроведенияДокумента - Режим проведения документа (оперативный, неоперативный)
//  Отказ 			- Булево - Признак отказа от выполнения проведения
//  ВидыУчетов 		- Строка - Список видов учета, по которым необходимо провести документ. Если параметр пустой или Неопределено, то документ проведется по всем учетам
//  Движения 		- Коллекция движений документа - Передается только при вызове из обработки проведения документа
//  Объект			- ДокументОбъект.Увольнение - Передается только при вызове из обработки проведения документа
//  ДополнительныеПараметры - Структура - Дополнительные параметры, необходимые для проведения документа.
//
Процедура ПровестиПоУчетам(ДокументСсылка, РежимПроведения, Отказ, ВидыУчетов = Неопределено, Движения = Неопределено, Объект = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	СтруктураВидовУчета = ПроведениеРасширенныйСервер.СтруктураВидовУчета();
	
	РеквизитыДляПроведения = РеквизитыДляПроведения(ДокументСсылка);
	ПеремещаемыеСовместители = Неопределено;
	
	ИсключаемыеРегистраторы = Новый Массив;
	ИсключаемыеРегистраторы.Добавить(РеквизитыДляПроведения.Ссылка);
	Если ЗначениеЗаполнено(РеквизитыДляПроведения.ИсправленныйДокумент) Тогда
		ИсключаемыеРегистраторы.Добавить(РеквизитыДляПроведения.ИсправленныйДокумент);
	КонецЕсли;
	
	Если Объект <> Неопределено И ВидыУчетов = Неопределено Тогда
		ЗарплатаКадрыРасширенный.ИнициализироватьОтложеннуюРегистрациюВторичныхДанныхПоДвижениямДокумента(Движения);
	КонецЕсли;
	
	ДанныеДляРегистрацииПерерасчетов = Новый МенеджерВременныхТаблиц;
	
	Если ДополнительныеПараметры <> Неопределено
		И ДополнительныеПараметры.Свойство("ПервоеПроведение")
		И ЗначениеЗаполнено(РеквизитыДляПроведения.ИсправленныйДокумент) Тогда
		
		СсылкаНаДокумент = РеквизитыДляПроведения.ИсправленныйДокумент;
		
	Иначе
		СсылкаНаДокумент = РеквизитыДляПроведения.Ссылка;
	КонецЕсли;
	
	СоздатьВТДанныеДокументов(ДанныеДляРегистрацииПерерасчетов, СсылкаНаДокумент);
	ЕстьПерерасчеты = ПерерасчетЗарплаты.СборДанныхДляРегистрацииПерерасчетов(СсылкаНаДокумент, ДанныеДляРегистрацииПерерасчетов, РеквизитыДляПроведения.Организация);
	
	ПроведениеРасширенныйСервер.ПодготовитьНаборыЗаписейКРегистрацииДвиженийПоВидамУчета(РежимПроведения, ДокументСсылка, СтруктураВидовУчета, ВидыУчетов, Движения, Объект, Отказ);
	
	ПроведениеСервер.ОтключитьПроверкуДатыЗапретаИзменения(Движения, ЗначениеЗаполнено(РеквизитыДляПроведения.ИсправленныйДокумент));
	
	ИсправлениеДокументовЗарплатаКадры.ПриПроведенииИсправления(ДокументСсылка, Движения, РежимПроведения, Отказ, РеквизитыДляПроведения, СтруктураВидовУчета, Объект);
	
	Если СтруктураВидовУчета.ОстальныеВидыУчета Тогда
		
		// Сбор сведений об увольняемых сотрудниках
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		СоздатьВТКадровыеДанныеУвольняемыхСотрудников(МенеджерВременныхТаблиц, РеквизитыДляПроведения.Ссылка);
		
		ПеремещаемыеСовместители = КадровыйУчетРасширенный.ОсновныеСотрудникиВнутреннихСовместителей(
			РеквизитыДляПроведения.Организация, ДатыУвольненияСотрудников(РеквизитыДляПроведения.Ссылка), ИсключаемыеРегистраторы);
		
		КадровыеСобытия = КадровыеСобытияУвольнениеПоВременнойТаблице(МенеджерВременныхТаблиц);
		
		КадровыйУчет.СформироватьКадровыеДвижения(РеквизитыДляПроведения.Ссылка, Движения, КадровыеСобытия, , Истина);
		
		КадровыйУчет.СформироватьДвиженияВидовЗанятостиСотрудников(Движения, ПеремещаемыеСовместители);
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьПодработки") 
			И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.Подработки") Тогда
			
			Модуль = ОбщегоНазначения.ОбщийМодуль("Подработки");
			Модуль.ПрекратитьПодработкиСотрудников(РеквизитыДляПроведения.Ссылка, Движения, КадровыеСобытия);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если РеквизитыДляПроведения.ДокументРассчитан Тогда
		
		ДанныеДляПроведения = ДанныеДляПроведения(
			РеквизитыДляПроведения, МенеджерВременныхТаблиц, РеквизитыДляПроведения.ПериодРегистрации, РеквизитыДляПроведения.Организация, ПеремещаемыеСовместители, СтруктураВидовУчета);
		
		Если СтруктураВидовУчета.ОстальныеВидыУчета Тогда
			
			// Прекращаем плановые начисления
			РасчетЗарплатыРасширенный.ПрекратитьВсеПлановыеНачисленияПоТаблицеСотрудников(
				Движения, ДанныеДляПроведения.ДанныеДляРегистрацииПрекращенияПлановыхНачислений);
			// Прекращаем плановые удержания
			РасчетЗарплатыРасширенный.ПрекратитьВсеПлановыеУдержанияПоТаблицеСотрудников(
				Движения, ДанныеДляПроведения.ДанныеДляРегистрацииПрекращенияПлановыхУдержаний);
			
			// Учет зарплаты
			Для Каждого СтрокаПериодовРегистрации Из РеквизитыДляПроведения.ПериодыРегистрации Цикл
				
				ПериодРегистрации = СтрокаПериодовРегистрации.ПериодРегистрации;
				ЭтоПервыйПериод = СтрокаПериодовРегистрации.ЭтоПервыйПериод;
				
				Если РеквизитыДляПроведения.ПериодыРегистрации.Количество() = 1 Тогда
					
					Начисления = ДанныеДляПроведения.Начисления;
					ПоказателиНачислений = ДанныеДляПроведения.ПоказателиНачислений;
					Удержания = ДанныеДляПроведения.Удержания;
					ПоказателиУдержаний = ДанныеДляПроведения.ПоказателиУдержаний;
					
				Иначе
					
					Начисления = РасчетЗарплатыРасширенный.СтрокиТаблицыДокументаПоПериодуРегистрации(ДанныеДляПроведения.Начисления, ПериодРегистрации, ЭтоПервыйПериод, СтрокаПериодовРегистрации.Сотрудники);
					ПоказателиНачислений = РасчетЗарплатыРасширенный.СтрокиПоказателейНачисленийУдержаний(ДанныеДляПроведения.ПоказателиНачислений, Начисления);
					Удержания = РасчетЗарплатыРасширенный.СтрокиТаблицыДокументаПоПериодуРегистрации(ДанныеДляПроведения.Удержания, ПериодРегистрации, ЭтоПервыйПериод, СтрокаПериодовРегистрации.ФизическиеЛица, , "ФизическоеЛицо");
					ПоказателиУдержаний = РасчетЗарплатыРасширенный.СтрокиПоказателейНачисленийУдержаний(ДанныеДляПроведения.ПоказателиУдержаний, Удержания);
					
				КонецЕсли;
				
				РасчетЗарплатыРасширенный.СформироватьДвиженияНачислений(
					Движения, Отказ, РеквизитыДляПроведения.Организация, КонецМесяца(ПериодРегистрации), Начисления, ПоказателиНачислений, Истина);
				
				РасчетЗарплатыРасширенный.СформироватьДвиженияУдержаний(
					Движения, Отказ, РеквизитыДляПроведения.Организация, КонецМесяца(ПериодРегистрации), Удержания, ПоказателиУдержаний);
				
			КонецЦикла;
			
			РасчетЗарплатыРасширенный.СформироватьДвиженияРаспределенияПоТерриториямУсловиямТруда(
				Движения, Отказ, РеквизитыДляПроведения.Ссылка, РеквизитыДляПроведения.РаспределениеПоТерриториямУсловиямТруда);
				
			ИсполнительныеЛисты.СформироватьУдержанияПоИсполнительнымДокументам(
				Движения, ДанныеДляПроведения.УдержанияПоИсполнительнымДокументам);
			
			РасчетЗарплатыРасширенный.СформироватьЗадолженностьПоУдержаниямФизическихЛиц(
				Движения, ДанныеДляПроведения.ЗадолженностьПоУдержаниям);
			
			Для Каждого СтрокаПериодовРегистрации Из РеквизитыДляПроведения.ПериодыРегистрации Цикл
				
				ПериодРегистрации = СтрокаПериодовРегистрации.ПериодРегистрации;
				ЭтоПервыйПериод = СтрокаПериодовРегистрации.ЭтоПервыйПериод;
				
				Если РеквизитыДляПроведения.ПериодыРегистрации.Количество() = 1 Тогда
					УдержанияДоПределаПоСотрудникам = ДанныеДляПроведения.УдержанияДоПределаПоСотрудникам;
				Иначе
					УдержанияДоПределаПоСотрудникам = РасчетЗарплатыРасширенный.СтрокиТаблицыДокументаПоПериодуРегистрации(ДанныеДляПроведения.УдержанияДоПределаПоСотрудникам, ПериодРегистрации, ЭтоПервыйПериод, СтрокаПериодовРегистрации.ФизическиеЛица, , "ФизическоеЛицо");
				КонецЕсли;
				
				РасчетЗарплатыРасширенный.СформироватьДвиженияУдержанийДоПределаПоСотрудникам(
					Движения, Отказ, ПериодРегистрации, УдержанияДоПределаПоСотрудникам);
				
				Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
					
					Если ПолучитьФункциональнуюОпцию("ИспользоватьУправленческуюЗарплату") Тогда
						
						УправленческийУчет = ДанныеДляПроведения.УправленческийУчет;
						
						Если РеквизитыДляПроведения.ПериодыРегистрации.Количество() = 1 Тогда
							
							Начисления = УправленческийУчет.Начисления;
							ПоказателиНачислений = УправленческийУчет.ПоказателиНачислений;
							
						Иначе
							
							Начисления = РасчетЗарплатыРасширенный.СтрокиТаблицыДокументаПоПериодуРегистрации(УправленческийУчет.Начисления, ПериодРегистрации, ЭтоПервыйПериод, СтрокаПериодовРегистрации.Сотрудники);
							ПоказателиНачислений = РасчетЗарплатыРасширенный.СтрокиПоказателейНачисленийУдержаний(УправленческийУчет.ПоказателиНачислений, Начисления);
							
						КонецЕсли;
						
						Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплата");
						Модуль.СформироватьДвиженияНачисленийПериодаРегистрации(Движения, Отказ, Начисления, ПоказателиНачислений, ПериодРегистрации);
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
			ПроверитьПересечениеФактическогоПериодаДействия(ДокументСсылка, Отказ);
			
#Область РегистрацияДоходовВУчетеНДФЛ

			Если РеквизитыДляПроведения.ПериодыРегистрации.Количество() > 1 Тогда
				
				Запрос = Новый Запрос;
				Запрос.МенеджерВременныхТаблиц = ДанныеДляПроведения.МенеджерВременныхТаблиц;
				
				Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
				
				Запрос.Текст =
					"ВЫБРАТЬ
					|	*
					|ПОМЕСТИТЬ ВТВсеНачисленияДокумента
					|ИЗ
					|	ВТНачисления КАК ВсеНачисленияДокумента
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ ВТНачисления";
				
				Запрос.Выполнить();
				
			КонецЕсли;
			
			Для Каждого СтрокаПериодовРегистрации Из РеквизитыДляПроведения.ПериодыРегистрации Цикл
				
				ПериодРегистрации = СтрокаПериодовРегистрации.ПериодРегистрации;
				
				Если РеквизитыДляПроведения.ПериодыРегистрации.Количество() = 1 Тогда
					
					НачисленияПоСотрудникам = ДанныеДляПроведения.НачисленияПоСотрудникам;
					УдержанияПоСотрудникам = ДанныеДляПроведения.УдержанияПоСотрудникам;
					УдержанияЗаймов = ДанныеДляПроведения.УдержанияЗаймов;
					МатериальнаяВыгода = ДанныеДляПроведения.МатериальнаяВыгода;
					ОтработанноеВремяПоСотрудникам = ДанныеДляПроведения.ОтработанноеВремяПоСотрудникам;
					НДФЛ = ДанныеДляПроведения.НДФЛ;
					НДФЛПоСотрудникам = ДанныеДляПроведения.НДФЛПоСотрудникам;
					СтраховыеВзносы = ДанныеДляПроведения.СтраховыеВзносы;
					ВзаиморасчетыПоЗаймам = ДанныеДляПроведения.ВзаиморасчетыПоЗаймам;
					НалогНаМатериальнуюВыгоду = ДанныеДляПроведения.НалогНаМатериальнуюВыгоду;
					Пособия = ДанныеДляПроведения.Пособия;
					ПособияПоУходу = ДанныеДляПроведения.ПособияПоУходу;
					Удержания = ДанныеДляПроведения.Удержания;
					
				Иначе
					
					Запрос = Новый Запрос;
					Запрос.МенеджерВременныхТаблиц = ДанныеДляПроведения.МенеджерВременныхТаблиц;
					
					Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
					
					Запрос.Текст = 
						"ВЫБРАТЬ * Поместить ВТНачисления ИЗ ВТВсеНачисленияДокумента КАК Т";
					
					Если СтрокаПериодовРегистрации.ЭтоПервыйПериод Тогда
						Запрос.Текст = Запрос.Текст + Символы.ПС
							+ "ГДЕ Т.ПериодДействия <= &ПериодРегистрации"
					Иначе
						Запрос.Текст = Запрос.Текст + Символы.ПС
							+ "ГДЕ Т.ПериодДействия = &ПериодРегистрации"
					КонецЕсли;
					
					Запрос.Выполнить();
					
					НачисленияПоСотрудникам = РасчетЗарплатыРасширенный.СтрокиТаблицыДокументаПоПериодуРегистрации(ДанныеДляПроведения.НачисленияПоСотрудникам, ПериодРегистрации, ЭтоПервыйПериод, СтрокаПериодовРегистрации.Сотрудники);
					УдержанияПоСотрудникам = РасчетЗарплатыРасширенный.СтрокиТаблицыДокументаПоПериодуРегистрации(ДанныеДляПроведения.УдержанияПоСотрудникам, ПериодРегистрации, ЭтоПервыйПериод, СтрокаПериодовРегистрации.ФизическиеЛица, , "ФизическоеЛицо");
					УдержанияЗаймов = РасчетЗарплатыРасширенный.СтрокиТаблицыДокументаПоПериодуРегистрации(ДанныеДляПроведения.УдержанияЗаймов, ПериодРегистрации, ЭтоПервыйПериод, СтрокаПериодовРегистрации.Сотрудники, "НачалоМесяцаДатаОкончания");
					МатериальнаяВыгода = РасчетЗарплатыРасширенный.СтрокиТаблицыДокументаПоПериодуРегистрации(ДанныеДляПроведения.МатериальнаяВыгода, ПериодРегистрации, ЭтоПервыйПериод, СтрокаПериодовРегистрации.ФизическиеЛица, "НачалоМесяцаДатаПолученияДохода", "ФизическоеЛицо");
					ОтработанноеВремяПоСотрудникам = РасчетЗарплатыРасширенный.СтрокиТаблицыДокументаПоПериодуРегистрации(ДанныеДляПроведения.ОтработанноеВремяПоСотрудникам, ПериодРегистрации, ЭтоПервыйПериод, СтрокаПериодовРегистрации.Сотрудники);
					НДФЛ = РасчетЗарплатыРасширенный.СтрокиТаблицыДокументаПоПериодуРегистрации(ДанныеДляПроведения.НДФЛ, ПериодРегистрации, ЭтоПервыйПериод, СтрокаПериодовРегистрации.ФизическиеЛица, "НачалоМесяцаМесяцНалоговогоПериода", "ФизическоеЛицо");
					НДФЛПоСотрудникам = РасчетЗарплатыРасширенный.СтрокиТаблицыДокументаПоПериодуРегистрации(ДанныеДляПроведения.НДФЛПоСотрудникам, ПериодРегистрации, ЭтоПервыйПериод, СтрокаПериодовРегистрации.ФизическиеЛица, "НачалоМесяцаМесяцНалоговогоПериода", "ФизическоеЛицо");
					СтраховыеВзносы = РасчетЗарплатыРасширенный.СтрокиТаблицыДокументаПоПериодуРегистрации(ДанныеДляПроведения.СтраховыеВзносы, ПериодРегистрации, ЭтоПервыйПериод, СтрокаПериодовРегистрации.ФизическиеЛица, "ДатаПолученияДохода", "ФизическоеЛицо");
					ВзаиморасчетыПоЗаймам = РасчетЗарплатыРасширенный.СтрокиТаблицыДокументаПоПериодуРегистрации(ДанныеДляПроведения.ВзаиморасчетыПоЗаймам, ПериодРегистрации, ЭтоПервыйПериод, СтрокаПериодовРегистрации.ФизическиеЛица, "НачалоМесяцаДатаПогашения", "ФизическоеЛицо");
					НалогНаМатериальнуюВыгоду = РасчетЗарплатыРасширенный.СтрокиТаблицыДокументаПоПериодуРегистрации(ДанныеДляПроведения.НалогНаМатериальнуюВыгоду, ПериодРегистрации, ЭтоПервыйПериод, СтрокаПериодовРегистрации.Сотрудники, "МесяцНалоговогоПериода");
					Пособия = РасчетЗарплатыРасширенный.СтрокиТаблицыДокументаПоПериодуРегистрации(ДанныеДляПроведения.Пособия, ПериодРегистрации, ЭтоПервыйПериод, СтрокаПериодовРегистрации.Сотрудники);
					ПособияПоУходу = РасчетЗарплатыРасширенный.СтрокиТаблицыДокументаПоПериодуРегистрации(ДанныеДляПроведения.ПособияПоУходу, ПериодРегистрации, ЭтоПервыйПериод, СтрокаПериодовРегистрации.Сотрудники);
					Удержания = РасчетЗарплатыРасширенный.СтрокиТаблицыДокументаПоПериодуРегистрации(ДанныеДляПроведения.Удержания, ПериодРегистрации, ЭтоПервыйПериод, СтрокаПериодовРегистрации.ФизическиеЛица, , "ФизическоеЛицо");
					
				КонецЕсли;
				
				// - Регистрация бухучета начислений и удержаний, выполняется до вызова регистрации доходов в учете НДФЛ.
				ОтражениеЗарплатыВБухучетеРасширенный.СформироватьДвиженияБухучетНачисленияУдержанияПоСотрудникам(
							Движения, Отказ, РеквизитыДляПроведения.Организация, ПериодРегистрации,
							НачисленияПоСотрудникам, УдержанияПоСотрудникам, Неопределено);
				
				// - Регистрация бухучета займов.
				ОтражениеЗарплатыВБухучетеРасширенный.СформироватьДвиженияБухучетНачисленияУдержанияПоСотрудникам(
							Движения, Отказ, РеквизитыДляПроведения.Организация, ПериодРегистрации,
							Неопределено, УдержанияЗаймов, Неопределено);
				
				// НДФЛ
				ДатаОперацииПоНалогам = УчетНДФЛРасширенный.ДатаОперацииПоДокументу(РеквизитыДляПроведения.Дата, ПериодРегистрации);
				
				// - Регистрация материальной выгоды в учете НДФЛ.
				УчетНДФЛ.СформироватьДоходыНДФЛПоКодамДоходовИзТаблицыЗначений(
					Движения, Отказ, РеквизитыДляПроведения.Организация, ДатаОперацииПоНалогам, МатериальнаяВыгода, Ложь, , ДокументСсылка);
				
				ДанныеДляПроведенияПроведенияНДФЛ = Новый Структура;
				ДанныеДляПроведенияПроведенияНДФЛ.Вставить("НДФЛ", НДФЛ);
				ДанныеДляПроведенияПроведенияНДФЛ.Вставить("Удержания", Удержания);
				ДанныеДляПроведенияПроведенияНДФЛ.Вставить("НДФЛПоСотрудникам", НДФЛПоСотрудникам);
				ДанныеДляПроведенияПроведенияНДФЛ.Вставить("МенеджерВременныхТаблиц", ДанныеДляПроведения.МенеджерВременныхТаблиц);
				
				ЗарегистрироватьДоходыИСуммыНДФЛПоВременнойТаблицеНачислений(РеквизитыДляПроведения, Движения, ДанныеДляПроведенияПроведенияНДФЛ,
					ДатаОперацииПоНалогам, Отказ, ПериодРегистрации);
				
#КонецОбласти
				
				РасчетЗарплатыРасширенный.СформироватьДвиженияКорректировкиВыплатыПоВременнойТаблицеНачислений(
					Движения, Отказ, РеквизитыДляПроведения.Организация, ПериодРегистрации, РеквизитыДляПроведения.ПорядокВыплаты, ДанныеДляПроведения, Истина, Истина);
				
				// - Регистрация начислений и удержаний в учете начислений и удержаний.
				УчетНачисленнойЗарплаты.ЗарегистрироватьНачисленияУдержания(
					Движения, Отказ, РеквизитыДляПроведения.Организация, ПериодРегистрации, НачисленияПоСотрудникам,
					УдержанияПоСотрудникам, Неопределено, Неопределено, РеквизитыДляПроведения.ПорядокВыплаты);
				
				// - Регистрация отработанного времени в учете начислений и удержаний.
				УчетНачисленнойЗарплаты.ЗарегистрироватьОтработанноеВремя(
					Движения, Отказ, РеквизитыДляПроведения.Организация, ПериодРегистрации, ОтработанноеВремяПоСотрудникам, РеквизитыДляПроведения.ПорядокВыплаты, Истина);
				
				Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
					
					Если ПолучитьФункциональнуюОпцию("ИспользоватьУправленческуюЗарплату") Тогда
						
						Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплата");
						
						УправленческийУчет = ДанныеДляПроведения.УправленческийУчет;
						
						НачисленияПоСотрудникам = РасчетЗарплатыРасширенный.СтрокиТаблицыДокументаПоПериодуРегистрации(УправленческийУчет.НачисленияПоСотрудникам, ПериодРегистрации, ЭтоПервыйПериод, СтрокаПериодовРегистрации.Сотрудники);
						Модуль.ЗарегистрироватьНачисленияУдержанияПериодаРегистрации(Движения, Отказ, ПериодРегистрации, НачисленияПоСотрудникам, РеквизитыДляПроведения.ПорядокВыплаты);
						
						ОтработанноеВремяПоСотрудникам = РасчетЗарплатыРасширенный.СтрокиТаблицыДокументаПоПериодуРегистрации(УправленческийУчет.ОтработанноеВремяПоСотрудникам, ПериодРегистрации, ЭтоПервыйПериод, СтрокаПериодовРегистрации.Сотрудники);
						Модуль.ЗарегистрироватьОтработанноеВремяПериодаРегистрации(Движения, Отказ, ПериодРегистрации, ОтработанноеВремяПоСотрудникам, РеквизитыДляПроведения.ПорядокВыплаты);
						
					КонецЕсли;
					
				КонецЕсли;
			
				// - Регистрация бухучета НДФЛ.
				ОтражениеЗарплатыВБухучетеРасширенный.СформироватьДвиженияБухучетНачисленияУдержанияПоСотрудникам(
							Движения, Отказ, РеквизитыДляПроведения.Организация, ПериодРегистрации,
							Неопределено, Неопределено, ДанныеДляПроведенияПроведенияНДФЛ.НДФЛПоСотрудникам);
				
				// - Регистрация начислений в доходах для страховых взносов.
				УчетСтраховыхВзносов.СформироватьСведенияОДоходахСтраховыеВзносы(
					Движения, Отказ, РеквизитыДляПроведения.Организация, ПериодРегистрации, ДанныеДляПроведения.МенеджерВременныхТаблиц, Ложь, Истина, РеквизитыДляПроведения.Ссылка);
				
				УчетСтраховыхВзносов.СформироватьИсчисленныеВзносы(
					Движения, Отказ, РеквизитыДляПроведения.Организация, ПериодРегистрации, СтраховыеВзносы);
				УчетСтраховыхВзносов.СформироватьСтраховыеВзносыПоФизическимЛицам(
					Движения, Отказ, РеквизитыДляПроведения.Организация, ПериодРегистрации, РеквизитыДляПроведения.Ссылка, СтраховыеВзносы);
				
				// Займы
				// - взаиморасчеты по займам
				ЗаймыСотрудникам.ЗарегистрироватьВзаиморасчетыПоЗаймам(Движения, ВзаиморасчетыПоЗаймам, Отказ);
				// - Регистрация займов в учете заработной платы.
				УчетНачисленнойЗарплатыРасширенный.ЗарегистрироватьПогашениеЗаймов(
					Движения, Отказ, РеквизитыДляПроведения.Организация, ПериодРегистрации, УдержанияЗаймов, РеквизитыДляПроведения.ПорядокВыплаты);
				
				УчетНДФЛ.СформироватьНалогиВычеты(
					Движения, Отказ, РеквизитыДляПроведения.Организация, ДатаОперацииПоНалогам, НалогНаМатериальнуюВыгоду);
				УчетНачисленнойЗарплаты.ПодготовитьДанныеНДФЛКРегистрации(НалогНаМатериальнуюВыгоду, РеквизитыДляПроведения.Организация, ДатаОперацииПоНалогам);
				УчетНачисленнойЗарплаты.ЗарегистрироватьНДФЛ(
					Движения, Отказ, РеквизитыДляПроведения.Организация, ПериодРегистрации, НалогНаМатериальнуюВыгоду, ДанныеДляПроведения.МенеджерВременныхТаблиц, РеквизитыДляПроведения.ПорядокВыплаты);
				
				УчетСтраховыхВзносов.СформироватьПособия(
					Движения, Отказ, РеквизитыДляПроведения.Организация, ПериодРегистрации, Пособия, ПособияПоУходу);
				
				// - Регистрация бухучета займов.
				ОтражениеЗарплатыВБухучетеРасширенный.СформироватьДвиженияБухучетНачисленияУдержанияПоСотрудникам(
					Движения, Отказ, РеквизитыДляПроведения.Организация, ПериодРегистрации,
					Неопределено, Неопределено, НалогНаМатериальнуюВыгоду);
				
				Если РеквизитыДляПроведения.ПериодыРегистрации.Количество() > 1 Тогда
					ЗарплатаКадры.УничтожитьВТ(ДанныеДляПроведения.МенеджерВременныхТаблиц, "ВТНачисления");
				КонецЕсли;
				
			КонецЦикла;
			
			ПараметрыДвиженийОтпусков = ОстаткиОтпусков.ПараметрыДляСформироватьДвиженияФактическихОтпусков();
			ПараметрыДвиженийОтпусков.ДатаРегистрации = РеквизитыДляПроведения.Дата;
			ПараметрыДвиженийОтпусков.Начисления = ДанныеДляПроведения.Начисления;
			ПараметрыДвиженийОтпусков.РабочиеПериоды = ДанныеДляПроведения.РабочиеПериодыКомпенсацийОтпусков;
			ПараметрыДвиженийОтпусков.Основания = ДанныеДляПроведения.ОснованияКомпенсацийОтпусков;
			ПараметрыДвиженийОтпусков.ПериодНачисления = РеквизитыДляПроведения.ПериодРегистрации;
			ПараметрыДвиженийОтпусков.ДатыНачалаКомпенсаций = ДанныеДляПроведения.ДатыНачалаКомпенсаций;
			ОстаткиОтпусков.СформироватьДвиженияФактическихОтпусков(Движения, Отказ, ПараметрыДвиженийОтпусков);
			
			ПараметрыДвиженийОтпусков = ОстаткиОтпусков.ПараметрыДляСформироватьДвиженияФактическихОтпусков();
			ПараметрыДвиженийОтпусков.Начисления = ДанныеДляПроведения.УдержанияОтпусков;
			ПараметрыДвиженийОтпусков.РабочиеПериоды = ДанныеДляПроведения.РабочиеПериодыУдержанийОтпусков;
			ПараметрыДвиженийОтпусков.Основания = ДанныеДляПроведения.ОснованияУдержанийОтпусков;
			ПараметрыДвиженийОтпусков.ПериодНачисления = РеквизитыДляПроведения.ПериодРегистрации;
			ОстаткиОтпусков.СформироватьДвиженияФактическихОтпусковПоУдержаниям(Движения, ПараметрыДвиженийОтпусков);
			
			// Прекращение вычетов
			Для каждого МесяцУвольнения Из ДанныеДляПроведения.ДанныеДляПрекращенияВычетов Цикл
				
				КадровыйУчет.СформироватьПрекращениеВычетовСтандартныхИНаДетей(
					Движения, МесяцУвольнения.Значение, РеквизитыДляПроведения.Организация, КонецМесяца(МесяцУвольнения.Ключ) + 1, Отказ, ИсключаемыеРегистраторы);
				
			КонецЦикла;
			
			КадровыйУчетРасширенный.ЗарегистрироватьВРеестреОтпусков(Движения, ДанныеДляПроведения.ДанныеРеестраОтпусков, Отказ);
			
			Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба.РасчетДенежногоСодержания") Тогда
				Модуль = ОбщегоНазначения.ОбщийМодуль("РасчетДенежногоСодержания");
				Модуль.ЗарегистрироватьКорректировкиДляРасчетаСохраняемогоДенежногоСодержанияСотрудников(ДанныеДляПроведения.КорректировкиДляРегистрацииДенежногоСодержания);
				
				Модуль.ЗарегистрироватьНачисленияСоставМесячногоДенежногоСодержания(Движения, Отказ, РеквизитыДляПроведения.ПериодРегистрации, ДанныеДляПроведения.СоставМесячногоДенежногоСодержания);				
			КонецЕсли;
			КадровыйУчетРасширенный.ЗарегистрироватьВРеестреКадровыхПриказов(Движения, ДанныеДляПроведения.ДанныеРеестраКадровыхПриказов, Отказ);
			
		КонецЕсли;
		
		Если СтруктураВидовУчета.ДанныеДляРасчетаСреднего Тогда
			
			// Учет среднего заработка
			УчетСреднегоЗаработка.ЗарегистрироватьДанныеСреднегоЗаработка(
				Движения, Отказ, ДанныеДляПроведения.НачисленияДляСреднегоЗаработка);
			
		КонецЕсли;
		
		// Удаление сведений о необходимости перерасчетов
		
		// Получение признака о том, что нужно удалить перерасчеты текущего периода
		УдалитьПерерасчетыТекущегоПериода = Неопределено;
		Если ДополнительныеПараметры <> Неопределено Тогда
			ДополнительныеПараметры.Свойство("УдалитьПерерасчетыТекущегоПериода", УдалитьПерерасчетыТекущегоПериода);
		КонецЕсли;
		УдалитьПерерасчетыТекущегоПериода = (УдалитьПерерасчетыТекущегоПериода = Истина);
		
		ПериодыРасчетаСотрудников =  Неопределено;
		Если ДополнительныеПараметры <> Неопределено Тогда
			ДополнительныеПараметры.Свойство("ПериодыРасчетаСотрудников", ПериодыРасчетаСотрудников);
		КонецЕсли;
		
		ПерерасчетЗарплаты.УдалениеПерерасчетов(РеквизитыДляПроведения.Ссылка, УдалитьПерерасчетыТекущегоПериода, ПериодыРасчетаСотрудников);
	
	Иначе
		
		ДанныеДляПроведения = Новый Структура;
		Если СтруктураВидовУчета.ОстальныеВидыУчета Тогда
			ДобавитьДанныеДляРегистрацииСостоянийСотрудников(ДанныеДляПроведения, РеквизитыДляПроведения);
			ДобавитьДанныеДляРегистрацииДокумента(ДанныеДляПроведения, РеквизитыДляПроведения, ПеремещаемыеСовместители, РеквизитыДляПроведения.Организация);
		КонецЕсли;
		
	КонецЕсли;
	
	Если СтруктураВидовУчета.ОстальныеВидыУчета Тогда
		
		СостоянияСотрудников.ЗарегистрироватьСостоянияСотрудников(
			Движения, РеквизитыДляПроведения.Ссылка, ДанныеДляПроведения.ДанныеДляРегистрацииСостоянийСотрудников);
		
		УчетСтажаПФР.ЗарегистрироватьПериодыВУчетеСтажаПФР(
			Движения, ДанныеДляПроведения.ДанныеДляРегистрацииВУчетаСтажаПФР);
		
		// Записываем показатели суммированного учета (в случае необходимости).
		ЗаписатьЗначенияПоказателейРасчетаЗарплаты(Движения, ДанныеДляПоказателейСверхурочных(РеквизитыДляПроведения.Ссылка));
		
		// Регистрация перерасчетов
		Если ЕстьПерерасчеты Тогда
			ПерерасчетЗарплаты.РегистрацияПерерасчетов(Движения, ДанныеДляРегистрацииПерерасчетов, РеквизитыДляПроведения.Организация);
		КонецЕсли;
		
		УчетСреднегоЗаработка.УдалитьПричиныПерерасчетов(РеквизитыДляПроведения.Ссылка, ДополнительныеПараметры);
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КадровыйРезерв") Тогда 
			Модуль = ОбщегоНазначения.ОбщийМодуль("КадровыйРезерв");
			Модуль.СформироватьДвиженияИсторииКадровогоРезерва(Движения, ДанныеДляПроведения, "ДанныеКадровогоРезерва");
		КонецЕсли;
		
		ПерерасчетЗарплаты.УдалениеПерерасчетовПоДополнительнымПараметрам(РеквизитыДляПроведения.Ссылка, ДополнительныеПараметры);
		
	КонецЕсли;
	
	ПроведениеРасширенныйСервер.ЗаписьДвиженийПоУчетам(Движения, СтруктураВидовУчета);
	
КонецПроцедуры

// Сторнирует документ по учетам. Используется подсистемой исправления документов.
//
// Параметры:
//  Движения				 - КоллекцияДвижений, Структура	 - Коллекция движений исправляющего документа в которую будут добавлены сторно стоки.
//  Регистратор				 - ДокументСсылка				 - Документ регистратор исправления (документ исправление).
//  ИсправленныйДокумент	 - ДокументСсылка				 - Исправленный документ движения которого будут сторнированы.
//  СтруктураВидовУчета		 - Структура					 - Виды учета, по которым будет выполнено сторнирование исправленного документа.
//  					Состав полей см. в ПроведениеРасширенныйСервер.СтруктураВидовУчета().
//  ДополнительныеПараметры	 - Структура					 - Структура со свойствами:
//  					* ИсправлениеВТекущемПериоде - Булево - Истина когда исправление выполняется в периоде регистрации исправленного документа.
//						* ОтменаДокумента - Булево - Истина когда исправление вызвано документом СторнированиеНачислений.
//  					* ПериодРегистрации	- Дата - Период регистрации документа регистратора исправления.
// 
// Возвращаемое значение:
//  Булево - "Истина" если сторнирование выполнено этой функцией, "Ложь" если специальной процедуры не предусмотрено.
//
Функция СторнироватьПоУчетам(Движения, Регистратор, ИсправленныйДокумент, СтруктураВидовУчета, ДополнительныеПараметры) Экспорт
	
	Если СтруктураВидовУчета.ОстальныеВидыУчета Тогда
		УправлениеШтатнымРасписанием.СторнироватьДвиженияДокумента(Движения, ИсправленныйДокумент);
	КонецЕсли;
	
	Если ДополнительныеПараметры.ОтменаДокумента Тогда
		ИсправлениеДокументовЗарплатаКадры.СторнироватьДвиженияПоВсемУчетам(Движения, ИсправленныйДокумент, Ложь, СтруктураВидовУчета);
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаДокумента.
Функция ОписаниеСоставаДокумента() Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.Увольнение;
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаДокументаПоМетаданнымФизическоеЛицоВШапке(МетаданныеДокумента);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Приказ об увольнении
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "ЗарплатаКадрыКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьКадровыхПриказов";
	КомандаПечати.Идентификатор = "ПФ_MXL_Т8";
	КомандаПечати.Представление = НСтр("ru = 'Приказ об увольнении (Т-8)'");
	КомандаПечати.Порядок = 10;
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	Если Пользователи.РолиДоступны("ДобавлениеИзменениеНачисленнойЗарплатыРасширенная,ЧтениеНачисленнойЗарплатыРасширенная", , Ложь) 
		И ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная") Тогда
		
		// Записка-расчет при увольнении.
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Обработчик = "ЗарплатаКадрыКлиент.ВыполнитьКомандуПечати";
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьКадровыхПриказовРасширенная";
		КомандаПечати.Идентификатор = "ПФ_MXL_Т61";
		КомандаПечати.Представление = НСтр("ru = 'Записка-расчет при увольнении (Т-61)'");
		КомандаПечати.Порядок = 20;
		КомандаПечати.ФункциональныеОпции = "РаботаВХозрасчетнойОрганизации";
		КомандаПечати.ДополнительныеПараметры.Вставить("ТребуетсяЧтениеБезОграничений", Истина);
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		
	КонецЕсли;
	
	// Расчет среднего заработка
	УчетСреднегоЗаработка.ДобавитьКомандуПечатиРасчетаСреднегоЗаработка(КомандыПечати, "Документ.Увольнение");
	УчетСреднегоЗаработка.ДобавитьКомандуПечатиРасчетаСреднегоЗаработкаВыходногоПособия(КомандыПечати, "Документ.Увольнение");
	УчетСреднегоЗаработка.ДобавитьКомандуПечатиРасчетаСреднегоЗаработка0504425(КомандыПечати, "Документ.Увольнение");
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		МодульГосударственнаяСлужба = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
		МодульГосударственнаяСлужба.ДобавитьКомандыПечатиСохраняемогоДенежногоСодержания(КомандыПечати);
	КонецЕсли;
	
	// Подробный расчет начислений.
	РасчетЗарплатыРасширенный.ДобавитьКомандуПечатиПодробногоРасчетаНачислений(КомандыПечати);
	
	КадровыйУчетФормы.ДобавитьКомандуПечатиСправкиОСреднемЗаработкеДляПособияПоБезработице(КомандыПечати);
	
КонецПроцедуры

Функция ДобавитьКомандыСозданияДокументов(КомандыСозданияДокументов, ДополнительныеПараметры) Экспорт
	
	Если КадровыйУчетРасширенный.ПравоИнтерактивногоСозданияКадровыхПриказовСотрудника() Тогда
		
		ЗарплатаКадрыРасширенный.ДобавитьВКоллекциюКомандуСозданияДокументаПоМетаданнымДокумента(
			КомандыСозданияДокументов, Метаданные.Документы.Увольнение);
		
	КонецЕсли; 
	
КонецФункции

// Сформировать печатные формы объектов.
//
// ВХОДЯЩИЕ:
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы.
//   ОшибкиПечати          - Список значений  - Ошибки печати  (значение - ссылка на объект, представление - текст
//                           ошибки).
//   ОбъектыПечати         - Список значений  - Объекты печати (значение - ссылка на объект, представление - имя
//                           области в которой был выведен объект).
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РасчетСреднегоЗаработка") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		ДанныеДокументов = ДанныеДокументовДляПечатиРасчетаСреднегоЗаработка(МассивОбъектов);
		ТабличныйДокумент = Обработки.ПечатьРасчетаСреднегоЗаработка.ТабличныйДокументРасчетаСреднегоЗаработка(ДанныеДокументов, ОбъектыПечати, "РасчетСреднегоЗаработка");
		Если НЕ ТабличныйДокумент = Неопределено Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "РасчетСреднегоЗаработка", НСтр("ru = 'Расчет среднего заработка'"), ТабличныйДокумент);
		КонецЕсли;
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РасчетСреднегоЗаработкаВыходногоПособия") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		ДанныеДокументов = ДанныеДокументовДляПечатиРасчетаСреднегоЗаработка(МассивОбъектов, "РасчетСреднегоЗаработкаВыходногоПособия");
		ТабличныйДокумент = Обработки.ПечатьРасчетаСреднегоЗаработка.ТабличныйДокументРасчетаСреднегоЗаработка(ДанныеДокументов, ОбъектыПечати, "РасчетСреднегоЗаработкаВыходногоПособия");
		Если НЕ ТабличныйДокумент = Неопределено Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "РасчетСреднегоЗаработкаВыходногоПособия", НСтр("ru = 'Расчет среднего заработка (для выходного пособия)'"), ТабличныйДокумент);
		КонецЕсли;
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РасчетСреднегоЗаработкаФорма0504425") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		ДанныеДокументов = ДанныеДокументовДляПечатиРасчетаСреднегоЗаработка0504425(МассивОбъектов);
		ТабличныйДокумент = Обработки.ПечатьРасчетаСреднегоЗаработка.ТабличныйДокументРасчетаСреднегоЗаработка0504425(ДанныеДокументов, 
		ОбъектыПечати,
		"ПФ_MXL_ЗапискаРасчетФорма0504425");
		Если НЕ ТабличныйДокумент = Неопределено Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, 
			"РасчетСреднегоЗаработкаФорма0504425", 
			НСтр("ru = 'Записка-расчет (0504425)'"), 
			ТабличныйДокумент);
		КонецЕсли;
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РасчетСреднегоЗаработкаФорма0504425с2015") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		ДанныеДокументов = ДанныеДокументовДляПечатиРасчетаСреднегоЗаработка0504425(МассивОбъектов);
		ТабличныйДокумент = Обработки.ПечатьРасчетаСреднегоЗаработка.ТабличныйДокументРасчетаСреднегоЗаработка0504425(ДанныеДокументов,
		ОбъектыПечати,
		"ПФ_MXL_ЗапискаРасчетФорма0504425с2015");
		Если НЕ ТабличныйДокумент = Неопределено Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
			"РасчетСреднегоЗаработкаФорма0504425с2015", 
			НСтр("ru = 'Записка-расчет (0504425) с 2015 года'"), 
			ТабличныйДокумент);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#Область ПечатьРасчетаСреднегоЗаработка

// Заполняет таблицу значений - параметры формирования печатной формы расчета среднего заработка.
//
// Параметры:
//	 МассивСсылок 		- массив, печатаемые документы.
//
Функция ДанныеДокументовДляПечатиРасчетаСреднегоЗаработка(МассивСсылок, ИмяМакета = "") Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	СоздатьВТКадровыеДанныеСотрудниковДокумента(Запрос);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Сотрудник КАК Сотрудник,
	|	ТаблицаДокумента.ДатаУвольнения КАК ДатаНачалаСобытия,
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.Дата КАК ДатаДокумента,
	|	ТаблицаДокумента.Номер КАК НомерДокумента,
	|	ТаблицаДокумента.ДатаУвольнения КАК ДатаНачалаОтсутствия,
	|	ТаблицаДокумента.ДатаУвольнения КАК ДатаОкончания,
	|	ТаблицаДокумента.ПериодРасчетаСреднегоЗаработкаНачало КАК НачалоРасчетногоПериода,
	|	ТаблицаДокумента.ПериодРасчетаСреднегоЗаработкаОкончание КАК ОкончаниеРасчетногоПериода,
	|	ТаблицаДокумента.ВидРасчетаКомпенсацииУдержанияОтпуска КАК Начисление,
	|	ТаблицаДокумента.СуммированныйУчет КАК СуммированныйУчет,
	|	КадровыеДанныеСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
	|	КадровыеДанныеСотрудников.ФИОПолные КАК ФИОПолные,
	|	КадровыеДанныеСотрудников.ТабельныйНомер КАК ТабельныйНомер,
	|	КадровыеДанныеСотрудников.Подразделение КАК Подразделение,
	|	КадровыеДанныеСотрудников.Должность КАК Должность,
	|	КадровыеДанныеСотрудников.ВидЗанятости КАК ВидЗанятости,
	|	Организации.Наименование КАК НаименованиеОрганизации,
	|	Организации.КодПоОКПО КАК КодПоОКПО,
	|	Организации.НаименованиеПолное КАК ПолноеНаименованиеОрганизации,
	|	ТаблицаДокумента.ПериодРегистрации КАК НачалоПериодаРасчетаЗарплаты,
	|	ИСТИНА КАК РассчитатьЗарплату,
	|	ИСТИНА КАК УчитыватьДвиженияДругихРегистраторов
	|ИЗ
	|	Документ.Увольнение КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
	|		ПО ТаблицаДокумента.Сотрудник = КадровыеДанныеСотрудников.Сотрудник
	|			И ТаблицаДокумента.ДатаУвольнения = КадровыеДанныеСотрудников.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО ТаблицаДокумента.Организация = Организации.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В(&МассивСсылок)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";
	
	Результат = Запрос.Выполнить();
	
	ДанныеДокументов = Новый Массив;
	
	Если Результат.Пустой() Тогда
		Возврат ДанныеДокументов;
	КонецЕсли;
		
	ТаблицыДанныхОСреднем = УчетСреднегоЗаработка.ТаблицыДанныхОСреднемЗаработке("Увольнение", МассивСсылок);
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.СледующийПоЗначениюПоля("Ссылка") Цикл
		Если ИмяМакета = "РасчетСреднегоЗаработкаВыходногоПособия" Тогда
			СпособРасчета = Неопределено;
			ИспользоватьСреднеЧасовойЗаработок = Выборка.СуммированныйУчет;
		Иначе
			ИспользоватьСреднеЧасовойЗаработок = Ложь;
			МассивОтпусков = ВидыОтпусковОбъекта(Выборка.Ссылка.ПолучитьОбъект());
			Если ОстаткиОтпусков.СодержатсяТолькоОтпускаПоРабочимДням(МассивОтпусков, Выборка.Сотрудник, Выборка.ДатаНачалаСобытия) Тогда
				СпособРасчета = Перечисления.СпособыРасчетаНачислений.ОплатаОтпускаПоШестидневке;
			Иначе
				СпособРасчета = Перечисления.СпособыРасчетаНачислений.ОплатаОтпускаПоКалендарнымДням;
			КонецЕсли;
		КонецЕсли;
			
		ДанныеДокумента = Обработки.ПечатьРасчетаСреднегоЗаработка.ПустаяСтруктураДанныхДляПечатиСреднегоЗаработка(); 
		ЗаполнитьЗначенияСвойств(ДанныеДокумента.РеквизитыДокумента, Выборка);
		ЗаполнитьЗначенияСвойств(ДанныеДокумента.КадровыеДанныеСотрудника, Выборка);
		
		ДанныеОНачислениях 	= УчетСреднегоЗаработка.ТаблицаОтобраннаяПоПолю(ТаблицыДанныхОСреднем["ДанныеОНачислениях"], 		"Ссылка", Выборка.Ссылка);
		ДанныеОВремени 		= УчетСреднегоЗаработка.ТаблицаОтобраннаяПоПолю(ТаблицыДанныхОСреднем["ДанныеОВремени"], 			"Ссылка", Выборка.Ссылка);
		ДанныеОбИндексации 	= УчетСреднегоЗаработка.ТаблицаОтобраннаяПоПолю(ТаблицыДанныхОСреднем["ДанныеОбИндексации"], 		"Ссылка", Выборка.Ссылка);
		
		ДополнительныеПараметры = УчетСреднегоЗаработкаКлиентСервер.ДополнительныеПараметрыРасчетаСреднегоЗаработка();
		ДополнительныеПараметры.Индексации = ДанныеОбИндексации;
		ДополнительныеПараметры.ДатаНачалаСобытия = Выборка.ДатаНачалаСобытия;
		ДополнительныеПараметры.НачалоПериода = Выборка.НачалоРасчетногоПериода;
		ДополнительныеПараметры.ОкончаниеПериода = Выборка.ОкончаниеРасчетногоПериода;
		ДополнительныеПараметры.ПоЧасам = ИспользоватьСреднеЧасовойЗаработок;
		ДополнительныеПараметры.СпособРасчетаОтпуска = СпособРасчета;

		ДанныеДокумента.ДанныеРасчетаСреднего = УчетСреднегоЗаработкаКлиентСервер.ДанныеДляРасчетаСреднегоЗаработка(ДанныеОНачислениях, ДанныеОВремени, ДополнительныеПараметры);
		
		ДанныеДокумента.ПараметрыРасчета.СпособРасчета = СпособРасчета;
		ДанныеДокумента.ПараметрыРасчета.ИспользоватьСреднеЧасовойЗаработок = ИспользоватьСреднеЧасовойЗаработок;
		ДанныеДокумента.ПараметрыРасчета.НачалоРасчетногоПериода = Выборка.НачалоРасчетногоПериода;
		ДанныеДокумента.ПараметрыРасчета.ОкончаниеРасчетногоПериода = Выборка.ОкончаниеРасчетногоПериода;
		
		ДанныеДокументов.Добавить(ДанныеДокумента);
	КонецЦикла;
	
	Возврат ДанныеДокументов;
	
КонецФункции

Процедура СоздатьВТКадровыеДанныеСотрудниковДокумента(Запрос)
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Сотрудник КАК Сотрудник,
	|	ТаблицаДокумента.ДатаУвольнения КАК Период
	|ПОМЕСТИТЬ ВТСотрудники
	|ИЗ
	|	Документ.Увольнение КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В(&МассивСсылок)";
	Запрос.Выполнить();
	
	Описатель = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(Запрос.МенеджерВременныхТаблиц, "ВТСотрудники");
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(Описатель, Истина, "ФизическоеЛицо,ФИОПолные,ТабельныйНомер,Подразделение,Должность,ВидЗанятости,ДатаПриема,ПриказОПриемеДатаЗавершенияТрудовогоДоговора");
	
	Запрос.Текст = "УНИЧТОЖИТЬ ВТСотрудники";
	Запрос.Выполнить();

КонецПроцедуры

// Заполняет таблицу значений - параметры формирования печатной формы расчета среднего заработка.
//
// Параметры:
//	 МассивСсылок 		- массив, печатаемые документы.
//
Функция ДанныеДокументовДляПечатиРасчетаСреднегоЗаработка0504425(МассивСсылок) Экспорт
	
	ЗапросПоОрганизациям = Новый Запрос;
	ЗапросПоОрганизациям.УстановитьПараметр("МассивСсылок", МассивСсылок);
	ЗапросПоОрганизациям.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.ДатаУвольнения КАК ДатаСобытия
	|ИЗ
	|	Документ.Увольнение КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В(&МассивСсылок)";
	
	ТаблицаРезультатовПоОрганизациям = ЗапросПоОрганизациям.Выполнить().Выгрузить();
	
	ОрганизацииИСведенияОНих = ЗарплатаКадрыРасширенный.ПолучитьИННиКППОрганизаций(ТаблицаРезультатовПоОрганизациям);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ДатаУвольнения КАК Дата,
	|	ТаблицаДокумента.ГлавныйБухгалтер КАК ГлавныйБухгалтер,
	|	ТаблицаДокумента.Бухгалтер КАК Бухгалтер,
	|	ТаблицаДокумента.Исполнитель КАК Исполнитель
	|ПОМЕСТИТЬ ВТСотрудникиИПериод
	|ИЗ
	|	Документ.Увольнение КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В(&МассивСсылок)";
	
	Запрос.Выполнить();
	
	ЗарплатаКадры.СоздатьВТФИООтветственныхЛиц(Запрос.МенеджерВременныхТаблиц, Истина, "ГлавныйБухгалтер,Бухгалтер,Исполнитель", "ВТСотрудникиИПериод");
		
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(ДополнительныеОтпуска.ДнейКомпенсацииУдержания) КАК КоличествоДней,
	|	ДополнительныеОтпуска.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТДополнительныеОтпуска
	|ИЗ
	|	Документ.Увольнение.ДополнительныеОтпуска КАК ДополнительныеОтпуска
	|ГДЕ
	|	ДополнительныеОтпуска.Ссылка В(&МассивСсылок)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДополнительныеОтпуска.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ВидРасчетаКомпенсацииУдержанияОтпуска КАК Начисление,
	|	ТаблицаДокумента.ДатаУвольнения КАК ДатаНачалаОтсутствия,
	|	ТаблицаДокумента.ДнейКомпенсацииУдержанияОтпуска КАК ДнейОсновногоОтпуска,
	|	ЕСТЬNULL(ВТДополнительныеОтпуска.КоличествоДней, 0) КАК ДнейДополнительногоОтпуска,
	|	ТаблицаДокумента.ДнейКомпенсацииУдержанияОтпуска + ЕСТЬNULL(ВТДополнительныеОтпуска.КоличествоДней, 0) КАК ДнейОтпускаВсего,
	|	ТаблицаДокумента.РабочийГодС КАК НачалоПериодаЗаКоторыйПредоставляетсяОтпуск,
	|	ТаблицаДокумента.РабочийГодПо КАК КонецПериодаЗаКоторыйПредоставляетсяОтпуск,
	|	ФИОГлавногоБухгалтера.РасшифровкаПодписи КАК ГлавныйБухгалтерРасшифровкаПодписи,
	|	ФИОБухгалтера.РасшифровкаПодписи КАК БухгалтерРасшифровкаПодписи,
	|	ФИОИсполнителя.РасшифровкаПодписи КАК ИсполнительРасшифровкаПодписи,
	|	ТаблицаДокумента.ДолжностьИсполнителя КАК ДолжностьИсполнителя
	|ИЗ
	|	Документ.Увольнение КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственныхЛиц КАК ФИОГлавногоБухгалтера
	|		ПО ТаблицаДокумента.Ссылка = ФИОГлавногоБухгалтера.Ссылка
	|			И ТаблицаДокумента.ГлавныйБухгалтер = ФИОГлавногоБухгалтера.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственныхЛиц КАК ФИОБухгалтера
	|		ПО ТаблицаДокумента.Ссылка = ФИОБухгалтера.Ссылка
	|			И ТаблицаДокумента.Бухгалтер = ФИОБухгалтера.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственныхЛиц КАК ФИОИсполнителя
	|		ПО ТаблицаДокумента.Ссылка = ФИОИсполнителя.Ссылка
	|			И ТаблицаДокумента.Исполнитель = ФИОИсполнителя.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДополнительныеОтпуска КАК ВТДополнительныеОтпуска
	|		ПО ТаблицаДокумента.Ссылка = ВТДополнительныеОтпуска.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В(&МассивСсылок)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ДанныеДокументов = Новый Массив;
	
	БазовыеДанныеДокументов = ДанныеДокументовДляПечатиРасчетаСреднегоЗаработка(МассивСсылок);
	
	Отбор = Новый Структура("Ссылка");
	
	Для каждого БазовыеДанныеДокумента Из БазовыеДанныеДокументов Цикл
		
		Выборка.Сбросить();		
		
		ДанныеДокумента = Обработки.ПечатьРасчетаСреднегоЗаработка.ПустаяСтруктураДанныхДляПечатиСреднегоЗаработка0504425(); 
		ЗаполнитьЗначенияСвойств(ДанныеДокумента, БазовыеДанныеДокумента, , "РеквизитыДокумента");   
		
		// Т.к. структура реквизитов документа в базовых и расширенных данных различается их надо обработать отдельно.
		ЗаполнитьЗначенияСвойств(ДанныеДокумента.РеквизитыДокумента, БазовыеДанныеДокумента.РеквизитыДокумента);		
		Отбор.Ссылка = БазовыеДанныеДокумента.РеквизитыДокумента.Ссылка;
		Если Выборка.НайтиСледующий(Отбор) Тогда
			ЗаполнитьЗначенияСвойств(ДанныеДокумента.РеквизитыДокумента, Выборка); 
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ДанныеДокумента.РеквизитыДокумента, 
			ОрганизацииИСведенияОНих.Получить(БазовыеДанныеДокумента.РеквизитыДокумента.Организация),
			"ИНН, КПП"); 
		
		ДанныеДокументов.Добавить(ДанныеДокумента);
		
	КонецЦикла;
	
	Возврат ДанныеДокументов;
	
КонецФункции

#КонецОбласти

#Область ПечатьПодробногоРасчетаНачислений

// Заполняет структуру - описание документа для формирования печатной формы подробного расчета начислений.
//
// Параметры:
//   ОписаниеДокумента - структура, определяется в Обработки.ПечатьРасчетаНачислений.ОписаниеДокументаРасчетаНачислений.
//
Процедура ЗаполнитьОписаниеДокументаРасчетаНачислений(ОписаниеДокумента) Экспорт
	КатегорииСпециализированногоНачисления = Новый Массив;
	КатегорииСпециализированногоНачисления.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.КомпенсацияОтпуска);
	КатегорииСпециализированногоНачисления.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ВыходноеПособие);
	
	МетаданныеДокумента = ПустаяСсылка().Метаданные();
	
	ОписаниеДокумента.Вставить("ИмяДокумента", 								МетаданныеДокумента.Имя);
	ОписаниеДокумента.Вставить("СинонимДокумента", 							МетаданныеДокумента.Синоним);
	ОписаниеДокумента.Вставить("ЕстьРасчетСреднегоЗаработка", 				Истина);
	ОписаниеДокумента.Вставить("ЕстьРасчетСпециализированныхНачислений",	Истина);
	ОписаниеДокумента.Вставить("ЕстьРасчетЗарплаты", 						Истина);
	ОписаниеДокумента.Вставить("КатегорииСпециализированногоНачисления", 	КатегорииСпециализированногоНачисления);
	ОписаниеДокумента.Вставить("НазваниеСпециализированногоНачисления", 	НСтр("ru='Расчет при увольнении'"));
КонецПроцедуры 

// Заполняет таблицу значений - параметры формирования печатной формы подробного расчета начислений.
//
// Параметры:
//	 МассивСсылок 		- массив, печатаемые документы.
//   ДанныеДокумента 	- таблица значений, определяется в
//                      Обработки.ПечатьРасчетаНачислений.ДанныеДокументовДляПодробногоРасчетаНачислений.
//
Процедура ЗаполнитьДанныеДокументовДляПодробногоРасчетаНачислений(МассивСсылок, ДанныеДокументов) Экспорт
	РасчетЗарплатыРасширенный.ЗаполнитьДанныеДокументовДляПодробногоРасчетаНачислений(МассивСсылок, ПустаяСсылка().Метаданные().Имя, ДанныеДокументов);	
КонецПроцедуры

// Возвращает структуру с двумя таблицами "Начисления" и "Показатели".
// Данные в таблицах представлены в разрезе ссылки на документ.
// 	Параметры:
//		МассивСсылок - массив ссылок на документы у которых есть табличные части "Начисления" и "Показатели".
//		ИмяДокумента - Имя объекта метаданных (документа) для формирования запроса.
//
Функция НачисленияПоказателиДокументов(МассивСсылок) Экспорт 
	Возврат РасчетЗарплатыРасширенный.НачисленияПоказателиДокументов(МассивСсылок, ПустаяСсылка().Метаданные().Имя);	
КонецФункции

#КонецОбласти

Функция ПолныеПраваНаДокумент() Экспорт 
	
	Возврат Пользователи.РолиДоступны("ДобавлениеИзменениеНачисленнойЗарплатыРасширенная, ЧтениеНачисленнойЗарплатыРасширенная", , Ложь);
	
КонецФункции	

Функция ДанныеДляПроверкиОграниченийНаУровнеЗаписей(Объект) Экспорт 

	ФизическоеЛицо = ?(ЗначениеЗаполнено(Объект.Сотрудник), ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Сотрудник, "ФизическоеЛицо"), Справочники.ФизическиеЛица.ПустаяСсылка());
	
	ДанныеДляПроверкиОграничений = ЗарплатаКадрыРасширенный.ОписаниеСтруктурыДанныхДляПроверкиОграниченийНаУровнеЗаписей();
	
	ДанныеДляПроверкиОграничений.Организация = Объект.Организация;
	ДанныеДляПроверкиОграничений.МассивФизическихЛиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо);
	
	Возврат ДанныеДляПроверкиОграничений;
	
КонецФункции

Функция ДанныеДляРегистрацииВУчетаСтажаПФР(МассивСсылок, ПоСпискуСотрудников = Ложь, ПеремещаемыеСовместители = Неопределено, Организация = Неопределено) Экспорт
	
	ДанныеДляРегистрацииВУчете = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Увольнение.Ссылка КАК Ссылка,
		|	Увольнение.Сотрудник КАК Сотрудник,
		|	Увольнение.ДатаУвольнения,
		|	Увольнение.Ссылка.Организация КАК Организация
		|ИЗ
		|	Документ.Увольнение КАК Увольнение
		|ГДЕ
		|	Увольнение.Ссылка В(&МассивСсылок)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка,
		|	Сотрудник";
	
	Если ПоСпискуСотрудников Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Документ.Увольнение", "Документ.УвольнениеСписком.Сотрудники");
	КонецЕсли; 
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.СледующийПоЗначениюПоля("Ссылка") Цикл
		
		ДанныеДляРегистрацииВУчетеПоДокументу = УчетСтажаПФР.ДанныеДляРегистрацииВУчетеСтажаПФР();
		ДанныеДляРегистрацииВУчете.Вставить(Выборка.Ссылка, ДанныеДляРегистрацииВУчетеПоДокументу);
		
		Пока Выборка.Следующий() Цикл
			
			ОписаниеПериода = УчетСтажаПФР.ОписаниеРегистрируемогоПериода();
			ОписаниеПериода.Сотрудник = Выборка.Сотрудник;	
			ОписаниеПериода.ДатаНачалаПериода = КонецДня(Выборка.ДатаУвольнения) + 1;
			ОписаниеПериода.Состояние = Перечисления.СостоянияСотрудника.Увольнение;
			
			РегистрируемыйПериод = УчетСтажаПФР.ДобавитьЗаписьВДанныеДляРегистрацииВУчета(ДанныеДляРегистрацииВУчетеПоДокументу, ОписаниеПериода);
			
			УчетСтажаПФР.УстановитьЗначениеРегистрируемогоРесурса(РегистрируемыйПериод, "Организация", Выборка.Организация);
			УчетСтажаПФР.УстановитьЗначениеРегистрируемогоРесурса(РегистрируемыйПериод, "Подразделение", Справочники.ПодразделенияОрганизаций.ПустаяСсылка());
			УчетСтажаПФР.УстановитьЗначениеРегистрируемогоРесурса(РегистрируемыйПериод, "Должность", Справочники.Должности.ПустаяСсылка());
			УчетСтажаПФР.УстановитьЗначениеРегистрируемогоРесурса(РегистрируемыйПериод, "ДолжностьПоШтатномуРасписанию", Справочники.ШтатноеРасписание.ПустаяСсылка());
			УчетСтажаПФР.УстановитьЗначениеРегистрируемогоРесурса(РегистрируемыйПериод, "ГрафикРаботы", Справочники.ГрафикиРаботыСотрудников.ПустаяСсылка());
			УчетСтажаПФР.УстановитьЗначениеРегистрируемогоРесурса(РегистрируемыйПериод, "КоличествоСтавок", 0);
			УчетСтажаПФР.УстановитьЗначениеРегистрируемогоРесурса(РегистрируемыйПериод, "ВидСтажаПФР", Перечисления.ВидыСтажаПФР2014.НеВключаетсяВСтраховойСтаж);
			
		КонецЦикла;
		
		Если ПеремещаемыеСовместители <> Неопределено Тогда
			
			Для каждого ДанныеПеремещаемогоСовместителя Из ПеремещаемыеСовместители Цикл
				
				ОписаниеПериода = УчетСтажаПФР.ОписаниеРегистрируемогоПериода();
				ОписаниеПериода.Сотрудник = ДанныеПеремещаемогоСовместителя.Сотрудник;
				ОписаниеПериода.ДатаНачалаПериода = ДанныеПеремещаемогоСовместителя.ДатаСобытия;
				ОписаниеПериода.Состояние = Перечисления.СостоянияСотрудника.Работа;
				ОписаниеПериода.ВидЗанятости = ДанныеПеремещаемогоСовместителя.ВидЗанятости;
				
				РегистрируемыйПериод = УчетСтажаПФР.ДобавитьЗаписьВДанныеДляРегистрацииВУчета(ДанныеДляРегистрацииВУчетеПоДокументу, ОписаниеПериода);
				УчетСтажаПФР.УстановитьЗначениеРегистрируемогоРесурса(РегистрируемыйПериод, "Организация", Организация);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДанныеДляРегистрацииВУчете;
	
КонецФункции

#Область ОбработкаПроверкиЗаполнения

Функция ДатыУвольненияСотрудников(Ссылка)
	
	ПроверяемыеДаты = Новый Соответствие;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Сотрудник КАК Сотрудник,
		|	ТаблицаДокумента.ДатаУвольнения КАК ДатаУвольнения
		|ИЗ
		|	Документ.Увольнение КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаУвольнения,
		|	Сотрудник";
	
	Если ТипЗнч(Ссылка) <> Тип("ДокументСсылка.Увольнение") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Документ.Увольнение", "Документ.УвольнениеСписком.Сотрудники");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ДатаУвольнения") Цикл
		
		СписокСотрудников = Новый Массив;
		Пока Выборка.Следующий() Цикл
			СписокСотрудников.Добавить(Выборка.Сотрудник);
		КонецЦикла;
		
		ПроверяемыеДаты.Вставить(Выборка.ДатаУвольнения, СписокСотрудников);;
		
	КонецЦикла;
	
	Возврат ПроверяемыеДаты;
	
КонецФункции

Процедура ПроверитьЗаполнениеКомпенсацииДополнительныхОтпусков(ДокументОбъект, Отказ, ВыводитьСообщения = Истина, ПраваНаДокумент = Неопределено) Экспорт
	
	ТекстСообщения = "";
	СтруктураСообщений  = Новый Соответствие;
	
	Если ПраваНаДокумент = Неопределено Тогда 
		ПраваНаДокумент = ЗарплатаКадрыРасширенный.ПраваНаМногофункциональныйДокумент(ДокументОбъект);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЭтоСписочныйДокумент = ТипЗнч(ДокументОбъект.Ссылка) <> Тип("ДокументСсылка.Увольнение");
	
	НомерСтрокиСотрудника = 0;
	НомерСтрокиОтпускаСотрудника = 0;
	Сотрудник = Неопределено;
	Для каждого ДополнительныйОтпуск Из ДокументОбъект.ДополнительныеОтпуска Цикл
		
		Если ЭтоСписочныйДокумент Тогда
			
			СтрокиСотрудников = ДокументОбъект.Сотрудники.НайтиСтроки(Новый Структура("ИдентификаторСтрокиСотрудника", ДополнительныйОтпуск.ИдентификаторСтрокиСотрудника));
			Если СтрокиСотрудников.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаСотрудника = СтрокиСотрудников[0];
			НомерСтрокиСотрудника = СтрокаСотрудника.НомерСтроки;
			
			Если Сотрудник <> СтрокаСотрудника.Сотрудник Тогда
				
				Сотрудник = СтрокаСотрудника.Сотрудник;
				НомерСтрокиОтпускаСотрудника = 1;
				
			Иначе
				НомерСтрокиОтпускаСотрудника = НомерСтрокиОтпускаСотрудника + 1;
			КонецЕсли;
			
		Иначе
			
			Сотрудник = ДокументОбъект.Сотрудник;
			НомерСтрокиОтпускаСотрудника = ДополнительныйОтпуск.НомерСтроки;
			
		КонецЕсли;
		
		Если ДополнительныйОтпуск.ПризнакКомпенсацииУдержания = Перечисления.КомпенсацияУдержаниеОтпускаПриУвольнении.НеИспользовать Тогда 
			Продолжить;
		КонецЕсли;
		
		Если ПраваНаДокумент.ОграниченияНаУровнеЗаписей.ИзменениеБезОграничений Тогда
			
			Если Документы.Увольнение.НеобходимоЗаполнитьВидРасчетаКомпенсацииУдержания(
				ДополнительныйОтпуск.ПризнакКомпенсацииУдержания, ПолучитьФункциональнуюОпцию("СпособУдержанияИзлишнеНачисленныхОтпускных"), ДополнительныйОтпуск.ВидРасчетаКомпенсацииУдержания) Тогда
				
				Если ЭтоСписочныйДокумент Тогда
					ТекстСообщения = НСтр("ru = 'По сотруднику %2, в строке %1 не заполнен вид расчета компенсации (удержания).'");
				Иначе
					ТекстСообщения = НСтр("ru = 'В строке %1 не заполнен вид расчета компенсации (удержания).'");
				КонецЕсли;
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, НомерСтрокиОтпускаСотрудника, Сотрудник);
				
				Если ЭтоСписочныйДокумент Тогда
					НомерСтрокиДанных = НомерСтрокиСотрудника - 1;
					ПутьКДанным = "Сотрудники[" + НомерСтрокиДанных + "]";
				Иначе
					НомерСтрокиДанных = ДополнительныйОтпуск.НомерСтроки - 1;
					ПутьКДанным = "ДополнительныеОтпуска[" + НомерСтрокиДанных + "]";
				КонецЕсли;
				
				СтруктураСообщений.Вставить(ПутьКДанным, ТекстСообщения);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДополнительныйОтпуск.ДнейКомпенсацииУдержания) Тогда
			
			Если ЭтоСписочныйДокумент Тогда
				ТекстСообщения = НСтр("ru = 'По сотруднику %2, в строке %1 не заполнено количество дней компенсации (удержания).'");
			Иначе
				ТекстСообщения = НСтр("ru = 'В строке %1 не заполнено количество дней компенсации (удержания).'");
			КонецЕсли;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, НомерСтрокиОтпускаСотрудника, Сотрудник);
			
			НомерСтрокиДанных = ДополнительныйОтпуск.НомерСтроки - 1;
			СтруктураСообщений.Вставить("ДополнительныеОтпуска[" + НомерСтрокиДанных + "]", ТекстСообщения);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ВыводитьСообщения Тогда
		Для каждого Сообщение Из СтруктураСообщений Цикл
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Сообщение.Значение, , "Объект" + ?(Сообщение.Ключ = "", "", ".") + Сообщение.Ключ);
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураСообщений.Количество() > 0 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьПериодДействияНачислений(ДокументОбъект, Отказ)
	
	Если Не ДокументОбъект.ДокументРассчитан Тогда
	  Возврат;
	КонецЕсли;
	
	ПараметрыПроверкиПериодаДействия = РасчетЗарплатыРасширенный.ПараметрыПроверкиПериодаДействия();
	ПараметрыПроверкиПериодаДействия.Ссылка = ДокументОбъект.Ссылка;
	ПроверяемыеКоллекции = Новый Массив;
	ПроверяемыеКоллекции.Добавить(РасчетЗарплатыРасширенный.ОписаниеКоллекцииДляПроверкиПериодаДействия("Начисления", НСтр("ru='Начисления'")));
	ПроверяемыеКоллекции.Добавить(РасчетЗарплатыРасширенный.ОписаниеКоллекцииДляПроверкиПериодаДействия("Пособия", НСтр("ru='Пособия'")));
	ПроверяемыеКоллекции.Добавить(РасчетЗарплатыРасширенный.ОписаниеКоллекцииДляПроверкиПериодаДействия("Удержания", НСтр("ru='Удержания'"), "Удержание"));
	ПроверяемыеКоллекции.Добавить(РасчетЗарплатыРасширенный.ОписаниеКоллекцииДляПроверкиПериодаДействия("НачисленияПерерасчет", НСтр("ru='Перерасчет прошлого периода'")));
	РасчетЗарплатыРасширенный.ПроверитьПериодДействияВКоллекцияхНачислений(ДокументОбъект, ПараметрыПроверкиПериодаДействия, ПроверяемыеКоллекции, Отказ);
	
КонецПроцедуры

Процедура ПроверитьЗаполненностьРеквизитовКомпенсацииУдержанияОтпусков(ДокументОбъект, ПроверяемыеРеквизиты, Отказ, ПраваНаДокумент)
	
	Если ТипЗнч(ДокументОбъект.Ссылка) <> Тип("ДокументСсылка.Увольнение") Тогда
		
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Сотрудники.ДнейКомпенсацииУдержанияОтпуска");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Сотрудники.ВидРасчетаКомпенсацииУдержанияОтпуска");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Сотрудники.ВыходноеПособие");
		
		ПроверяемаяКоллекция = ДокументОбъект.Сотрудники;
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ДнейКомпенсацииУдержанияОтпуска");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ВидРасчетаКомпенсацииУдержанияОтпуска");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ВыходноеПособие");
		
		ПроверяемаяКоллекция = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДокументОбъект);
		
	КонецЕсли;
	
	МетаданныеДокумента = Метаданные.Документы.Увольнение;
	СинонимДнейКомпенсацииУдержанияОтпуска = МетаданныеДокумента.Реквизиты.ДнейКомпенсацииУдержанияОтпуска.Синоним;
	СинонимВидРасчетаКомпенсацииУдержанияОтпуска = МетаданныеДокумента.Реквизиты.ВидРасчетаКомпенсацииУдержанияОтпуска.Синоним;
	СинонимВыходноеПособие = МетаданныеДокумента.Реквизиты.ВыходноеПособие.Синоним;
	
	НомерСтроки = 0;
	Для каждого ЭлементКоллекции Из ПроверяемаяКоллекция Цикл
		
		Если ЭлементКоллекции.ПризнакКомпенсацииУдержанияОтпуска <> Перечисления.КомпенсацияУдержаниеОтпускаПриУвольнении.НеИспользовать
			И Не ЗначениеЗаполнено(ЭлементКоллекции.ДнейКомпенсацииУдержанияОтпуска) Тогда 
			
			Если ПроверяемаяКоллекция.Количество() = 1 Тогда
				ТекстСообщения = НСтр("ru='Поле ""%1"" не заполнено'");
				ПутьКПолю = "Объект.ДнейКомпенсацииУдержанияОтпуска";
			Иначе
				ТекстСообщения = НСтр("ru='Для сотрудника %2 не заполнено поле ""%1""'");
				ПутьКПолю = "Объект.Сотрудники[" + НомерСтроки + "].ДнейКомпенсацииУдержанияОтпуска";
			КонецЕсли;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения,
				СинонимДнейКомпенсацииУдержанияОтпуска,
				ЭлементКоллекции.Сотрудник);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , ПутьКПолю, , Отказ);
			
		КонецЕсли;
		
		Если ДокументОбъект.ДокументРассчитан И ПраваНаДокумент.ОграниченияНаУровнеЗаписей.ИзменениеБезОграничений 
			И ЭлементКоллекции.ПризнакКомпенсацииУдержанияОтпуска <> Перечисления.КомпенсацияУдержаниеОтпускаПриУвольнении.НеИспользовать Тогда
			
			Если Не ЗначениеЗаполнено(ЭлементКоллекции.ВидРасчетаКомпенсацииУдержанияОтпуска)
				И Не (ПолучитьФункциональнуюОпцию("СпособУдержанияИзлишнеНачисленныхОтпускных") = ПредопределенноеЗначение("Перечисление.СпособыУдержанияИзлишнеНачисленныхОтпускных.СторнированиеРанееНачисленныхСумм")
				И ЭлементКоллекции.ПризнакКомпенсацииУдержанияОтпуска = ПредопределенноеЗначение("Перечисление.КомпенсацияУдержаниеОтпускаПриУвольнении.УдержатьЗаИспользованныеАвансом")) Тогда
			
				Если ПроверяемаяКоллекция.Количество() = 1 Тогда
					ТекстСообщения = НСтр("ru='Поле ""%1"" не заполнено'");
					ПутьКПолю = "Объект.ВидРасчетаКомпенсацииУдержанияОтпуска";
				Иначе
					ТекстСообщения = НСтр("ru='Для сотрудника %2 не заполнено поле ""%1""'");
					ПутьКПолю = "Объект.Сотрудники[" + НомерСтроки + "].ВидРасчетаКомпенсацииУдержанияОтпуска";
				КонецЕсли;
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения,
					СинонимВидРасчетаКомпенсацииУдержанияОтпуска,
					ЭлементКоллекции.Сотрудник);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , ПутьКПолю, , Отказ);
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ЭлементКоллекции.ДнейЧасовВыходногоПособия)
				И Не ЗначениеЗаполнено(ЭлементКоллекции.ВыходноеПособие) Тогда
			
				Если ПроверяемаяКоллекция.Количество() = 1 Тогда
					ТекстСообщения = НСтр("ru='Поле ""%1"" не заполнено'");
					ПутьКПолю = "Объект.ВыходноеПособие";
				Иначе
					ТекстСообщения = НСтр("ru='Для сотрудника %2 не заполнено поле ""%1""'");
					ПутьКПолю = "Объект.Сотрудники[" + НомерСтроки + "].ВыходноеПособие";
				КонецЕсли;
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения,
					СинонимВыходноеПособие,
					ЭлементКоллекции.Сотрудник);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , ПутьКПолю, , Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УточнитьНеобходимостьПроверкиДатыВыплаты(ДокументОбъект, ПроверяемыеРеквизиты, ПраваНаДокумент)
	
	Если Не ПраваНаДокумент.ОграниченияНаУровнеЗаписей.ИзменениеБезОграничений Тогда 
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ПланируемаяДатаВыплаты");
		Возврат;
	КонецЕсли;
	
	МассивНачисленийДокумента = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивНачисленийДокумента, ДокументОбъект.Начисления.ВыгрузитьКолонку("Начисление"), Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивНачисленийДокумента, ДокументОбъект.НачисленияПерерасчет.ВыгрузитьКолонку("Начисление"), Истина);
	
	Если НЕ УчетНДФЛРасширенный.ДатаВыплатыОбязательнаКЗаполнению(ДокументОбъект.ПорядокВыплаты, МассивНачисленийДокумента) Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ПланируемаяДатаВыплаты");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПодготовкаДанныхДляПроведенияДокумента

Процедура СоздатьВТКадровыеДанныеУвольняемыхСотрудников(МенеджерВременныхТаблиц, ДокументСсылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.ДатаУвольнения КАК Период,
		|	ТаблицаДокумента.Сотрудник
		|ПОМЕСТИТЬ ВТСотрудникиПериоды
		|ИЗ
		|	Документ.Увольнение КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка";
		
	Если ТипЗнч(ДокументСсылка) <> Тип("ДокументСсылка.Увольнение") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Документ.Увольнение", "Документ.УвольнениеСписком.Сотрудники");
	КонецЕсли; 
	
	Запрос.Выполнить();
	
	ОписательТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц, "ВТСотрудникиПериоды");
		
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательТаблиц, Истина, "ВидДоговора,ДолжностьПоШтатномуРасписанию,КоличествоСтавок,ВидЗанятости,ГоловнаяОрганизация");
	
КонецПроцедуры

Функция КадровыеСобытияУвольнение(Сотрудник, ДатаУвольнения, ФизическоеЛицо = Неопределено, Позиция = Неопределено, КоличествоСтавок = Неопределено, ВидДоговора = Неопределено) Экспорт
	
	Если ВидДоговора = Неопределено Тогда
		ВидДоговора = Перечисления.ВидыДоговоровССотрудниками.ТрудовойДоговор;
	КонецЕсли;
	
	// Создаем таблицу событий
	КадровыеСобытия = КадровыйУчетРасширенный.ПустаяТаблицаКадровыхСобытийПриУвольнении();
	
	// Заполняем таблицу
	НовоеСобытие = КадровыеСобытия.Добавить();
	НовоеСобытие.Сотрудник = Сотрудник;
	НовоеСобытие.ДатаСобытия = КонецДня(ДатаУвольнения) + 1;
	НовоеСобытие.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Увольнение;
	Если Не ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сотрудник, "ФизическоеЛицо");
	КонецЕсли;
	НовоеСобытие.ФизическоеЛицо = ФизическоеЛицо;
	
	Возврат КадровыеСобытия;
	
КонецФункции

Функция КадровыеСобытияУвольнениеПоВременнойТаблице(МенеджерВременныхТаблиц)
	
	КадровыеСобытия = КадровыйУчетРасширенный.ПустаяТаблицаКадровыхСобытийПриУвольнении();
	КадровыеСобытия.Колонки.Добавить("ДатаУвольнения", Новый ОписаниеТипов("Дата"));
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КадровыеДанныеСотрудников.Период КАК ДатаУвольнения,
		|	ДОБАВИТЬКДАТЕ(КадровыеДанныеСотрудников.Период, ДЕНЬ, 1) КАК ДатаСобытия,
		|	КадровыеДанныеСотрудников.Сотрудник,
		|	КадровыеДанныеСотрудников.ФизическоеЛицо,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение) КАК ВидСобытия,
		|	КадровыеДанныеСотрудников.ВидДоговора КАК ВидДоговора,
		|	КадровыеДанныеСотрудников.ДолжностьПоШтатномуРасписанию КАК Позиция,
		|	КадровыеДанныеСотрудников.ВидЗанятости КАК ВидЗанятости,
		|	КадровыеДанныеСотрудников.КоличествоСтавок КАК КоличествоСтавок
		|ИЗ
		|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(КадровыеСобытия.Добавить(), Выборка);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат КадровыеСобытия;
	
КонецФункции

Функция ДанныеДляПроведения(РеквизитыДляПроведения, МенеджерВременныхТаблиц, ПериодРегистрации, Организация, ПеремещаемыеСовместители = Неопределено, СтруктураВидовУчета = Неопределено) Экспорт
	
	Если СтруктураВидовУчета = Неопределено Тогда
		СтруктураВидовУчета = ПроведениеРасширенныйСервер.СтруктураВидовУчета();
		Для Каждого ВидУчета Из СтруктураВидовУчета Цикл
			СтруктураВидовУчета[ВидУчета.Ключ] = Истина;
		КонецЦикла;
	КонецЕсли;
	
	ДанныеДляПроведения = РасчетЗарплатыРасширенный.СоздатьДанныеДляПроведенияНачисленияЗарплаты();
	
	Если СтруктураВидовУчета.ОстальныеВидыУчета Тогда
		ДобавитьДанныеДляРегистрацииПрекращенияПлановыхНачислений(ДанныеДляПроведения, МенеджерВременныхТаблиц);
		ДобавитьДанныеДляРегистрацииПрекращенияПлановыхУдержаний(ДанныеДляПроведения, МенеджерВременныхТаблиц, ПеремещаемыеСовместители);
		ДобавитьДанныеДляРегистрацииСостоянийСотрудников(ДанныеДляПроведения, РеквизитыДляПроведения);
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьПодработки") 
			И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.Подработки") Тогда
			Модуль = ОбщегоНазначения.ОбщийМодуль("Подработки");
			Модуль.ДобавитьДанныеДляРегистрацииПрекращенияПодработок(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка);
		КонецЕсли;
		
		РасчетЗарплатыРасширенный.ЗаполнитьНачисления(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка, "Начисления,НачисленияПерерасчет,Пособия,ПособияПерерасчет,Льготы", "Ссылка.ПериодРегистрации");
		РасчетЗарплатыРасширенный.ЗаполнитьУдержания(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка);
		РасчетЗарплатыРасширенный.ЗаполнитьСписокФизическихЛиц(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка, "Начисления");
		ДобавитьДанныеДляПогашенияЗадолженности(ДанныеДляПроведения, РеквизитыДляПроведения, МенеджерВременныхТаблиц);
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
			Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплата");
			ПараметрыУправленческаяЗарплата = Модуль.ДополнительныеПараметрыПодготовкиДанныхДляПроведения();
			ПараметрыУправленческаяЗарплата.ПолеДатыДействия = "Ссылка.ПериодРегистрации"; 
			ПараметрыУправленческаяЗарплата.ПолеВидаНачисления = "Начисление"; 
			Модуль.ПриПодготовкеДанныхДляПроведенияДокументаРасчетаЗарплаты(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка, ПараметрыУправленческаяЗарплата);
		КонецЕсли;
		
		ОтражениеЗарплатыВБухучете.СоздатьВТНачисленияСДаннымиЕНВД(РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.ПериодРегистрации, ДанныеДляПроведения.МенеджерВременныхТаблиц, ДанныеДляПроведения.НачисленияПоСотрудникам);
		
		ПособиеПлатитУчастникПилотногоПроекта = ПрямыеВыплатыПособийСоциальногоСтрахования.ПособиеПлатитУчастникПилотногоПроекта(РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.ПериодРегистрации);
		
		УчетПособийСоциальногоСтрахованияРасширенный.ЗаполнитьСведенияОПособияхПоУходуЗаРебенком(РеквизитыДляПроведения.Ссылка, ПособиеПлатитУчастникПилотногоПроекта, ДанныеДляПроведения, , "ПособияПерерасчет");
		
		РасчетЗарплаты.ЗаполнитьДанныеНДФЛ(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка);
		
		ДобавитьКолонкуНачалоМесяца(ДанныеДляПроведения.НДФЛ, "МесяцНалоговогоПериода");
		ДобавитьКолонкуНачалоМесяца(ДанныеДляПроведения.НДФЛПоСотрудникам, "МесяцНалоговогоПериода");
		
		РасчетЗарплатыРасширенный.ЗаполнитьДанныеКорректировкиВыплаты(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка);
		РасчетЗарплаты.ЗаполнитьДанныеСтраховыхВзносов(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка);
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба.РасчетДенежногоСодержания") Тогда
			Модуль = ОбщегоНазначения.ОбщийМодуль("РасчетДенежногоСодержания");
			КорректировкиДляРегистрацииДенежногоСодержания = Модуль.СведенияОКорректировкахСотрудниковДляРегистрацииДенежногоСодержанияДокумента(
				РеквизитыДляПроведения.Ссылка, ?(ТипЗнч(РеквизитыДляПроведения.Ссылка) = Тип("ДокументСсылка.Увольнение"), "", "Сотрудники"));
			ДанныеДляПроведения.Вставить("КорректировкиДляРегистрацииДенежногоСодержания", КорректировкиДляРегистрацииДенежногоСодержания);
			
			СоставМесячногоДенежногоСодержания = Модуль.СведенияОСоставеМесячногоДенежногоСодержания(РеквизитыДляПроведения.Ссылка, "ДенежноеСодержание,ДенежноеСодержаниеФактическиеНачисления");
			ДанныеДляПроведения.Вставить("СоставМесячногоДенежногоСодержания", СоставМесячногоДенежногоСодержания);
		КонецЕсли;
		
		ДополнитьДанныеДляПроведенияОтпусков(ДанныеДляПроведения, РеквизитыДляПроведения);
		
		ДобавитьДанныеДляРегистрацииДокумента(ДанныеДляПроведения, РеквизитыДляПроведения, ПеремещаемыеСовместители, РеквизитыДляПроведения.Организация);
		
		ДобавитьДанныеДляПроведенияПоЗаймам(ДанныеДляПроведения, РеквизитыДляПроведения);
		
		ДобавитьКолонкуНачалоМесяца(ДанныеДляПроведения.УдержанияЗаймов, "ДатаОкончания");
		ДобавитьКолонкуНачалоМесяца(ДанныеДляПроведения.МатериальнаяВыгода, "ДатаПолученияДохода");
		ДобавитьКолонкуНачалоМесяца(ДанныеДляПроведения.ВзаиморасчетыПоЗаймам, "ДатаПогашения");
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КадровыйРезерв") Тогда 
			Модуль = ОбщегоНазначения.ОбщийМодуль("КадровыйРезерв");
			ДанныеКадровогоРезерва = Модуль.ДанныеУвольненияДляКадровогоРезерва(РеквизитыДляПроведения.Ссылка);
			ДанныеДляПроведения.Вставить("ДанныеКадровогоРезерва", ДанныеКадровогоРезерва);
		КонецЕсли;
		
		ДобавитьДанныеДляРеестраКадровыхПриказов(ДанныеДляПроведения, РеквизитыДляПроведения);
		ДобавитьДанныеДляРеестраОтпусков(ДанныеДляПроведения, РеквизитыДляПроведения);
		
		// Подготовка данных для прекращения вычетов
		ДанныеДляПрекращенияВычетов = Новый Соответствие;
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		Запрос.УстановитьПараметр("Ссылка", РеквизитыДляПроведения.Ссылка);
		
		Отборы = Новый Массив;
		
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(
			Отборы, "ГоловнаяОрганизация", "=", ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(РеквизитыДляПроведения.Организация));
		
		Запрос.Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТаблицаДокумента.ФизическоеЛицо КАК ФизическоеЛицо
			|ИЗ
			|	Документ.Увольнение КАК ТаблицаДокумента
			|ГДЕ
			|	ТаблицаДокумента.Ссылка = &Ссылка";
		
		Если ТипЗнч(РеквизитыДляПроведения.Ссылка) <> Тип("ДокументСсылка.Увольнение") Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Документ.Увольнение", "Документ.УвольнениеСписком.Сотрудники");
		КонецЕсли;
		
		ФизическиеЛица = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ФизическоеЛицо");
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(
			Отборы, "ФизическоеЛицо", "В", ФизическиеЛица);
		
		КадровыйУчет.СоздатьВТТекущиеКадровыеДанныеСотрудников(
			Запрос.МенеджерВременныхТаблиц, Ложь, Отборы, "ГоловнаяОрганизация,ДатаПриема,ДатаУвольнения");
		
		Запрос.Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТаблицаДокумента.ФизическоеЛицо КАК ФизическоеЛицо,
			|	НАЧАЛОПЕРИОДА(ТаблицаДокумента.ДатаУвольнения, МЕСЯЦ) КАК МесяцУвольнения
			|ИЗ
			|	Документ.Увольнение КАК ТаблицаДокумента
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ВидыЗанятостиСотрудниковИнтервальный КАК ВидыЗанятостиСотрудниковИнтервальный
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
			|			ПО ВидыЗанятостиСотрудниковИнтервальный.ФизическоеЛицо = ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо
			|				И ВидыЗанятостиСотрудниковИнтервальный.ГоловнаяОрганизация = ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация
			|		ПО ТаблицаДокумента.ФизическоеЛицо = ВидыЗанятостиСотрудниковИнтервальный.ФизическоеЛицо
			|			И (ТаблицаДокумента.ДатаУвольнения МЕЖДУ ВидыЗанятостиСотрудниковИнтервальный.ДатаНачала И ВидыЗанятостиСотрудниковИнтервальный.ДатаОкончания)
			|ГДЕ
			|	ТаблицаДокумента.Ссылка = &Ссылка
			|	И ТекущиеКадровыеДанныеСотрудников.ДатаПриема < ТаблицаДокумента.ДатаУвольнения
			|	И НЕ ТаблицаДокумента.ПрименятьВычетыПослеУвольнения
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаДокумента.ФизическоеЛицо,
			|	НАЧАЛОПЕРИОДА(ТаблицаДокумента.ДатаУвольнения, МЕСЯЦ)
			|
			|ИМЕЮЩИЕ
			|	МАКСИМУМ(ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1)
			|			ИЛИ ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения > ТаблицаДокумента.ДатаУвольнения) = ЛОЖЬ
			|
			|УПОРЯДОЧИТЬ ПО
			|	МесяцУвольнения";
		
		Если ТипЗнч(РеквизитыДляПроведения.Ссылка) <> Тип("ДокументСсылка.Увольнение") Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Документ.Увольнение", "Документ.УвольнениеСписком.Сотрудники");
		КонецЕсли;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("МесяцУвольнения") Цикл
			
			СписокФизическихЛиц = Новый Массив;
			Пока Выборка.Следующий() Цикл
				СписокФизическихЛиц.Добавить(Выборка.ФизическоеЛицо);
			КонецЦикла;
			
			ДанныеДляПрекращенияВычетов.Вставить(Выборка.МесяцУвольнения, СписокФизическихЛиц);
			
		КонецЦикла;
		
		ДанныеДляПроведения.Вставить("ДанныеДляПрекращенияВычетов", ДанныеДляПрекращенияВычетов);
		
	КонецЕсли;
	
	Если СтруктураВидовУчета.ДанныеДляРасчетаСреднего Тогда
		ДополнительныеПараметры = УчетСреднегоЗаработка.ДополнительныеПараметрыРегистрацииДанныхСреднегоЗаработка("Начисления,НачисленияПерерасчет,Пособия");
		ДополнительныеПараметры.МесяцНачисления = "Ссылка.ПериодРегистрации";
		УчетСреднегоЗаработка.ЗаполнитьТаблицыДляРегистрацииДанныхСреднегоЗаработка(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка, ДополнительныеПараметры);
	КонецЕсли;
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

Процедура ДобавитьКолонкуНачалоМесяца(Таблица, ИмяКолонкиСДатой)
	
	Таблица.Колонки.Добавить("НачалоМесяца" + ИмяКолонкиСДатой, Новый ОписаниеТипов("Дата"));
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		СтрокаТаблицы["НачалоМесяца" + ИмяКолонкиСДатой] = НачалоМесяца(СтрокаТаблицы[ИмяКолонкиСДатой]);
	КонецЦикла;
	
КонецПроцедуры

Функция РеквизитыДляПроведения(ДокументСсылка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Увольнение.Ссылка КАК Ссылка,
	|	Увольнение.Организация КАК Организация,
	|	Увольнение.ИсправленныйДокумент КАК ИсправленныйДокумент,
	|	Увольнение.ДокументРассчитан КАК ДокументРассчитан,
	|	Увольнение.ПериодРегистрации КАК ПериодРегистрации,
	|	Увольнение.Дата КАК Дата,
	|	Увольнение.ПорядокВыплаты КАК ПорядокВыплаты,
	|	Увольнение.Номер КАК Номер,
	|	Увольнение.ПланируемаяДатаВыплаты КАК ПланируемаяДатаВыплаты
	|ИЗ
	|	Документ.Увольнение КАК Увольнение
	|ГДЕ
	|	Увольнение.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УвольнениеСписком.Ссылка,
	|	УвольнениеСписком.Организация,
	|	УвольнениеСписком.ИсправленныйДокумент,
	|	УвольнениеСписком.ДокументРассчитан,
	|	УвольнениеСписком.ПериодРегистрации,
	|	УвольнениеСписком.Дата,
	|	УвольнениеСписком.ПорядокВыплаты,
	|	УвольнениеСписком.Номер,
	|	УвольнениеСписком.ПланируемаяДатаВыплаты
	|ИЗ
	|	Документ.УвольнениеСписком КАК УвольнениеСписком
	|ГДЕ
	|	УвольнениеСписком.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УвольнениеРаспределениеПоТерриториямУсловиямТруда.НомерСтроки КАК НомерСтроки,
	|	УвольнениеРаспределениеПоТерриториямУсловиямТруда.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	УвольнениеРаспределениеПоТерриториямУсловиямТруда.Территория КАК Территория,
	|	УвольнениеРаспределениеПоТерриториямУсловиямТруда.УсловияТруда КАК УсловияТруда,
	|	УвольнениеРаспределениеПоТерриториямУсловиямТруда.ДоляРаспределения КАК ДоляРаспределения,
	|	УвольнениеРаспределениеПоТерриториямУсловиямТруда.Результат КАК Результат,
	|	УвольнениеРаспределениеПоТерриториямУсловиямТруда.ИдентификаторСтрокиПоказателей КАК ИдентификаторСтрокиПоказателей
	|ИЗ
	|	Документ.Увольнение.РаспределениеПоТерриториямУсловиямТруда КАК УвольнениеРаспределениеПоТерриториямУсловиямТруда
	|ГДЕ
	|	УвольнениеРаспределениеПоТерриториямУсловиямТруда.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УвольнениеСпискомРаспределениеПоТерриториямУсловиямТруда.НомерСтроки,
	|	УвольнениеСпискомРаспределениеПоТерриториямУсловиямТруда.ИдентификаторСтроки,
	|	УвольнениеСпискомРаспределениеПоТерриториямУсловиямТруда.Территория,
	|	УвольнениеСпискомРаспределениеПоТерриториямУсловиямТруда.УсловияТруда,
	|	УвольнениеСпискомРаспределениеПоТерриториямУсловиямТруда.ДоляРаспределения,
	|	УвольнениеСпискомРаспределениеПоТерриториямУсловиямТруда.Результат,
	|	УвольнениеСпискомРаспределениеПоТерриториямУсловиямТруда.ИдентификаторСтрокиПоказателей
	|ИЗ
	|	Документ.УвольнениеСписком.РаспределениеПоТерриториямУсловиямТруда КАК УвольнениеСпискомРаспределениеПоТерриториямУсловиямТруда
	|ГДЕ
	|	УвольнениеСпискомРаспределениеПоТерриториямУсловиямТруда.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Увольнение.Сотрудник КАК Сотрудник,
	|	Увольнение.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВЫБОР
	|		КОГДА Увольнение.ИсправленныйДокумент <> ЗНАЧЕНИЕ(Документ.Увольнение.ПустаяСсылка)
	|			ТОГДА Увольнение.ПериодРегистрации
	|		ИНАЧЕ Увольнение.МесяцНачалаРасчетов
	|	КОНЕЦ КАК МесяцНачалаРасчетов,
	|	Увольнение.ПериодРегистрации КАК ПериодРегистрации,
	|	Увольнение.ДатаУвольнения КАК ДатаОкончания
	|ИЗ
	|	Документ.Увольнение КАК Увольнение
	|ГДЕ
	|	Увольнение.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УвольнениеСпискомСотрудники.Сотрудник,
	|	УвольнениеСпискомСотрудники.ФизическоеЛицо,
	|	ВЫБОР
	|		КОГДА УвольнениеСпискомСотрудники.Ссылка.ИсправленныйДокумент <> ЗНАЧЕНИЕ(Документ.УвольнениеСписком.ПустаяСсылка)
	|			ТОГДА УвольнениеСпискомСотрудники.Ссылка.ПериодРегистрации
	|		ИНАЧЕ УвольнениеСпискомСотрудники.МесяцНачалаРасчетов
	|	КОНЕЦ,
	|	УвольнениеСпискомСотрудники.Ссылка.ПериодРегистрации,
	|	УвольнениеСпискомСотрудники.ДатаУвольнения
	|ИЗ
	|	Документ.УвольнениеСписком.Сотрудники КАК УвольнениеСпискомСотрудники
	|ГДЕ
	|	УвольнениеСпискомСотрудники.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	МесяцНачалаРасчетов,
	|	Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Результаты = Запрос.ВыполнитьПакет();
	
	РеквизитыДляПроведения = РеквизитыДляПроведенияПустаяСтруктура();
	
	ВыборкаРеквизиты = Результаты[0].Выбрать();
	
	Пока ВыборкаРеквизиты.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(РеквизитыДляПроведения, ВыборкаРеквизиты);
		
	КонецЦикла;
	
	РаспределениеПоТерриториямУсловиямТруда = Результаты[1].Выгрузить();
	
	РеквизитыДляПроведения.РаспределениеПоТерриториямУсловиямТруда = РаспределениеПоТерриториямУсловиямТруда;
	
	ВыборкаПоМесяцамНачалаУчета = Результаты[2].Выбрать();
	РасчетЗарплатыРасширенный.ЗаполнитьПериодыРегистрацииПоПериодамДокумента(РеквизитыДляПроведения, ВыборкаПоМесяцамНачалаУчета);
	
	Возврат РеквизитыДляПроведения;
	
КонецФункции

Функция РеквизитыДляПроведенияПустаяСтруктура()
	
	РеквизитыДляПроведенияПустаяСтруктура = Новый Структура(
		"Ссылка,
		|Организация,
		|ИсправленныйДокумент,
		|ДокументРассчитан,
		|ПериодРегистрации,
		|Дата,
		|ПорядокВыплаты,
		|Номер,
		|РаспределениеПоТерриториямУсловиямТруда,
		|ПланируемаяДатаВыплаты,
		|ПериодыРегистрации");
	
	Возврат РеквизитыДляПроведенияПустаяСтруктура;
	
КонецФункции

Процедура ДобавитьДанныеДляРегистрацииДокумента(ДанныеДляПроведения, РеквизитыДляПроведения, ПеремещаемыеСовместители, Организация)
	
	ДанныеДляРегистрацииВУчете = ДанныеДляРегистрацииВУчетаСтажаПФР(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РеквизитыДляПроведения.Ссылка), ТипЗнч(РеквизитыДляПроведения.Ссылка) <> Тип("ДокументСсылка.Увольнение"), ПеремещаемыеСовместители, Организация);
		
	ДанныеДляПроведения.Вставить("ДанныеДляРегистрацииВУчетаСтажаПФР", ДанныеДляРегистрацииВУчете[РеквизитыДляПроведения.Ссылка]);
	
КонецПроцедуры

Процедура ДобавитьДанныеДляРегистрацииПрекращенияПлановыхНачислений(ДанныеДляПроведения, МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДОБАВИТЬКДАТЕ(КадровыеДанныеСотрудников.Период, ДЕНЬ, 1) КАК Период,
		|	КадровыеДанныеСотрудников.Сотрудник
		|ИЗ
		|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников";
		
	ДанныеДляПроведения.Вставить("ДанныеДляРегистрацииПрекращенияПлановыхНачислений", Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ДобавитьДанныеДляРегистрацииПрекращенияПлановыхУдержаний(ДанныеДляПроведения, МенеджерВременныхТаблиц, ПеремещаемыеСовместители)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если ПеремещаемыеСовместители = Неопределено Тогда
		Запрос.УстановитьПараметр("ПеремещаемыеСовместители", Новый Массив);
	Иначе
		Запрос.УстановитьПараметр("ПеремещаемыеСовместители", ПеремещаемыеСовместители.ВыгрузитьКолонку("Сотрудник"));
	КонецЕсли;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КадровыеДанныеСотрудников.Период КАК Период,
		|	КадровыеДанныеСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
		|	КадровыеДанныеСотрудников.ГоловнаяОрганизация КАК Организация
		|ИЗ
		|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО КадровыеДанныеСотрудников.ФизическоеЛицо = Сотрудники.ФизическоеЛицо
		|			И (Сотрудники.Ссылка В (&ПеремещаемыеСовместители))
		|ГДЕ
		|	КадровыеДанныеСотрудников.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ВнутреннееСовместительство)
		|	И Сотрудники.Ссылка ЕСТЬ NULL";
		
	ДанныеДляПроведения.Вставить("ДанныеДляРегистрацииПрекращенияПлановыхУдержаний", Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ДобавитьДанныеДляРегистрацииСостоянийСотрудников(ДанныеДляПроведения, РеквизитыДляПроведения)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", РеквизитыДляПроведения.Ссылка);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Сотрудник,
		|	ЗНАЧЕНИЕ(Перечисление.СостоянияСотрудника.Увольнение) КАК Состояние,
		|	ДОБАВИТЬКДАТЕ(ТаблицаДокумента.ДатаУвольнения, ДЕНЬ, 1) КАК Начало,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК Окончание,
		|	НЕОПРЕДЕЛЕНО КАК ВидВремени
		|ИЗ
		|	Документ.Увольнение КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка";
	
	Если ТипЗнч(РеквизитыДляПроведения.Ссылка) <> Тип("ДокументСсылка.Увольнение") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Документ.Увольнение", "Документ.УвольнениеСписком.Сотрудники");
	КонецЕсли; 
	
	ДанныеДляПроведения.Вставить("ДанныеДляРегистрацииСостоянийСотрудников", Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ДополнитьДанныеДляПроведенияОтпусков(ДанныеДляПроведения, РеквизитыДляПроведения)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", РеквизитыДляПроведения.Ссылка);
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Если ТипЗнч(РеквизитыДляПроведения.Ссылка) = Тип("ДокументСсылка.Увольнение") Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	Увольнение.Сотрудник КАК Сотрудник,
			|	Увольнение.ДатаУвольнения КАК ДатаУвольнения,
			|	ЗНАЧЕНИЕ(Справочник.ВидыОтпусков.Основной) КАК ВидОтпуска,
			|	Увольнение.РабочийГодС КАК РабочийПериодС,
			|	Увольнение.РабочийГодПо КАК РабочийПериодПо,
			|	Увольнение.ПризнакКомпенсацииУдержанияОтпуска КАК ПризнакКомпенсацииУдержания,
			|	Увольнение.ДнейКомпенсацииУдержанияОтпуска КАК КоличествоДнейКомпенсации,
			|	Увольнение.ОснованиеУвольнения КАК ОснованиеУвольнения
			|ПОМЕСТИТЬ ВТДанныеОтпусков
			|ИЗ
			|	Документ.Увольнение КАК Увольнение
			|ГДЕ
			|	Увольнение.Ссылка = &Ссылка
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Увольнение.Сотрудник,
			|	Увольнение.ДатаУвольнения,
			|	УвольнениеДополнительныеОтпуска.ВидОтпуска,
			|	УвольнениеДополнительныеОтпуска.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск,
			|	УвольнениеДополнительныеОтпуска.КонецПериодаЗаКоторыйПредоставляетсяОтпуск,
			|	УвольнениеДополнительныеОтпуска.ПризнакКомпенсацииУдержания,
			|	УвольнениеДополнительныеОтпуска.ДнейКомпенсацииУдержания,
			|	Увольнение.ОснованиеУвольнения
			|ИЗ
			|	Документ.Увольнение.ДополнительныеОтпуска КАК УвольнениеДополнительныеОтпуска
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Увольнение КАК Увольнение
			|		ПО УвольнениеДополнительныеОтпуска.Ссылка = Увольнение.Ссылка
			|ГДЕ
			|	УвольнениеДополнительныеОтпуска.Ссылка = &Ссылка";
		
	Иначе
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	Сотрудники.Сотрудник КАК Сотрудник,
			|	Сотрудники.ДатаУвольнения КАК ДатаУвольнения,
			|	ЗНАЧЕНИЕ(Справочник.ВидыОтпусков.Основной) КАК ВидОтпуска,
			|	Сотрудники.РабочийГодС КАК РабочийПериодС,
			|	Сотрудники.РабочийГодПо КАК РабочийПериодПо,
			|	Сотрудники.ПризнакКомпенсацииУдержанияОтпуска КАК ПризнакКомпенсацииУдержания,
			|	Сотрудники.ДнейКомпенсацииУдержанияОтпуска КАК КоличествоДнейКомпенсации,
			|	Сотрудники.ОснованиеУвольнения КАК ОснованиеУвольнения
			|ПОМЕСТИТЬ ВТДанныеОтпусков
			|ИЗ
			|	Документ.УвольнениеСписком.Сотрудники КАК Сотрудники
			|ГДЕ
			|	Сотрудники.Ссылка = &Ссылка
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Сотрудники.Сотрудник,
			|	Сотрудники.ДатаУвольнения,
			|	ДополнительныеОтпуска.ВидОтпуска,
			|	ДополнительныеОтпуска.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск,
			|	ДополнительныеОтпуска.КонецПериодаЗаКоторыйПредоставляетсяОтпуск,
			|	ДополнительныеОтпуска.ПризнакКомпенсацииУдержания,
			|	ДополнительныеОтпуска.ДнейКомпенсацииУдержания,
			|	Сотрудники.ОснованиеУвольнения
			|ИЗ
			|	Документ.УвольнениеСписком.ДополнительныеОтпуска КАК ДополнительныеОтпуска
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УвольнениеСписком.Сотрудники КАК Сотрудники
			|		ПО ДополнительныеОтпуска.Ссылка = Сотрудники.Ссылка
			|			И ДополнительныеОтпуска.ИдентификаторСтрокиСотрудника = Сотрудники.ИдентификаторСтрокиСотрудника
			|ГДЕ
			|	ДополнительныеОтпуска.Ссылка = &Ссылка";
		
	КонецЕсли; 
	
	Запрос.Выполнить();
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаДокумента.Сотрудник,
		|	КОНЕЦПЕРИОДА(ТаблицаДокумента.ДатаУвольнения, ДЕНЬ) КАК ДатаУвольнения
		|ИЗ
		|	ВТДанныеОтпусков КАК ТаблицаДокумента";
	Выборка = Запрос.Выполнить().Выбрать();
	
	ДатыНачалаКомпенсаций= Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		ДатыНачалаКомпенсаций.Вставить(Выборка.Сотрудник, Выборка.ДатаУвольнения);
	КонецЦикла; 
	ДанныеДляПроведения.Вставить("ДатыНачалаКомпенсаций", ДатыНачалаКомпенсаций);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Сотрудник КАК Сотрудник,
		|	ТаблицаДокумента.ДатаУвольнения КАК ДатаУвольнения,
		|	ТаблицаДокумента.ВидОтпуска КАК ВидОтпуска,
		|	ТаблицаДокумента.РабочийПериодС КАК РабочийПериодС,
		|	ТаблицаДокумента.РабочийПериодПо КАК РабочийПериодПо
		|ИЗ
		|	ВТДанныеОтпусков КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.ПризнакКомпенсацииУдержания = ЗНАЧЕНИЕ(Перечисление.КомпенсацияУдержаниеОтпускаПриУвольнении.КомпенсироватьНеИспользованные)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник";
	
	ПериодыОтпусков = Новый Соответствие;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Сотрудник") Цикл
		
		ПериодыПоВидамОтпусков = Новый Соответствие;
		Пока Выборка.Следующий() Цикл
			ПериодыПоВидамОтпусков.Вставить(Выборка.ВидОтпуска,
				Новый Структура("РабочийПериодС, РабочийПериодПо", Выборка.РабочийПериодС, Выборка.РабочийПериодПо));
		КонецЦикла; 
		
		ПериодыОтпусков.Вставить(Выборка.Сотрудник, ПериодыПоВидамОтпусков);
		
	КонецЦикла; 
	
	ДанныеДляПроведения.Вставить("РабочиеПериодыКомпенсацийОтпусков", ПериодыОтпусков);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Сотрудник КАК Сотрудник,
	|	ТаблицаДокумента.ВидОтпуска КАК ВидОтпуска,
	|	ТаблицаДокумента.ОснованиеУвольнения
	|ИЗ
	|	ВТДанныеОтпусков КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.ПризнакКомпенсацииУдержания = ЗНАЧЕНИЕ(Перечисление.КомпенсацияУдержаниеОтпускаПриУвольнении.КомпенсироватьНеИспользованные)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник";
		
	ОснованияКомпенсацийОтпусков = Новый Соответствие;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Сотрудник") Цикл
		
		ОснованиеПоВидамОтпусков = Новый Соответствие;
		Пока Выборка.Следующий() Цикл
			ОснованиеПоВидамОтпусков.Вставить(Выборка.ВидОтпуска, Выборка.ОснованиеУвольнения);
		КонецЦикла; 
		
		ОснованияКомпенсацийОтпусков.Вставить(Выборка.Сотрудник, ОснованиеПоВидамОтпусков);
		
	КонецЦикла; 
	
	ДанныеДляПроведения.Вставить("ОснованияКомпенсацийОтпусков", ОснованияКомпенсацийОтпусков);
	
	СпособУдержанияИзлишнеНачисленныхОтпускных = ПолучитьФункциональнуюОпцию("СпособУдержанияИзлишнеНачисленныхОтпускных");
	ОснованияУдержанийОтпусков = Новый Соответствие;
	ПериодыОтпусков = Новый Соответствие;
	
	Если СпособУдержанияИзлишнеНачисленныхОтпускных = Перечисления.СпособыУдержанияИзлишнеНачисленныхОтпускных.Удержание Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ТаблицаДокумента.Сотрудник КАК Сотрудник,
			|	ТаблицаДокумента.ВидОтпуска КАК ВидОтпуска,
			|	ТаблицаДокумента.РабочийПериодС КАК РабочийПериодС,
			|	ТаблицаДокумента.РабочийПериодПо КАК РабочийПериодПо
			|ИЗ
			|	ВТДанныеОтпусков КАК ТаблицаДокумента
			|ГДЕ
			|	ТаблицаДокумента.ПризнакКомпенсацииУдержания = ЗНАЧЕНИЕ(Перечисление.КомпенсацияУдержаниеОтпускаПриУвольнении.УдержатьЗаИспользованныеАвансом)
			|
			|УПОРЯДОЧИТЬ ПО
			|	Сотрудник";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("Сотрудник") Цикл
			
			ПериодыПоВидамОтпусков = Новый Соответствие;
			Пока Выборка.Следующий() Цикл
				ПериодыПоВидамОтпусков.Вставить(Выборка.ВидОтпуска,
					Новый Структура("РабочийПериодС, РабочийПериодПо", Выборка.РабочийПериодС, Выборка.РабочийПериодПо));
			КонецЦикла; 
			
			ПериодыОтпусков.Вставить(Выборка.Сотрудник, ПериодыПоВидамОтпусков);
			
		КонецЦикла; 
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ТаблицаДокумента.Сотрудник КАК Сотрудник,
			|	ТаблицаДокумента.ВидОтпуска КАК ВидОтпуска,
			|	ТаблицаДокумента.ОснованиеУвольнения
			|ИЗ
			|	ВТДанныеОтпусков КАК ТаблицаДокумента
			|ГДЕ
			|	ТаблицаДокумента.ПризнакКомпенсацииУдержания = ЗНАЧЕНИЕ(Перечисление.КомпенсацияУдержаниеОтпускаПриУвольнении.УдержатьЗаИспользованныеАвансом)
			|
			|УПОРЯДОЧИТЬ ПО
			|	Сотрудник";
			
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("Сотрудник") Цикл
			
			ОснованиеПоВидамОтпусков = Новый Соответствие;
			Пока Выборка.Следующий() Цикл
				ОснованиеПоВидамОтпусков.Вставить(Выборка.ВидОтпуска, Выборка.ОснованиеУвольнения);
			КонецЦикла; 
			
			ОснованияУдержанийОтпусков.Вставить(Выборка.Сотрудник, ОснованиеПоВидамОтпусков);
			
		КонецЦикла;
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Сотрудник,
		|	ТаблицаДокумента.ДатаУвольнения,
		|	ТаблицаДокумента.ВидОтпуска КАК ВидЕжегодногоОтпуска,
		|	ТаблицаДокумента.КоличествоДнейКомпенсации КАК КоличествоДнейКомпенсации
		|ИЗ
		|	ВТДанныеОтпусков КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.ПризнакКомпенсацииУдержания = ЗНАЧЕНИЕ(Перечисление.КомпенсацияУдержаниеОтпускаПриУвольнении.УдержатьЗаИспользованныеАвансом)";
		
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаУвольнения,
		|	ЗНАЧЕНИЕ(Справочник.ВидыОтпусков.ПустаяСсылка) КАК ВидЕжегодногоОтпуска,
		|	0 КАК КоличествоДнейКомпенсации";
	КонецЕсли;
	
	ДанныеДляПроведения.Вставить("РабочиеПериодыУдержанийОтпусков", ПериодыОтпусков);
	ДанныеДляПроведения.Вставить("ОснованияУдержанийОтпусков", ОснованияУдержанийОтпусков);
	ДанныеДляПроведения.Вставить("УдержанияОтпусков", Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ДобавитьДанныеДляПроведенияПоЗаймам(ДанныеДляПроведения, РеквизитыДляПроведения)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", РеквизитыДляПроведения.Ссылка);
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Если ТипЗнч(РеквизитыДляПроведения.Ссылка) = Тип("ДокументСсылка.Увольнение") Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПогашениеЗаймов.Ссылка КАК ДокументСсылка,
			|	ПогашениеЗаймов.ИдентификаторСтроки,
			|	ПогашениеЗаймов.НомерСтроки,
			|	ПогашениеЗаймов.ФизическоеЛицо,
			|	ПогашениеЗаймов.Ссылка.Организация КАК Организация,
			|	ПогашениеЗаймов.Подразделение,
			|	ПогашениеЗаймов.Ссылка.ПериодРегистрации КАК Месяц,
			|	ПогашениеЗаймов.ДоговорЗайма,
			|	ПогашениеЗаймов.НачисленоПроцентов,
			|	ПогашениеЗаймов.ПогашениеПроцентов,
			|	ПогашениеЗаймов.ПогашениеЗайма,
			|	ПогашениеЗаймов.МатериальнаяВыгода,
			|	ПогашениеЗаймов.НалогНаМатериальнуюВыгоду,
			|	ПогашениеЗаймов.Ссылка.ДатаУвольнения КАК ДатаОперации
			|ПОМЕСТИТЬ ВТПогашениеЗаймов
			|ИЗ
			|	Документ.Увольнение.ПогашениеЗаймов КАК ПогашениеЗаймов
			|ГДЕ
			|	ПогашениеЗаймов.Ссылка = &Ссылка";
			
	Иначе
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПогашениеЗаймов.Ссылка КАК ДокументСсылка,
			|	ПогашениеЗаймов.ИдентификаторСтроки,
			|	ПогашениеЗаймов.НомерСтроки,
			|	ПогашениеЗаймов.ФизическоеЛицо,
			|	ПогашениеЗаймов.Ссылка.Организация КАК Организация,
			|	ПогашениеЗаймов.Подразделение,
			|	ПогашениеЗаймов.Ссылка.ПериодРегистрации КАК Месяц,
			|	ПогашениеЗаймов.ДоговорЗайма,
			|	ПогашениеЗаймов.НачисленоПроцентов,
			|	ПогашениеЗаймов.ПогашениеПроцентов,
			|	ПогашениеЗаймов.ПогашениеЗайма,
			|	ПогашениеЗаймов.МатериальнаяВыгода,
			|	ПогашениеЗаймов.НалогНаМатериальнуюВыгоду,
			|	Сотрудники.ДатаУвольнения КАК ДатаОперации
			|ПОМЕСТИТЬ ВТПогашениеЗаймов
			|ИЗ
			|	Документ.УвольнениеСписком.ПогашениеЗаймов КАК ПогашениеЗаймов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УвольнениеСписком.Сотрудники КАК Сотрудники
			|		ПО ПогашениеЗаймов.Ссылка = Сотрудники.Ссылка
			|			И ПогашениеЗаймов.ИдентификаторСтрокиСотрудника = Сотрудники.ИдентификаторСтрокиСотрудника
			|ГДЕ
			|	ПогашениеЗаймов.Ссылка = &Ссылка";
			
	КонецЕсли;
		
	Запрос.Выполнить();
	
	ЗаймыСотрудникам.ЗаполнитьДанныеДляПроведенияПоЗаймамПоВременнойТаблице(ДанныеДляПроведения, Запрос.МенеджерВременныхТаблиц, РеквизитыДляПроведения.Ссылка);
	
КонецПроцедуры

Процедура ДобавитьДанныеДляРеестраКадровыхПриказов(ДанныеДляПроведения, РеквизитыДляПроведения)
	
	ДанныеРеестраКадровыхПриказов = КадровыйУчетРасширенный.ТаблицаРеестраКадровыхПриказов();
	НомерПриказа = "";
	ДатаПриказа = Дата(1, 1, 1);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", РеквизитыДляПроведения.Ссылка);
	
	Если ТипЗнч(РеквизитыДляПроведения.Ссылка) = Тип("ДокументСсылка.Увольнение") Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Увольнение.Сотрудник,
		|	Увольнение.ФизическоеЛицо,
		|	Увольнение.Ссылка КАК ДокументОснование,
		|	Увольнение.Номер КАК НомерПриказа,
		|	Увольнение.Дата КАК ДатаПриказа,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение) КАК ВидСобытия,
		|	Увольнение.ДатаУвольнения КАК Дата,
		|	1 КАК Номер,
		|	Увольнение.Организация
		|ИЗ
		|	Документ.Увольнение КАК Увольнение
		|ГДЕ
		|	Увольнение.Ссылка = &Ссылка";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Номер = 1;
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = ДанныеРеестраКадровыхПриказов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			КадровыеДанныеСотрудника = КадровыйУчет.КадровыеДанныеСотрудников(
				Истина, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Выборка.Сотрудник), "ВидДоговора", НоваяСтрока.Дата);
			НоваяСтрока.ВидДоговора	= КадровыеДанныеСотрудника[0].ВидДоговора; 
			НомерПриказа = Выборка.НомерПриказа;
			ДатаПриказа = Выборка.ДатаПриказа;
		КонецЦикла;
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	УвольнениеСпискомСотрудники.Сотрудник,
		|	УвольнениеСпискомСотрудники.ФизическоеЛицо,
		|	УвольнениеСпискомСотрудники.Ссылка КАК ДокументОснование,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение) КАК ВидСобытия,
		|	УвольнениеСпискомСотрудники.Ссылка.Номер КАК НомерПриказа,
		|	УвольнениеСпискомСотрудники.Ссылка.Дата КАК ДатаПриказа,
		|	УвольнениеСпискомСотрудники.НомерСтроки КАК Номер,
		|	УвольнениеСпискомСотрудники.ДатаУвольнения КАК Дата,
		|	УвольнениеСпискомСотрудники.Ссылка.Организация
		|ИЗ
		|	Документ.УвольнениеСписком.Сотрудники КАК УвольнениеСпискомСотрудники
		|ГДЕ
		|	УвольнениеСпискомСотрудники.Ссылка = &Ссылка";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = ДанныеРеестраКадровыхПриказов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			КадровыеДанныеСотрудника = КадровыйУчет.КадровыеДанныеСотрудников(
				Истина, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Выборка.Сотрудник), "ВидДоговора", НоваяСтрока.Дата);
			НоваяСтрока.ВидДоговора	= КадровыеДанныеСотрудника[0].ВидДоговора;   
			НомерПриказа = Выборка.НомерПриказа;
			ДатаПриказа = Выборка.ДатаПриказа;
		КонецЦикла;
	КонецЕсли;
	
	Основание = КадровыйУчетРасширенный.ОснованиеДляРеестра(ДатаПриказа, НомерПриказа);
	ДанныеРеестраКадровыхПриказов.ЗаполнитьЗначения(Основание, "Основание");
	ДанныеДляПроведения.Вставить("ДанныеРеестраКадровыхПриказов", ДанныеРеестраКадровыхПриказов);  
	
КонецПроцедуры

Процедура ДобавитьДанныеДляРеестраОтпусков(ДанныеДляПроведения, РеквизитыДляПроведения)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", РеквизитыДляПроведения.Ссылка);

	ДанныеРеестраОтпусков = КадровыйУчетРасширенный.ТаблицаРеестраОтпусков();
	Основание = КадровыйУчетРасширенный.ОснованиеДляРеестра(РеквизитыДляПроведения.Дата, РеквизитыДляПроведения.Номер);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Увольнение.Сотрудник,
		|	Увольнение.ФизическоеЛицо,
		|	Увольнение.Ссылка КАК ДокументОснование,
		|	ЗНАЧЕНИЕ(Справочник.ВидыОтпусков.Основной) КАК ВидОтпуска,
		|	Увольнение.РабочийГодС КАК НачалоПериодаЗаКоторыйПредоставляетсяОтпуск,
		|	Увольнение.РабочийГодПо КАК КонецПериодаЗаКоторыйПредоставляетсяОтпуск,
		|	Увольнение.ДнейКомпенсацииУдержанияОтпуска КАК КоличествоДнейОтпуска,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаНачалаПериодаОтсутствия,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОкончанияПериодаОтсутствия,
		|	Увольнение.ВидДоговора,
		|	Увольнение.ОснованиеУвольнения,
		|	Увольнение.ИсправленныйДокумент,
		|	Увольнение.Дата КАК Период
		|ПОМЕСТИТЬ ВТДанныеДокумента
		|ИЗ
		|	Документ.Увольнение КАК Увольнение
		|ГДЕ
		|	Увольнение.Ссылка = &Ссылка
		|	И Увольнение.ПризнакКомпенсацииУдержанияОтпуска = ЗНАЧЕНИЕ(Перечисление.КомпенсацияУдержаниеОтпускаПриУвольнении.КомпенсироватьНеИспользованные)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	УвольнениеДополнительныеОтпуска.Ссылка.Сотрудник,
		|	УвольнениеДополнительныеОтпуска.Ссылка.ФизическоеЛицо,
		|	УвольнениеДополнительныеОтпуска.Ссылка,
		|	УвольнениеДополнительныеОтпуска.ВидОтпуска,
		|	УвольнениеДополнительныеОтпуска.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск,
		|	УвольнениеДополнительныеОтпуска.КонецПериодаЗаКоторыйПредоставляетсяОтпуск,
		|	УвольнениеДополнительныеОтпуска.ДнейКомпенсацииУдержания,
		|	ДАТАВРЕМЯ(1, 1, 1),
		|	ДАТАВРЕМЯ(1, 1, 1),
		|	УвольнениеДополнительныеОтпуска.Ссылка.ВидДоговора,
		|	УвольнениеДополнительныеОтпуска.Ссылка.ОснованиеУвольнения,
		|	УвольнениеДополнительныеОтпуска.Ссылка.ИсправленныйДокумент,
		|	УвольнениеДополнительныеОтпуска.Ссылка.Дата
		|ИЗ
		|	Документ.Увольнение.ДополнительныеОтпуска КАК УвольнениеДополнительныеОтпуска
		|ГДЕ
		|	УвольнениеДополнительныеОтпуска.Ссылка = &Ссылка
		|	И УвольнениеДополнительныеОтпуска.ПризнакКомпенсацииУдержания = ЗНАЧЕНИЕ(Перечисление.КомпенсацияУдержаниеОтпускаПриУвольнении.КомпенсироватьНеИспользованные)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	УвольнениеСпискомСотрудники.Сотрудник,
		|	УвольнениеСпискомСотрудники.ФизическоеЛицо,
		|	УвольнениеСпискомСотрудники.Ссылка,
		|	ЗНАЧЕНИЕ(Справочник.ВидыОтпусков.Основной),
		|	УвольнениеСпискомСотрудники.РабочийГодС,
		|	УвольнениеСпискомСотрудники.РабочийГодПо,
		|	УвольнениеСпискомСотрудники.ДнейКомпенсацииУдержанияОтпуска,
		|	ДАТАВРЕМЯ(1, 1, 1),
		|	ДАТАВРЕМЯ(1, 1, 1),
		|	УвольнениеСпискомСотрудники.Ссылка.ВидДоговора,
		|	УвольнениеСпискомСотрудники.ОснованиеУвольнения,
		|	НЕОПРЕДЕЛЕНО,
		|	УвольнениеСпискомСотрудники.Ссылка.Дата
		|ИЗ
		|	Документ.УвольнениеСписком.Сотрудники КАК УвольнениеСпискомСотрудники
		|ГДЕ
		|	УвольнениеСпискомСотрудники.Ссылка = &Ссылка
		|	И УвольнениеСпискомСотрудники.ПризнакКомпенсацииУдержанияОтпуска = ЗНАЧЕНИЕ(Перечисление.КомпенсацияУдержаниеОтпускаПриУвольнении.КомпенсироватьНеИспользованные)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	УвольнениеСпискомСотрудники.Сотрудник,
		|	УвольнениеСпискомСотрудники.ФизическоеЛицо,
		|	УвольнениеСпискомДополнительныеОтпуска.Ссылка,
		|	УвольнениеСпискомДополнительныеОтпуска.ВидОтпуска,
		|	УвольнениеСпискомДополнительныеОтпуска.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск,
		|	УвольнениеСпискомДополнительныеОтпуска.КонецПериодаЗаКоторыйПредоставляетсяОтпуск,
		|	УвольнениеСпискомДополнительныеОтпуска.ДнейКомпенсацииУдержания,
		|	ДАТАВРЕМЯ(1, 1, 1),
		|	ДАТАВРЕМЯ(1, 1, 1),
		|	УвольнениеСпискомДополнительныеОтпуска.Ссылка.ВидДоговора,
		|	УвольнениеСпискомСотрудники.ОснованиеУвольнения,
		|	НЕОПРЕДЕЛЕНО,
		|	УвольнениеСпискомСотрудники.Ссылка.Дата
		|ИЗ
		|	Документ.УвольнениеСписком.ДополнительныеОтпуска КАК УвольнениеСпискомДополнительныеОтпуска
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УвольнениеСписком.Сотрудники КАК УвольнениеСпискомСотрудники
		|		ПО УвольнениеСпискомДополнительныеОтпуска.Ссылка = УвольнениеСпискомСотрудники.Ссылка
		|			И УвольнениеСпискомДополнительныеОтпуска.ИдентификаторСтрокиСотрудника = УвольнениеСпискомСотрудники.ИдентификаторСтрокиСотрудника
		|ГДЕ
		|	УвольнениеСпискомДополнительныеОтпуска.Ссылка = &Ссылка
		|	И УвольнениеСпискомДополнительныеОтпуска.ПризнакКомпенсацииУдержания = ЗНАЧЕНИЕ(Перечисление.КомпенсацияУдержаниеОтпускаПриУвольнении.КомпенсироватьНеИспользованные)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокумента.Период,
		|	ДанныеДокумента.Сотрудник,
		|	ДанныеДокумента.ФизическоеЛицо,
		|	ЕСТЬNULL(РеестрОтпусков.ДокументОснование, ДанныеДокумента.ДокументОснование) КАК ДокументОснование,
		|	ДанныеДокумента.ВидОтпуска,
		|	ДанныеДокумента.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск,
		|	ДанныеДокумента.КонецПериодаЗаКоторыйПредоставляетсяОтпуск,
		|	ДанныеДокумента.КоличествоДнейОтпуска,
		|	ДанныеДокумента.ДатаНачалаПериодаОтсутствия,
		|	ДанныеДокумента.ДатаОкончанияПериодаОтсутствия,
		|	ДанныеДокумента.ВидДоговора КАК ВидДоговора,
		|	ЕСТЬNULL(РеестрОтпусков.Основание, ДанныеДокумента.ОснованиеУвольнения) КАК ОснованиеУвольнения,
		|	ДанныеДокумента.ИсправленныйДокумент
		|ИЗ
		|	ВТДанныеДокумента КАК ДанныеДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрОтпусков КАК РеестрОтпусков
		|		ПО ДанныеДокумента.Сотрудник = РеестрОтпусков.Сотрудник
		|			И ДанныеДокумента.ФизическоеЛицо = РеестрОтпусков.ФизическоеЛицо
		|			И ДанныеДокумента.ВидОтпуска = РеестрОтпусков.ВидОтпуска
		|			И ДанныеДокумента.ИсправленныйДокумент = РеестрОтпусков.Регистратор";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Номер = 1;
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ДанныеРеестраОтпусков.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.Номер = Номер;
		
		Если ЗначениеЗаполнено(Выборка.ИсправленныйДокумент) Тогда
			НоваяСтрока.Основание = Выборка.ОснованиеУвольнения;
		Иначе
			НоваяСтрока.Основание = Основание + " " + Выборка.ОснованиеУвольнения;
		КонецЕсли;
		
		Номер = Номер + 1;
		
	КонецЦикла;
	
	ДанныеДляПроведения.Вставить("ДанныеРеестраОтпусков", ДанныеРеестраОтпусков);

КонецПроцедуры

Процедура ДобавитьДанныеДляПогашенияЗадолженности(ДанныеДляПроведения, РеквизитыДляПроведения, МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КадровыеДанныеСотрудников.Период КАК Период,
		|	КадровыеДанныеСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
		|	КадровыеДанныеСотрудников.ГоловнаяОрганизация КАК Организация
		|ПОМЕСТИТЬ ВТОсновныеСотрудники
		|ИЗ
		|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
		|ГДЕ
		|	КадровыеДанныеСотрудников.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ВнутреннееСовместительство)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОсновныеСотрудники.Период КАК Период
		|ИЗ
		|	ВТОсновныеСотрудники КАК ОсновныеСотрудники";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		НаборЗаписей = РегистрыНакопления.ЗадолженностьПоУдержаниямФизическихЛиц.СоздатьНаборЗаписей();
		ДанныеДляПроведения.ЗадолженностьПоУдержаниям = НаборЗаписей.Выгрузить();
		Возврат;
	КонецЕсли;
	
	Выборка.Следующий();
	Запрос.УстановитьПараметр("Период", НачалоМесяца(Выборка.Период));
	Запрос.УстановитьПараметр("Регистратор", РеквизитыДляПроведения.Ссылка);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	&Период КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ЗадолженностьПоУдержаниямФизическихЛиц.Организация КАК Организация,
		|	ЗадолженностьПоУдержаниямФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ЗадолженностьПоУдержаниямФизическихЛиц.Удержание КАК Удержание,
		|	ЗадолженностьПоУдержаниямФизическихЛиц.ДокументОснование КАК ДокументОснование,
		|	ЗадолженностьПоУдержаниямФизическихЛиц.СуммаОстаток КАК Сумма
		|ПОМЕСТИТЬ ВТЗадолженностьПоУдержаниямФизическихЛиц
		|ИЗ
		|	РегистрНакопления.ЗадолженностьПоУдержаниямФизическихЛиц.Остатки(
		|			&Период,
		|			(Организация, ФизическоеЛицо) В
		|				(ВЫБРАТЬ
		|					ОсновныеСотрудники.Организация,
		|					ОсновныеСотрудники.ФизическоеЛицо
		|				ИЗ
		|					ВТОсновныеСотрудники КАК ОсновныеСотрудники)) КАК ЗадолженностьПоУдержаниямФизическихЛиц
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
		|	ЗадолженностьПоУдержаниямФизическихЛиц.Организация,
		|	ЗадолженностьПоУдержаниямФизическихЛиц.ФизическоеЛицо,
		|	ЗадолженностьПоУдержаниямФизическихЛиц.Удержание,
		|	ЗадолженностьПоУдержаниямФизическихЛиц.ДокументОснование,
		|	ЗадолженностьПоУдержаниямФизическихЛиц.Сумма
		|ИЗ
		|	РегистрНакопления.ЗадолженностьПоУдержаниямФизическихЛиц КАК ЗадолженностьПоУдержаниямФизическихЛиц
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОсновныеСотрудники КАК ОсновныеСотрудники
		|		ПО ЗадолженностьПоУдержаниямФизическихЛиц.Организация = ОсновныеСотрудники.Организация
		|			И ЗадолженностьПоУдержаниямФизическихЛиц.ФизическоеЛицо = ОсновныеСотрудники.ФизическоеЛицо
		|			И (ЗадолженностьПоУдержаниямФизическихЛиц.Период < &Период)
		|			И (ЗадолженностьПоУдержаниямФизическихЛиц.Регистратор = &Регистратор)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗадолженностьПоУдержаниямФизическихЛиц.Период КАК Период,
		|	ЗадолженностьПоУдержаниямФизическихЛиц.ВидДвижения КАК ВидДвижения,
		|	ЗадолженностьПоУдержаниямФизическихЛиц.Организация КАК Организация,
		|	ЗадолженностьПоУдержаниямФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ЗадолженностьПоУдержаниямФизическихЛиц.Удержание КАК Удержание,
		|	ЗадолженностьПоУдержаниямФизическихЛиц.ДокументОснование КАК ДокументОснование,
		|	СУММА(ЗадолженностьПоУдержаниямФизическихЛиц.Сумма) КАК Сумма
		|ИЗ
		|	ВТЗадолженностьПоУдержаниямФизическихЛиц КАК ЗадолженностьПоУдержаниямФизическихЛиц
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗадолженностьПоУдержаниямФизическихЛиц.Период,
		|	ЗадолженностьПоУдержаниямФизическихЛиц.ВидДвижения,
		|	ЗадолженностьПоУдержаниямФизическихЛиц.Организация,
		|	ЗадолженностьПоУдержаниямФизическихЛиц.ФизическоеЛицо,
		|	ЗадолженностьПоУдержаниямФизическихЛиц.Удержание,
		|	ЗадолженностьПоУдержаниямФизическихЛиц.ДокументОснование
		|
		|ИМЕЮЩИЕ
		|	СУММА(ЗадолженностьПоУдержаниямФизическихЛиц.Сумма) > 0";
	
	РезультатЗапроса = Запрос.Выполнить();
	ДанныеДляПроведения.ЗадолженностьПоУдержаниям = РезультатЗапроса.Выгрузить();
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(ДокументОбъект, Отказ) Экспорт
	
	// Подготовка к регистрации перерасчетов
	ДанныеДляРегистрацииПерерасчетов = Новый МенеджерВременныхТаблиц;
	
	СоздатьВТДанныеДокументов(ДанныеДляРегистрацииПерерасчетов, ДокументОбъект.Ссылка);
	ЕстьПерерасчеты = ПерерасчетЗарплаты.СборДанныхДляРегистрацииПерерасчетов(
		ДокументОбъект.Ссылка, ДанныеДляРегистрацииПерерасчетов, ДокументОбъект.Организация, , , Истина);
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКУдалениюПроведения(ДокументОбъект, ЗначениеЗаполнено(ДокументОбъект.ИсправленныйДокумент));
	
	Если Не ЗначениеЗаполнено(ДокументОбъект.ИсправленныйДокумент) Тогда
		ПерерасчетЗарплаты.ВосстановлениеПерерасчетов(ДокументОбъект.Ссылка, ДокументОбъект.Организация);
	КонецЕсли;
	
	// Регистрация перерасчетов
	Если ЕстьПерерасчеты Тогда
		
		ПерерасчетЗарплаты.РегистрацияПерерасчетовПриОтменеПроведения(
			ДокументОбъект.Ссылка, ДанныеДляРегистрацииПерерасчетов, ДокументОбъект.Организация, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(ДокументОбъект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	ПроверкаСтрокиСписочногоДокумента = ДокументОбъект.ДополнительныеСвойства.Свойство("ПроверкаСтрокиСписочногоДокумента");
	Если ПроверкаСтрокиСписочногоДокумента Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Дата");
	КонецЕсли;
	
	ПроверяетсяУвольнение = (ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.Увольнение"));
	
	ПраваНаДокумент = ЗарплатаКадрыРасширенный.ПраваНаМногофункциональныйДокумент(ДокументОбъект);
	
	Если Не ПроверкаСтрокиСписочногоДокумента Тогда
		
		Если ПроверяетсяУвольнение Тогда
			ЗарплатаКадры.ПроверитьКорректностьДаты(ДокументОбъект.Ссылка, ДокументОбъект.ДатаУвольнения, "Объект.ДатаУвольнения", Отказ, НСтр("ru='Дата увольнения'"), , , Ложь);
			ЗарплатаКадры.ПроверитьКорректностьДаты(ДокументОбъект.Ссылка, ДокументОбъект.РабочийГодС, "Объект.РабочийГодС", Отказ, НСтр("ru='Начало периода работы'"), , , Ложь);
		КонецЕсли;
		
		Если ПраваНаДокумент.ОграниченияНаУровнеЗаписей.ИзменениеБезОграничений Тогда 
			ЗарплатаКадры.ПроверитьДатуВыплаты(ДокументОбъект, Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПроверяетсяУвольнение Тогда
		ПроверяемаяКоллекция = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДокументОбъект);
	Иначе
		ПроверяемаяКоллекция = ДокументОбъект.Сотрудники;
	КонецЕсли;
	
	Если Не ПроверкаСтрокиСписочногоДокумента Тогда
		
		СотрудникиДокумента = Новый Соответствие;
		Для каждого ЭлементКоллекции Из ПроверяемаяКоллекция Цикл
			
			// Проверка дублирования строк по сотруднику списочного документа
			Если Не ПроверяетсяУвольнение Тогда
				
				НомерСтроки = СотрудникиДокумента.Получить(ЭлементКоллекции.Сотрудник);
				Если НомерСтроки <> Неопределено Тогда
					
					ТекстСообщения = СтрШаблон(НСтр("ru='По сотруднику %1 уже оформлено увольнение в строке номер'") + " " + НомерСтроки,
						ЭлементКоллекции.Сотрудник);
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ДокументОбъект.Ссылка, "Объект.Сотрудники[" + (ЭлементКоллекции.НомерСтроки - 1) + "].Сотрудник", , Отказ);
					
				Иначе
					СотрудникиДокумента.Вставить(ЭлементКоллекции.Сотрудник, ЭлементКоллекции.НомерСтроки);
				КонецЕсли;
				
			КонецЕсли;
			
			// Проверка возможности проведения по кадровому учету
			ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияРабочихМестВОрганизацийПоВременнойТаблице();
			ПараметрыПолученияСотрудниковОрганизаций.Организация 				= ДокументОбъект.Организация;
			ПараметрыПолученияСотрудниковОрганизаций.НачалоПериода				= ЭлементКоллекции.ДатаУвольнения;
			ПараметрыПолученияСотрудниковОрганизаций.ОкончаниеПериода			= ЭлементКоллекции.ДатаУвольнения;
			ПараметрыПолученияСотрудниковОрганизаций.РаботникиПоДоговорамГПХ 	= Неопределено;
			ПараметрыПолученияСотрудниковОрганизаций.ИсключаемыйРегистратор 	= ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДокументОбъект.Ссылка);
			
			Если ЗначениеЗаполнено(ДокументОбъект.ИсправленныйДокумент) Тогда
				ПараметрыПолученияСотрудниковОрганизаций.ИсключаемыйРегистратор.Добавить(ДокументОбъект.ИсправленныйДокумент);
			КонецЕсли;
			
			КадровыйУчет.ПроверитьРаботающихСотрудников(
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЭлементКоллекции.Сотрудник),
				ПараметрыПолученияСотрудниковОрганизаций,
				Отказ,
				Новый Структура("ИмяПоляСотрудник, ИмяОбъекта", "Сотрудник", "Объект.Сотрудники"));
			
		КонецЦикла;
		
		СотрудникиДаты = Новый ТаблицаЗначений;
		СотрудникиДаты.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
		СотрудникиДаты.Колонки.Добавить("ДатаСобытия", Новый ОписаниеТипов("Дата"));
		СотрудникиДаты.Колонки.Добавить("ВидСобытия", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыКадровыхСобытий"));
		
		Для Каждого ПроверяемаяСтруктура Из ПроверяемаяКоллекция Цикл
			СтрокаСотрудникиДаты = СотрудникиДаты.Добавить();
			СтрокаСотрудникиДаты.Сотрудник = ПроверяемаяСтруктура.Сотрудник;
			СтрокаСотрудникиДаты.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Увольнение;
			СтрокаСотрудникиДаты.ДатаСобытия = ПроверяемаяСтруктура.ДатаУвольнения;
		КонецЦикла;
		
		ВремяРегистрацииДокумента = ЗарплатаКадрыРасширенный.ЗначенияВремениРегистрацииДокумента(ДокументОбъект.Ссылка, СотрудникиДаты);
		
		Для Каждого СтрокаСотрудника Из СотрудникиДаты Цикл
			ВремяРегистрацииСотрудников = ВремяРегистрацииДокумента.Получить(СтрокаСотрудника.ДатаСобытия);
			Если ВремяРегистрацииСотрудников <> Неопределено Тогда 
				СтрокаСотрудника.ДатаСобытия = ВремяРегистрацииСотрудников.Получить(СтрокаСотрудника.Сотрудник) + 86400;
			КонецЕсли;
		КонецЦикла;
		
		ДокументыДляИсключения = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДокументОбъект.Ссылка);
		Если ЗначениеЗаполнено(ДокументОбъект.ИсправленныйДокумент) Тогда
			ДокументыДляИсключения.Добавить(ДокументОбъект.ИсправленныйДокумент);
		КонецЕсли;
		
		КадровыйУчет.ПроверитьВозможностьПроведенияПоКадровомуУчетуТаблицыСотрудников(СотрудникиДаты, ДокументыДляИсключения, Отказ);
		
	КонецЕсли;
	
	ПроверитьЗаполнениеКомпенсацииДополнительныхОтпусков(ДокументОбъект, Отказ, Истина, ПраваНаДокумент);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КадровыйРезерв") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("КадровыйРезерв");
		Модуль.ПроверитьЗаполнениеВидаРезерваВТабличнойЧасти(ДокументОбъект, "КадровыйРезерв", ПроверяемыеРеквизиты, Отказ);
	КонецЕсли;
	
	Если ПраваНаДокумент.ОграниченияНаУровнеЗаписей.ИзменениеБезОграничений
		И ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная") Тогда
		
		ИсправлениеДокументовЗарплатаКадры.ПроверитьЗаполнение(ДокументОбъект, ПроверяемыеРеквизиты, Отказ);
		
		Если Не ПроверкаСтрокиСписочногоДокумента Тогда
			
			// Проверка утверждения производится в не привилегированном режиме
			ЗарплатаКадрыРасширенный.ПроверитьУтверждениеДокумента(ДокументОбъект, Отказ);
			
		КонецЕсли;
		
		ПроверитьПериодДействияНачислений(ДокументОбъект, Отказ);
		
		// Проверка корректности распределения по источникам финансирования и по территориям и условиям труда.
		ИменаТаблицРаспределяемыхПоСтатьямФинансирования   = "Начисления,НачисленияПерерасчет,Пособия,ПособияПерерасчет,Удержания,НДФЛ,ПогашениеЗаймов,КорректировкиВыплаты";
		ИменаТаблицРаспределенияПоТерриториямУсловиямТруда = "Начисления,НачисленияПерерасчет,Пособия,ПособияПерерасчет";
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЛьготыСотрудников") Тогда
			
			ИмяФО = "ИспользоватьЛьготыСотрудников";
			Если ПолучитьФункциональнуюОпцию(ИмяФО) Тогда
				ИменаТаблицРаспределяемыхПоСтатьямФинансирования  = ИменаТаблицРаспределяемыхПоСтатьямФинансирования + ",Льготы";
				ИменаТаблицРаспределенияПоТерриториямУсловиямТруда = ИменаТаблицРаспределенияПоТерриториямУсловиямТруда + ",Льготы";
			КонецЕсли;
			
		КонецЕсли;
		
		ОтражениеЗарплатыВБухучетеРасширенный.ПроверитьРезультатыРаспределенияНачисленийУдержанийОбъекта(
			ДокументОбъект, ИменаТаблицРаспределяемыхПоСтатьямФинансирования, Отказ);
		
		РасчетЗарплатыРасширенный.ПроверитьРаспределениеПоТерриториямУсловиямТрудаДокумента(
			ДокументОбъект, ИменаТаблицРаспределенияПоТерриториямУсловиямТруда, Отказ);
		
	КонецЕсли;
	
	ПроверитьЗаполненностьРеквизитовКомпенсацииУдержанияОтпусков(ДокументОбъект, ПроверяемыеРеквизиты, Отказ, ПраваНаДокумент);
	УточнитьНеобходимостьПроверкиДатыВыплаты(ДокументОбъект, ПроверяемыеРеквизиты, ПраваНаДокумент);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.КадровыйУчетКорпоративная") Тогда
		МодульКадровыйУчетКорпоративный = ОбщегоНазначения.ОбщийМодуль("КадровыйУчетКорпоративный");
		МодульКадровыйУчетКорпоративный.УвольнениеОбработкаПроверкиЗаполнения(ДокументОбъект, Отказ, ПроверяемыеРеквизиты, ПраваНаДокумент);
	Иначе
		
		Если ТипЗнч(ДокументОбъект.Ссылка) <> Тип("ДокументСсылка.Увольнение") Тогда
			
			ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Сотрудники.ДатаУведомленияОбУвольнении");
			ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Сотрудники.КомпенсацияЗаНеотработанныеДниЧасыПриУвольнении");
			
		Иначе
			
			ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ДатаУведомленияОбУвольнении");
			ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "КомпенсацияЗаНеотработанныеДниЧасыПриУвольнении");
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Локально перепишем УчетНДФЛРасширенный.ЗарегистрироватьДоходыИСуммыНДФЛПоВременнойТаблицеНачислений()
// т.к. Увольнение требует особенной регистрации налогов.
//
Процедура ЗарегистрироватьДоходыИСуммыНДФЛПоВременнойТаблицеНачислений(РеквизитыДляПроведения, Движения, ДанныеДляПроведения, ДатаОперацииПоНалогам, Отказ, ПериодРегистрации)
	
	ОкончательныйРасчет = (РеквизитыДляПроведения.ПорядокВыплаты = Перечисления.ХарактерВыплатыЗарплаты.Зарплата);
	УчетНДФЛРасширенный.СформироватьДоходыНДФЛПоНачислениям(
		Движения, Отказ, РеквизитыДляПроведения.Организация, ДатаОперацииПоНалогам, РеквизитыДляПроведения.ПланируемаяДатаВыплаты,
		ДанныеДляПроведения.МенеджерВременныхТаблиц, ПериодРегистрации, Ложь, ОкончательныйРасчет, , РеквизитыДляПроведения.Ссылка);
	
	ОтражениеЗарплатыВБухучетеРасширенный.ДополнитьДоходыНДФЛСведениямиОРаспределенииПоСтатьямФинансирования(Движения);
	
	// Регистрация исчисленного налога.
	УчетНДФЛ.СформироватьНалогиВычеты(
		Движения, Отказ, РеквизитыДляПроведения.Организация, ДатаОперацииПоНалогам, ДанныеДляПроведения.НДФЛ,, ОкончательныйРасчет, РеквизитыДляПроведения.ПланируемаяДатаВыплаты);
	
	УчетНДФЛРасширенный.СформироватьСоциальныеВычетыПоУдержаниям(
		РеквизитыДляПроведения.Ссылка, Движения, Отказ, РеквизитыДляПроведения.Организация, ДатаОперацииПоНалогам, ПериодРегистрации, ДанныеДляПроведения.Удержания,, ОкончательныйРасчет);
	
	// Учет исчисленного налога в "зарплате".
	УчетНачисленнойЗарплаты.ПодготовитьДанныеНДФЛКРегистрации(ДанныеДляПроведения.НДФЛПоСотрудникам, РеквизитыДляПроведения.Организация, ДатаОперацииПоНалогам);
	
	УчетНачисленнойЗарплаты.ЗарегистрироватьНДФЛ(
		Движения, Отказ, РеквизитыДляПроведения.Организация, ПериодРегистрации, ДанныеДляПроведения.НДФЛПоСотрудникам, ДанныеДляПроведения.МенеджерВременныхТаблиц, РеквизитыДляПроведения.ПорядокВыплаты);
	
КонецПроцедуры

Процедура СоздатьВТДанныеДокументов(МенеджерВременныхТаблиц, ДокументСсылка)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Регистратор", ДокументСсылка);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка.Организация КАК Организация,
		|	ТаблицаДокумента.Сотрудник КАК Сотрудник,
		|	ТаблицаДокумента.ДатаУвольнения КАК ПериодДействия,
		|	ТаблицаДокумента.Ссылка КАК ДокументОснование
		|ПОМЕСТИТЬ ВТДанныеДокументов
		|ИЗ
		|	Документ.Увольнение КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Регистратор";
		
	Если ТипЗнч(ДокументСсылка) <> Тип("ДокументСсылка.Увольнение") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Документ.Увольнение", "Документ.УвольнениеСписком.Сотрудники");
	КонецЕсли; 
		
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПроверитьПересечениеФактическогоПериодаДействия(ДокументСсылка, Отказ)
	
	Если Отказ Тогда
		Возврат;	
	КонецЕсли;
	
	ИменаРеквизитов = 
	"ПериодРегистрации,
	|Организация,
	|ИсправленныйДокумент";
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, ИменаРеквизитов);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	Начисления.*
	               |ИЗ
	               |	Документ.Увольнение.Начисления КАК Начисления
	               |ГДЕ
	               |	Начисления.Ссылка = &Ссылка";
				   
	Начисления = Запрос.Выполнить().Выгрузить();
	
	ПараметрыПроверки = РасчетЗарплатыРасширенный.ПараметрыПроверкиПересеченияФактическогоПериодаДействия();
	ПараметрыПроверки.Организация = РеквизитыДокумента.Организация;
	ПараметрыПроверки.ПериодРегистрации = РеквизитыДокумента.ПериодРегистрации;
	ПараметрыПроверки.Документ = ДокументСсылка;
	ПараметрыПроверки.Начисления = Начисления;
	ПараметрыПроверки.ИсправленныйДокумент = РеквизитыДокумента.ИсправленныйДокумент;
	
	РасчетЗарплатыРасширенный.ПроверитьПересечениеФактическогоПериодаДействия(ПараметрыПроверки, Отказ);
	
КонецПроцедуры

#КонецОбласти

Функция ДанныеДляПоказателейСверхурочных(ДокументСсылка) Экспорт
	
	ЗначенияПоказателей = Новый ТаблицаЗначений;
	
	ПереработаноПоСуммированномуУчетуВПределах2Часов = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.ПереработаноПоСуммированномуУчетуВПределах2Часов");
	ПереработаноПоСуммированномуУчету = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.ПереработаноПоСуммированномуУчету");
	
	Если ПереработаноПоСуммированномуУчетуВПределах2Часов = Неопределено ИЛИ ПереработаноПоСуммированномуУчету = Неопределено Тогда
		Возврат ЗначенияПоказателей;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ПереработаноПоСуммированномуУчетуВПределах2Часов", ПереработаноПоСуммированномуУчетуВПределах2Часов);
	Запрос.УстановитьПараметр("ПереработаноПоСуммированномуУчету", ПереработаноПоСуммированномуУчету);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Сотрудник,
		|	ТаблицаДокумента.ФизическоеЛицо,
		|	НАЧАЛОПЕРИОДА(ТаблицаДокумента.ДатаУвольнения, МЕСЯЦ) КАК ПериодДействия,
		|	ТаблицаДокумента.Ссылка.Организация КАК Организация,
		|	&ПереработаноПоСуммированномуУчету КАК Показатель,
		|	ТаблицаДокумента.Сверхурочно1_5 + ТаблицаДокумента.Сверхурочно2 КАК Значение
		|ИЗ
		|	Документ.Увольнение КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|	И ТаблицаДокумента.Сверхурочно1_5 + ТаблицаДокумента.Сверхурочно2 > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаДокумента.Сотрудник,
		|	ТаблицаДокумента.ФизическоеЛицо,
		|	НАЧАЛОПЕРИОДА(ТаблицаДокумента.ДатаУвольнения, МЕСЯЦ),
		|	ТаблицаДокумента.Ссылка.Организация,
		|	&ПереработаноПоСуммированномуУчетуВПределах2Часов,
		|	ТаблицаДокумента.Сверхурочно1_5
		|ИЗ
		|	Документ.Увольнение КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|	И ТаблицаДокумента.Сверхурочно1_5 > 0";
	
	Если ТипЗнч(ДокументСсылка) <> Тип("ДокументСсылка.Увольнение") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Документ.Увольнение", "Документ.УвольнениеСписком.Сотрудники");
	КонецЕсли; 
	
	ЗначенияПоказателей = Запрос.Выполнить().Выгрузить();
	
	Возврат ЗначенияПоказателей;

КонецФункции

Процедура ЗаписатьЗначенияПоказателейРасчетаЗарплаты(Движения, ТаблицаЗначенийПоказателей)
	
	Для Каждого СтрокаЗначений Из ТаблицаЗначенийПоказателей Цикл
		НаборЗаписей = Движения.ЗначенияРазовыхПоказателейРасчетаЗарплатыСотрудников;
		ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), СтрокаЗначений);
		НаборЗаписей.Записывать = Истина;
	КонецЦикла;
	
КонецПроцедуры

Функция ДанныеДляСторнированияУдержанийЗаОтпуск(ДокументСсылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ПризнакКомпенсацииУдержания", Перечисления.КомпенсацияУдержаниеОтпускаПриУвольнении.УдержатьЗаИспользованныеАвансом);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Увольнение.ВидРасчетаКомпенсацииУдержанияОтпуска КАК Удержание
	|ПОМЕСТИТЬ ВТВидыУдержаний
	|ИЗ
	|	Документ.Увольнение КАК Увольнение
	|ГДЕ
	|	Увольнение.ПризнакКомпенсацииУдержанияОтпуска = &ПризнакКомпенсацииУдержания
	|	И Увольнение.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УвольнениеДополнительныеОтпуска.ВидРасчетаКомпенсацииУдержания
	|ИЗ
	|	Документ.Увольнение.ДополнительныеОтпуска КАК УвольнениеДополнительныеОтпуска
	|ГДЕ
	|	УвольнениеДополнительныеОтпуска.ПризнакКомпенсацииУдержания = &ПризнакКомпенсацииУдержания
	|	И УвольнениеДополнительныеОтпуска.Ссылка = &Ссылка";
	
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УвольнениеУдержания.ФизическоеЛицо,
	|	УвольнениеУдержания.ДатаНачала,
	|	УвольнениеУдержания.ДатаОкончания,
	|	УвольнениеУдержания.Удержание,
	|	УвольнениеУдержания.ДокументОснование,
	|	УвольнениеУдержания.Получатель,
	|	УвольнениеУдержания.ПлатежныйАгент,
	|	УвольнениеУдержания.Результат * -1 КАК Результат,
	|	ИСТИНА КАК ФиксРасчет,
	|	ИСТИНА КАК ФиксЗаполнение,
	|	ИСТИНА КАК ФиксСтрока,
	|	УвольнениеУдержания.ИдентификаторСтрокиВидаРасчета
	|ИЗ
	|	ВТВидыУдержаний КАК ВТВидыУдержаний
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Увольнение.Удержания КАК УвольнениеУдержания
	|		ПО ВТВидыУдержаний.Удержание = УвольнениеУдержания.Удержание
	|			И (УвольнениеУдержания.Ссылка = &Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УвольнениеПоказатели.Показатель,
	|	УвольнениеПоказатели.Значение,
	|	УвольнениеПоказатели.ИдентификаторСтрокиВидаРасчета
	|ИЗ
	|	ВТВидыУдержаний КАК ВТВидыУдержаний
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Увольнение.Удержания КАК УвольнениеУдержания
	|		ПО ВТВидыУдержаний.Удержание = УвольнениеУдержания.Удержание
	|			И (УвольнениеУдержания.Ссылка = &Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Увольнение.Показатели КАК УвольнениеПоказатели
	|		ПО (УвольнениеУдержания.ИдентификаторСтрокиВидаРасчета = УвольнениеПоказатели.ИдентификаторСтрокиВидаРасчета)
	|			И (УвольнениеПоказатели.Ссылка = &Ссылка)";
	
	Результат = Запрос.ВыполнитьПакет();
	
	ДанныеДляСторнирования = Новый Структура("Удержания, Показатели");
	ДанныеДляСторнирования.Вставить("Удержания", Результат[0].Выгрузить());
	ДанныеДляСторнирования.Вставить("Показатели", Результат[1].Выгрузить());
	
	Возврат ДанныеДляСторнирования;
	
КонецФункции

Функция ВидыОтпусковОбъекта(ТекущийОбъект) Экспорт

	МассивВидовОтпусков = Новый Массив;
	
	Если ТекущийОбъект.ПризнакКомпенсацииУдержанияОтпуска = ПредопределенноеЗначение("Перечисление.КомпенсацияУдержаниеОтпускаПриУвольнении.КомпенсироватьНеИспользованные")
		ИЛИ ТекущийОбъект.ПризнакКомпенсацииУдержанияОтпуска = ПредопределенноеЗначение("Перечисление.КомпенсацияУдержаниеОтпускаПриУвольнении.УдержатьЗаИспользованныеАвансом") Тогда
		МассивВидовОтпусков.Добавить(ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыОтпусков.Основной"));
	КонецЕсли;
	
	Для каждого ДополнительныйОтпуск Из ТекущийОбъект.ДополнительныеОтпуска Цикл
		Если ДополнительныйОтпуск.ПризнакКомпенсацииУдержания = ПредопределенноеЗначение("Перечисление.КомпенсацияУдержаниеОтпускаПриУвольнении.КомпенсироватьНеИспользованные")
			ИЛИ ДополнительныйОтпуск.ПризнакКомпенсацииУдержания = ПредопределенноеЗначение("Перечисление.КомпенсацияУдержаниеОтпускаПриУвольнении.УдержатьЗаИспользованныеАвансом") Тогда
			МассивВидовОтпусков.Добавить(ДополнительныйОтпуск.ВидОтпуска);
		КонецЕсли;
	КонецЦикла; 
	
	Возврат МассивВидовОтпусков;

КонецФункции

Функция НеобходимоЗаполнитьВидРасчетаКомпенсацииУдержания(ПризнакКомпенсацииУдержания, СпособУдержанияИзлишнеНачисленныхОтпускных, ВидРасчетаКомпенсацииУдержания) Экспорт
	
	Если ПризнакКомпенсацииУдержания <> Перечисления.КомпенсацияУдержаниеОтпускаПриУвольнении.НеИспользовать Тогда
		
		Если СпособУдержанияИзлишнеНачисленныхОтпускных = ПредопределенноеЗначение("Перечисление.СпособыУдержанияИзлишнеНачисленныхОтпускных.СторнированиеРанееНачисленныхСумм")
			И ПризнакКомпенсацииУдержания = ПредопределенноеЗначение("Перечисление.КомпенсацияУдержаниеОтпускаПриУвольнении.УдержатьЗаИспользованныеАвансом") Тогда
			
			Возврат Ложь;
			
		Иначе
			
			Возврат (ПризнакКомпенсацииУдержания = ПредопределенноеЗначение("Перечисление.КомпенсацияУдержаниеОтпускаПриУвольнении.КомпенсироватьНеИспользованные")
						И ТипЗнч(ВидРасчетаКомпенсацииУдержания) <> Тип("ПланВидовРасчетаСсылка.Начисления")
					Или ПризнакКомпенсацииУдержания = ПредопределенноеЗначение("Перечисление.КомпенсацияУдержаниеОтпускаПриУвольнении.УдержатьЗаИспользованныеАвансом")
						И ТипЗнч(ВидРасчетаКомпенсацииУдержания) <> Тип("ПланВидовРасчетаСсылка.Удержания")
					Или Не ЗначениеЗаполнено(ВидРасчетаКомпенсацииУдержания));
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции




// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецЕсли

