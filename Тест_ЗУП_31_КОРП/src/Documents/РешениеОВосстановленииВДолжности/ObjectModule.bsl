#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ДатаВосстановления = ТекущаяДатаСеанса();
	ОбъектОснование = ДанныеЗаполнения;
	
	Если ТипЗнч(ОбъектОснование) = Тип("ДокументСсылка.РешениеОбУвольнении") Тогда
		
		ДокументОснование = ОбъектОснование;
		ОбъектОснование = ОбъектОснование.Сотрудник;
		
		УстановитьПривилегированныйРежим(Истина);
		КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Ложь, ОбъектОснование, "ДатаУвольнения", ДатаВосстановления);
		УстановитьПривилегированныйРежим(Ложь);
		
		ДатаУвольнения = '00010101';
		Если КадровыеДанные.Количество() > 0 Тогда
			ДатаУвольнения = КадровыеДанные[0].ДатаУвольнения;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДатаУвольнения) Тогда
			
			ТекстПредупреждения = НСтр("ru='Сотрудник %1 еще не уволен'");
			ТекстПредупреждения = СтрШаблон(ТекстПредупреждения, ОбъектОснование);
			
			ВызватьИсключение ТекстПредупреждения;
			
		КонецЕсли;
		
	КонецЕсли;
	
	АдаптацияУвольнение.ЗаполнитьПоСотруднику(ЭтотОбъект, ОбъектОснование, Истина, Истина);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Проведение документа
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ДанныеДляПроведения = ДанныеДляПроведения();
	
	АдаптацияУвольнение.СформироватьДвиженияМероприятияАдаптацииУвольненияПоДокументу(Движения, ДанныеДляПроведения.ДанныеМероприятийАдаптацииУвольнения);
	АдаптацияУвольнение.ЗарегистрироватьДокументДляОбновленияЗаданийАдаптацииУвольнения(Движения);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, ДатаВосстановления, "Объект.ДатаВосстановления", Отказ, НСтр("ru='Дата восстановления'"), , , Ложь);
	
	АдаптацияУвольнение.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты)
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	АдаптацияУвольнение.ОбработкаУдаленияПроведения(ЭтотОбъект, Отказ);
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	АдаптацияУвольнение.ПриЗаписи(ЭтотОбъект, Отказ)
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене.
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	Возврат ОбменДаннымиЗарплатаКадры.ОграниченияРегистрацииПоОрганизацииИСотруднику(ЭтотОбъект, Организация, Сотрудник, ДатаВосстановления);
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведения() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВосстановленииВДолжности.Сотрудник КАК Сотрудник,
	|	ВосстановленииВДолжности.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Мероприятия.Мероприятие КАК Мероприятие,
	|	Мероприятия.ДатаНачала КАК ДатаНачала,
	|	Мероприятия.ДатаОкончания КАК ДатаОкончания,
	|	Мероприятия.Исполнитель КАК Исполнитель
	|ИЗ
	|	Документ.РешениеОВосстановленииВДолжности.МероприятияАдаптацииУвольнения КАК Мероприятия
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РешениеОВосстановленииВДолжности КАК ВосстановленииВДолжности
	|		ПО Мероприятия.Ссылка = ВосстановленииВДолжности.Ссылка
	|ГДЕ
	|	Мероприятия.Ссылка = &Ссылка";
	
	ДанныеМероприятий = Запрос.Выполнить().Выгрузить();
	ДанныеМероприятий.Колонки.Добавить("ДокументОснование", Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.СобытияАдаптацииУвольнения.Тип));
	ДанныеМероприятий.ЗаполнитьЗначения(Ссылка, "ДокументОснование");
	
	ДанныеДляПроведения = Новый Структура;
	ДанныеДляПроведения.Вставить("ДанныеМероприятийАдаптацииУвольнения", ДанныеМероприятий);
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

#КонецОбласти

#КонецЕсли
