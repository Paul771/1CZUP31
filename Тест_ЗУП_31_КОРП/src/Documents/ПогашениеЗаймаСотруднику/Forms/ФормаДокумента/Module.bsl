
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая() Тогда
		ЗначенияДляЗаполнения = Новый Структура("Организация, Ответственный", 
			"Объект.Организация", "Объект.Ответственный");
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
		Элементы.Сумма.РасширеннаяПодсказка.Заголовок = ИнформацияОЗадолженности(Объект.ДоговорЗайма, Объект.Дата, Объект.Ссылка);
	КонецЕсли;
	
	// Обработчик подсистемы "ВерсионированиеОбъектов".
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	Элементы.Сумма.РасширеннаяПодсказка.Заголовок = ИнформацияОЗадолженности(ТекущийОбъект.ДоговорЗайма, ТекущийОбъект.Дата, ТекущийОбъект.Ссылка);
	
	ОбменДаннымиЗарплатаКадры.ПриЧтенииНаСервереДокумента(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Запись_ПогашениеЗаймаСотруднику", ПараметрыЗаписи, Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	Элементы.Сумма.РасширеннаяПодсказка.Заголовок = ИнформацияОЗадолженности(Объект.ДоговорЗайма, Объект.Дата, Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ФизическоеЛицоПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Объект.ДоговорЗайма) Тогда
		Объект.ДоговорЗайма = ФизическоеЛицоПриИзмененииНаСервере(Объект.Организация, Объект.ФизическоеЛицо, Объект.Дата);
	КонецЕсли;
	
	Элементы.Сумма.РасширеннаяПодсказка.Заголовок = ИнформацияОЗадолженности(Объект.ДоговорЗайма, Объект.Дата, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорЗаймаПриИзменении(Элемент)
	
	Элементы.Сумма.РасширеннаяПодсказка.Заголовок = ИнформацияОЗадолженности(Объект.ДоговорЗайма, Объект.Дата, Объект.Ссылка);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ИнформацияОЗадолженности(Договор, ДатаДокумента, ДокументСсылка)
	
	Возврат ЗаймыСотрудникам.ИнформацияОЗадолженности(Договор, ДатаДокумента, ДокументСсылка);
	
КонецФункции

&НаСервереБезКонтекста
Функция ФизическоеЛицоПриИзмененииНаСервере(Организация, ФизическоеЛицо, ДатаАктуальности)
	
	ДействующиеДоговоры = ЗаймыСотрудникам.ДействующиеДоговорыЗаймаПоФизическомуЛицу(Организация, ФизическоеЛицо, ДатаАктуальности);
	
	Если ДействующиеДоговоры <> Неопределено И ДействующиеДоговоры.Количество() = 1 Тогда
		Возврат ДействующиеДоговоры[0];
	КонецЕсли;
	
КонецФункции

#КонецОбласти
