
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Если Параметры.Ключ.Пустая() Тогда
		
		// Заполнение нового документа.
		ЗначенияДляЗаполнения = Новый Структура("Организация, Ответственный", "Объект.Организация", "Объект.Ответственный");
		ЗначенияДляЗаполнения.Вставить("Год", "Объект.ГодАттестации");
		ЗначенияДляЗаполнения.Вставить("Руководитель", "Объект.Руководитель");
		ЗначенияДляЗаполнения.Вставить("ДолжностьРуководителя", "Объект.ДолжностьРуководителя");
		ЗначенияДляЗаполнения.Вставить("ВидАттестации", "Объект.ВидАттестации");
		
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);

	КонецЕсли;	
	
	// Обработчик подсистемы "ВерсионированиеОбъектов".
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбменДаннымиЗарплатаКадры.ПриЧтенииНаСервереДокумента(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Запись_ФормированиеАттестационнойКомиссииСотрудников", ПараметрыЗаписи, Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидАттестацииПриИзменении(Элемент)
	ВидАттестацииПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыСоставАттестационнойКомиссии

&НаКлиенте
Процедура СоставАттестационнойКомиссииОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Для каждого ЭлементМассива Из ВыбранноеЗначение Цикл
	  НоваяСтрока = Объект.СоставАттестационнойКомиссии.Добавить();
	  НоваяСтрока.ЧленКомиссии = ЭлементМассива;
	  НоваяСтрока.РольВКомиссии = ПредопределенноеЗначение("Перечисление.РолиЧленовАттестационнойКомиссии.ЧленКомиссии");
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодборФизическихЛиц(Команда)
		
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("МножественныйВыбор", 	Истина);
	ПараметрыОткрытияФормы.Вставить("ЗакрыватьПриВыборе", 	Ложь);
	ПараметрыОткрытияФормы.Вставить("РежимВыбора", Истина);

	ОткрытьФорму("Справочник.ФизическиеЛица.ФормаВыбора", ПараметрыОткрытияФормы, Элементы.СоставАттестационнойКомиссии,);	
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СерверныеОбработчикиСобытийФормы

&НаСервере
Процедура ВидАттестацииПриИзмененииНаСервере()
	НастроитьФормуПоВидуАттестации();
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура НастроитьФормуПоВидуАттестации()
	
	УстановитьВидимостьГодаАттестации();
	УстановитьВидимостьВидаАкта();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьГодаАттестации()
	ОтображатьГодаАттестации = АттестацииСотрудниковФормы.ДлительностьАттестационнойКампанииГод(Объект.ВидАттестации);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "ГодАттестации", "Видимость", ОтображатьГодаАттестации);
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьВидаАкта()
	ОтображатьВидАкта = АттестацииСотрудниковФормы.РедактироватьВидАктаУтверждающегоКомиссию(Объект.ВидАттестации);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "ВидАкта", "Видимость", ОтображатьВидАкта);
КонецПроцедуры

#КонецОбласти
