#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	// Посчитать суммы документа и записать ее в соответствующий реквизит шапки.
	СуммаДокумента = Депоненты.Итог("Сумма");

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ДанныеДляПроведения = ДанныеДляПроведения();

	Если НЕ Отказ Тогда
		УчетДепонированнойЗарплаты.СписатьДепонированнуюЗарплату(Движения, Отказ, Организация, Дата, ДанныеДляПроведения.Депоненты, Ложь, НомерПлатежногоДокумента);
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	Возврат ОбменДаннымиЗарплатаКадры.ОграниченияРегистрацииПоОрганизацииИФизическомуЛицу(ЭтотОбъект, Организация, ФизическоеЛицо, Дата);
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура Автозаполнение() Экспорт

	ОстатокДепонированнойЗарплаты = 
		УчетДепонированнойЗарплаты.ОстатокДепонированнойЗарплаты(Ссылка, Организация, Дата, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо));
		
	ОстатокДепонированнойЗарплаты.Свернуть("Ведомость", "Сумма");		
	Депоненты.Загрузить(ОстатокДепонированнойЗарплаты);
	
КонецПроцедуры //  Автозаполнение

Функция ДанныеДляПроведения()
	
	ТаблицаДепонентов = Депоненты.Выгрузить();
	ТаблицаДепонентов.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаДепонентов.ЗаполнитьЗначения(ФизическоеЛицо, "ФизическоеЛицо");
	
	ДанныеДляПроведения = Новый Структура;
	ДанныеДляПроведения.Вставить("Депоненты", ТаблицаДепонентов);
	
	Возврат ДанныеДляПроведения
	
КонецФункции

#КонецОбласти

#КонецЕсли