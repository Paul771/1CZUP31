#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	ДанныеДляПроведения = Документы.ЗаявкаНаВключениеВКадровыйРезерв.ПолучитьДанныеДляПроведения(Ссылка);
	КадровыйРезерв.СформироватьДвиженияИсторииКадровогоРезерва(Движения, ДанныеДляПроведения, "ДвиженияИсторииКадровогоРезерва");
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, ДатаВыдвижения, "Объект.ДатаВыдвижения", Отказ, НСтр("ru='Дата выдвижения'"), , , Ложь);
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйРезервПоВидам") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ВидРезерва");
	КонецЕсли;
		
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ДатаВыдвижения = НачалоДня(ДатаВыдвижения) + КадровыйРезерв.ЗначениеВремениПоСтатусуРезерва(ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Рассматривается"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	Возврат ОбменДаннымиЗарплатаКадрыРасширенный.ОграниченияРегистрацииПоПодразделениюИФизическомуЛицу(ЭтотОбъект, Подразделение, ФизическоеЛицо, ДатаВыдвижения);
КонецФункции

#КонецОбласти

#КонецЕсли