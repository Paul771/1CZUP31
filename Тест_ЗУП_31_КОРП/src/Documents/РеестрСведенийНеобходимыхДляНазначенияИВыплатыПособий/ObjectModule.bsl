#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	// Удаление полей с условной проверкой заполнения.
	Если Не (ВидРеестра = Перечисления.ВидыРеестровСведенийНеобходимыхДляНазначенияИВыплатыПособий.ПособияПоНетрудоспособности) Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.НомерЛисткаНетрудоспособности");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.ДатаВыдачиЛисткаНетрудоспособности");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.КодПричиныНетрудоспособности");
	КонецЕсли;
	
	// Удаление полей, проверка которых выполняется в функции ПроверитьЗаполнениеПоРегламентуФСС.
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "КодПодчиненностиФСС");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ОГРН");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "КПП");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ИНН");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Руководитель");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ГлавныйБухгалтер");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ДолжностьРуководителя");
	
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.МедицинскаяОрганизация");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.НаименованиеМедицинскойОрганизации");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.ОГРНМедицинскойОрганизации");
	
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.ФамилияРебенка");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.ИмяРебенка");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.ДатаРожденияРебенка");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.ОчередностьРожденияРебенка");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.ВидПодтверждающегоДокумента");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.НаименованиеПодтверждающегоДокумента");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.ДатаДокумента");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.СерияДокумента");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.НомерДокумента");
	
	// Проверка значений.
	ПроверитьВыбранныеЗаявления(Отказ);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение И ОбъектЗафиксирован() Тогда
		ФиксацияВторичныхДанныхВДокументах.ЗафиксироватьВторичныеДанныеДокумента(ЭтотОбъект);
	КонецЕсли;
	
	ЗаполнитьФизическихЛицИзЗаявлений();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене.
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//  Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	Если ДополнительныеСвойства.Свойство("ОграниченияРегистрации") Тогда
		// Ограничения регистрации уже были получены
		Возврат ДополнительныеСвойства.ОграниченияРегистрации;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСсылок", СведенияНеобходимыеДляНазначенияПособий.ВыгрузитьКолонку("Заявление"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаявлениеСотрудникаНаВыплатуПособия.ФизическоеЛицо
	|ИЗ
	|	Документ.ЗаявлениеСотрудникаНаВыплатуПособия КАК ЗаявлениеСотрудникаНаВыплатуПособия
	|ГДЕ
	|	ЗаявлениеСотрудникаНаВыплатуПособия.Ссылка В(&МассивСсылок)";
	
	МассивФизическихЛиц = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ФизическоеЛицо");
	
	ОграниченияРегистрации = ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации();
	ОграниченияРегистрации.ФизическиеЛица = МассивФизическихЛиц;
	ОграниченияРегистрации.ДатаПолученияДанных = Дата;
	ОграниченияРегистрации.Организации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
	
	Возврат ОграниченияРегистрации;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПередЗаписью

Процедура ЗаполнитьФизическихЛицИзЗаявлений()
	ФизическиеЛица.Очистить();
	
	Если СведенияНеобходимыеДляНазначенияПособий.Количество() > 0 Тогда
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаявлениеСотрудникаНаВыплатуПособия.ФизическоеЛицо
		|ИЗ
		|	Документ.ЗаявлениеСотрудникаНаВыплатуПособия КАК ЗаявлениеСотрудникаНаВыплатуПособия
		|ГДЕ
		|	ЗаявлениеСотрудникаНаВыплатуПособия.Ссылка В(&МассивСсылок)";
		Запрос.УстановитьПараметр("МассивСсылок", СведенияНеобходимыеДляНазначенияПособий.ВыгрузитьКолонку("Заявление"));
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицуИзМассива(ФизическиеЛица, Результат.Выгрузить().ВыгрузитьКолонку("ФизическоеЛицо"), "ФизическоеЛицо");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработкаПроверкиЗаполнения

Процедура ПроверитьВыбранныеЗаявления(Отказ)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Заявления.НомерСтроки КАК НомерСтроки,
	|	Заявления.Заявление КАК Заявление
	|ПОМЕСТИТЬ ВТЗаявления
	|ИЗ
	|	&Заявления КАК Заявления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ВТЗаявления.НомерСтроки) КАК НомерСтроки,
	|	ЗаявленияПовторы.НомерСтроки КАК НомерСтрокиПовтора,
	|	ВТЗаявления.Заявление КАК Заявление
	|ИЗ
	|	ВТЗаявления КАК ВТЗаявления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЗаявления КАК ЗаявленияПовторы
	|		ПО ВТЗаявления.Заявление = ЗаявленияПовторы.Заявление
	|			И ВТЗаявления.НомерСтроки < ЗаявленияПовторы.НомерСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТЗаявления.Заявление,
	|	ЗаявленияПовторы.НомерСтроки
	|ИТОГИ ПО
	|	Заявление
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТЗаявления.НомерСтроки КАК НомерСтроки,
	|	ВТЗаявления.Заявление КАК Заявление
	|ИЗ
	|	ВТЗаявления КАК ВТЗаявления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявлениеСотрудникаНаВыплатуПособия КАК ЗаявлениеСотрудникаНаВыплатуПособия
	|		ПО ВТЗаявления.Заявление = ЗаявлениеСотрудникаНаВыплатуПособия.Ссылка
	|ГДЕ
	|	НЕ ЗаявлениеСотрудникаНаВыплатуПособия.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТЗаявления.Заявление КАК Заявление,
	|	ВТЗаявления.НомерСтроки КАК НомерСтроки,
	|	ДругиеРеестрыСЭтимиЗаявлениями.Ссылка КАК Ссылка
	|ИЗ
	|	ВТЗаявления КАК ВТЗаявления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеестрСведенийНеобходимыхДляНазначенияИВыплатыПособий.СведенияНеобходимыеДляНазначенияПособий КАК ДругиеРеестрыСЭтимиЗаявлениями
	|		ПО ВТЗаявления.Заявление = ДругиеРеестрыСЭтимиЗаявлениями.Заявление
	|ГДЕ
	|	ДругиеРеестрыСЭтимиЗаявлениями.Ссылка <> &Ссылка
	|	И НЕ ДругиеРеестрыСЭтимиЗаявлениями.Ссылка.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Заявления", СведенияНеобходимыеДляНазначенияПособий.Выгрузить(,"НомерСтроки, Заявление"));
	
	УстановитьПривилегированныйРежим(Истина);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	ПовторяющиесяЗаявления = МассивРезультатов[1];
	НепроведенныеЗаявления = МассивРезультатов[2];
	УжеИспользованныеЗаявления = МассивРезультатов[3];
	
	ИмяТаблицы = "СведенияНеобходимыеДляНазначенияПособий";
	
	Если Не ПовторяющиесяЗаявления.Пустой() Тогда
		Выборка = ПовторяющиесяЗаявления.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока Выборка.Следующий() Цикл
			ВыборкаПоЗаявлению = Выборка.Выбрать();
			Если ВыборкаПоЗаявлению.Следующий() Тогда
				ТекстПовторныеСтроки = "" + ВыборкаПоЗаявлению.НомерСтроки + ", " + ВыборкаПоЗаявлению.НомерСтрокиПовтора;
				Пока ВыборкаПоЗаявлению.Следующий() Цикл
					ТекстПовторныеСтроки = ТекстПовторныеСтроки + ", " + ВыборкаПоЗаявлению.НомерСтрокиПовтора;
				КонецЦикла;
				Текст = СтрШаблон(НСтр("ru = '%1 использовано в нескольких строках: %2.'"), Выборка.Заявление, ТекстПовторныеСтроки);
				СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Выборка.НомерСтроки, "Заявление");
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не НепроведенныеЗаявления.Пустой() Тогда
		Выборка = НепроведенныеЗаявления.Выбрать();
		Пока Выборка.Следующий() Цикл
			Текст = СтрШаблон(НСтр("ru = '%1 не проведено.'"), Выборка.Заявление);
			СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Выборка.НомерСтроки, "Заявление");
		КонецЦикла;
	КонецЕсли;
	
	Если Не УжеИспользованныеЗаявления.Пустой() Тогда
		Выборка = УжеИспользованныеЗаявления.Выбрать();
		Пока Выборка.Следующий() Цикл
			Текст = СтрШаблон(НСтр("ru = '%1 уже использовано в документе %2.'"), Выборка.Заявление, Выборка.Ссылка);
			СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Выборка.НомерСтроки, "Заявление");
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ПроверкиЗаполненияПоРегламентуФСС

Функция ПроверитьЗаполнениеПоРегламентуФСС() Экспорт
	
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(КодПодчиненностиФСС) Тогда
		СообщитьОбОшибке(Отказ, Неопределено, Неопределено, Неопределено, "КодПодчиненностиФСС");
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ОГРН) Тогда
		СообщитьОбОшибке(Отказ, Неопределено, Неопределено, Неопределено, "ОГРН");
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ИНН) Тогда
		СообщитьОбОшибке(Отказ, Неопределено, Неопределено, Неопределено, "ИНН");
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Руководитель) Тогда
		СообщитьОбОшибке(Отказ, Неопределено, Неопределено, Неопределено, "Руководитель");
	КонецЕсли;
	
	Если ЗарплатаКадры.ЭтоЮридическоеЛицо(Организация) Тогда
		Если Не ЗначениеЗаполнено(КПП) Тогда
			СообщитьОбОшибке(Отказ, Неопределено, Неопределено, Неопределено, "КПП");
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ГлавныйБухгалтер) Тогда
			СообщитьОбОшибке(Отказ, Неопределено, Неопределено, Неопределено, "ГлавныйБухгалтер");
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ДолжностьРуководителя) Тогда
			СообщитьОбОшибке(Отказ, Неопределено, Неопределено, Неопределено, "ДолжностьРуководителя");
		КонецЕсли;
	КонецЕсли;
	
	Если ВидРеестра = Перечисления.ВидыРеестровСведенийНеобходимыхДляНазначенияИВыплатыПособий.ПособияПоНетрудоспособности Тогда
		ПроверитьЗаполнениеСтрокСведенийПоВременнойНетрудоспособности(Отказ);
	ИначеЕсли ВидРеестра = Перечисления.ВидыРеестровСведенийНеобходимыхДляНазначенияИВыплатыПособий.ЕжемесячныеПособияПоУходуЗаРебенком Тогда
		ПроверитьЗаполнениеСтрокСведенийПоУходуЗаРебенком(Отказ);
	КонецЕсли;
	
	Возврат Не Отказ;
	
КонецФункции

Процедура ПроверитьЗаполнениеСтрокСведенийПоВременнойНетрудоспособности(Отказ)
	ИмяТаблицы = "СведенияНеобходимыеДляНазначенияПособий";
	
	МассивЗаявлений = СведенияНеобходимыеДляНазначенияПособий.ВыгрузитьКолонку("Заявление");
	УстановитьПривилегированныйРежим(Истина);
	РеквизитыЗаявлений = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивЗаявлений, "ВидПособия,ТипЗанятостиРаботника,ДатаНачалаТрудовогоДоговораПолучателя,ДатаОкончанияТрудовогоДоговораПолучателя,ПорядокРасчетаСреднегоЗаработка");
	УстановитьПривилегированныйРежим(Ложь);
	
	Для Каждого Строка Из СведенияНеобходимыеДляНазначенияПособий Цикл
		
		Если Не ЗначениеЗаполнено(Строка.МедицинскаяОрганизация) Тогда
			Текст = НСтр("ru = 'Не выбрана медицинская организация, выдавшая листок нетрудоспособности.'");
			СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "МедицинскаяОрганизация");
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.НаименованиеМедицинскойОрганизации) Тогда
			Текст = НСтр("ru = 'Не заполнено наименование медицинской организации.'");
			СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "НаименованиеМедицинскойОрганизации");
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.ОГРНМедицинскойОрганизации) Тогда
			Текст = НСтр("ru = 'Не заполнен ОГРН медицинской организации.'");
			СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ОГРНМедицинскойОрганизации");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Строка.Заявление) Тогда
			РеквизитыЗаявления = РеквизитыЗаявлений.Получить(Строка.Заявление);
			
			Если РеквизитыЗаявления.ТипЗанятостиРаботника = Перечисления.ВидыЗанятости.Совместительство
				И Не ЗначениеЗаполнено(Строка.НомерЛисткаПоОсновномуМестуРаботы) Тогда
				Текст = НСтр("ru = 'Для совместителя должен быть указан номер листка нетрудоспособности, предъявляемого на основном месте работы.'");
				СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "НомерЛисткаПоОсновномуМестуРаботы");
			КонецЕсли;
			
			Если (РеквизитыЗаявления.ВидПособия = Перечисления.ПособияНазначаемыеФСС.ПособиеПоБеременностиИРодам
				Или РеквизитыЗаявления.ВидПособия = Перечисления.ПособияНазначаемыеФСС.ПособиеПоБеременностиИРодамВставшимНаУчетВРанниеСроки) Тогда
				
				Если Строка.КодПричиныНетрудоспособности <> "05" Тогда
					Текст = НСтр("ru = 'Для пособия по беременности и родам должен быть указан код причины нетрудоспособности 05.'");
					СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "КодПричиныНетрудоспособности");
				КонецЕсли;
				
				Если РеквизитыЗаявления.ПорядокРасчетаСреднегоЗаработка <> Перечисления.СпособыРасчетаСреднегоЗаработкаДляОплатыПособия.СреднийЗаработок Тогда
					ДатаПроверки = УчетПособийСоциальногоСтрахованияКлиентСервер.ДатаОтдельногоРасчетаПособийПоМатеринству();
					Если Строка.ОсвобождениеДатаНачала1 >= ДатаПроверки Тогда
						Текст = НСтр("ru = 'Для отпуска по беременности и родам, начинающегося 1 января 2013 года или позже, указан недопустимый порядок расчета среднего заработка: из оклада или МРОТ.'");
						СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ПорядокРасчетаСреднегоЗаработка");
					КонецЕсли;
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(Строка.ДатаИзмененияКодаПричиныНетрудоспособности)
					И Строка.ЯвляетсяПервичнымЛисткомНетрудоспособности
					И Не ЗначениеЗаполнено(Строка.ДополнительныйКодПричиныНетрудоспособности) Тогда
					Текст = НСтр("ru = 'Для пособия по беременности и родам в поле ""Дата 1"" должна быть указана предполагаемая дата родов.'");
					СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ДатаИзмененияКодаПричиныНетрудоспособности");
				КонецЕсли;
				
			КонецЕсли;
			
			Если (Строка.УсловияИсчисленияКод1 = "46" Или Строка.УсловияИсчисленияКод2 = "46" Или Строка.УсловияИсчисленияКод3 = "46")
				И (Не ЗначениеЗаполнено(РеквизитыЗаявления.ДатаНачалаТрудовогоДоговораПолучателя)
					Или Не ЗначениеЗаполнено(РеквизитыЗаявления.ДатаОкончанияТрудовогоДоговораПолучателя)) Тогда
				Текст = НСтр("ru = 'Для принятого по срочному трудовому договору сроком менее 6 месяцев должен быть указан период этого договора.'");
				СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ДатаНачалаТрудовогоДоговораПолучателя");
			КонецЕсли;
		КонецЕсли;
		
		Если (Строка.УсловияИсчисленияКод1 = "43" Или Строка.УсловияИсчисленияКод2 = "43" Или Строка.УсловияИсчисленияКод3 = "43")
			И Не ЗначениеЗаполнено(Строка.ФинансированиеФедеральнымБюджетом) Тогда
			Текст = НСтр("ru = 'Для лица, подвергшегося воздействию радиации, не указана причина этого воздействия'");
			СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ФинансированиеФедеральнымБюджетом");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Строка.ВторойКодПричиныНетрудоспособности)
			И Не ЗначениеЗаполнено(Строка.ДатаИзмененияКодаПричиныНетрудоспособности) Тогда
			Текст = НСтр("ru = 'В поле ""Дата 1"" не указана дата изменения кода причины нетрудоспособности.'");
			СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ДатаИзмененияКодаПричиныНетрудоспособности");
		КонецЕсли;
		
		Если ЭтоСанаторнаяПутевка(Строка) Тогда
			Если Не ЗначениеЗаполнено(Строка.ДатаИзмененияКодаПричиныНетрудоспособности) Тогда
				Текст = НСтр("ru = 'В поле ""Дата 1"" не указана дата начала санаторной путевки.'");
				СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ДатаИзмененияКодаПричиныНетрудоспособности");
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Строка.ДатаОкончанияПутевки) Тогда
				Текст = НСтр("ru = 'В поле ""Дата 2"" не указана дата окончания санаторной путевки.'");
				СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ДатаОкончанияПутевки");
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Строка.ОГРН_Санатория) Тогда
				СообщитьОбОшибке(Отказ, Неопределено, ИмяТаблицы, Строка.НомерСтроки, "ОГРН_Санатория");
			КонецЕсли;
		КонецЕсли;
		
		Если ЭтоУходЗаРодственниками(Строка) Тогда
			Если Не ЗначениеЗаполнено(Строка.ПоУходуФИО1) Тогда
				Текст = НСтр("ru = 'Не заполнено ФИО первого родственника, за которым осуществлялся уход.'");
				СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ПоУходуФИО1");
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Строка.ПоУходуВозрастЛет1 + Строка.ПоУходуВозрастМесяцев1) Тогда
				Текст = НСтр("ru = 'Не заполнен возраст первого родственника, за которым осуществлялся уход.'");
				СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ПоУходуВозрастЛет1");
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Строка.ПоУходуРодственнаяСвязь1) Тогда
				Текст = НСтр("ru = 'Не заполнена родственная связь первого родственника, за которым осуществлялся уход.'");
				СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ПоУходуРодственнаяСвязь1");
			КонецЕсли;
			Если ЗначениеЗаполнено(Строка.ПоУходуФИО2) Тогда
				Если Не ЗначениеЗаполнено(Строка.ПоУходуВозрастЛет2 + Строка.ПоУходуВозрастМесяцев2) Тогда
					Текст = НСтр("ru = 'Не заполнен возраст второго родственника, за которым осуществлялся уход.'");
					СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ПоУходуВозрастЛет2");
				КонецЕсли;
				Если Не ЗначениеЗаполнено(Строка.ПоУходуРодственнаяСвязь2) Тогда
					Текст = НСтр("ru = 'Не заполнена родственная связь второго родственника, за которым осуществлялся уход.'");
					СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ПоУходуРодственнаяСвязь2");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ЭтоНарушениеРежима(Строка) Тогда
			Если Не ЗначениеЗаполнено(Строка.ДатаНарушенияРежима) Тогда
				Текст = НСтр("ru = 'Не заполнена дата нарушения режима.'");
				СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ДатаНарушенияРежима");
			ИначеЕсли Не ЗначениеЗаполнено(Строка.КодНарушенияРежима) Тогда
				Текст = НСтр("ru = 'Не заполнен код нарушения режима.'");
				СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "КодНарушенияРежима");
			КонецЕсли;
		КонецЕсли;
		
		ПериодыНетрудоспособностиЗаполнены = ПроверитьПериодНетрудоспособности(
			Отказ,
			Строка,
			1,
			ИмяТаблицы,
			НСтр("ru = 'первого периода освобождения от работы'"));
		
		Если ЗначениеЗаполнено(Строка.ОсвобождениеДатаНачала2) Тогда
			ПериодЗаполнен = ПроверитьПериодНетрудоспособности(
				Отказ,
				Строка,
				2,
				ИмяТаблицы,
				НСтр("ru = 'второго периода освобождения от работы'"));
			Если ПериодЗаполнен И Строка.ОсвобождениеДатаОкончания1 + 86400 <> Строка.ОсвобождениеДатаНачала2 Тогда
				Текст = НСтр("ru = 'Начало второго периода освобождения от работы должно непосредственно следовать за окончанием первого периода.'");
				СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ОсвобождениеДатаНачала2");
			КонецЕсли;
			ПериодыНетрудоспособностиЗаполнены = ПериодыНетрудоспособностиЗаполнены И ПериодЗаполнен;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Строка.ОсвобождениеДатаНачала3) Тогда
			ПериодЗаполнен = ПроверитьПериодНетрудоспособности(
				Отказ,
				Строка,
				3,
				ИмяТаблицы,
				НСтр("ru = 'третьего периода освобождения от работы'"));
			Если ПериодЗаполнен И Строка.ОсвобождениеДатаОкончания2 + 86400 <> Строка.ОсвобождениеДатаНачала3 Тогда
				Текст = НСтр("ru = 'Начало третьего периода освобождения от работы должно непосредственно следовать за окончанием второго периода.'");
				СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ОсвобождениеДатаНачала3");
			КонецЕсли;
			ПериодыНетрудоспособностиЗаполнены = ПериодыНетрудоспособностиЗаполнены И ПериодЗаполнен;
		КонецЕсли;
		
		Если Строка.ДатаВыдачиЛисткаНетрудоспособности > Строка.ОсвобождениеДатаНачала1
			И Не ЭтоНахождениеВСтационареСРебенком(Строка)
			И Не ЭтоПродолжение(Строка) Тогда
			Текст = НСтр("ru = 'Листок нетрудоспособности выдан после начала освобождения от работы.'");
			СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ДатаВыдачиЛисткаНетрудоспособности");
		КонецЕсли;
		
		Если ЕстьОплатаЗаСчетФСС(Строка) Тогда
			ПериодОплатыЗаполнен = ПроверитьПериод(
				Отказ,
				Строка,
				"ДатаНачалаОплаты",
				"ДатаОкончанияОплаты",
				ИмяТаблицы,
				Строка.НомерСтроки,
				НСтр("ru = 'выплаты пособия за счет ФСС'"));
			Если ПериодОплатыЗаполнен И ПериодыНетрудоспособностиЗаполнены Тогда
				Если Строка.ДатаНачалаОплаты < Строка.ОсвобождениеДатаНачала1 Тогда
					Текст = НСтр("ru = 'Период оплаты за счет ФСС начинается до начала периода освобождения от работы.'");
					СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ДатаОкончанияОплаты");
				КонецЕсли;
				Если Строка.ДатаОкончанияОплаты > Макс(Строка.ОсвобождениеДатаОкончания1, Строка.ОсвобождениеДатаОкончания2, Строка.ОсвобождениеДатаОкончания3) Тогда
					Текст = НСтр("ru = 'Период оплаты за счет ФСС заканичается после окончания периода освобождения от работы.'");
					СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ДатаОкончанияОплаты");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ЭтоНахождениеВСтационареСРебенком(Строка) Тогда
			ПроверитьПериод(
				Отказ,
				Строка,
				"ПериодНахожденияВСтационареСРебенкомС",
				"ПериодНахожденияВСтационареСРебенкомПо",
				ИмяТаблицы,
				Строка.НомерСтроки,
				НСтр("ru = 'нахождения в стационаре'"));
		КонецЕсли;
		
		Если ЕстьИзменениеСостоянияНетрудоспособного(Строка) Тогда
			Если Не ЗначениеЗаполнено(Строка.НовыйСтатусНетрудоспособного) Тогда
				Текст = НСтр("ru = 'В поле ""Иное"" не заполнен код изменения состояния нетрудоспособного.'");
				СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "НовыйСтатусНетрудоспособного");
			ИначеЕсли Не ЗначениеЗаполнено(Строка.ДатаНовыйСтатусНетрудоспособного)
				И (Строка.НовыйСтатусНетрудоспособного = "32"
					Или Строка.НовыйСтатусНетрудоспособного = "33"
					Или Строка.НовыйСтатусНетрудоспособного = "34"
					Или Строка.НовыйСтатусНетрудоспособного = "36") Тогда
				Текст = НСтр("ru = 'В поле ""Иное"" не заполнена дата изменения состояния нетрудоспособного.'");
				СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ДатаНовыйСтатусНетрудоспособного");
			КонецЕсли;
		Иначе
			Если Не ЗначениеЗаполнено(Строка.ПриступитьКРаботеС) Тогда
				СообщитьОбОшибке(Отказ, Неопределено, ИмяТаблицы, Строка.НомерСтроки, "ПриступитьКРаботеС");
			КонецЕсли;
		КонецЕсли;
		
		Если ЭтоПродолжение(Строка) И Не ЗначениеЗаполнено(Строка.НомерЛисткаПродолжения) Тогда
			СообщитьОбОшибке(Отказ, Неопределено, ИмяТаблицы, Строка.НомерСтроки, "НомерЛисткаПродолжения");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеСтрокСведенийПоУходуЗаРебенком(Отказ)
	ИмяТаблицы = "СведенияНеобходимыеДляНазначенияПособий";
	
	Для Каждого Строка Из СведенияНеобходимыеДляНазначенияПособий Цикл
		
		Если Не ЗначениеЗаполнено(Строка.ФамилияРебенка) Тогда
			Текст = НСтр("ru = 'Не заполнена фамилия ребенка.'");
			СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ФамилияРебенка");
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.ИмяРебенка) Тогда
			Текст = НСтр("ru = 'Не заполнено имя ребенка.'");
			СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ИмяРебенка");
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.ДатаРожденияРебенка) Тогда
			Текст = НСтр("ru = 'Не заполнена дата рождения ребенка.'");
			СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ДатаРожденияРебенка");
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.ОчередностьРожденияРебенка) Тогда
			Текст = НСтр("ru = 'Не заполнена очередность рождения ребенка.'");
			СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ОчередностьРожденияРебенка");
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.ВидПодтверждающегоДокумента) Тогда
			Текст = НСтр("ru = 'Не заполнен вид подтверждающего документа.'");
			СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ВидПодтверждающегоДокумента");
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.НаименованиеПодтверждающегоДокумента) Тогда
			Текст = НСтр("ru = 'Не заполнено наименование подтверждающего документа.'");
			СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "НаименованиеПодтверждающегоДокумента");
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.ДатаДокумента) Тогда
			Текст = НСтр("ru = 'Не заполнена дата подтверждающего документа.'");
			СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ДатаДокумента");
		КонецЕсли;
		
		Если Строка.ВидПодтверждающегоДокумента = Перечисления.ВидыПодтверждающихДокументовОтпускаПоУходу.СвидетельствоОРождении
			И Не ЗначениеЗаполнено(Строка.СерияДокумента) Тогда
			Текст = НСтр("ru = 'Не заполнена серия подтверждающего документа.'");
			СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "СерияДокумента");
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.НомерДокумента) Тогда
			Текст = НСтр("ru = 'Не заполнен номер подтверждающего документа.'");
			СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "НомерДокумента");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПроверитьПериодНетрудоспособности(Отказ, Строка, НомерПериода, ИмяТаблицы, Представление)
	ПериодЗаполнен = ПроверитьПериод(
		Отказ,
		Строка,
		"ОсвобождениеДатаНачала" + НомерПериода,
		"ОсвобождениеДатаОкончания" + НомерПериода,
		ИмяТаблицы,
		Строка.НомерСтроки,
		Представление);
	Если Не ЗначениеЗаполнено(Строка["ОсвобождениеФИОВрача" + НомерПериода]) Тогда
		Текст = НСтр("ru = 'Не заполнено ФИО врача %1.'");
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, Представление);
		СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ОсвобождениеФИОВрача1");
		ПериодЗаполнен = Ложь;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Строка["ОсвобождениеДолжностьВрача" + НомерПериода]) Тогда
		Текст = НСтр("ru = 'Не заполнена должность врача %1.'");
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, Представление);
		СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, Строка.НомерСтроки, "ОсвобождениеДолжностьВрача1");
		ПериодЗаполнен = Ложь;
	КонецЕсли;
	
	Возврат ПериодЗаполнен;
КонецФункции

Функция ЭтоСанаторнаяПутевка(Строка)
	Возврат Строка.КодПричиныНетрудоспособности = "08"
		Или Строка.ВторойКодПричиныНетрудоспособности = "08"
		Или Строка.ДополнительныйКодПричиныНетрудоспособности = "017"
		Или Строка.ДополнительныйКодПричиныНетрудоспособности = "018"
		Или Строка.ДополнительныйКодПричиныНетрудоспособности = "019"
		Или ЗначениеЗаполнено(Строка.ДатаОкончанияПутевки);
КонецФункции

Функция ЭтоУходЗаРодственниками(Строка)
	Возврат Строка.КодПричиныНетрудоспособности = "09"
		Или Строка.КодПричиныНетрудоспособности = "12"
		Или Строка.КодПричиныНетрудоспособности = "13"
		Или Строка.КодПричиныНетрудоспособности = "14"
		Или Строка.КодПричиныНетрудоспособности = "15"
		Или Строка.ВторойКодПричиныНетрудоспособности = "09"
		Или Строка.ВторойКодПричиныНетрудоспособности = "12"
		Или Строка.ВторойКодПричиныНетрудоспособности = "13"
		Или Строка.ВторойКодПричиныНетрудоспособности = "14"
		Или Строка.ВторойКодПричиныНетрудоспособности = "15";
КонецФункции

Функция ЭтоНарушениеРежима(Строка)
	Возврат ЗначениеЗаполнено(Строка.ДатаНарушенияРежима)
		Или ЗначениеЗаполнено(Строка.КодНарушенияРежима);
КонецФункции

Функция ЭтоНахождениеВСтационареСРебенком(Строка)
	Возврат ЗначениеЗаполнено(Строка.ПериодНахожденияВСтационареСРебенкомС)
		Или ЗначениеЗаполнено(Строка.ПериодНахожденияВСтационареСРебенкомПо);
КонецФункции

Функция ЭтоПродолжение(Строка)
	Возврат Строка.НовыйСтатусНетрудоспособного = "31"
		Или ЗначениеЗаполнено(Строка.НомерЛисткаПродолжения);
КонецФункции

Функция ЕстьОплатаЗаСчетФСС(Строка)
	Возврат ЗначениеЗаполнено(Строка.ДатаНачалаОплаты)
		Или ЗначениеЗаполнено(Строка.ДатаОкончанияОплаты);
КонецФункции

Функция ЕстьИзменениеСостоянияНетрудоспособного(Строка)
	Возврат ЗначениеЗаполнено(Строка.НовыйСтатусНетрудоспособного)
		Или ЗначениеЗаполнено(Строка.ДатаНовыйСтатусНетрудоспособного);
КонецФункции

#КонецОбласти

#Область БазоваяФункциональностьПроверокЗаполнения

Функция ПроверитьПериод(Отказ, Коллекция, ПолеДаты1, ПолеДаты2, ИмяТаблицы, НомерСтроки, Представление)
	Дата1 = Коллекция[ПолеДаты1];
	Дата2 = Коллекция[ПолеДаты2];
	Если Не ЗначениеЗаполнено(Дата1) Тогда
		Текст = СтрШаблон(НСтр("ru = 'Не заполнена дата начала %1.'"), Представление);
		СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, НомерСтроки, ПолеДаты1);
		Возврат Ложь;
	ИначеЕсли Не ЗначениеЗаполнено(Дата2) Тогда
		Текст = СтрШаблон(НСтр("ru = 'Не заполнена дата окончания %1.'"), Представление);
		СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, НомерСтроки, ПолеДаты2);
		Возврат Ложь;
	ИначеЕсли Дата1 > Дата2 Тогда
		Текст = СтрШаблон(НСтр("ru = 'Дата начала %1 больше даты окончания.'"), Представление);
		СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, НомерСтроки, ПолеДаты1);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

Процедура СообщитьОбОшибке(Отказ, Текст, ИмяТаблицы, НомерСтроки, ИмяРеквизита)
	Если Текст = Неопределено Тогда
		Текст = НСтр("ru = 'Не заполнено поле ""%1"".'");
		Если ИмяТаблицы = Неопределено Тогда
			Текст = СтрШаблон(Текст, Метаданные().Реквизиты[ИмяРеквизита].Представление());
		Иначе
			Текст = СтрШаблон(Текст, Метаданные().ТабличныеЧасти[ИмяТаблицы].Реквизиты[ИмяРеквизита].Представление());
		КонецЕсли;
	КонецЕсли;
	
	Отказ = Истина;
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Если ИмяТаблицы = Неопределено Тогда
		Сообщение.Поле = ИмяРеквизита;
	Иначе
		Сообщение.Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТаблицы, НомерСтроки, ИмяРеквизита);
	КонецЕсли;
	Сообщение.Сообщить();
КонецПроцедуры

#КонецОбласти

#Область МеханизмФиксацииИзменений

Функция ОбъектЗафиксирован() Экспорт
	Возврат Не ПрямыеВыплатыПособийСоциальногоСтрахования.СтатусПозволяетРедактироватьДокумент(СтатусДокумента);
КонецФункции

Функция ОбновитьВторичныеДанныеДокумента(ДанныеОрганизации = Истина, ДанныеОПособиях = Истина, Знач МассивЗаявлений = Неопределено) Экспорт
	Модифицирован = Ложь;
	
	Если ОбъектЗафиксирован() Тогда
		Возврат Модифицирован;
	КонецЕсли;
	
	ПараметрыФиксации = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(Метаданные().ПолноеИмя()).ПараметрыФиксацииВторичныхДанных();
	
	Если ДанныеОрганизации И ОбновитьДанныеСтрахователя(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ДанныеОПособиях И ОбновитьДанныеОПособиях(ПараметрыФиксации, МассивЗаявлений) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Возврат Модифицирован;
КонецФункции

Функция ОбновитьДанныеСтрахователя(ПараметрыФиксации)
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	РеквизитыДокумента = Новый Структура;
	Для Каждого ОписаниеРеквизита Из ПараметрыФиксации.ОписаниеФиксацииРеквизитов Цикл
		Если Не ОписаниеРеквизита.Значение.РеквизитСтроки Тогда
			РеквизитыДокумента.Вставить(ОписаниеРеквизита.Значение.ИмяРеквизита);
		КонецЕсли;
	КонецЦикла;
	
	СтрокаРеквизитыОрганизации = "РегистрационныйНомерФСС, КодПодчиненностиФСС, ДополнительныйКодФСС, ОГРН";
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, СтрокаРеквизитыОрганизации);
	
	ЗаполнитьЗначенияСвойств(РеквизитыДокумента, РеквизитыОрганизации, СтрокаРеквизитыОрганизации);
	
	ЗаполняемыеЗначения = Новый Структура("Организация,Руководитель,ДолжностьРуководителя,ГлавныйБухгалтер", Организация);
	ЗарплатаКадры.ПолучитьЗначенияПоУмолчанию(ЗаполняемыеЗначения);
	ЗаполняемыеЗначения.Свойство("Руководитель",          РеквизитыДокумента.Руководитель);
	ЗаполняемыеЗначения.Свойство("ДолжностьРуководителя", РеквизитыДокумента.ДолжностьРуководителя);
	ЗаполняемыеЗначения.Свойство("ГлавныйБухгалтер",      РеквизитыДокумента.ГлавныйБухгалтер);
	
	Сведения = Новый СписокЗначений;
	Сведения.Добавить("", "ТелОрганизации");
	Сведения.Добавить("", "АдресЭлектроннойПочтыОрганизации");
	Сведения.Добавить("", "ИННЮЛ");
	Сведения.Добавить("", "КППЮЛ");
	ОргСведения = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса()), Сведения);
	ОргСведения.Свойство("ТелОрганизации",                   РеквизитыДокумента.ТелефонСоставителя);
	ОргСведения.Свойство("АдресЭлектроннойПочтыОрганизации", РеквизитыДокумента.АдресЭлектроннойПочтыСоставителя);
	ОргСведения.Свойство("ИННЮЛ",                            РеквизитыДокумента.ИНН);
	ОргСведения.Свойство("КППЮЛ",                            РеквизитыДокумента.КПП);
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(РеквизитыДокумента, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ОбновитьДанныеОПособиях(ПараметрыФиксации, МассивЗаявлений)
	Если МассивЗаявлений = Неопределено Тогда
		МассивЗаявлений = СведенияНеобходимыеДляНазначенияПособий.Выгрузить().ВыгрузитьКолонку("Заявление");
	КонецЕсли;
	Запрос = ПрямыеВыплатыПособийСоциальногоСтрахования.ЗапросРеестраПрямыхВыплатПоПервичнымДокументам(ЭтотОбъект, МассивЗаявлений, "ВТВторичныеДанные");
	Если Запрос = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	Запрос.Выполнить();
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьВторичныеДанные(Запрос.МенеджерВременныхТаблиц, ЭтотОбъект, "СведенияНеобходимыеДляНазначенияПособий", "ВТВторичныеДанные", ПараметрыФиксации);
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
