#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьНастройки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтображатьВремяСПриИзменении(Элемент)
	
	ОтображатьВремяПоНормализованное = 24 - ОтображатьВремяПо;
	Если ОтображатьВремяС >= ОтображатьВремяПоНормализованное Тогда
		ОтображатьВремяПоНормализованное = ОтображатьВремяС + 1;
		ОтображатьВремяПо = 24 - ОтображатьВремяПоНормализованное;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьВремяПоПриИзменении(Элемент)
	
	ОтображатьВремяПоНормализованное = 24 - ОтображатьВремяПо;
	Если ОтображатьВремяС >= ОтображатьВремяПоНормализованное Тогда
		ОтображатьВремяС = ОтображатьВремяПоНормализованное - 1;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	ГотовоНаСервере();
	Оповестить("Запись_НастройкиПросмотраБроней");
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ГотовоНаСервере()
	
	СохранитьНастройки();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()
	
	БронированиеПомещений.УстановитьПерсональнуюНастройку(
		"ОтображатьВремяС", ОтображатьВремяС);
	БронированиеПомещений.УстановитьПерсональнуюНастройку(
		"ОтображатьВремяПо", ОтображатьВремяПо);
	БронированиеПомещений.УстановитьПерсональнуюНастройку(
		"РазмерЯчейкиВремени", РазмерЯчейкиВремени);
	БронированиеПомещений.УстановитьПерсональнуюНастройку(
		"ПоказыватьУдаленные", ПоказыватьУдаленные);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройки()
	
	ОтображатьВремяС = БронированиеПомещений.ПолучитьПерсональнуюНастройку("ОтображатьВремяС");
	ОтображатьВремяПо = БронированиеПомещений.ПолучитьПерсональнуюНастройку("ОтображатьВремяПо");
	РазмерЯчейкиВремени = БронированиеПомещений.ПолучитьПерсональнуюНастройку("РазмерЯчейкиВремени");
	ПоказыватьУдаленные = БронированиеПомещений.ПолучитьПерсональнуюНастройку("ПоказыватьУдаленные");
	
КонецПроцедуры

#КонецОбласти