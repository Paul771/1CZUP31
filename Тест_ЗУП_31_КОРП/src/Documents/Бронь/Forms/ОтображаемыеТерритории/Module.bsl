#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Заполнение дерева территорий.
	Дерево = РеквизитФормыВЗначение("Территории");
	БронированиеПомещений.ЗаполнитьДеревоТерриторий(Дерево, Ложь);
	ЗначениеВРеквизитФормы(Дерево, "Территории");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТерритории

&НаКлиенте
Процедура ТерриторииОтображатьТерриториюПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Территории.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЭлементДерева = Территории.НайтиПоИдентификатору(Элементы.Территории.ТекущаяСтрока);
	
	БронированиеПомещенийКлиент.ПриИзмененииПометкиОтображенияТерриторий(
		Элементы.Территории, Территории, ЭлементДерева, ТекущиеДанные.ОтображатьТерриторию);
	
КонецПроцедуры

&НаКлиенте
Процедура ТерриторииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДанныеСтроки = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Поле = Элементы.ТерриторииДоступнаСхема Тогда
		СтандартнаяОбработка = Ложь;
		БронированиеПомещенийКлиент.ПоказатьСхемуТерритории(ДанныеСтроки.Ссылка);
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеСтроки.ОтображатьТерриторию = Не ДанныеСтроки.ОтображатьТерриторию;
	ЭлементДерева = Территории.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	БронированиеПомещенийКлиент.ПриИзмененииПометкиОтображенияТерриторий(
		Элемент, Территории, ЭлементДерева, ДанныеСтроки.ОтображатьТерриторию);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьВсеТерритории(Команда)
	
	БронированиеПомещенийКлиент.УстановитьПометкуОтображенияПодчиненныхТерриторий(Территории, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	
	ЗапомнитьТерритории();
	Оповестить("Запись_ОтображаемыеТерритории");
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьТерриторию(Команда)
	
	ТекущиеДанные = Элементы.Территории.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(, ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВыборВсехТерриторий(Команда)
	
	БронированиеПомещенийКлиент.УстановитьПометкуОтображенияПодчиненныхТерриторий(Территории, Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗапомнитьТерритории()
	
	Дерево = РеквизитФормыВЗначение("Территории");
	ОтображаемыеТерритории = БронированиеПомещений.ОтображаемыеТерриторииИзДереваТерриторий(Дерево);
	БронированиеПомещений.УстановитьПерсональнуюНастройку("ОтображаемыеТерритории", ОтображаемыеТерритории);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	БронированиеПомещений.УстановитьУсловноеОформлениеТерритории(УсловноеОформление);
	
КонецПроцедуры

#КонецОбласти