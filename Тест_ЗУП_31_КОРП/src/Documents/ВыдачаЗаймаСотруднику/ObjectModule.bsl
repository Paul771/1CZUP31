#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияРабочихМестВОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудниковОрганизаций.Организация = Организация;
	ПараметрыПолученияСотрудниковОрганизаций.НачалоПериода = Дата;
	ПараметрыПолученияСотрудниковОрганизаций.ОкончаниеПериода = Дата;
	ПараметрыПолученияСотрудниковОрганизаций.РаботникиПоТрудовымДоговорам = Истина;
	ПараметрыПолученияСотрудниковОрганизаций.РаботникиПоДоговорамГПХ = Истина;
	
	КадровыйУчет.ПроверитьРаботающихФизическихЛиц(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо),
		ПараметрыПолученияСотрудниковОрганизаций,
		Отказ,
		Новый Структура("ИмяПоляСотрудник, ИмяОбъекта", "ФизическоеЛицо", "Объект")
	);
	
	Если ЗначениеЗаполнено(ДоговорЗайма) Тогда
		
		ДатаПредоставления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорЗайма, "ДатаПредоставления");
		ЗаймыСотрудникам.ПроверитьДатуВыдачиЗайма(Дата, ДатаПредоставления, "Объект.Дата", Отказ);
		
		НевыданныйОстаток = ЗаймыСотрудникам.ОстатокНевыданныхСумм(ДоговорЗайма, Дата, Ссылка);
		Если НевыданныйОстаток < Сумма Тогда 
			ТекстСообщения = НСтр("ru='На %1 по договору займа не выдано %2 руб. Сумма выдачи не может превышать невыданный остаток.'");
		    ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Формат(Дата, "ДЛФ=Д"), НевыданныйОстаток);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Сумма", "Объект", Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗаймыСотрудникамВХО.УточнятьФормуРасчетовПриВыдачеПогашенииЗайма(Организация) Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ФормаРасчетов");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, 
			ЗаймыСотрудникам.ДанныеЗаполненияДокументаПоСотруднику(ДанныеЗаполнения));
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ДоговорЗаймаСотруднику") Тогда
		ДокументПроведен = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "Проведен");
		Если Не ДокументПроведен Тогда 
			ТекстОшибки = НСтр("ru='Документ %1 не проведен. Ввод на основании непроведенного документа запрещен.'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ДанныеЗаполнения);
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;
		Если ЗаемПоДоговоруВыданПолностью(ДанныеЗаполнения)	Тогда 
			ТекстИсключения = НСтр("ru='Заем по договору выдан полностью'");
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, 
			ЗаймыСотрудникам.ДанныеЗаполненияДокументаПоДоговоруЗайма(ДанныеЗаполнения));
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДоговорЗайма) Тогда		
		
		ДействующиеДоговоры = ЗаймыСотрудникам.ДействующиеДоговорыЗаймаПоФизическомуЛицу(Организация, ФизическоеЛицо, Дата, Истина);
		
		Если ДействующиеДоговоры <> Неопределено И ДействующиеДоговоры.Количество() = 1 Тогда
			ДоговорЗайма = ДействующиеДоговоры[0];
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ЗаймыСотрудникам.ЗарегистрироватьПредоставлениеЗайма(Движения, ДоговорЗайма, Сумма, Дата, Организация, ФизическоеЛицо, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	Возврат ОбменДаннымиЗарплатаКадры.ОграниченияРегистрацииПоОрганизацииИФизическомуЛицу(ЭтотОбъект, Организация, ФизическоеЛицо, Дата);
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗаемПоДоговоруВыданПолностью(ДоговорЗаймаСотруднику)
	
	Если Не ЗначениеЗаполнено(ДоговорЗаймаСотруднику) Тогда 
		Возврат Ложь;
	КонецЕсли;	
	
	РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДоговорЗаймаСотруднику, "СпособПредоставления, ЗаемПоДоговоруВыданПолностью");
	
	Если РеквизитыДоговора.ЗаемПоДоговоруВыданПолностью 
		И РеквизитыДоговора.СпособПредоставления = Перечисления.СпособыПредоставленияЗаймаСотруднику.Единовременно Тогда 
        Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#КонецЕсли
