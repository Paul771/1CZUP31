#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		МетаданныеОбъекта = ЭтотОбъект.Метаданные();
		Для Каждого ПараметрЗаполнения Из ДанныеЗаполнения Цикл
			Если МетаданныеОбъекта.Реквизиты.Найти(ПараметрЗаполнения.Ключ)<>Неопределено Тогда
				ЭтотОбъект[ПараметрЗаполнения.Ключ] = ПараметрЗаполнения.Значение;
			Иначе
				Если ОбщегоНазначения.ЭтоСтандартныйРеквизит(МетаданныеОбъекта.СтандартныеРеквизиты, ПараметрЗаполнения.Ключ) Тогда
					ЭтотОбъект[ПараметрЗаполнения.Ключ] = ПараметрЗаполнения.Значение;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПодготовкаСпецоценкиУсловийТруда")
		Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РезультатыСпецоценкиУсловийТруда") Тогда
		
		ЭтотОбъект.ДокументОснование = ДанныеЗаполнения;
		ЭтотОбъект.РабочиеМеста.Очистить();
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
		Запрос.УстановитьПараметр("ДатаДокумента", Дата);
		
		Если ЗначениеЗаполнено(ДокументОснование) Тогда
			ЭтотОбъект.Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "Организация");
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ЭтотОбъект.Организация) Тогда
			Возврат;
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Истина);
		ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
		ПараметрыПолученияСотрудниковОрганизаций.Организация = ЭтотОбъект.Организация;
		
		КадровыйУчет.СоздатьВТСотрудникиОрганизации(Запрос.МенеджерВременныхТаблиц, Истина, ПараметрыПолученияСотрудниковОрганизаций);
		
		ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц, "ВТСотрудникиОрганизации");
		КадровыеДанные = "ДолжностьПоШтатномуРасписанию";
		КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, Истина, КадровыеДанные);
		УстановитьПривилегированныйРежим(Ложь);
		
		Если ЗначениеЗаполнено(ДокументОснование) Тогда
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	ТаблицаДокумента.Ссылка КАК Документ,
			|	ТаблицаДокумента.Исполнитель,
			|	ТаблицаДокументаРабочиеМеста.РабочееМесто
			|ПОМЕСТИТЬ ВТРабочиеМеста
			|ИЗ
			|	#ТаблицаДокументаОсновная КАК ТаблицаДокумента
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ТаблицаДокументаРабочиеМеста КАК ТаблицаДокументаРабочиеМеста
			|		ПО ТаблицаДокумента.Ссылка = ТаблицаДокументаРабочиеМеста.Ссылка
			|ГДЕ
			|	ТаблицаДокумента.Ссылка = &ДокументОснование
			|	И ТаблицаДокументаРабочиеМеста.КлассУсловийТрудаПоРезультатамПредыдущейОценки В (ЗНАЧЕНИЕ(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.Оптимальный), ЗНАЧЕНИЕ(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.Допустимый), ЗНАЧЕНИЕ(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.ПустаяСсылка))
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	РабочиеМеста.Документ,
			|	РабочиеМеста.Исполнитель,
			|	ТаблицаДокументаАналогичныеМеста.АналогичноеМесто КАК РабочееМесто
			|ПОМЕСТИТЬ ВТВсеРабочиеМеста
			|ИЗ
			|	#ТаблицаДокументаАналогичныеМеста КАК ТаблицаДокументаАналогичныеМеста
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРабочиеМеста КАК РабочиеМеста
			|		ПО ТаблицаДокументаАналогичныеМеста.Ссылка = РабочиеМеста.Документ
			|			И ТаблицаДокументаАналогичныеМеста.РабочееМесто = РабочиеМеста.РабочееМесто
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	РабочиеМеста.Документ,
			|	РабочиеМеста.Исполнитель,
			|	РабочиеМеста.РабочееМесто
			|ИЗ
			|	ВТРабочиеМеста КАК РабочиеМеста
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВсеРабочиеМеста.Исполнитель,
			|	ВсеРабочиеМеста.РабочееМесто,
			|	КОЛИЧЕСТВО(КадровыеДанныеСотрудников.Сотрудник) КАК Численность
			|ИЗ
			|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВсеРабочиеМеста КАК ВсеРабочиеМеста
			|		ПО КадровыеДанныеСотрудников.ДолжностьПоШтатномуРасписанию = ВсеРабочиеМеста.РабочееМесто
			|
			|СГРУППИРОВАТЬ ПО
			|	ВсеРабочиеМеста.Исполнитель,
			|	ВсеРабочиеМеста.РабочееМесто";
			
			МетаданныеДокумента = ДокументОснование.Метаданные();
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ТаблицаДокументаОсновная", МетаданныеДокумента.ПолноеИмя());
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ТаблицаДокументаРабочиеМеста", МетаданныеДокумента.ПолноеИмя() + ".РабочиеМеста");
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ТаблицаДокументаАналогичныеМеста", МетаданныеДокумента.ПолноеИмя() + ".АналогичныеМеста");
			Если МетаданныеДокумента.ПолноеИмя() = "Документ.РезультатыСпецоценкиУсловийТруда" Тогда
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "ТаблицаДокументаРабочиеМеста.КлассУсловийТрудаПоРезультатамПредыдущейОценки", "ТаблицаДокументаРабочиеМеста.КлассУсловийТруда");
			КонецЕсли;
		Иначе
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	НЕОПРЕДЕЛЕНО КАК Исполнитель,
			|	КадровыеДанныеСотрудников.ДолжностьПоШтатномуРасписанию КАК РабочееМесто,
			|	КОЛИЧЕСТВО(КадровыеДанныеСотрудников.Сотрудник) КАК Численность
			|ИЗ
			|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КлассыУсловийТрудаПоДолжностям.СрезПоследних(&ДатаДокумента, ) КАК КлассыУсловийТрудаПоДолжностямСрезПоследних
			|		ПО КадровыеДанныеСотрудников.ДолжностьПоШтатномуРасписанию = КлассыУсловийТрудаПоДолжностямСрезПоследних.Должность
			|ГДЕ
			|	КлассыУсловийТрудаПоДолжностямСрезПоследних.КлассУсловийТруда В (ЗНАЧЕНИЕ(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.Оптимальный), ЗНАЧЕНИЕ(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.Допустимый), ЗНАЧЕНИЕ(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.ПустаяСсылка))
			|
			|СГРУППИРОВАТЬ ПО
			|	КадровыеДанныеСотрудников.ДолжностьПоШтатномуРасписанию";
			
		КонецЕсли;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Не ЗначениеЗаполнено(ЭтотОбъект.Исполнитель) Тогда
				ЭтотОбъект.Исполнитель = Выборка.Исполнитель;
			КонецЕсли;
			СтрокаРабочегоМеста = ЭтотОбъект.РабочиеМеста.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРабочегоМеста, Выборка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ЗарплатаКадры.ПроверитьКорректностьМесяца(Ссылка, ДатаПодачи, "Объект.ДатаПодачи", Отказ, НСтр("ru='Период отчета'"), , , Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	Возврат ОбменДаннымиЗарплатаКадрыРасширенный.ОграниченияРегистрацииПоПозициямШтатногоРасписания(
		ЭтотОбъект, РабочиеМеста.ВыгрузитьКолонку("РабочееМесто"), Организация);
КонецФункции

#КонецОбласти

#КонецЕсли

