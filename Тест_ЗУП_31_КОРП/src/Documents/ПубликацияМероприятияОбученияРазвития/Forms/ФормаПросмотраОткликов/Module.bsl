
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Публикация") Тогда
		Публикация = Параметры.Публикация;
	КонецЕсли;
	
	Если Параметры.Свойство("Отказники") И Параметры.Отказники Тогда
		ПоказОтказников = Истина;
	КонецЕсли;
	
	УстановитьПараметрыДинамическогоСписка(Параметры);
	УстановитьСвойстваЭлементовФормы(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВвестиОбучение(Команда)
	
	ПараметрыФормы = Новый Структура("Основание", Публикация);
	ОткрытьФорму("Документ.ОбучениеРазвитиеСотрудников.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьПараметрыДинамическогоСписка(Параметры)

	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СписокСотрудников,
		"Публикация",
		Публикация,
		Истина);
		
	Если ПоказОтказников Тогда
		ОтображаемыеСтатусы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПредопределенноеЗначение("Перечисление.ВариантыОткликовСотрудников.Отказ"));
	Иначе
		ОтображаемыеСтатусы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПредопределенноеЗначение("Перечисление.ВариантыОткликовСотрудников.Согласие"));
		ОтображаемыеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОткликовСотрудников.ВозможноеСогласие"));
	КонецЕсли;
		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СписокСотрудников,
		"ОтображаемыеСтатусы",
		ОтображаемыеСтатусы,
		Истина);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСвойстваЭлементовФормы(Форма)

	Если Форма.ПоказОтказников Тогда
		ОтображениеПодсказкиСписка = ОтображениеПодсказки.Нет;
		Форма.Заголовок = НСтр("ru = 'Отказавшиеся сотрудники'");
	Иначе
		ОтображениеПодсказкиСписка = ОтображениеПодсказки.ОтображатьСнизу;
		Форма.Заголовок = НСтр("ru = 'Откликнувшиеся сотрудники'");
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"СписокСотрудников",
		"ОтображениеПодсказки",
		ОтображениеПодсказкиСписка);

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ФормаВвестиОбучение",
		"Видимость",
		НЕ Форма.ПоказОтказников);
		
КонецПроцедуры


#КонецОбласти 