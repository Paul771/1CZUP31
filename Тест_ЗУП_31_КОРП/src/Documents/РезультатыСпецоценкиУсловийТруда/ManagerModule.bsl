#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Приказ о завершении специальной оценки условий труда
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "ЗарплатаКадрыКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.МенеджерПечати = "Документ.РезультатыСпецоценкиУсловийТруда";
	КомандаПечати.Идентификатор = "ПФ_MXL_ПриказОЗавершенииСпециальнойОценкиУсловийТруда";
	КомандаПечати.Представление = НСтр("ru = 'Приказ о завершении специальной оценки условий труда'");
	КомандаПечати.Порядок = 10;
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнитьКомплектВнешнимиПечатнымиФормами = Истина;
	
КонецПроцедуры

// Формирует печатные формы
//
// Параметры:
//  (входные)
//    МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//    ПараметрыПечати - Структура - дополнительные настройки печати;
//  (выходные)
//   КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы
//   ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                             представление - имя области в которой был выведен объект;
//   ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_ПриказОЗавершенииСпециальнойОценкиУсловийТруда") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,	"ПФ_MXL_ПриказОЗавершенииСпециальнойОценкиУсловийТруда",
			НСтр("ru = 'Приказ о завершении специальной оценки условий труда'"), ПечатьПриказаОЗавершенииСпециальнойОценкиУсловийТруда(МассивОбъектов, ОбъектыПечати), ,
			"Документ.РезультатыСпецоценкиУсловийТруда.ПФ_MXL_ПриказОЗавершенииСпециальнойОценкиУсловийТруда");
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьПриказаОЗавершенииСпециальнойОценкиУсловийТруда(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РезультатыСпецоценкиУсловийТруда_ПриказОЗавершенииСпециальнойОценкиУсловийТруда";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РезультатыСпецоценкиУсловийТруда.ПФ_MXL_ПриказОЗавершенииСпециальнойОценкиУсловийТруда");
	
	ДанныеПечатиОбъектов = ДанныеПечатиДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Для Каждого ДанныеПечати Из ДанныеПечатиОбъектов Цикл
		
		// Документы нужно выводить на разных страницах.
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		Иначе
			ПервыйДокумент = Ложь;
		КонецЕсли;
		
		ДанныеДокумента = ДанныеПечати.Значение;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
		ОбластьШапка.Параметры.Заполнить(ДанныеДокумента);
		ОбластьШапка.Параметры.Город = ?(ПустаяСтрока(ДанныеДокумента.Город), "____________", ДанныеДокумента.Город);
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		ОбластьПодвалСтрока = Макет.ПолучитьОбласть("ПодвалСтрока");
		Для каждого ДанныеЧленаКомиссии Из ДанныеДокумента.ЧленыКомиссии Цикл
			ОбластьПодвалСтрока.Параметры.ФИОЧленКомиссии = ДанныеЧленаКомиссии.ФИОЧленКомиссии;
			ОбластьПодвалСтрока.Параметры.Должность = ДанныеЧленаКомиссии.ДолжностьЧленаКомиссии;
			ТабличныйДокумент.Вывести(ОбластьПодвалСтрока);
		КонецЦикла;
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ключ);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ДанныеПечатиДокументов(МассивОбъектов)
	
	ДанныеПечатиОбъектов = Новый Соответствие;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РезультатыСпецоценкиУсловийТруда.Ссылка КАК Документ,
	|	РезультатыСпецоценкиУсловийТруда.Номер,
	|	РезультатыСпецоценкиУсловийТруда.Дата,
	|	РезультатыСпецоценкиУсловийТруда.Организация,
	|	Организации.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
	|	РезультатыСпецоценкиУсловийТруда.Руководитель КАК Руководитель,
	|	РезультатыСпецоценкиУсловийТруда.ДолжностьРуководителя КАК ДолжностьРуководителя,
	|	РезультатыСпецоценкиУсловийТрудаКомиссия.ЧленКомиссии КАК ЧленКомиссии,
	|	РезультатыСпецоценкиУсловийТрудаКомиссия.Должность КАК ДолжностьЧленаКомиссии,
	|	РезультатыСпецоценкиУсловийТрудаКомиссия.РольВКомиссии КАК РольВКомиссии,
	|	РезультатыСпецоценкиУсловийТруда.ДатаНачала,
	|	РезультатыСпецоценкиУсловийТруда.ДатаРезультатов
	|ИЗ
	|	Документ.РезультатыСпецоценкиУсловийТруда КАК РезультатыСпецоценкиУсловийТруда
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО РезультатыСпецоценкиУсловийТруда.Организация = Организации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РезультатыСпецоценкиУсловийТруда.Комиссия КАК РезультатыСпецоценкиУсловийТрудаКомиссия
	|		ПО РезультатыСпецоценкиУсловийТруда.Ссылка = РезультатыСпецоценкиУсловийТрудаКомиссия.Ссылка
	|ГДЕ
	|	РезультатыСпецоценкиУсловийТруда.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Документ") Цикл
		
		ДанныеПечати = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Выборка);
		
		// ФИОРуководителя
		КадровыеДанные = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, ДанныеПечати.Руководитель, "ФамилияИО, Пол, Фамилия", ОбщегоНазначения.ТекущаяДатаПользователя());
		Для каждого СтрокаКадровыхДанных Из КадровыеДанные Цикл
			ДанныеПечати.Вставить("ФИОРуководителя", СтрокаКадровыхДанных.ФамилияИО);
			ДанныеПечати.Вставить("ПолРуководителя", СтрокаКадровыхДанных.Пол);
			ДанныеПечати.Вставить("ФамилияРуководителя", СтрокаКадровыхДанных.Фамилия);
		КонецЦикла;
		
		ДанныеПечати.Дата = Формат(ДанныеПечати.Дата, "ДЛФ=ДД");
		ДанныеПечати.ДатаНачала = Формат(ДанныеПечати.ДатаНачала, "ДЛФ=ДД");
		ДанныеПечати.Вставить("ДатаУведомления", Формат(ДобавитьМесяц(ДанныеПечати.ДатаРезультатов, 1), "ДЛФ=ДД"));
		ДанныеПечати.ДатаРезультатов = Формат(ДанныеПечати.ДатаРезультатов, "ДЛФ=ДД");
		
		// Подготовка номера документа
		ДанныеПечати.Номер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеПечати.Номер, Истина, Истина);
		
		// Фактический адрес организации
		АдресаОрганизаций = УправлениеКонтактнойИнформациейЗарплатаКадры.АдресаОрганизаций(Выборка.Организация);
		ОписаниеФактическогоАдреса = УправлениеКонтактнойИнформациейЗарплатаКадры.АдресОрганизации(
			АдресаОрганизаций,
			Выборка.Организация,
			Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации);
		ДанныеПечати.Вставить("Город", ОписаниеФактическогоАдреса.Город);
		
		МассивЧленовКомиссии = Новый Массив;
		Пока Выборка.Следующий() Цикл
			Если Выборка.РольВКомиссии = Перечисления.РолиЧленовКомиссииОхраныТруда.Председатель Тогда
				ФамилияИмяОтчествоПредседателя = ФизическиеЛицаКлиентСервер.ЧастиИмени(Выборка.ЧленКомиссии);
				ДанныеПечати.Вставить("ПредседательКомиссии", Выборка.ЧленКомиссии);
				// ФИОПредседателяКомиссии
				КадровыеДанные = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, Выборка.ЧленКомиссии, "ФамилияИО, Пол, Фамилия", ОбщегоНазначения.ТекущаяДатаПользователя());
				Для каждого СтрокаКадровыхДанных Из КадровыеДанные Цикл
					ДанныеПечати.Вставить("ФИОПредседателяКомиссии", СтрокаКадровыхДанных.ФамилияИО);
					ДанныеПечати.Вставить("ПолПредседателяКомиссии", СтрокаКадровыхДанных.Пол);
					ДанныеПечати.Вставить("ФамилияПредседателяКомиссии", СтрокаКадровыхДанных.Фамилия);
				КонецЦикла;
				ДанныеПечати.Вставить("ДолжностьПредседателяКомиссии", Выборка.ДолжностьЧленаКомиссии);
			Иначе
				КадровыеДанные = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, Выборка.ЧленКомиссии, "ФамилияИО, Пол, Фамилия", ОбщегоНазначения.ТекущаяДатаПользователя());
				Для каждого СтрокаКадровыхДанных Из КадровыеДанные Цикл
					МассивЧленовКомиссии.Добавить(Новый Структура("ЧленКомиссии, ФИОЧленКомиссии, ДолжностьЧленаКомиссии", Выборка.ЧленКомиссии, СтрокаКадровыхДанных.ФамилияИО, Выборка.ДолжностьЧленаКомиссии));
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		ДанныеПечати.Вставить("ЧленыКомиссии", МассивЧленовКомиссии);
		
		// Заполнение соответствия
		ДанныеПечатиОбъектов.Вставить(Выборка.Документ, ДанныеПечати);
		
	КонецЦикла;
	
	Возврат ДанныеПечатиОбъектов;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли