
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	РасчетЗарплатыРасширенныйФормы.ДокументыПриСозданииНаСервере(ЭтаФорма);
	
	Если Не ЗначениеЗаполнено(Параметры.Ключ) Тогда
		ЗаполнитьПервоначальныеЗначения();
		ПриПолученииДанныхНаСервере();
	КонецЕсли;
	
	ЭтотОбъект.ПодробныйРасчетФОТ = Ложь;
	
	КадровыйУчетРасширенный.УстановитьПараметрыВыбораНачисленийПоКатегорииЭлементуФормы(
		ЭтаФорма, "Начисление", КадровыйУчетРасширенный.ПараметрыВыбораКатегорииНачислений());
	
	// Обработчик подсистемы "ВерсионированиеОбъектов".
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ПриПолученииДанныхНаСервере();
	
	ОбменДаннымиЗарплатаКадры.ПриЧтенииНаСервереДокумента(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	РеквизитФормыВДанныеСотрудников(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ДанныеВРеквизит();
	УстановитьОтображениеНадписей();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Запись_НазначениеПлановогоНачисления", ПараметрыЗаписи, Объект.Ссылка);
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Элементы.Найти("Сотрудники") <> Неопределено Тогда
		
		ТекущийОбъект = РеквизитФормыВЗначение("Объект");
		РеквизитФормыВДанныеСотрудников(ТекущийОбъект);
		
		Если Не ТекущийОбъект.ПроверитьЗаполнение() Тогда
			Отказ = Истина;
		КонецЕсли;
		
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Объект"));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененыНачисления" И Источник = ЭтаФорма Тогда
		ЗаполнитьДанныеСотрудникаИзВРеменногоХранилища(Параметр.АдресВХранилище);
	КонецЕсли;
	
	Если ИмяСобытия = "ИзмененыПоказателиДокумента" И Источник.ВладелецФормы = ЭтаФорма Тогда
		
		Если Параметр.Показатели.Количество() > 0 Тогда 
			ОбработатьИзменениеПоказателейНаСервере(Параметр.Показатели);
		КонецЕсли;
		
		Если Параметр.Начисления.Количество() > 0 Тогда 
			ОбработатьИзменениеПоказателейНаСервере(Параметр.Начисления);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ЗарплатаКадрыРасширенныйКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтаФорма);
	ОрганизацияПриИзмененииНаСервере();
КонецПроцедуры
  
&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
    ЗарплатаКадрыРасширенныйКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ДатаНазначенияПриИзменении(Элемент)
	ДатаИзмененияПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура НачислениеПриИзменении(Элемент)
	НачислениеПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьДокументыВведенныеПозже(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗарплатаКадрыРасширенныйКлиент.ОткрытьВведенныеНаДатуДокументы(ЭтотОбъект.ДокументыВведенныеПозже);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьРанееВведенныеДокументы(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗарплатаКадрыРасширенныйКлиент.ОткрытьВведенныеНаДатуДокументы(ЭтотОбъект.РанееВведенныеДокументы);
	
КонецПроцедуры

#Область ОбработчикиСобытийЭлементовТаблицыФормыСотрудники

&НаКлиенте
Процедура СотрудникиПередНачаломИзменения(Элемент, Отказ)
	
	Строка = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.Сотрудники.ТекущаяСтрока);
	Если НЕ Строка = Неопределено Тогда
		МассивУдаляемыхСотрудников = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Строка.ИдентификаторСтрокиСотрудника);
		ЗафиксироватьУдаляемыхСотрудников(МассивУдаляемыхСотрудников);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиСотрудникПриИзменении(Элемент)
	СотрудникиСотрудникПриИзмененииНаСервере(Элементы.Сотрудники.ТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "СотрудникиФОТ"
		Или Поле.Имя = "СотрудникиПредставлениеНачислений" Тогда
		РедактироватьСоставНачислений = Поле.Имя = "СотрудникиПредставлениеНачислений";
		ОткрытьФормуРедактированияСоставаНачисленийИУдержаний(Элемент.ТекущиеДанные, Элемент.ТекущаяСтрока, РедактироватьСоставНачислений);		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПередУдалением(Элемент, Отказ)
	
	МассивУдаляемыхСотрудников = Новый Массив;
	
	Для каждого ИдентификаторУдаляемойСтроки Из Элемент.ВыделенныеСтроки Цикл
		Строка = Объект.Сотрудники.НайтиПоИдентификатору(ИдентификаторУдаляемойСтроки);
		Если НЕ Строка = Неопределено Тогда
			МассивУдаляемыхСотрудников.Добавить(Строка.ИдентификаторСтрокиСотрудника);
		КонецЕсли;
	КонецЦикла;
	
	ЗафиксироватьУдаляемыхСотрудников(МассивУдаляемыхСотрудников);
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПослеУдаления(Элемент)
	
	СотрудникиПослеУдаленияНаСервере();	
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.ТекущийЭлемент.Имя <> "СотрудникиСовокупнаяТарифнаяСтавка" Тогда
		Строка = Элементы.Сотрудники.ТекущиеДанные;
		Если НЕ Строка = Неопределено 
			И ЗначениеЗаполнено(Строка.Сотрудник) Тогда
			СотрудникиПриОкончанииРедактированияНаСервере(Элементы.Сотрудники.ТекущаяСтрока);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		
		МаксимальныйИдентификаторСтрокиСотрудника = МаксимальныйИдентификаторСтрокиСотрудника + 1;
		Элемент.ТекущиеДанные.ИдентификаторСтрокиСотрудника = МаксимальныйИдентификаторСтрокиСотрудника;
		
		Если Не Копирование Тогда
			
			Элемент.ТекущиеДанные.ДатаНазначения = Объект.ДатаНазначения;
			
			Если ЗначениеЗаполнено(Объект.ДатаОкончания) Тогда
				Элемент.ТекущиеДанные.ДатаОкончания = Объект.ДатаОкончания;
			КонецЕсли;
			
		КонецЕсли;
		
		ОбновитьДопустимостьНулевогоЗначенияПоказателейСтроки(Элемент.ТекущиеДанные, КолонкиПоказателей, ОписаниеПоказателей);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СотрудникиОбработкаВыбораНаСервере(ВыбранноеЗначение);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ПодборСотрудников(Команда)
	
	ПараметрыОткрытия = Неопределено;
	КадровыйУчетРасширенныйКлиент.ДобавитьПараметрыОтбораПоФункциональнойОпцииВыполнятьРасчетЗарплатыПоПодразделениям(
		ЭтаФорма, ПараметрыОткрытия);
		
	КадровыйУчетКлиент.ВыбратьСотрудниковРаботающихВПериодеПоПараметрамОткрытияФормыСписка(
		Элементы.Сотрудники,
		Объект.Организация, 
		Объект.Подразделение,
		Объект.ДатаНазначения, 
		Объект.ДатаОкончания,
		Истина, 
		АдресСпискаПодобранныхСотрудников(),
		ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетФОТПодробно(Команда)
	РасчетФОТПодробноНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоказатели(Команда)
	
	МассивПоказателей = Новый Массив;
	МассивНачислений = Новый Массив;
	
	Для каждого КолонкаПоказатель Из КолонкиПоказателей Цикл
		
		Если ТипЗнч(КолонкаПоказатель.Значение) = Тип("СправочникСсылка.ПоказателиРасчетаЗарплаты") Тогда
			МассивПоказателей.Добавить(КолонкаПоказатель.Значение);
		Иначе
			МассивНачислений.Добавить(КолонкаПоказатель.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	Если МассивПоказателей.Количество() = 0 И МассивНачислений.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru='Нет показателей к заполнению'"));
	Иначе
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("МассивПоказателей", МассивПоказателей);
		
		Если МассивНачислений.Количество() > 0 Тогда
			ПараметрыФормы.Вставить("МассивНачислений", МассивНачислений);
		КонецЕсли;
		
		ОткрытьФорму("ОбщаяФорма.ГрупповоеЗаполнениеПоказателейДокументов", ПараметрыФормы, ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СервернаяЧастьОбработчиковСобытийЭлементовФормы

&НаСервере
Процедура ДатаИзмененияПриИзмененииНаСервере()
	
	ПрочитатьВремяРегистрации();
	УстановитьОтображениеНадписей();
	УстановитьФункциональныеОпцииФормы();
	
КонецПроцедуры

&НаСервере
Процедура СотрудникиСотрудникПриИзмененииНаСервере(ИдентификаторСтроки)
	
	ЗавершитьУдалениеСотрудников();
		
КонецПроцедуры

&НаСервере
Процедура СотрудникиПослеУдаленияНаСервере()
	
	Если НЕ ЕстьУдаляемыеСотрудники() Тогда
		Возврат;
	КонецЕсли;
	
	ЗавершитьУдалениеСотрудников();
	
	УстановитьОтображениеНадписей();
	
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	
	ОбновитьИтогиСтрок();
	
КонецПроцедуры

&НаСервере
Процедура СотрудникиПриОкончанииРедактированияНаСервере(ИдентификаторСтроки)
	
	СтрокаСотрудника = Объект.Сотрудники.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	ПрочитатьВремяРегистрацииСтроки(Объект.Ссылка, СтрокаСотрудника);
	
	РеквизитФормыВНачисленияСотрудника(СтрокаСотрудника);
	
	ФильтрСотрудников = Документы.ИзменениеПлановыхНачислений.ПустойФильтрСотрудников();
	ЭлементФильтра = ФильтрСотрудников.Добавить();
	ЭлементФильтра.Сотрудник = СтрокаСотрудника.Сотрудник;
	ЭлементФильтра.ДатаИзменения = СтрокаСотрудника.ВремяРегистрации;
	ЭлементФильтра.ИдентификаторСтрокиСотрудника = СтрокаСотрудника.ИдентификаторСтрокиСотрудника;
	
	ПлановыеНачисленияПоказателиСотрудников = ПлановыеНачисленияПоказателиСотрудниковСИзменениямиДокумента(ФильтрСотрудников);
	
	ФОТПлановыхНачисленийСотрудников = ФОТПлановыхНачисленийСотрудниковСИзменениямиДокумента(ПлановыеНачисленияПоказателиСотрудников);
	ЗаполнитьФОТНачисленийСотрудников(ФОТПлановыхНачисленийСотрудников.ПлановыйФОТ);
	ФОТСотрудников = ФОТСотрудниковПоОписаниюНачислений(ФОТПлановыхНачисленийСотрудников.ПлановыйФОТ);
	СтрокаСотрудника.ФОТ = ФОТСотрудников.Получить(СтрокаСотрудника.ИдентификаторСтрокиСотрудника);
	
	ЗаполнитьЗначенияСовокупныхТарифныхСтавокСотрудников(ФОТПлановыхНачисленийСотрудников.ТарифныеСтавки);
	
	Запрос = ЗапросИзменяемыхНачислений(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаСотрудника));
	НачисленияСотрудникаСОписанием = НачисленияСотрудниковСОписанием(Запрос);
	НачисленияСотрудникаВСтроку(СтрокаСотрудника, НачисленияСотрудникаСОписанием, ПредставлениеПустойКоллекцииНачислений());
	
	УстановитьОтображениеНадписей();
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	
	ОбновитьИтогиСтрок();
КонецПроцедуры

&НаСервере
Процедура НачислениеПриИзмененииНаСервере()
	
	ФильтрСотрудников = Документы.ИзменениеПлановыхНачислений.ПустойФильтрСотрудников();
	Для каждого СтрокаСотрудника Из Объект.Сотрудники Цикл
		
		СтрокаСотрудника.Размер = 0;
		
		ЭлементФильтра = ФильтрСотрудников.Добавить();
		ЭлементФильтра.Сотрудник = СтрокаСотрудника.Сотрудник;
		ЭлементФильтра.ДатаИзменения = СтрокаСотрудника.ВремяРегистрации;
		ЭлементФильтра.ИдентификаторСтрокиСотрудника = СтрокаСотрудника.ИдентификаторСтрокиСотрудника;
		
	КонецЦикла;
	
	ИзменяемыеПоказатели = ИзменяемыеПоказатели(Объект.Начисление);
	ОписаниеПоказателей  = ОписаниеПоказателей(ИзменяемыеПоказатели);
	ИзменяемыеПоказателиВРеквизитФормы(ИзменяемыеПоказатели);
	
	ДанныеСотрудниковВРеквизитФормы();
	ФОТСотрудниковВРеквизитФормы();
	
	ПлановыеНачисленияПоказателиСотрудников = ПлановыеНачисленияПоказателиСотрудниковСИзменениямиДокумента(ФильтрСотрудников);
	
	ФОТПлановыхНачисленийСотрудников = ФОТПлановыхНачисленийСотрудниковСИзменениямиДокумента(ПлановыеНачисленияПоказателиСотрудников);
	ЗаполнитьФОТНачисленийСотрудников(ФОТПлановыхНачисленийСотрудников.ПлановыйФОТ);
	ФОТСотрудников = ФОТСотрудниковПоОписаниюНачислений(ФОТПлановыхНачисленийСотрудников.ПлановыйФОТ);
	ФОТСотрудниковВРеквизитФормы(ФОТСотрудников);
	
	ЗаполнитьЗначенияСовокупныхТарифныхСтавокСотрудников(ФОТПлановыхНачисленийСотрудников.ТарифныеСтавки);
	
	УстановитьДоступностьТаблицыСотрудники();
	
КонецПроцедуры

&НаСервере
Процедура СотрудникиОбработкаВыбораНаСервере(ВыбранноеЗначение)
	
	ФильтрСотрудников = Документы.ИзменениеПлановыхНачислений.ПустойФильтрСотрудников();
	
	ВремяРегистрацииСотрудников = ЗарплатаКадрыРасширенный.ВремяРегистрацииСотрудниковДокумента(Объект.Ссылка, ВыбранноеЗначение, Объект.ДатаНазначения);
	
	Для Каждого Сотрудник Из ВыбранноеЗначение Цикл
		
		МаксимальныйИдентификаторСтрокиСотрудника = МаксимальныйИдентификаторСтрокиСотрудника + 1;
		
		НоваяСтрока = Объект.Сотрудники.Добавить();
		НоваяСтрока.Сотрудник = Сотрудник;
		НоваяСтрока.ИдентификаторСтрокиСотрудника = МаксимальныйИдентификаторСтрокиСотрудника;
		
		НоваяСтрока.ДатаНазначения = Объект.ДатаНазначения;
		НоваяСтрока.ВремяРегистрации = ВремяРегистрацииСотрудников.Получить(Сотрудник);
		
		НоваяСтрока.ДатаОкончания = Объект.ДатаОкончания;
		
		ЭлементФильтра = ФильтрСотрудников.Добавить();
		ЭлементФильтра.Сотрудник = Сотрудник;
		ЭлементФильтра.ДатаИзменения = НоваяСтрока.ВремяРегистрации;
		ЭлементФильтра.ИдентификаторСтрокиСотрудника = НоваяСтрока.ИдентификаторСтрокиСотрудника;
		
		ОбновитьДопустимостьНулевогоЗначенияПоказателейСтроки(НоваяСтрока, КолонкиПоказателей, ОписаниеПоказателей);

	КонецЦикла;
	
	ПлановыеНачисленияПоказателиСотрудников = ПлановыеНачисленияПоказателиСотрудниковСИзменениямиДокумента(ФильтрСотрудников);
	
	ФОТПлановыхНачисленийСотрудников = ФОТПлановыхНачисленийСотрудниковСИзменениямиДокумента(ПлановыеНачисленияПоказателиСотрудников);
	ЗаполнитьФОТНачисленийСотрудников(ФОТПлановыхНачисленийСотрудников.ПлановыйФОТ);
	ФОТСотрудников = ФОТСотрудниковПоОписаниюНачислений(ФОТПлановыхНачисленийСотрудников.ПлановыйФОТ);
	ФОТСотрудниковВРеквизитФормы(ФОТСотрудников);
	
	ЗаполнитьЗначенияСовокупныхТарифныхСтавокСотрудников(ФОТПлановыхНачисленийСотрудников.ТарифныеСтавки);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДопустимостьНулевогоЗначенияПоказателейСтроки(Строка, КолонкиПоказателей, ОписаниеПоказателей)
	
	Для каждого КолонкаПоказатель Из КолонкиПоказателей Цикл		
		Строка[КолонкаПоказатель.Ключ + "ДопускаетсяНулевоеЗначение"] = ОписаниеПоказателей.Получить(КолонкаПоказатель.Значение).ДопускаетсяНулевоеЗначение;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	УстановитьФункциональныеОпцииФормы();
	УстановитьДоступностьТаблицыСотрудники();
	
КонецПроцедуры

#КонецОбласти

#Область ПриПолученииДанныхНаСервере

&НаСервере
Процедура ПриПолученииДанныхНаСервере()
	
	ДанныеВРеквизит();
	
	ЗарплатаКадрыРасширенный.ОформлениеНесколькихДокументовНаОднуДатуДополнитьФорму(ЭтотОбъект);	
	
	ЗарплатаКадры.КлючевыеРеквизитыЗаполненияФормыЗаполнитьПредупреждения(ЭтаФорма);
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	
	УстановитьФункциональныеОпцииФормы();
	УстановитьВидимостьЭлементовФормы();
	УстановитьОтображениеНадписей();
	
	УстановитьДоступностьТаблицыСотрудники();
	
КонецПроцедуры

&НаСервере
Процедура ДанныеВРеквизит()
	
	КолонкиПоказателей = Неопределено;
	
	ПрочитатьВремяРегистрации();
	
	МаксимальныйИдентификаторСтрокиСотрудника = ЗарплатаКадрыРасширенный.МаксимальныйИдентификаторСтроки(
		Объект.Сотрудники, "ИдентификаторСтрокиСотрудника");
	
	ИзменяемыеПоказатели = ИзменяемыеПоказатели(Объект.Начисление);
	ОписаниеПоказателей  = ОписаниеПоказателей(ИзменяемыеПоказатели);
	ИзменяемыеПоказателиВРеквизитФормы(ИзменяемыеПоказатели);
	
	ДанныеСотрудниковВРеквизитФормы();
	ФОТСотрудниковВРеквизитФормы();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы()
	
	УстановитьВидимостьКолонокПодробногоРасчета();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКолонокПодробногоРасчета()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "СотрудникиСовокупнаяТарифнаяСтавка", "Видимость", ЭтотОбъект.ПодробныйРасчетФОТ);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "СотрудникиФОТ", "Видимость", ЭтотОбъект.ПодробныйРасчетФОТ);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "СотрудникиПредставлениеНачислений", "Видимость", ЭтотОбъект.ПодробныйРасчетФОТ);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеНадписей()
	
	УстановитьПривилегированныйРежим(Истина);
	СотрудникиДаты = Объект.Сотрудники.Выгрузить(, "ВремяРегистрации, Сотрудник");
	ЗарплатаКадрыРасширенный.УстановитьТекстНадписиОКонкурирующихДокументахПлановыхНачислений(ЭтотОбъект, СотрудникиДаты, Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьТаблицыСотрудники()
	
	ДоступностьТаблицы = ЗначениеЗаполнено(Объект.Организация) И ЗначениеЗаполнено(Объект.Начисление);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Сотрудники",
		"Доступность",
		ДоступностьТаблицы);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СотрудникиЗаполнитьПоказатели",
		"Доступность",
		ДоступностьТаблицы И ЕстьРедактируемыеПоказатели());
	
КонецПроцедуры

&НаСервере
Функция ЕстьРедактируемыеПоказатели()
	Возврат НЕ КолонкиПоказателей = Неопределено И КолонкиПоказателей.Количество() > 0;
КонецФункции

#КонецОбласти

#Область ДанныеСотрудниковВРеквизитФормы

&НаСервере
Процедура ДанныеСотрудниковВРеквизитФормы()
	
	Если Объект.Сотрудники.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Запрос 							= ЗапросИзменяемыхНачислений();
	НачисленияСотрудниковСОписанием = НачисленияСотрудниковСОписанием(Запрос);
	ПоказателиСотрудников 			= ПоказателиСотрудников(Запрос);
	
	ДанныеСотрудниковВСтроки(НачисленияСотрудниковСОписанием, ПоказателиСотрудников);
	
КонецПроцедуры

&НаСервере
Процедура ИзменяемыеПоказателиВРеквизитФормы(ИзменяемыеПоказатели)
	
	Если ИзменяемыеПоказатели.Количество() = 0
		И (КолонкиПоказателей = Неопределено
			Или КолонкиПоказателей.Количество() = 0) Тогда
		
		КолонкиПоказателей = Новый ФиксированнаяСтруктура;
		Возврат;
		
	КонецЕсли;
	
	ДополнитьФормуИзменяемымиПоказателями(ИзменяемыеПоказатели);
	
КонецПроцедуры

&НаСервере
Процедура ФОТСотрудниковВРеквизитФормы(ФОТСотрудников = Неопределено)
	
	Если Объект.Сотрудники.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Если ФОТСотрудников = Неопределено Тогда
		ФОТСотрудников = ФОТСотрудников();
	КонецЕсли;
	
	Для каждого СтрокаСотрудника Из Объект.Сотрудники Цикл
		
		ФОТСотрудника = ФОТСотрудников.Получить(СтрокаСотрудника.ИдентификаторСтрокиСотрудника);
		Если ФОТСотрудника <> Неопределено Тогда
			СтрокаСотрудника.ФОТ = ФОТСотрудника;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ЗапросИзменяемыхНачислений(НачисленияСотрудника = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачисленияСотрудников.Сотрудник,
		|	НачисленияСотрудников.ИдентификаторСтрокиСотрудника,
		|	ЗНАЧЕНИЕ(Перечисление.ДействияСНачислениямиИУдержаниями.Утвердить) КАК Действие,
		|	&Начисление КАК Начисление,
		|	НачисленияСотрудников.Размер
		|ПОМЕСТИТЬ ВТНачисления
		|ИЗ
		|	&НачисленияСотрудников КАК НачисленияСотрудников";
	
	Если НачисленияСотрудника = Неопределено Тогда
		НачисленияСотрудников = Объект.Сотрудники.Выгрузить(, "Сотрудник,Размер,ИдентификаторСтрокиСотрудника");
	Иначе
		НачисленияСотрудников = Объект.Сотрудники.Выгрузить(НачисленияСотрудника, "Сотрудник,Размер,ИдентификаторСтрокиСотрудника");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("НачисленияСотрудников", НачисленияСотрудников);
	Запрос.УстановитьПараметр("Начисление", Объект.Начисление);
	
	Запрос.Выполнить();
	
	Возврат Запрос;
	
КонецФункции

&НаСервере
Функция НачисленияСотрудниковСОписанием(Запрос)
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	НачисленияДокумента.Сотрудник КАК Сотрудник,
		|	НачисленияДокумента.ИдентификаторСтрокиСотрудника КАК ИдентификаторСтрокиСотрудника,
		|	НачисленияДокумента.Начисление,
		|	НачисленияДокумента.Размер,
		|	НачисленияДокумента.Действие,
		|	Начисления.Представление,
		|	Начисления.РеквизитДопУпорядочивания КАК РеквизитДопУпорядочивания,
		|	НЕ Начисления.Рассчитывается КАК НачислениеФиксированнойСуммой,
		|	Начисления.Код
		|ИЗ
		|	ВТНачисления КАК НачисленияДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК Начисления
		|		ПО НачисленияДокумента.Начисление = Начисления.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник,
		|	РеквизитДопУпорядочивания";
	
	НачисленияСотрудниковСОписанием = Запрос.Выполнить().Выгрузить();
	НачисленияСотрудниковСОписанием.Индексы.Добавить("ИдентификаторСтрокиСотрудника");
	
	Возврат НачисленияСотрудниковСОписанием;
	
КонецФункции

&НаСервере
Функция ПоказателиСотрудников(Запрос)
	
	ПоказателиСотрудников = Новый ТаблицаЗначений;	
	ПоказателиСотрудников.Колонки.Добавить("Сотрудник");
	ПоказателиСотрудников.Колонки.Добавить("Показатель");
	ПоказателиСотрудников.Колонки.Добавить("Значение");
	ПоказателиСотрудников.Колонки.Добавить("ИдентификаторСтрокиСотрудника");
	Для каждого Строка Из Объект.ПоказателиСотрудников Цикл
		ЗаполнитьЗначенияСвойств(ПоказателиСотрудников.Добавить(), Строка);
	КонецЦикла;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачисленияДокумента.Сотрудник КАК Сотрудник,
	|	НачисленияДокумента.ИдентификаторСтрокиСотрудника КАК ИдентификаторСтрокиСотрудника,
	|	НачисленияДокумента.Начисление КАК Показатель,
	|	НачисленияДокумента.Размер КАК Значение
	|ИЗ
	|	ВТНачисления КАК НачисленияДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК Начисления
	|		ПО НачисленияДокумента.Начисление = Начисления.Ссылка
	|ГДЕ
	|	НЕ Начисления.Рассчитывается
	|	И НЕ НачисленияДокумента.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСНачислениямиИУдержаниями.Отменить)";
	
	НачисленияФиксированнойСуммой = Запрос.Выполнить().Выгрузить();
	Для каждого Строка Из НачисленияФиксированнойСуммой Цикл
		ЗаполнитьЗначенияСвойств(ПоказателиСотрудников.Добавить(), Строка);
	КонецЦикла;
	
	ПоказателиСотрудников.Индексы.Добавить("ИдентификаторСтрокиСотрудника");
	
	Возврат ПоказателиСотрудников;
	
КонецФункции

&НаСервере
Функция ИзменяемыеПоказатели(Начисление)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Начисление", Начисление);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НачисленияПоказатели.Показатель,
	|	ПоказателиРасчетаЗарплаты.Идентификатор,
	|	ПоказателиРасчетаЗарплаты.КраткоеНаименование КАК Заголовок,
	|	ПоказателиРасчетаЗарплаты.ДопускаетсяНулевоеЗначение КАК ДопускаетсяНулевоеЗначение,
	|	ПоказателиРасчетаЗарплаты.РеквизитДопУпорядочивания КАК РеквизитДопУпорядочивания,
	|	ПоказателиРасчетаЗарплаты.ТипПоказателя
	|ИЗ
	|	ПланВидовРасчета.Начисления.Показатели КАК НачисленияПоказатели
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПоказателиРасчетаЗарплаты КАК ПоказателиРасчетаЗарплаты
	|		ПО НачисленияПоказатели.Показатель = ПоказателиРасчетаЗарплаты.Ссылка
	|ГДЕ
	|	НачисленияПоказатели.Ссылка = &Начисление
	|	И НачисленияПоказатели.ЗапрашиватьПриВводе
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПВРНачисления.Ссылка,
	|	NULL,
	|	ВЫБОР
	|		КОГДА ПВРНачисления.КраткоеНаименование = """"
	|			ТОГДА ПВРНачисления.Наименование
	|		ИНАЧЕ ПВРНачисления.КраткоеНаименование
	|	КОНЕЦ,
	|	ЛОЖЬ,
	|	ПВРНачисления.РеквизитДопУпорядочивания,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейРасчетаЗарплаты.Денежный)
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК ПВРНачисления
	|ГДЕ
	|	ПВРНачисления.Ссылка = &Начисление
	|	И НЕ ПВРНачисления.Рассчитывается
	|
	|УПОРЯДОЧИТЬ ПО
	|	РеквизитДопУпорядочивания";
		
	ИзменяемыеПоказатели = Запрос.Выполнить().Выгрузить();
	ИзменяемыеПоказатели.Индексы.Добавить("Показатель"); 
	ЗаполнитьИдентификаторыНачисленийФиксированнойСуммой(ИзменяемыеПоказатели);
	
	Возврат ИзменяемыеПоказатели;
	
КонецФункции 

#Область ДополнитьФормуИзменяемымиПоказателями

&НаСервере
Процедура ДополнитьФормуИзменяемымиПоказателями(ИзменяемыеПоказатели)
	
	ДобавитьРеквизитыПоказателей(ИзменяемыеПоказатели);
	ДобавитьЭлементыПоказателей(ИзменяемыеПоказатели);
	
КонецПроцедуры

#Область ДобавитьРеквизитыПоказателей

&НаСервере
Процедура ДобавитьРеквизитыПоказателей(ИзменяемыеПоказатели)
	
	СуществующиеРеквизиты = МассивИменРеквизитовФормы();
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	УдаляемыеРеквизиты = Новый Массив;
	Если КолонкиПоказателей <> Неопределено Тогда
		
		ИмяТаблицы = ИмяТаблицыПоказатели();
		Для каждого СтрокаПоказателя Из КолонкиПоказателей Цикл
			
			ПолныйПутьКРеквизиту = ИмяТаблицы + "." + СтрокаПоказателя.Ключ;
			Если СуществующиеРеквизиты.Найти(ПолныйПутьКРеквизиту) = Неопределено Тогда
				УдаляемыеРеквизиты.Добавить(ПолныйПутьКРеквизиту);
				УдаляемыеРеквизиты.Добавить(ПолныйПутьКРеквизиту + "ДопускаетсяНулевоеЗначение");
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ДобавитьРеквизитыИзменяемыеПоказатели(ИзменяемыеПоказатели, ДобавляемыеРеквизиты);
	
	ЗарплатаКадры.ИзменитьРеквизитыФормы(ЭтаФорма, ДобавляемыеРеквизиты, СуществующиеРеквизиты, УдаляемыеРеквизиты);
	
	ЗаполнитьСоответствиеРеквизитовИПоказателей(ИзменяемыеПоказатели);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьРеквизитыИзменяемыеПоказатели(Показатели, ДобавляемыеРеквизиты)
	
	Для Каждого Показатель Из Показатели Цикл
		ДобавитьРеквизитыПоказателя(ДобавляемыеРеквизиты, Показатель.Идентификатор, Показатель.Заголовок, Показатель.ТипПоказателя);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСоответствиеРеквизитовИПоказателей(ТаблицаПоказателей)
	
	СтруктураКолонкиПоказателей = Новый Структура;
	Для Каждого СтрокаПоказателя Из ТаблицаПоказателей Цикл
		СтруктураКолонкиПоказателей.Вставить(СтрокаПоказателя.Идентификатор, СтрокаПоказателя.Показатель); 
	КонецЦикла;
	
	ЭтотОбъект.КолонкиПоказателей = Новый ФиксированнаяСтруктура(СтруктураКолонкиПоказателей);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьРеквизитыПоказателя(ДобавляемыеРеквизиты, ИмяРеквизита, Заголовок, ТипПоказателя)
	
	ДобавитьРеквизитПоказателя(ДобавляемыеРеквизиты, ИмяРеквизита, Справочники.ПоказателиРасчетаЗарплаты.ОписаниеТиповЗначенияПоказателяРасчетаЗарплаты(), Заголовок, ТипПоказателя);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьРеквизитПоказателя(ДобавляемыеРеквизиты, ПутьКДанным, ТипЗначения, Заголовок, ТипПоказателя)
	
	ИмяТаблицы = ИмяТаблицыПоказатели();
	
	НовыйПоказатель = Новый РеквизитФормы(ПутьКДанным, ТипЗначения, ИмяТаблицы, Заголовок);
	ДобавляемыеРеквизиты.Добавить(НовыйПоказатель);
	
	ДопускаетсяНулевоеЗначениеПоказателя = Новый РеквизитФормы(ПутьКДанным + "ДопускаетсяНулевоеЗначение", Новый ОписаниеТипов("Булево"), ИмяТаблицы);
	ДобавляемыеРеквизиты.Добавить(ДопускаетсяНулевоеЗначениеПоказателя);
	
	Если ТипПоказателя = Перечисления.ТипыПоказателейРасчетаЗарплаты.Денежный Тогда
	
		НовыйПоказатель = Новый РеквизитФормы("Итог" + ПутьКДанным, Новый ОписаниеТипов("Число"));
		ДобавляемыеРеквизиты.Добавить(НовыйПоказатель);
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция МассивИменРеквизитовФормы()
	МассивИменРеквизитовФормы = Новый Массив;
	ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(ЭтаФорма, МассивИменРеквизитовФормы);
	ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(ЭтаФорма, МассивИменРеквизитовФормы, "Объект.Сотрудники");
	Возврат МассивИменРеквизитовФормы;
КонецФункции 

#КонецОбласти

#Область ДобавитьЭлементыПоказателей

&НаСервере
Процедура ДобавитьЭлементыПоказателей(ИзменяемыеПоказатели)
	
	УдалитьЭлементыИзменяемыхПоказателей();
	
	ДобавитьЭлементыИзменяемыеПоказатели(ИзменяемыеПоказатели);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьЭлементыИзменяемыхПоказателей()
	ГруппаПоказателиСотрудников = ГруппаПоказателиСотрудников();
	ЗарплатаКадры.УдалитьПодчиненныеЭлементыГруппы(ЭтаФорма, ГруппаПоказателиСотрудников);
КонецПроцедуры

&НаСервере
Процедура ДобавитьЭлементыИзменяемыеПоказатели(ТаблицаПоказателей)
	
	ГруппаПоказатели = ГруппаПоказателиСотрудников();
	ПоказыватьИтоги = Ложь;
	
	Для Каждого СтрокаПоказателя Из ТаблицаПоказателей Цикл
		
		НовыйЭлемент = НовыйЭлементФормыИзменяемыйПоказатель(СтрокаПоказателя.Идентификатор, ГруппаПоказатели, СтрокаПоказателя.Заголовок);
		УстановитьФорматИзменяемогоПоказателя(НовыйЭлемент, СтрокаПоказателя.Показатель);
		УстановитьУсловноеОформлениеПоказателя(НовыйЭлемент, СтрокаПоказателя.Показатель);
		
		Если СтрокаПоказателя.ТипПоказателя = Перечисления.ТипыПоказателейРасчетаЗарплаты.Денежный Тогда
			
			ПоказыватьИтоги = Истина;
			НовыйЭлемент.ОтображатьВПодвале = Истина;
			НовыйЭлемент.ПутьКДаннымПодвала = "Итог" + СтрокаПоказателя.Идентификатор;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Сотрудники",
		"Подвал",
		ПоказыватьИтоги);
	
КонецПроцедуры

&НаСервере
Функция НовыйЭлементФормыИзменяемыйПоказатель(ПутьКДанным, Группа, Заголовок)
	
	ИмяЭлемента = "ПоказателиСотрудников" + ПутьКДанным;
	ЭлементПоказатель = Элементы.Найти(ИмяЭлемента);
	
	Если ЭлементПоказатель = Неопределено Тогда
		
		ЭлементПоказатель = ЭтаФорма.Элементы.Добавить("ПоказателиСотрудников" + ПутьКДанным, Тип("ПолеФормы"), Группа);
		ЭлементПоказатель.ПутьКДанным = "Объект.Сотрудники." + ПутьКДанным;
		ЭлементПоказатель.Заголовок = Заголовок;
		ЭлементПоказатель.Вид = ВидПоляФормы.ПолеВвода;
		ЭлементПоказатель.ОтображатьВШапке = Истина;
		ЭлементПоказатель.РастягиватьПоГоризонтали = Истина;
		
	КонецЕсли;
	
	Возврат ЭлементПоказатель;
	
КонецФункции

&НаСервере
Процедура УстановитьФорматИзменяемогоПоказателя(ЭлементПоказатель, Показатель)
	Если ТипЗнч(Показатель) = Тип("СправочникСсылка.ПоказателиРасчетаЗарплаты") Тогда
		ПоказательИнфо = ЗарплатаКадрыРасширенный.СведенияОПоказателеРасчетаЗарплаты(Показатель);
		ЭлементПоказатель.ОграничениеТипа = ПоказательИнфо.ТипПоказателя;
		ЭлементПоказатель.Формат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ЧДЦ=%1", ПоказательИнфо["Точность"]);
	Иначе	
		ЭлементПоказатель.ОграничениеТипа = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2));
		ЭлементПоказатель.Формат = "ЧДЦ=2";
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеПоказателя(ЭлементПоказатель, Показатель)
	
	ЭлементУсловногоОформления = ЭтаФорма.УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	ЭлементОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ЭлементПоказатель.ПутьКДанным);
	
	ЭлементОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ЭлементПоказатель.ПутьКДанным + "ДопускаетсяНулевоеЗначение");	
	ЭлементОтбора.ПравоеЗначение = Ложь;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить(); 
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(ЭлементПоказатель.Имя);
	
КонецПроцедуры

&НаСервере
Функция ГруппаПоказателиСотрудников()
	Возврат ЭтаФорма.Элементы.Найти("ГруппаПоказателиСотрудников");
КонецФункции 

&НаСервере
Функция ИмяТаблицыПоказатели()
	Возврат "Объект.Сотрудники";
КонецФункции 

#КонецОбласти

&НаСервере
Процедура ЗаполнитьИдентификаторыНачисленийФиксированнойСуммой(ИзменяемыеПоказатели)
	Для каждого Строка Из ИзменяемыеПоказатели Цикл
		Если Строка.Идентификатор = Null Тогда
			Строка.Идентификатор = "а" + СтрЗаменить(Строка(Строка.Показатель.УникальныйИдентификатор()), "-", "");
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти 

&НаСервере
Процедура ДанныеСотрудниковВСтроки(НачисленияСотрудниковСОписанием, ПоказателиСотрудников)
	
	Отбор = Новый Структура("ИдентификаторСтрокиСотрудника");
	
	ПредставлениеПустойКоллекцииНачислений = ПредставлениеПустойКоллекцииНачислений();
	
	Для каждого СтрокаСотрудника Из Объект.Сотрудники Цикл
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаСотрудника);
		
		ПоказателиСотрудника = ПоказателиСотрудников.НайтиСтроки(Отбор);
		НачисленияСотрудника = НачисленияСотрудниковСОписанием.НайтиСтроки(Отбор);
		
		ДанныеСотрудникаВСтроку(СтрокаСотрудника, ПоказателиСотрудника, НачисленияСотрудника, ПредставлениеПустойКоллекцииНачислений);
		
	КонецЦикла;
	
	ОбновитьИтогиСтрок();
	
	Объект.ПоказателиСотрудников.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИтогиСтрок()
	
	Для каждого ОписаниеПоказателя Из ОписаниеПоказателей Цикл
		
		Если ОписаниеПоказателя.Значение.ТипПоказателя = Перечисления.ТипыПоказателейРасчетаЗарплаты.Денежный Тогда
			
			ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
				ЭтаФорма, "Итог" + ОписаниеПоказателя.Значение.ИдентификаторПоказателя, Объект.Сотрудники.Итог(ОписаниеПоказателя.Значение.ИдентификаторПоказателя));
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДанныеСотрудникаВСтроку(СтрокаСотрудника, ПоказателиСотрудника, НачисленияСотрудника, ПредставлениеПустойКоллекцииНачислений)
	
	НачисленияСотрудникаВСтроку(СтрокаСотрудника, НачисленияСотрудника, ПредставлениеПустойКоллекцииНачислений);
	
	ПоказателиСотрудникаВСтроку(СтрокаСотрудника, ПоказателиСотрудника);
	
	ОбновитьДопустимостьНулевогоЗначенияПоказателейСтроки(СтрокаСотрудника, КолонкиПоказателей, ОписаниеПоказателей);
	
КонецПроцедуры	

&НаСервере
Процедура НачисленияСотрудникаВСтроку(СтрокаСотрудника, НачисленияСотрудника, ПредставлениеПустойКоллекцииНачислений)
	
	Если НачисленияСотрудника.Количество() = 0 Тогда
		СтрокаСотрудника.ПредставлениеНачислений = ПредставлениеПустойКоллекцииНачислений;
		СтрокаСотрудника.ФОТ = 0;
		Возврат;	
	КонецЕсли;
	
	ПредставлениеНачисленийСотрудника = "";
	ПредставлениеОтмененныхНачисленийСотрудника = "";
	ЕстьОтмененныеНачисления = Ложь;
	
	Для каждого СтрокаНачисления Из НачисленияСотрудника Цикл
		
		Если СтрокаНачисления.Размер = 0 Тогда
			ПредставлениеНачисленийСотрудника = ПредставлениеНачисленийСотрудника + СтрокаНачисления.Представление+ ", ";
		Иначе
			ПредставлениеНачисленийСотрудника = ПредставлениеНачисленийСотрудника + СтрокаНачисления.Представление + "=" + СтрокаНачисления.Размер + ", ";		
		КонецЕсли;
		
	КонецЦикла;
	
	ПредставлениеНачисленийСотрудника = Лев(ПредставлениеНачисленийСотрудника, СтрДлина(ПредставлениеНачисленийСотрудника) - 2);
	Если ЕстьОтмененныеНачисления Тогда
		
		ПредставлениеОтмененныхНачисленийСотрудника = Лев(ПредставлениеОтмененныхНачисленийСотрудника, СтрДлина(ПредставлениеОтмененныхНачисленийСотрудника) - 2);
		
		Если Не ПустаяСтрока(ПредставлениеНачисленийСотрудника) Тогда
			ПредставлениеНачисленийСотрудника = ПредставлениеНачисленийСотрудника + "; ";
		КонецЕсли;
		
		ПредставлениеНачисленийСотрудника = ПредставлениеНачисленийСотрудника + СтрШаблон(НСтр("ru = 'Отменены: %1'"), ПредставлениеОтмененныхНачисленийСотрудника); 
		
	КонецЕсли;
	
	СтрокаСотрудника.ПредставлениеНачислений = ПредставлениеНачисленийСотрудника;
	
КонецПроцедуры	

&НаСервере
Процедура ПоказателиСотрудникаВСтроку(СтрокаСотрудника, ПоказателиСотрудника)
	
	Для каждого ПоказательСотрудника Из ПоказателиСотрудника Цикл
		ОписаниеИзменяемогоПоказателя = ОписаниеПоказателей[ПоказательСотрудника.Показатель];
		Если Не ОписаниеИзменяемогоПоказателя = Неопределено Тогда
			ПоместитьДанныеПоказателяВСтроку(СтрокаСотрудника, ОписаниеИзменяемогоПоказателя, ПоказательСотрудника.Значение);	
		КонецЕсли;	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ОписаниеПоказателей(ИзменяемыеПоказатели)
	
	ИдентификаторыПоказателей = Новый Соответствие;	
	
	Для каждого Строка Из ИзменяемыеПоказатели Цикл
		СтруктураОписания = Новый Структура("ИдентификаторПоказателя, ДопускаетсяНулевоеЗначение, ТипПоказателя", Строка.Идентификатор, Строка.ДопускаетсяНулевоеЗначение, Строка.ТипПоказателя);
		ИдентификаторыПоказателей.Вставить(Строка.Показатель, СтруктураОписания);
	КонецЦикла;
	
	Возврат Новый ФиксированноеСоответствие(ИдентификаторыПоказателей);
	
КонецФункции

&НаСервере
Процедура ПоместитьДанныеПоказателяВСтроку(Строка, ОписаниеПоказателя, Размер)
	
	Строка[ОписаниеПоказателя.ИдентификаторПоказателя] = Размер;
	
КонецПроцедуры

&НаСервере
Функция ПредставлениеПустойКоллекцииНачислений()
	
	Возврат НСТр("ru='Ввести'");	
	
КонецФункции

#КонецОбласти

#Область РеквизитФормыВДанныеСотрудников

&НаСервере
Процедура РеквизитФормыВДанныеСотрудников(ОбъектПолучатель)
	
	Если ЕстьРедактируемыеПоказатели() Тогда
		РеквизитФормыВПоказателиСотрудников(ОбъектПолучатель);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РеквизитФормыВПоказателиСотрудников(ОбъектПолучатель)
	Для каждого ПоказателиСотрудника Из Объект.Сотрудники Цикл
		РеквизитФормыВПоказателиСотрудника(ОбъектПолучатель.ПоказателиСотрудников, ПоказателиСотрудника);
		РеквизитФормыВНачисленияСотрудника(ПоказателиСотрудника, ОбъектПолучатель);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура РеквизитФормыВПоказателиСотрудника(ПоказателиСотрудников, ПоказателиСотрудника)
	
	Если Не ЕстьРедактируемыеПоказатели() Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого КолонкаПоказатель Из КолонкиПоказателей Цикл
		
		Если ТипЗнч(КолонкаПоказатель.Значение) = Тип("СправочникСсылка.ПоказателиРасчетаЗарплаты") Тогда	
			Строка									= ПоказателиСотрудников.Добавить();
			Строка.ИдентификаторСтрокиСотрудника	= ПоказателиСотрудника.ИдентификаторСтрокиСотрудника;
			Строка.Показатель						= КолонкаПоказатель.Значение;
			Строка.Значение							= ПоказателиСотрудника[КолонкаПоказатель.Ключ];
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура РеквизитФормыВНачисленияСотрудника(ПоказателиСотрудника, ТекущийОбъект = Неопределено)
	
	Если Не ЕстьРедактируемыеПоказатели() Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого КолонкаПоказатель Из КолонкиПоказателей Цикл
		
		Если ТипЗнч(КолонкаПоказатель.Значение) = Тип("ПланВидовРасчетаСсылка.Начисления") Тогда
			
			Если ТекущийОбъект = Неопределено Тогда
				ПоказателиСотрудника.Размер = ПоказателиСотрудника[КолонкаПоказатель.Ключ];
			Иначе
				
				СтрокиСотрудника = ТекущийОбъект.Сотрудники.НайтиСтроки(Новый Структура("ИдентификаторСтрокиСотрудника", ПоказателиСотрудника.ИдентификаторСтрокиСотрудника));
				Для каждого СтрокаСотрудника Из СтрокиСотрудника Цикл
					СтрокаСотрудника.Размер = ПоказателиСотрудника[КолонкаПоказатель.Ключ];
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ФОТ

&НаСервере
Функция ФОТПлановыхНачисленийСотрудниковСИзменениямиДокумента(ПлановыеНачисленияПоказателиСотрудников)
	СоответсвиеСотрудниковПериодовИдентификаторам = Новый Соответствие;
	
	ГоловнаяОрганизация = ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(Объект.Организация);
	ТаблицаНачислений = ПлановыеНачисленияСотрудников.ТаблицаНачисленийДляРасчетаВторичныхДанных();
	ТаблицаПоказателей = ПлановыеНачисленияСотрудников.ТаблицаИзвестныеПоказатели();
	РассчитываемыеОбъекты = Новый Соответствие;
	
	Отбор = Новый Структура;
	ВремяРегистрацииСотрудников = ВремяРегистрацииСотрудников();
	Для Каждого СтрокаСотрудника Из ПлановыеНачисленияПоказателиСотрудников.Сотрудники Цикл
		ВремяРегистрации = ВремяРегистрацииСотрудников[СтрокаСотрудника.ИдентификаторСтрокиСотрудника];
		
		СоответсвеПериодаИдентфикатору = СоответсвиеСотрудниковПериодовИдентификаторам[СтрокаСотрудника.Сотрудник];
		Если СоответсвеПериодаИдентфикатору = Неопределено Тогда
			СоответсвеПериодаИдентфикатору = Новый Соответствие;
			СоответсвиеСотрудниковПериодовИдентификаторам.Вставить(СтрокаСотрудника.Сотрудник, СоответсвеПериодаИдентфикатору);
		КонецЕсли;
		
		СоответсвеПериодаИдентфикатору.Вставить(ВремяРегистрации, СтрокаСотрудника.ИдентификаторСтрокиСотрудника);	
		
		
		Отбор.Вставить("ИдентификаторСтрокиСотрудника", СтрокаСотрудника.ИдентификаторСтрокиСотрудника);
				
		СтрокиПоСотруднику = ПлановыеНачисленияПоказателиСотрудников.НачисленияСотрудников.НайтиСтроки(Отбор);
		Для Каждого СтрокаНачисления Из СтрокиПоСотруднику Цикл
			ДанныеНачисления = ТаблицаНачислений.Добавить();
			ДанныеНачисления.Сотрудник = СтрокаСотрудника.Сотрудник;
			ДанныеНачисления.ГоловнаяОрганизация = ГоловнаяОрганизация;
			ДанныеНачисления.Период = ВремяРегистрации;
			ДанныеНачисления.Начисление = СтрокаНачисления.Начисление;
			ДанныеНачисления.ДокументОснование = СтрокаНачисления.ДокументОснование;
			ДанныеНачисления.Размер = СтрокаНачисления.Размер;
		КонецЦикла;
		
		СтрокиПоСотруднику = ПлановыеНачисленияПоказателиСотрудников.ПоказателиСотрудников.НайтиСтроки(Отбор);
		Для Каждого СтрокаПоказателя Из СтрокиПоСотруднику Цикл
			ДанныеПоказателя = ТаблицаПоказателей.Добавить();
			ДанныеПоказателя.Сотрудник = СтрокаСотрудника.Сотрудник;
			ДанныеПоказателя.ГоловнаяОрганизация = ГоловнаяОрганизация;
			ДанныеПоказателя.Период = ВремяРегистрации;
			ДанныеПоказателя.Показатель = СтрокаПоказателя.Показатель;
			ДанныеПоказателя.ДокументОснование = СтрокаПоказателя.ДокументОснование;
			ДанныеПоказателя.Значение = СтрокаПоказателя.Значение;
		КонецЦикла;		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Истина);	
	РассчитанныеДанные = ПлановыеНачисленияСотрудников.РассчитатьВторичныеДанныеПлановыхНачислений(ТаблицаНачислений, ТаблицаПоказателей);	
	УстановитьПривилегированныйРежим(Ложь);
	
	РассчитанныеДанные.ПлановыйФОТ.Колонки.Добавить("ИдентификаторСтрокиСотрудника");
	Для Каждого СтрокаНачисления Из РассчитанныеДанные.ПлановыйФОТ Цикл
		СтрокаНачисления.ИдентификаторСтрокиСотрудника = СоответсвиеСотрудниковПериодовИдентификаторам[СтрокаНачисления.Сотрудник][СтрокаНачисления.Период];
	КонецЦикла;		
	РассчитанныеДанные.ТарифныеСтавки.Колонки.Добавить("ИдентификаторСтрокиСотрудника");
	
	Для Каждого СтрокаТарифнойСтавки Из РассчитанныеДанные.ТарифныеСтавки Цикл
		СтрокаТарифнойСтавки.ИдентификаторСтрокиСотрудника = СоответсвиеСотрудниковПериодовИдентификаторам[СтрокаТарифнойСтавки.Сотрудник][СтрокаТарифнойСтавки.Период];
	КонецЦикла;	
	
	Возврат РассчитанныеДанные;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьФОТНачисленийСотрудников(ФОТПлановыхНачисленийСотрудников)
	
	Для Каждого ДанныеФОТНачисления Из ФОТПлановыхНачисленийСотрудников Цикл
		
		Отбор = Новый Структура;
		Отбор.Вставить("ИдентификаторСтрокиСотрудника", ДанныеФОТНачисления.ИдентификаторСтрокиСотрудника);
			
		Если Не ДанныеФОТНачисления.Начисление = Объект.Начисление Тогда
			Продолжить;
		КонецЕсли;
			
		СтрокиСотрудника = Объект.Сотрудники.НайтиСтроки(Отбор);
		Для каждого СтрокаСотрудника Из СтрокиСотрудника Цикл
			СтрокаСотрудника.Размер = ДанныеФОТНачисления.ВкладВФОТ;
		КонецЦикла;
				
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ФильтрСотрудниковДокумента()
	
	Если Объект.Сотрудники.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ФильтрСотрудников = Документы.ИзменениеПлановыхНачислений.ПустойФильтрСотрудников();
	Для каждого СтрокаСотрудника Из Объект.Сотрудники Цикл
		ЭлементФильтра = ФильтрСотрудников.Добавить();
		ЭлементФильтра.Сотрудник = СтрокаСотрудника.Сотрудник;
		ЭлементФильтра.ДатаИзменения = СтрокаСотрудника.ВремяРегистрации;
		ЭлементФильтра.ИдентификаторСтрокиСотрудника = СтрокаСотрудника.ИдентификаторСтрокиСотрудника;
	КонецЦикла;
	
	Возврат ФильтрСотрудников;
	
КонецФункции

&НаСервере
Функция ФОТСотрудников()
	
	ФОТСотрудников = Новый Соответствие;
	
	Если ЭтотОбъект.ПодробныйРасчетФОТ Тогда
		
		ФильтрСотрудников = ФильтрСотрудниковДокумента();
		ПлановыеНачисленияПоказателиСотрудников = ПлановыеНачисленияПоказателиСотрудниковСИзменениямиДокумента(ФильтрСотрудников);
		
		ФОТПлановыхНачисленийСотрудников = ФОТПлановыхНачисленийСотрудниковСИзменениямиДокумента(ПлановыеНачисленияПоказателиСотрудников).ПлановыйФОТ;
		
		ФОТПлановыхНачисленийСотрудников.Свернуть("ИдентификаторСтрокиСотрудника", "ВкладВФОТ");
		
		Для Каждого СтрокаТаблицы Из ФОТПлановыхНачисленийСотрудников Цикл
			ФОТСотрудников.Вставить(СтрокаТаблицы.ИдентификаторСтрокиСотрудника, СтрокаТаблицы.ВкладВФОТ);
		КонецЦикла;	
		
	КонецЕсли;
	
	Возврат ФОТСотрудников;	
	
КонецФункции

&НаСервере
Функция ФОТСотрудниковПоОписаниюНачислений(ФОТНачисленийСотрудников)
	
	ФОТСотрудников = Новый Соответствие;
	
	Для Каждого СтрокаНачисления Из ФОТНачисленийСотрудников Цикл
		
		ФОТПоСотруднику = ФОТСотрудников[СтрокаНачисления.ИдентификаторСтрокиСотрудника];
		
		Если ФОТПоСотруднику = Неопределено Тогда  
			ФОТПоСотруднику = 0;
		КонецЕсли;	
		
		ФОТПоСотруднику = ФОТПоСотруднику + СтрокаНачисления.ВкладВФОТ;
		
		ФОТСотрудников.Вставить(СтрокаНачисления.ИдентификаторСтрокиСотрудника, ФОТПоСотруднику);
		
	КонецЦикла;
	
	Возврат ФОТСотрудников;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьФОТСотрудниковВФорме()
	
	ФОТСотрудников = ФОТСотрудников();
	
	Для каждого СтрокаСотрудника Из Объект.Сотрудники Цикл
		СтрокаСотрудника.ФОТ = ФОТСотрудников.Получить(СтрокаСотрудника.ИдентификаторСтрокиСотрудника);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура РасчетФОТПодробноНаСервере()
	
	Пометка = ОбщегоНазначенияКлиентСервер.ЗначениеСвойстваЭлементаФормы(ЭтотОбъект.Элементы, "СотрудникиРасчетФОТПодробно", "Пометка");
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "СотрудникиРасчетФОТПодробно", "Пометка", Не Пометка);
	ЭтотОбъект.ПодробныйРасчетФОТ = Не Пометка;
	
	УстановитьВидимостьКолонокПодробногоРасчета();
	
	Если ЭтотОбъект.ПодробныйРасчетФОТ Тогда
		ЗаполнитьФОТСотрудниковВФорме();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СовокупнаяТарифнаяСтавка

&НаСервере
Процедура ЗаполнитьЗначенияСовокупныхТарифныхСтавокСотрудников(ЗначенияСовокупныхТарифныхСтавок)
	ЗначенияСовокупныхТарифныхСтавок.Индексы.Добавить("ИдентификаторСтрокиСотрудника");
		
	Для Каждого СтрокаСотрудника Из ЗначенияСовокупныхТарифныхСтавок Цикл 
		
		Отбор = Новый Структура("ИдентификаторСтрокиСотрудника", СтрокаСотрудника.ИдентификаторСтрокиСотрудника);
		
		НайденныеСтроки = Объект.Сотрудники.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() > 0 Тогда
			НайденныеСтроки[0].СовокупнаяТарифнаяСтавка = СтрокаСотрудника.СовокупнаяТарифнаяСтавка;
			НайденныеСтроки[0].ВидТарифнойСтавки = СтрокаСотрудника.ВидТарифнойСтавки;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область УдалитьНачисленияПоказателиСотрудника

&НаСервере
Функция УдалитьПоказателиПоСотруднику(ИдентификаторСтрокиСотрудника)
	
	ОтборПоСотруднику = Новый Структура("ИдентификаторСтрокиСотрудника", ИдентификаторСтрокиСотрудника);
	
	СтрокиДляУдаления = Объект.ПоказателиСотрудников.НайтиСтроки(ОтборПоСотруднику);
	Для Каждого СтрокаДляУдаления Из СтрокиДляУдаления Цикл
		Объект.ПоказателиСотрудников.Удалить(СтрокаДляУдаления);
	КонецЦикла;
	
КонецФункции

&НаСервере
Функция ЕстьУдаляемыеСотрудники()
	Возврат НЕ (УдаляемыеСотрудники = Неопределено Или УдаляемыеСотрудники.Количество() = 0);
КонецФункции

&НаСервере
Процедура ЗавершитьУдалениеСотрудников()
	
	Если НЕ ЕстьУдаляемыеСотрудники() Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого ИдентификаторСтрокиСотрудника Из УдаляемыеСотрудники Цикл
		УдалитьПоказателиПоСотруднику(ИдентификаторСтрокиСотрудника);
	КонецЦикла;
	
	ОчиститьУдаляемыхСотрудников();
	
КонецПроцедуры

&НаСервере
Процедура ЗафиксироватьУдаляемыхСотрудников(МассивУдаляемыхСотрудников)
	УдаляемыеСотрудники = Новый ФиксированныйМассив(МассивУдаляемыхСотрудников);
КонецПроцедуры

&НаСервере
Процедура ОчиститьУдаляемыхСотрудников()
	УдаляемыеСотрудники = Неопределено;
КонецПроцедуры

#КонецОбласти

#Область ПлановыеНачисленияПоказателиСотрудниковСИзменениямиДокумента

&НаСервере
Функция ПлановыеНачисленияПоказателиСотрудниковСИзменениямиДокумента(ФильтрСотрудников)
	
	НачисленияПоказателиСотрудников = ПлановыеНачисленияПоказателиСотрудников(ФильтрСотрудников);
	
	ПрименитьИзмененияВДокументеКПлановымНачислениямСотрудников(НачисленияПоказателиСотрудников, ФильтрСотрудников);
	ПрименитьИзмененияВДокументеКПлановымПоказателямСотрудников(НачисленияПоказателиСотрудников, ФильтрСотрудников);
	
	Возврат НачисленияПоказателиСотрудников;
	
КонецФункции

&НаСервере
Функция ПлановыеНачисленияПоказателиСотрудников(ФильтрСотрудников = Неопределено)
	
	ПараметрыПолучения = ПараметрыПолученияНачисленийПоказателейСотрудников();
	Если Не ФильтрСотрудников = Неопределено Тогда
		ПараметрыПолучения.Вставить("ИспользоватьФильтрСотрудников", Истина);
		ПараметрыПолучения.Вставить("ФильтрСотрудников", ФильтрСотрудников);
	КонецЕсли;
	
	НачисленияПоказателиСотрудников = Документы.ИзменениеПлановыхНачислений.НачисленияПоказателиСотрудников(ПараметрыПолучения);
	
	Возврат НачисленияПоказателиСотрудников;
	
КонецФункции

&НаСервере
Функция НачисленияПоказателиСотрудникаПриведенныеДляФормыРедактирования(ПлановыеНачисленияПоказателиСотрудника, ФОТПлановыхНачисленийСотрудника)
	ФОТПлановыхНачисленийСотрудника.Индексы.Добавить("Начисление, ДокументОснование");
	
	МассивНачислений = Новый Массив;
	МассивПоказателей = Новый Массив;
	
	ИдентификаторСтрокиВидаРасчета = 1;
	
	ПостоянныеПоказателиНачислений = Новый Соответствие;
	
	// Добавление всех начислений сотрудника.
	Для Каждого СтрокаНачислений Из ПлановыеНачисленияПоказателиСотрудника.НачисленияСотрудников Цикл
		
		СтруктураНачисления = Новый Структура("Начисление,ДокументОснование,ИдентификаторСтрокиВидаРасчета,Размер,Действие");
		ЗаполнитьЗначенияСвойств(СтруктураНачисления, СтрокаНачислений);
		СтруктураНачисления.ИдентификаторСтрокиВидаРасчета = ИдентификаторСтрокиВидаРасчета;
		
		Отбор = Новый Структура("Начисление, ДокументОснование", СтрокаНачислений.Начисление, СтрокаНачислений.ДокументОснование);
		СтрокиПлановыхНачислений = ФОТПлановыхНачисленийСотрудника.НайтиСтроки(Отбор);
		Если СтрокиПлановыхНачислений.Количество() > 0 Тогда 
			СтруктураНачисления.Размер = СтрокиПлановыхНачислений[0].ВкладВФОТ;
		КонецЕсли;
		МассивНачислений.Добавить(СтруктураНачисления);
		
		// Добавление показателей начислений.
		
		ПостоянныеПоказателиНачисления = ПостоянныеПоказателиНачислений.Получить(СтрокаНачислений.Начисление); 
		Если ПостоянныеПоказателиНачисления = Неопределено Тогда
			ПостоянныеПоказателиНачисления = ПостоянныеПоказателиНачисления(СтрокаНачислений.Начисление);
			ПостоянныеПоказателиНачислений.Вставить(СтрокаНачислений.Начисление, ПостоянныеПоказателиНачисления);	
		КонецЕсли;
		
		Для Каждого ПостоянныйПоказатель Из ПостоянныеПоказателиНачисления Цикл
			
			СтрокиПоказателей = ПлановыеНачисленияПоказателиСотрудника.ПоказателиСотрудников.НайтиСтроки(Новый Структура("Показатель,ДокументОснование", ПостоянныйПоказатель, СтрокаНачислений.ДокументОснование));
			Если СтрокиПоказателей.Количество() = 0 Тогда
				ИдентификаторСтрокиВидаРасчета = ИдентификаторСтрокиВидаРасчета + 1;
				Продолжить;
			КонецЕсли;
			
			СтруктураПоказателя = Новый Структура("Показатель,ДокументОснование,ИдентификаторСтрокиВидаРасчета,Значение");
			СтруктураПоказателя.Показатель = СтрокиПоказателей[0].Показатель;
			СтруктураПоказателя.ДокументОснование = СтрокиПоказателей[0].ДокументОснование;
			СтруктураПоказателя.Значение = СтрокиПоказателей[0].Значение;
			СтруктураПоказателя.ИдентификаторСтрокиВидаРасчета = ИдентификаторСтрокиВидаРасчета;
			
			МассивПоказателей.Добавить(СтруктураПоказателя);
			
		КонецЦикла;	  
		
		ИдентификаторСтрокиВидаРасчета = ИдентификаторСтрокиВидаРасчета + 1;
		
	КонецЦикла;
	
	Возврат Новый Структура("Начисления,Показатели", МассивНачислений, МассивПоказателей);
	
КонецФункции

&НаСервере
Функция ПараметрыПолученияНачисленийПоказателейСотрудников()
	
	ПараметрыПолучения = Документы.ИзменениеПлановыхНачислений.ПараметрыПолученияНачисленийПоказателейСотрудников();
	ПараметрыПолучения.Вставить("Ссылка", Объект.Ссылка);
	ПараметрыПолучения.Вставить("Организация", Объект.Организация);
	ПараметрыПолучения.Вставить("Подразделение", Объект.Подразделение);
	ПараметрыПолучения.Вставить("ДатаИзменения", Объект.ДатаНазначения);
	ПараметрыПолучения.Вставить("ДатаОкончания", Объект.ДатаОкончания);
	
	Возврат ПараметрыПолучения;
	
КонецФункции

&НаСервере
Процедура ПрименитьИзмененияВДокументеКПлановымНачислениямСотрудников(НачисленияПоказателиСотрудников, ФильтрСотрудников = Неопределено)
	
	Если Не ЗначениеЗаполнено(Объект.Начисление) Тогда
		Возврат;
	КонецЕсли;
	
	НачисленияПоказателиСотрудников.НачисленияСотрудников.Индексы.Добавить("ИдентификаторСтрокиСотрудника, Начисление, ДокументОснование");
	
	СвойстваНачисления = ЗарплатаКадрыРасширенныйПовтИсп.ПолучитьИнформациюОВидеРасчета(Объект.Начисление);
	
	Отбор = Новый Структура("ИдентификаторСтрокиСотрудника, Начисление, ДокументОснование");
	Для каждого Строка Из Объект.Сотрудники Цикл
		
		Если ФильтрСотрудников <> Неопределено Тогда
			
			СтрокиФильтра = ФильтрСотрудников.НайтиСтроки(Новый Структура("ИдентификаторСтрокиСотрудника", Строка.ИдентификаторСтрокиСотрудника));
			Если СтрокиФильтра.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		Отбор.Начисление = Объект.Начисление;
		Отбор.ИдентификаторСтрокиСотрудника = Строка.ИдентификаторСтрокиСотрудника;
		
		Если СвойстваНачисления.ПоддерживаетНесколькоПлановыхНачислений Тогда
			Отбор.ДокументОснование = Объект.Ссылка;
		Иначе
			Отбор.ДокументОснование = Неопределено;
		КонецЕсли;
		
		СтрокиНачисления = НачисленияПоказателиСотрудников.НачисленияСотрудников.НайтиСтроки(Отбор);
		Если СтрокиНачисления.Количество() > 0 Тогда
			СтрокаНачисления = СтрокиНачисления[0];
		Иначе 
			СтрокаНачисления = НачисленияПоказателиСотрудников.НачисленияСотрудников.Добавить();
			СтрокаНачисления.Сотрудник = Строка.Сотрудник;
			СтрокаНачисления.ИдентификаторСтрокиСотрудника = Строка.ИдентификаторСтрокиСотрудника;
			СтрокаНачисления.Начисление = Объект.Начисление;
			СтрокаНачисления.ДокументОснование = Объект.Ссылка;
		КонецЕсли;
		
		Если Строка.Размер <> 0 Тогда
			СтрокаНачисления.Размер = Строка.Размер;
		Иначе
			
			Строка.Размер = СтрокаНачисления.Размер;
			
			Для каждого ОписаниеКолонкиПоказателя Из КолонкиПоказателей Цикл
				
				Если Объект.Начисление = ОписаниеКолонкиПоказателя.Значение Тогда
					Строка[ОписаниеКолонкиПоказателя.Ключ] = Строка.Размер;
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		СтрокаНачисления.Действие = Перечисления.ДействияСНачислениямиИУдержаниями.Утвердить;
		
	КонецЦикла;
	
	Для каждого СтрокаСотрудника Из Объект.Сотрудники Цикл
		
		Если ФильтрСотрудников <> Неопределено Тогда
			
			СтрокиФильтра = ФильтрСотрудников.НайтиСтроки(Новый Структура("ИдентификаторСтрокиСотрудника", СтрокаСотрудника.ИдентификаторСтрокиСотрудника));
			Если СтрокиФильтра.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		РеквизитФормыВНачисленияСотрудника(СтрокаСотрудника);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПрименитьИзмененияВДокументеКПлановымПоказателямСотрудников(НачисленияПоказателиСотрудников, ФильтрСотрудников = Неопределено)
	
	ПоказателиСотрудниковВДокументе = Объект.ПоказателиСотрудников.Выгрузить(Новый Массив);
	Для каждого СтрокаСотрудника Из Объект.Сотрудники Цикл
		
		Если ФильтрСотрудников <> Неопределено Тогда
			
			СтрокиФильтра = ФильтрСотрудников.НайтиСтроки(Новый Структура("ИдентификаторСтрокиСотрудника", СтрокаСотрудника.ИдентификаторСтрокиСотрудника));
			Если СтрокиФильтра.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		РеквизитФормыВПоказателиСотрудника(ПоказателиСотрудниковВДокументе, СтрокаСотрудника);
		
	КонецЦикла;
	
	НачисленияПоказателиСотрудников.ПоказателиСотрудников.Индексы.Добавить("ИдентификаторСтрокиСотрудника, Показатель, ДокументОснование");
	
	СвойстваНачисления = ЗарплатаКадрыРасширенныйПовтИсп.ПолучитьИнформациюОВидеРасчета(Объект.Начисление);
	
	Отбор = Новый Структура("ИдентификаторСтрокиСотрудника, Показатель, ДокументОснование");
	
	Для каждого Строка Из ПоказателиСотрудниковВДокументе Цикл
		
		Отбор.Показатель = Строка.Показатель;
		Отбор.ИдентификаторСтрокиСотрудника = Строка.ИдентификаторСтрокиСотрудника;
		
		Если СвойстваНачисления.ПоддерживаетНесколькоПлановыхНачислений Тогда
			Отбор.ДокументОснование = Объект.Ссылка;
		Иначе
			Отбор.ДокументОснование = Неопределено;
		КонецЕсли;
		
		СтрокиПоказателей = НачисленияПоказателиСотрудников.ПоказателиСотрудников.НайтиСтроки(Отбор);
		Если СтрокиПоказателей.Количество() > 0 Тогда
			СтрокаПоказателей = СтрокиПоказателей[0];
		Иначе 
			СтрокаПоказателей = НачисленияПоказателиСотрудников.ПоказателиСотрудников.Добавить();
			СтрокаПоказателей.ИдентификаторСтрокиСотрудника = Строка.ИдентификаторСтрокиСотрудника;
			СтрокаПоказателей.Показатель = Строка.Показатель;
			СтрокаПоказателей.ДокументОснование = Объект.Ссылка;
		КонецЕсли;
		
		Если Строка.Значение <> 0 Тогда
			СтрокаПоказателей.Значение = Строка.Значение;
		Иначе
			
			Строка.Значение = СтрокаПоказателей.Значение;
			
			СтрокиСотрудников = Объект.Сотрудники.НайтиСтроки(Новый Структура("ИдентификаторСтрокиСотрудника", Строка.ИдентификаторСтрокиСотрудника));
			Если СтрокиСотрудников.Количество() > 0 Тогда
				
				СтрокаСотрудника = СтрокиСотрудников[0];
				Для каждого ОписаниеКолонкиПоказателя Из КолонкиПоказателей Цикл
					
					Если Строка.Показатель = ОписаниеКолонкиПоказателя.Значение Тогда
						СтрокаСотрудника[ОписаниеКолонкиПоказателя.Ключ] = Строка.Значение;
						Прервать;
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область КлючевыеРеквизитыЗаполненияФормы

// Функция возвращает описание таблиц формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыТаблицыОчищаемыеПриИзменении() Экспорт
	Массив = Новый Массив;
	Массив.Добавить("Объект.Сотрудники");
	Массив.Добавить("Объект.ПоказателиСотрудников");
	Возврат Массив
КонецФункции 

// Функция возвращает массив реквизитов формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыОписаниеКлючевыхРеквизитов() Экспорт
	Массив = Новый Массив;
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "Организация",	Нстр("ru = 'организации'")));
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "Подразделение", Нстр("ru = 'подразделения'")));
	Возврат Массив
КонецФункции

#КонецОбласти

#Область ФормаРедактированияСоставаНачисленийИУдержаний

&НаКлиенте
Процедура ОткрытьФормуРедактированияСоставаНачисленийИУдержаний(ТекущиеДанные, ТекущаяСтрока, РедактироватьСоставНачислений)
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Сотрудник) Тогда
		
		ПараметрыРедактирования = ПараметрыРедактированияСоставаНачисленийИУдержаний(ТекущаяСтрока, РедактироватьСоставНачислений);
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("АдресВХранилище", ПоместитьВоВременноеХранилище(ПараметрыРедактирования, УникальныйИдентификатор));
		ПараметрыОткрытия.Вставить("ТолькоПросмотр", ТолькоПросмотр);
		
		ЗарплатаКадрыРасширенныйКлиент.ОткрытьФормуРедактированияСоставаНачисленийИУдержаний(ПараметрыОткрытия, ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПараметрыРедактированияСоставаНачисленийИУдержаний(ТекущаяСтрока, РедактироватьСоставНачислений)
	
	СтрокаСотрудника = Объект.Сотрудники.НайтиПоИдентификатору(ТекущаяСтрока);
	Сотрудник = СтрокаСотрудника.Сотрудник;
	
	ПараметрыРедактирования = ЗарплатаКадрыРасширенныйКлиентСервер.ПараметрыРедактированияСоставаНачисленийИУдержаний();
	
	ПараметрыРедактирования.ВладелецНачисленийИУдержаний = Сотрудник;
	ПараметрыРедактирования.ДатаРедактирования = СтрокаСотрудника.ВремяРегистрации;
	ПараметрыРедактирования.Организация = Объект.Организация;
	ПараметрыРедактирования.РежимРаботы = 3;
	
	ФильтрСотрудников = Документы.ИзменениеПлановыхНачислений.ПустойФильтрСотрудников();
	ЭлементФильтра = ФильтрСотрудников.Добавить();
	ЭлементФильтра.Сотрудник = СтрокаСотрудника.Сотрудник;
	ЭлементФильтра.ДатаИзменения = СтрокаСотрудника.ВремяРегистрации;
	ЭлементФильтра.ИдентификаторСтрокиСотрудника = СтрокаСотрудника.ИдентификаторСтрокиСотрудника;
	
	ПлановыеНачисленияПоказателиСотрудников = ПлановыеНачисленияПоказателиСотрудниковСИзменениямиДокумента(ФильтрСотрудников);
	ФОТПлановыхНачисленийСотрудникаСИзменениямиДокумента = ФОТПлановыхНачисленийСотрудниковСИзменениямиДокумента(ПлановыеНачисленияПоказателиСотрудников).ПлановыйФОТ;
	ПриведенныеНачисленияПоказатели = НачисленияПоказателиСотрудникаПриведенныеДляФормыРедактирования(ПлановыеНачисленияПоказателиСотрудников, ФОТПлановыхНачисленийСотрудникаСИзменениямиДокумента);
	
	ПараметрыРедактирования.ОписаниеТаблицыНачислений.Используется = Истина;
	ПараметрыРедактирования.ОписаниеТаблицыНачислений.ИзменятьСоставВидовРасчета = РедактироватьСоставНачислений;
	ПараметрыРедактирования.ОписаниеТаблицыНачислений.ИзменятьЗначенияПоказателей = РедактироватьСоставНачислений;
	ПараметрыРедактирования.ОписаниеТаблицыНачислений.НомерТаблицы = 1;
	ПараметрыРедактирования.ОписаниеТаблицыНачислений.ПоказатьФОТ = НЕ РедактироватьСоставНачислений;
	
	РедактируемыеНачисления = Новый Массив;
	РедактируемыеНачисления.Добавить(Объект.Начисление);
	
	ПараметрыРедактирования.ОписаниеТаблицыНачислений.РедактируемыеНачисления = РедактируемыеНачисления;
	ПараметрыРедактирования.ОписаниеТаблицыНачислений.Таблица = ПриведенныеНачисленияПоказатели.Начисления;
	ПараметрыРедактирования.Показатели = ПриведенныеНачисленияПоказатели.Показатели;
	
	Возврат ПараметрыРедактирования;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДанныеСотрудникаИзВременногоХранилища(АдресВХранилище)
	
	ДанныеИзХранилища = ПолучитьИзВременногоХранилища(АдресВХранилище);
	
	Если ДанныеИзХранилища <> Неопределено Тогда
		
		РеквизитФормыВДанныеСотрудников(Объект);
		
		ЗаполнитьНачисленияПоСотрудникуИзХранилища(ДанныеИзХранилища);
		
		ЗаполнитьПоказателиПоСотрудникуИзХранилища(ДанныеИзХранилища);
		
		ИзменяемыеПоказатели = ИзменяемыеПоказатели(Объект.Начисление);
		ОписаниеПоказателей  = ОписаниеПоказателей(ИзменяемыеПоказатели);
		
		ДанныеСотрудниковВРеквизитФормы();
		ФОТСотрудниковВРеквизитФормы();
		
		ЗаполнитьЗначенияСовокупныхСтавокПоСотрудникуИзХранилища(ДанныеИзХранилища);
		
		УстановитьДоступностьТаблицыСотрудники();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНачисленияПоСотрудникуИзХранилища(ДанныеИзХранилища)
	
	Для каждого НачислениеСотрудника Из ДанныеИзХранилища.Начисления Цикл
		
		Если НачислениеСотрудника.Начисление <> Объект.Начисление Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаСотрудника = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.Сотрудники.ТекущаяСтрока);
		СтрокаСотрудника.Размер = НачислениеСотрудника.Размер;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоказателиПоСотрудникуИзХранилища(ДанныеИзХранилища)
	
	СтрокаСотрудника = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.Сотрудники.ТекущаяСтрока);
	УдалитьПоказателиПоСотруднику(СтрокаСотрудника.ИдентификаторСтрокиСотрудника);
	
	Для каждого ПоказательСотрудника Из ДанныеИзХранилища.Показатели Цикл
		
		Для каждого КолонкаПоказателя Из ЭтотОбъект.КолонкиПоказателей Цикл
			
			Если КолонкаПоказателя.Значение = ПоказательСотрудника.Показатель Тогда
				
				НоваяСтрока = Объект.ПоказателиСотрудников.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ПоказательСотрудника);
				НоваяСтрока.ИдентификаторСтрокиСотрудника = СтрокаСотрудника.ИдентификаторСтрокиСотрудника;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗначенияСовокупныхСтавокПоСотрудникуИзХранилища(ДанныеИзХранилища)
	
	ДанныеСотрудника = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.Сотрудники.ТекущаяСтрока);
	
	ДанныеСотрудника.ВидТарифнойСтавки = ДанныеИзХранилища.ВидТарифнойСтавки;
	ДанныеСотрудника.СовокупнаяТарифнаяСтавка = ДанныеИзХранилища.СовокупнаяТарифнаяСтавка;		
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ЗаполнитьПервоначальныеЗначения()
	ЗначенияДляЗаполнения = Новый Структура;
	ЗначенияДляЗаполнения.Вставить("Ответственный", "Объект.Ответственный");
	
	Если Параметры.ЗначенияЗаполнения.Свойство("Организация") 
		И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.Организация) Тогда
		Объект.Организация = Параметры.ЗначенияЗаполнения.Организация;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		ЗначенияДляЗаполнения.Вставить("Организация", "Объект.Организация");
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ВыполнятьРасчетЗарплатыПоПодразделениям") Тогда
		ЗначенияДляЗаполнения.Вставить("Подразделение", "Объект.Подразделение");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ДатаНазначения) Тогда
		ЗначенияДляЗаполнения.Вставить("ДатаСобытия", "Объект.ДатаНазначения");
	КонецЕсли;
	
	ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
КонецПроцедуры

&НаСервере
Процедура ПрочитатьВремяРегистрации()
	
	СотрудникиДаты = Объект.Сотрудники.Выгрузить(, "Сотрудник, ДатаНазначения");
	СотрудникиДаты.Колонки.ДатаНазначения.Имя = "ДатаСобытия";
	
	ВремяРегистрацииДокумента = ЗарплатаКадрыРасширенный.ЗначенияВремениРегистрацииДокумента(Объект.Ссылка, СотрудникиДаты);
	
	Для Каждого Строка Из Объект.Сотрудники Цикл
		ВремяРегистрацииСотрудников = ВремяРегистрацииДокумента.Получить(Строка.ДатаНазначения);
		Если ВремяРегистрацииСотрудников <> Неопределено Тогда 
			Строка.ВремяРегистрации = ВремяРегистрацииСотрудников.Получить(Строка.Сотрудник);
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьВремяРегистрацииСтроки(Ссылка, Строка)
	
	Строка.ВремяРегистрации = ЗарплатаКадрыРасширенный.ВремяРегистрацииСотрудникаДокумента(Ссылка, Строка.Сотрудник, Строка.ДатаНазначения);
	
КонецПроцедуры

&НаСервере
Функция ВремяРегистрацииСотрудников()
	
	ВремяРегистрацииСотрудников = Новый Соответствие;
	
	Для каждого Строка Из Объект.Сотрудники Цикл
		ВремяРегистрацииСотрудников.Вставить(Строка.ИдентификаторСтрокиСотрудника, Строка.ВремяРегистрации);
	КонецЦикла;	
	
	Возврат ВремяРегистрацииСотрудников;
	
КонецФункции

&НаСервере
Функция ПостоянныеПоказателиНачисления(Начисление) 
	
	ПостоянныеПоказатели = Новый Массив;
	
	ИнфоОВидеРасчета = ЗарплатаКадрыРасширенныйПовтИсп.ПолучитьИнформациюОВидеРасчета(Начисление);
	Для Каждого СтрокаПоказателя Из ИнфоОВидеРасчета.Показатели Цикл
		Если ЭтоПостоянныйПоказатель(СтрокаПоказателя) Тогда
			ПостоянныеПоказатели.Добавить(СтрокаПоказателя.Показатель);
		КонецЕсли;
	КонецЦикла;	
	
	Возврат ПостоянныеПоказатели;
	
КонецФункции

&НаСервере
Функция ЭтоПостоянныйПоказатель(СтрокаПоказателя) 
	
	ЭтоПостоянныйПоказатель = Истина;
	
	Если СтрокаПоказателя.ЗапрашиватьПриВводе Тогда
		ПоказательИнфо = ЗарплатаКадрыРасширенныйПовтИсп.СведенияОПоказателеРасчетаЗарплаты(СтрокаПоказателя.Показатель);
		Если ПоказательИнфо.СпособПримененияЗначений <> Перечисления.СпособыПримененияЗначенийПоказателейРасчетаЗарплаты.Постоянное
			Или ПоказательИнфо.ЗначениеРассчитываетсяАвтоматически Тогда
			ЭтоПостоянныйПоказатель = Ложь;	
		КонецЕсли;
	Иначе
		ЭтоПостоянныйПоказатель = Ложь;	
	КонецЕсли;

	Возврат ЭтоПостоянныйПоказатель;
	
КонецФункции

&НаСервере
Функция АдресСпискаПодобранныхСотрудников()
	
	Возврат ПоместитьВоВременноеХранилище(Объект.Сотрудники.Выгрузить(,"Сотрудник").ВыгрузитьКолонку("Сотрудник"), УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Процедура ОбработатьИзменениеПоказателейНаСервере(ЗначенияПоказателей)
	
	ФильтрСотрудников = Документы.ИзменениеПлановыхНачислений.ПустойФильтрСотрудников();
	Для Каждого СтрокаСотрудника Из Объект.Сотрудники Цикл
		
		СтрокаФильтраСотрудников = Неопределено;
		Для каждого КолонкаПоказатель Из КолонкиПоказателей Цикл
			
			ЗначениеПоказателя = ЗначенияПоказателей[КолонкаПоказатель.Значение];
			Если ЗначениеПоказателя = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрокаФильтраСотрудников = Неопределено Тогда
				СтрокаФильтраСотрудников = ФильтрСотрудников.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаФильтраСотрудников, СтрокаСотрудника);
				СтрокаФильтраСотрудников.ДатаИзменения = СтрокаСотрудника.ВремяРегистрации;
			КонецЕсли;
			
			СтрокаСотрудника[КолонкаПоказатель.Ключ] = ЗначениеПоказателя;
			
			Если ТипЗнч(КолонкаПоказатель.Значение) = Тип("ПланВидовРасчетаСсылка.Начисления") Тогда
				
				Если СтрокаФильтраСотрудников = Неопределено Тогда
					СтрокаФильтраСотрудников = ФильтрСотрудников.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаФильтраСотрудников, СтрокаСотрудника);
					СтрокаФильтраСотрудников.ДатаИзменения = СтрокаСотрудника.ВремяРегистрации;
				КонецЕсли;
				
				СтрокаСотрудника.Размер = ЗначениеПоказателя;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ПлановыеНачисленияПоказателиСотрудников = ПлановыеНачисленияПоказателиСотрудниковСИзменениямиДокумента(ФильтрСотрудников);
	ФОТПлановыхНачисленийСотрудников = ФОТПлановыхНачисленийСотрудниковСИзменениямиДокумента(ПлановыеНачисленияПоказателиСотрудников);
	ЗаполнитьФОТНачисленийСотрудников(ФОТПлановыхНачисленийСотрудников.ПлановыйФОТ);
	ФОТСотрудников = ФОТСотрудниковПоОписаниюНачислений(ФОТПлановыхНачисленийСотрудников.ПлановыйФОТ);
	ЗаполнитьЗначенияСовокупныхТарифныхСтавокСотрудников(ФОТПлановыхНачисленийСотрудников.ТарифныеСтавки);	
	
	ИзменяемыеПоказатели = ИзменяемыеПоказатели(Объект.Начисление);
	ОписаниеПоказателей  = ОписаниеПоказателей(ИзменяемыеПоказатели);

	ДанныеСотрудниковВРеквизитФормы();
	ФОТСотрудниковВРеквизитФормы(ФОТСотрудников);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	ПараметрыФО = Новый Структура("Организация, Период", Объект.Организация, НачалоДня(Объект.ДатаНазначения));
	УстановитьПараметрыФункциональныхОпцийФормы(ПараметрыФО);
	
КонецПроцедуры


#КонецОбласти

