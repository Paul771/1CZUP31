#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ДанныеЭЛН);
	
	АвтоЗаголовок = Ложь;
	УстановитьЗаголовок();
	ОбновитьДоступностьЭлементов();
	
	ПрямыеВыплатыПособийСоциальногоСтрахованияФормы.ЗаполнитьСписокВыбораКодУсловийИсчисления(Элементы.УсловияИсчисленияКод1);
	ПрямыеВыплатыПособийСоциальногоСтрахованияФормы.ЗаполнитьСписокВыбораКодУсловийИсчисления(Элементы.УсловияИсчисленияКод2);
	ПрямыеВыплатыПособийСоциальногоСтрахованияФормы.ЗаполнитьСписокВыбораКодУсловийИсчисления(Элементы.УсловияИсчисленияКод3);
	
	ОбменЛисткамиНетрудоспособностиФСС.КодыПричинИсправления(Элементы.КодПричиныИсправления.СписокВыбора);
	
	Элементы.КодПричиныИсправления.Подсказка = НаименованиеПричиныИсправления(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЛистокНетрудоспособностиПриИзменении(Элемент)
	ЗаполнитьДанныеЛисткаНетрудоспособности(ЛистокНетрудоспособности);
КонецПроцедуры

&НаКлиенте
Процедура НомерЛисткаНетрудоспособностиПриИзменении(Элемент)
	УстановитьЗаголовок();
КонецПроцедуры

&НаКлиенте
Процедура ИсправлениеПриИзменении(Элемент)
	ОбновитьДоступностьЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура КодПричиныИсправленияПриИзменении(Элемент)
	НаименованиеПричиныИсправления = НаименованиеПричиныИсправления(ЭтотОбъект);
	Если Не ЗначениеЗаполнено(ОписаниеПричиныИсправления)
		Или ОписаниеПричиныИсправления = Элементы.КодПричиныИсправления.Подсказка Тогда
		ОписаниеПричиныИсправления = НаименованиеПричиныИсправления;
	КонецЕсли;
	Элементы.КодПричиныИсправления.Подсказка = НаименованиеПричиныИсправления;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НаименованиеПричиныИсправления(Форма)
	ЭлементСписка = Форма.Элементы.КодПричиныИсправления.СписокВыбора.НайтиПоЗначению(Форма.КодПричиныИсправления);
	Если ЭлементСписка <> Неопределено Тогда
		Если КодСимвола(ЭлементСписка.Представление, 3) = КодСимвола(".") Тогда
			Возврат СокрЛП(Сред(ЭлементСписка.Представление, 4));
		Иначе
			Возврат СокрЛП(ЭлементСписка.Представление)
		КонецЕсли;
	КонецЕсли;
	Возврат "";
КонецФункции

&НаКлиенте
Процедура ОписаниеПричиныИсправленияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ВариантыОтчетовКлиент.РедактироватьМногострочныйТекст(
		ЭтотОбъект,
		Элемент.ТекстРедактирования,
		ЭтотОбъект,
		"ОписаниеПричиныИсправления",
		Элемент.ПодсказкаВвода);
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	Закрыть(РезультатВыбора());
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	Закрыть(Неопределено);
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьЗаголовок()
	Если Не ЗначениеЗаполнено(ЛистокНетрудоспособности) Тогда
		Заголовок = НСтр("ru = 'Выберите листок нетрудоспособности'");
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ИнформацияОДокументе.Номер) Тогда
		ШаблонЗаголовка = НСтр("ru = 'Сведения о листке нетрудоспособности %1 из реестра %2 от %3'");
	Иначе
		ШаблонЗаголовка = НСтр("ru = 'Сведения о листке нетрудоспособности %1 из реестра от %3'");
	КонецЕсли;
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонЗаголовка,
		НомерЛисткаНетрудоспособности,
		Параметры.ИнформацияОДокументе.Номер,
		Формат(Параметры.ИнформацияОДокументе.Дата, "ДЛФ=D"));
	
КонецПроцедуры

&НаСервере
Функция РезультатВыбора()
	ЗначенияРеквизитов = Новый Структура;
	
	РеквизитыФормы = ПолучитьРеквизиты();
	Для Каждого РеквизитФормы Из РеквизитыФормы Цикл
		Если РеквизитФормы.Имя = "Объект" Тогда
			Продолжить;
		КонецЕсли;
		ЗначенияРеквизитов.Вставить(РеквизитФормы.Имя, ЭтотОбъект[РеквизитФормы.Имя]);
	КонецЦикла;
	
	Возврат ЗначенияРеквизитов;
КонецФункции

&НаСервере
Процедура ЗаполнитьДанныеЛисткаНетрудоспособности(ЛистокНетрудоспособности)
	
	РеквизитыФормы = ПолучитьРеквизиты();
	Для каждого РеквизитФормы Из РеквизитыФормы Цикл
		Если РеквизитФормы.Имя = "Объект"
			Или РеквизитФормы.Имя = "НомерСтроки" Тогда
			Продолжить;
		КонецЕсли;
		ЭтотОбъект[РеквизитФормы.Имя] = Неопределено;
	КонецЦикла;
	
	ТаблицаЗначений = Документы.БольничныйЛист.ДанныеДляРеестраЭЛН(ЛистокНетрудоспособности);
	Если ТаблицаЗначений.Количество() > 0 Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ТаблицаЗначений[0]);
	КонецЕсли;
	
	УстановитьЗаголовок();
КонецПроцедуры

&НаСервере
Процедура ОбновитьДоступностьЭлементов()
	Элементы.КодПричиныИсправления.Доступность      = Исправление;
	Элементы.ОписаниеПричиныИсправления.Доступность = Исправление;
КонецПроцедуры

#КонецОбласти
