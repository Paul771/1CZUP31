#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс		

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

Функция ДобавитьКомандыСозданияДокументов(КомандыСозданияДокументов, ДополнительныеПараметры) Экспорт
	
	ОписаниеКоманды = ЗарплатаКадрыРасширенный.ОписаниеКомандыСозданияДокумента(
		"Документ.ФактПоказателяЭффективностиСотрудников",
		НСтр("ru = 'Факт показателя сотрудников'"));
	
	ЗарплатаКадрыРасширенный.ДобавитьВКоллекциюКомандуСозданияДокумента(
		КомандыСозданияДокументов, ОписаниеКоманды);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДанныеДляПроведения(Ссылка)Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФактПоказателяЭффективностиСотрудниковСотрудники.Ссылка.Период КАК Период,
		|	ФактПоказателяЭффективностиСотрудниковСотрудники.Сотрудник КАК Сотрудник,
		|	ФактПоказателяЭффективностиСотрудниковСотрудники.Ссылка.Показатель КАК Показатель,
		|	ФактПоказателяЭффективностиСотрудниковСотрудники.Значение КАК Значение
		|ИЗ
		|	Документ.ФактПоказателяЭффективностиСотрудников.Сотрудники КАК ФактПоказателяЭффективностиСотрудниковСотрудники
		|ГДЕ
		|	ФактПоказателяЭффективностиСотрудниковСотрудники.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ДанныеДляПроведения = Новый Структура;
	ДанныеДляПроведения.Вставить("ФактСотрудников", РезультатЗапроса.Выгрузить());
	
	Возврат ДанныеДляПроведения;

КонецФункции	

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаДанныхИзФайла

// Устанавливает параметры загрузки.
//
Процедура УстановитьПараметрыЗагрузкиИзФайлаВТЧ(Параметры) Экспорт
	
КонецПроцедуры

// Производит сопоставление данных, загружаемых в табличную часть ПолноеИмяТабличнойЧасти,
// с данными в ИБ, и заполняет параметры АдресТаблицыСопоставления и СписокНеоднозначностей.
//
// Параметры:
//   АдресЗагружаемыхДанных    - Строка - Адрес временного хранилища с таблицей значений, в которой
//                                        находятся загруженные данные из файла. Состав колонок:
//     * Идентификатор - Число - Порядковый номер строки;
//     * остальные колонки соответствуют колонкам макета ЗагрузкаИзФайла.
//   АдресТаблицыСопоставления - Строка - Адрес временного хранилища с пустой таблицей значений,
//                                        являющейся копией табличной части документа, 
//                                        которую необходимо заполнить из таблицы АдресЗагружаемыхДанных.
//   СписокНеоднозначностей - ТаблицаЗначений - Список неоднозначных значений, для которых в ИБ имеется несколько
//                                              подходящих вариантов.
//     * Колонка       - Строка - Имя колонки, в которой была обнаружена неоднозначность;
//     * Идентификатор - Число  - Идентификатор строки, в которой была обнаружена неоднозначность.
//   ПолноеИмяТабличнойЧасти   - Строка - Полное имя табличной части, в которую загружаются данные.
//   ДополнительныеПараметры   - ЛюбойТип - Любые дополнительные сведения.
//
Процедура СопоставитьЗагружаемыеДанные(АдресЗагружаемыхДанных, АдресТаблицыСопоставления, СписокНеоднозначностей, ПолноеИмяТабличнойЧасти, ДополнительныеПараметры) Экспорт
	
	Сотрудники =  ПолучитьИзВременногоХранилища(АдресТаблицыСопоставления);
	ЗагружаемыеДанные = ПолучитьИзВременногоХранилища(АдресЗагружаемыхДанных);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	УдалитьВТ = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДанныеДляСопоставления.Сотрудник КАК СТРОКА(200)) КАК Сотрудник,
		|	ДанныеДляСопоставления.Значение КАК Значение,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ ДанныеДляСопоставления
		|ИЗ
		|	&ДанныеДляСопоставления КАК ДанныеДляСопоставления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ ВТНайденныеСотрудники
		|ИЗ
		|	ДанныеДляСопоставления КАК ДанныеДляСопоставления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО ДанныеДляСопоставления.Сотрудник = Сотрудники.Наименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТНайденныеСотрудники.Идентификатор КАК Идентификатор,
		|	КОЛИЧЕСТВО(ВТНайденныеСотрудники.Идентификатор) КАК КоличествоСовпадений
		|ПОМЕСТИТЬ ВТКоличествоСовпадений
		|ИЗ
		|	ВТНайденныеСотрудники КАК ВТНайденныеСотрудники
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТНайденныеСотрудники.Идентификатор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТКоличествоСовпадений.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ ВТНеоднозначности
		|ИЗ
		|	ВТКоличествоСовпадений КАК ВТКоличествоСовпадений
		|ГДЕ
		|	ВТКоличествоСовпадений.КоличествоСовпадений > 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор,
		|	ВТНайденныеСотрудники.Сотрудник КАК Сотрудник,
		|	ДанныеДляСопоставления.Значение КАК Значение
		|ИЗ
		|	ДанныеДляСопоставления КАК ДанныеДляСопоставления
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНайденныеСотрудники КАК ВТНайденныеСотрудники
		|		ПО ДанныеДляСопоставления.Идентификатор = ВТНайденныеСотрудники.Идентификатор
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНеоднозначности КАК ВТНеоднозначности
		|		ПО ДанныеДляСопоставления.Идентификатор = ВТНеоднозначности.Идентификатор
		|ГДЕ
		|	ВТНеоднозначности.Идентификатор ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДляСопоставления.Идентификатор,
		|	NULL,
		|	ДанныеДляСопоставления.Значение
		|ИЗ
		|	ДанныеДляСопоставления КАК ДанныеДляСопоставления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНеоднозначности КАК ВТНеоднозначности
		|		ПО ДанныеДляСопоставления.Идентификатор = ВТНеоднозначности.Идентификатор
		|
		|УПОРЯДОЧИТЬ ПО
		|	Идентификатор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТНеоднозначности.Идентификатор КАК Идентификатор
		|ИЗ
		|	ВТНеоднозначности КАК ВТНеоднозначности";

	Запрос.УстановитьПараметр("ДанныеДляСопоставления", ЗагружаемыеДанные);
	
	УдалитьВТ.Добавить("ДанныеДляСопоставления");
	УдалитьВТ.Добавить("ВТНайденныеСотрудники");
	УдалитьВТ.Добавить("ВТКоличествоСовпадений");
	УдалитьВТ.Добавить("ВТНеоднозначности");
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	РезультатНайденных = РезультатыЗапроса[РезультатыЗапроса.Количество() - 2];
	РезультатНеоднозначностей = РезультатыЗапроса[РезультатыЗапроса.Количество() - 1];
	
	Выборка = РезультатНайденных.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Сотрудники.Добавить(), Выборка);
	КонецЦикла;
	
	Выборка = РезультатНеоднозначностей.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Идентификатор") Цикл
		ЗаписьОНеоднозначности = СписокНеоднозначностей.Добавить();
		ЗаписьОНеоднозначности.Идентификатор = Выборка.Идентификатор; 
		ЗаписьОНеоднозначности.Колонка = "Сотрудник";
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(Сотрудники, АдресТаблицыСопоставления);
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
КонецПроцедуры

// Возвращает список подходящих объектов ИБ для неоднозначного значения ячейки.
// 
// Параметры:
//   ПолноеИмяТабличнойЧасти  - Строка - Полное имя табличной части, в которую загружаются данные.
//  ИмяКолонки                - Строка - Имя колонки, в который возникла неоднозначность.
//  СписокНеоднозначностей    - Массив - Массив для заполнения с неоднозначными данными.
//  ЗагружаемыеЗначенияСтрока - Строка - Загружаемые данные на основании которых возникла неоднозначность.
//  ДополнительныеПараметры   - ЛюбойТип - Любые дополнительные сведения.
//
Процедура ЗаполнитьСписокНеоднозначностей(ПолноеИмяТабличнойЧасти, СписокНеоднозначностей, ИмяКолонки, ЗагружаемыеЗначенияСтрока, ДополнительныеПараметры) Экспорт
	
	Если ИмяКолонки = "Сотрудник" Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Сотрудники.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Сотрудники КАК Сотрудники
			|ГДЕ
			|	Сотрудники.Наименование = &Наименование";
		Запрос.УстановитьПараметр("Наименование", ЗагружаемыеЗначенияСтрока.Сотрудник);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			СписокНеоднозначностей.Добавить(Выборка.Ссылка);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
