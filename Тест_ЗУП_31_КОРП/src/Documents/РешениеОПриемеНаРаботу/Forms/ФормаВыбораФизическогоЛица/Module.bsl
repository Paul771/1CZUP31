
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("Организация", Организация);
	Параметры.Свойство("ТекущийДокумент", ТекущийДокумент);
	
	ПоказыватьНеПринятыхКандидатов = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("Документ.РешениеОПриемеНаРаботу", "ПоказыватьНеПринятыхКандидатов", Истина);
	
	НастроитьДинамическийСписок();
	
	ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "Список", , , , , Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоказыватьВсехФизическихЛицПриИзменении(Элемент)
	
	НастроитьДинамическийСписок();
	
	СохранитьНастройкуПоказыватьНеПринятыхКандидатов(ПоказыватьНеПринятыхКандидатов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыСписок

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, Настройки);
КонецПроцедуры

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, , СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьДинамическийСписок()
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	
	Если ПоказыватьНеПринятыхКандидатов Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФизическиеЛицаПереопределяемый.Ссылка КАК Ссылка,
		|	ФизическиеЛицаПереопределяемый.Код КАК Код,
		|	ФизическиеЛицаПереопределяемый.Наименование КАК Наименование,
		|	ФизическиеЛицаПереопределяемый.ИНН КАК ИНН,
		|	ФизическиеЛицаПереопределяемый.СтраховойНомерПФР КАК СтраховойНомерПФР
		|ИЗ
		|	Справочник.Кандидаты КАК Кандидаты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛицаПереопределяемый
		|		ПО Кандидаты.ФизическоеЛицо = ФизическиеЛицаПереопределяемый.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РешениеОПриемеНаРаботу КАК РешениеОПриемеНаРаботу
		|		ПО Кандидаты.ФизическоеЛицо = РешениеОПриемеНаРаботу.ФизическоеЛицо
		|			И Кандидаты.Позиция = РешениеОПриемеНаРаботу.ДолжностьПоШтатномуРасписанию
		|			И Кандидаты.ДатаРегистрации < РешениеОПриемеНаРаботу.ДатаПриема
		|			И (НЕ РешениеОПриемеНаРаботу.ПометкаУдаления)
		|ГДЕ
		|	Кандидаты.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСогласования.Согласовано)
		|	И (Кандидаты.Позиция.Владелец = &Организация
		|			ИЛИ &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
		|	И (РешениеОПриемеНаРаботу.Ссылка ЕСТЬ NULL
		|			ИЛИ РешениеОПриемеНаРаботу.Ссылка = &ТекущийДокумент)";
		
		Если Не ПолучитьФункциональнуюОпцию("ОткрытиеВакансииТребуетСогласования") Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Кандидаты.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСогласования.Согласовано)", "ИСТИНА");
		КонецЕсли;
		
	Иначе
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ФизическиеЛицаПереопределяемый.Ссылка КАК Ссылка,
		|	ФизическиеЛицаПереопределяемый.Код КАК Код,
		|	ФизическиеЛицаПереопределяемый.Наименование КАК Наименование,
		|	ФизическиеЛицаПереопределяемый.ИНН КАК ИНН,
		|	ФизическиеЛицаПереопределяемый.СтраховойНомерПФР КАК СтраховойНомерПФР
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛицаПереопределяемый";
		
	КонецЕсли;
	
	СвойстваСписка.ОсновнаяТаблица  = "Справочник.ФизическиеЛица";
	СвойстваСписка.ДинамическоеСчитываниеДанных = Истина;
	СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
	
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список, СвойстваСписка);
	
	Если ПоказыватьНеПринятыхКандидатов Тогда
		Список.Параметры.УстановитьЗначениеПараметра("ТекущийДокумент", ТекущийДокумент);
		Список.Параметры.УстановитьЗначениеПараметра("Организация", Организация);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНастройкуПоказыватьНеПринятыхКандидатов(ПоказыватьНеПринятыхКандидатов)
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("Документ.РешениеОПриемеНаРаботу", "ПоказыватьНеПринятыхКандидатов", ПоказыватьНеПринятыхКандидатов);
	
КонецПроцедуры

#КонецОбласти
