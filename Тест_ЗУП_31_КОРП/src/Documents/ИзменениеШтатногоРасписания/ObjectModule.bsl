#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем СозданныеПозиции;

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Подсистема "Управление доступом".

// Процедура ЗаполнитьНаборыЗначенийДоступа по свойствам объекта заполняет наборы значений доступа
// в таблице с полями:
//    НомерНабора     - Число                                     (необязательно, если набор один),
//    ВидДоступа      - ПланВидовХарактеристикСсылка.ВидыДоступа, (обязательно),
//    ЗначениеДоступа - Неопределено, СправочникСсылка или др.    (обязательно),
//    Чтение          - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Добавление      - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Изменение       - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Удаление        - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора.
//
//  Вызывается из процедуры УправлениеДоступомСлужебный.ЗаписатьНаборыЗначенийДоступа(),
// если объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьНаборыЗначенийДоступа" и
// из таких же процедур объектов, у которых наборы значений доступа зависят от наборов этого
// объекта (в этом случае объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьЗависимыеНаборыЗначенийДоступа").
//
// Параметры:
//  Таблица      - ТабличнаяЧасть,
//                 РегистрСведенийНаборЗаписей.НаборыЗначенийДоступа,
//                 ТаблицаЗначений, возвращаемая УправлениеДоступом.ТаблицаНаборыЗначенийДоступа().
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	УправлениеШтатнымРасписанием.ЗаполнитьНаборыЗначенийДоступа(ЭтотОбъект, Таблица);
	
КонецПроцедуры

// Подсистема "Управление доступом".

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	
	МассивПозиций = Позиции.ВыгрузитьКолонку("Позиция");
	Возврат ОбменДаннымиЗарплатаКадрыРасширенный.ОграниченияРегистрацииПоПозициямШтатногоРасписания(ЭтотОбъект, МассивПозиций, Организация);
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	УправлениеШтатнымРасписанием.ДокументыПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения, СозданныеПозиции, ДатаВступленияВСилу, "ДатаВступленияВСилу");
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	УправлениеШтатнымРасписанием.ДокументыОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения, СозданныеПозиции, ДатаВступленияВСилу);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
		Модуль.ПриПроведенииДокументовИзмененияШтатногоРасписания(ЭтотОбъект);
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, ДатаВступленияВСилу, "Объект.ДатаВступленияВСилу", Отказ, НСтр("ru='Дата изменений'"), , , Ложь);
	
	УправлениеШтатнымРасписанием.ДокументыОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ДатаВступленияВСилу, "ДатаВступленияВСилу", Ложь);
	ОстаткиОтпусков.ОбработкаПроверкиЗаполненияЕжегодныхОтпусковПозицийВДокументе(Позиции, ЕжегодныеОтпуска, ПроверяемыеРеквизиты, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура АвтозаполнениеТекущимиДаннымиШР() Экспорт
	
	УправлениеШтатнымРасписанием.АвтозаполнениеТекущимиДаннымиШтатногоРасписания(ЭтотОбъект, ДатаВступленияВСилу);
	
КонецПроцедуры

Процедура ЗаполнитьДокументПоИзменениямТарифнойСетки(ТарифнаяСетка) Экспорт
	
	РезультатЗапроса = УправлениеШтатнымРасписанием.РезультатЗапросаПоИзменениямПозицийШтатногоРасписания(ТарифнаяСетка, Организация, ДатаВступленияВСилу);
	
	Если РезультатЗапроса <> Неопределено Тогда
		
		ТаблицаПозицийКИзменению = РезультатЗапроса.Выгрузить();
		
		УправлениеШтатнымРасписанием.ЗаполнитьОбъектИзменениямиПозицийШтатногоРасписания(ЭтотОбъект, ТаблицаПозицийКИзменению, ТарифнаяСетка);
			
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли