#Область ОписаниеПеременных

&НаКлиенте
Перем ТекущийИдентификаторСтрокиЗаседаниеКомиссии; 

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Если Параметры.Ключ.Пустая() Тогда
		
		// Заполнение нового документа.
		ЗначенияДляЗаполнения = Новый Структура("Организация, Ответственный", "Объект.Организация", "Объект.Ответственный");   		
		ЗначенияДляЗаполнения.Вставить("Руководитель", "Объект.Руководитель");
		ЗначенияДляЗаполнения.Вставить("ДолжностьРуководителя", "Объект.ДолжностьРуководителя");
		ЗначенияДляЗаполнения.Вставить("РуководительКадровойСлужбы", "Объект.НачальникОтделаКадров");
		ЗначенияДляЗаполнения.Вставить("ВидАттестации", "Объект.ВидАттестации");
		
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
		
		Объект.НачалоПериода = НачалоГода(ТекущаяДатаСеанса());
		Объект.КонецПериода = КонецГода(Объект.НачалоПериода);
		
		ПриПолученииДанныхНаСервере();
		
	КонецЕсли;	
	
	// Обработчик подсистемы "ВерсионированиеОбъектов".
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужбаФормы");
		Модуль.УстановитьПараметрыВыбораСотрудников(ЭтаФорма, "СотрудникиСотрудник,АттестуемыеСотрудникиСотрудник");
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ПриПолученииДанныхНаСервере();
	
	ОбменДаннымиЗарплатаКадры.ПриЧтенииНаСервереДокумента(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Запись_ПриказОбАттестацииСотрудников", ПараметрыЗаписи, Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
    ЗарплатаКадрыРасширенныйКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтаФорма);
	ОбновитьАттестуемыхСотрудников();
КонецПроцедуры

&НаКлиенте
Процедура ВидАттестацииПриИзменении(Элемент)
	ВидАттестацииПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	НачалоПериодаПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	КонецПериодаПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПериодАттестацииГодПриИзменении(Элемент)
	ПериодАттестацииГодПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыЗаседания

&НаКлиенте
Процедура ЗаседанияКомиссииПриАктивизацииСтроки(Элемент)
	УстановитьОтборСтрокПоЗаседанию();
КонецПроцедуры

&НаКлиенте
Процедура ЗаседанияКомиссииПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ЗаседанияКомиссииПриОкончанииРедактированияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаседанияКомиссииПередУдалением(Элемент, Отказ)
	ТекущийИдентификаторСтрокиЗаседаниеКомиссии = Элементы.ЗаседанияКомиссии.ТекущиеДанные.ИдентификаторСтрокиЗаседаниеКомиссии;
КонецПроцедуры

&НаКлиенте
Процедура ЗаседанияКомиссииПослеУдаления(Элемент)
	ЗаседанияКомиссииПослеУдаленияНаСервере(ТекущийИдентификаторСтрокиЗаседаниеКомиссии);
	ОбновитьДатуВремяКомиссииАттестуемыхСотрудников(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ЗаседанияКомиссииПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		УстановитьНовыйИдентификаторЗаседания();
		УстановитьПродолжительностьАттестации();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаседанияКомиссииМестоПроведенияЗаседанияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДополнительныеПараметры = Новый Структура("ФормаВладелец, ТекущаяСтрока", ЭтотОбъект, Элементы.ЗаседанияКомиссии.ТекущаяСтрока);
	ОповещениеОЗакрытии 	= Новый ОписаниеОповещения("ЗаседанияКомиссииМестоПроведенияЗаседанияОкончаниеВвода", ЭтотОбъект, ДополнительныеПараметры);
	МногострочныйТекст		= Элементы.ЗаседанияКомиссии.ТекущиеДанные.МестоПроведенияЗаседания;
	ТекстЗаголовка			= НСтр("ru = 'Укажите место проведения заседания'");
	ПоказатьВводСтроки(ОповещениеОЗакрытии, МногострочныйТекст, ТекстЗаголовка, , Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаседанияКомиссииМестоПроведенияЗаседанияОкончаниеВвода(Знач ВведенныйТекст, Знач ДополнительныеПараметры) Экспорт
	
	Если ВведенныйТекст = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	Если ДополнительныеПараметры.Свойство("ФормаВладелец") 
		И НЕ ДополнительныеПараметры.ФормаВладелец = ЭтотОбъект Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДополнительныеПараметры.Свойство("ТекущаяСтрока") Тогда
		Возврат;
	КонецЕсли;
	
	Строка = Объект.ЗаседанияКомиссии.НайтиПоИдентификатору(ДополнительныеПараметры.ТекущаяСтрока);
	Если НЕ Строка = Неопределено Тогда
		Строка.МестоПроведенияЗаседания = ВведенныйТекст;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыСотрудникиСотрудники

&НаКлиенте
Процедура СотрудникиПриИзменении(Элемент)
	ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьДатуВремяКомиссииАттестуемыхСотрудников(ЭтотОбъект);	
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		Если Не ЗначениеЗаполнено(Элементы.Сотрудники.ТекущиеДанные.ИдентификаторСтрокиЗаседаниеКомиссии) Тогда
			Элементы.Сотрудники.ТекущиеДанные.ИдентификаторСтрокиЗаседаниеКомиссии = Элементы.ЗаседанияКомиссии.ТекущиеДанные.ИдентификаторСтрокиЗаседаниеКомиссии; 
		КонецЕсли;
		Элементы.Сотрудники.ТекущиеДанные.ВремяАттестации = НачалоСледующейАттестации(Элементы.Сотрудники.ТекущиеДанные);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиСотрудникПриИзменении(Элемент)
	СотрудникиСотрудникПриИзмененииНаСервере(Элементы.Сотрудники.ТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПослеУдаления(Элемент)
	ОбновитьДатуВремяКомиссииАттестуемыхСотрудников(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	ДобавитьВыбранныхСотрудниковКЗаседанию(ПараметрыПеретаскивания.Значение, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиВремяАттестацииПриИзменении(Элемент)
	НачалоСледующейАттестации = Элементы.Сотрудники.ТекущиеДанные.ВремяАттестации + ПродолжительностьАттестацииТекущегоЗаседания();
	СдвинутьНачалоПоследующихАттестаций(Элементы.Сотрудники.ТекущиеДанные.НомерСтроки, НачалоСледующейАттестации)
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыАттестуемыеСотрудники

&НаКлиенте
Процедура АттестуемыеСотрудникиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДобавитьВыбранныхСотрудниковКЗаседанию(ВыбраннаяСтрока, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПриПолученииДанныхНаСервере

&НаСервере
Процедура ПриПолученииДанныхНаСервере()
	ЗарплатаКадры.КлючевыеРеквизитыЗаполненияФормыЗаполнитьПредупреждения(ЭтаФорма);
	ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	ОбновитьДополнительныеДанныеФормы();
	УстановитьДоступностьЭлементовФормы();
	НастроитьФормуПоВидуАттестации();
КонецПроцедуры

&НаСервере
Процедура ОбновитьДополнительныеДанныеФормы()
	ОбновитьТипАттестации();
	ОбновитьАттестуемыхСотрудников();
	ОбновитьДатуВремяКомиссииАттестуемыхСотрудников(ЭтотОбъект);
	УстановитьПродолжительностьПоУмолчанию();
КонецПроцедуры	

&НаСервере
Процедура УстановитьПродолжительностьПоУмолчанию()
	ПродолжительностьАттестацииПоУмолчанию = АттестацииСотрудниковФормы.ПродолжительностьАттестацииОдногоСотрудникаПоУмолчанию(); 
КонецПроцедуры

#КонецОбласти

#Область СерверныеОбработчикиСобытийФормы

&НаСервере
Процедура ВидАттестацииПриИзмененииНаСервере()
	ОбновитьТипАттестации();
	НастроитьФормуПоВидуАттестации();
	ОбновитьАттестуемыхСотрудников();
КонецПроцедуры

&НаСервере
Процедура НачалоПериодаПриИзмененииНаСервере()
	ОбновитьАттестуемыхСотрудников();
КонецПроцедуры

&НаСервере
Процедура КонецПериодаПриИзмененииНаСервере()
	ОбновитьАттестуемыхСотрудников();
КонецПроцедуры

&НаСервере
Процедура ПериодАттестацииГодПриИзмененииНаСервере()
	Объект.КонецПериода = КонецГода(Объект.НачалоПериода);
	ОбновитьАттестуемыхСотрудников();
КонецПроцедуры

&НаСервере
Процедура ЗаседанияКомиссииПриОкончанииРедактированияНаСервере()
	ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	УстановитьДоступностьСписковСотрудников(); 
КонецПроцедуры

#Область ЗаседанияКомиссииПослеУдаленияНаСервере

&НаСервере
Процедура ЗаседанияКомиссииПослеУдаленияНаСервере(ТекущийИдентификаторСтрокиЗаседаниеКомиссии)
	ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	УдалитьСотрудниковЗаседания(ТекущийИдентификаторСтрокиЗаседаниеКомиссии);
	УдалитьОтветственныхЗаседания(ТекущийИдентификаторСтрокиЗаседаниеКомиссии);
	УстановитьДоступностьСписковСотрудников();
КонецПроцедуры

&НаСервере
Функция УдалитьСотрудниковЗаседания(ТекущийИдентификаторСтрокиЗаседаниеКомиссии)
	Строки = Объект.Сотрудники.НайтиСтроки(Новый Структура("ИдентификаторСтрокиЗаседаниеКомиссии", ТекущийИдентификаторСтрокиЗаседаниеКомиссии));
	Для каждого Строка Из Строки Цикл
		Объект.Сотрудники.Удалить(Строка); 
	КонецЦикла;
КонецФункции

&НаСервере
Функция УдалитьОтветственныхЗаседания(ТекущийИдентификаторСтрокиЗаседаниеКомиссии)
	Строки = Объект.ГрафикПредоставленияДокументов.НайтиСтроки(Новый Структура("ИдентификаторСтрокиЗаседаниеКомиссии", ТекущийИдентификаторСтрокиЗаседаниеКомиссии));
	Для каждого Строка Из Строки Цикл
		Объект.ГрафикПредоставленияДокументов.Удалить(Строка); 
	КонецЦикла;
КонецФункции

#КонецОбласти

&НаСервере
Процедура СотрудникиСотрудникПриИзмененииНаСервере(ИдентификаторСтроки)
	
	Строка 					= Объект.Сотрудники.НайтиПоИдентификатору(ИдентификаторСтроки);
	Строка.Подразделение 	= Неопределено;
	Строка.Должность 		= Неопределено;
	
	Если ЗначениеЗаполнено(Строка.Сотрудник) Тогда
		КадровыеДанныеСотрудника = КадровыйУчет.КадровыеДанныеСотрудников(Истина, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Строка.Сотрудник), "Подразделение, Должность");	
		Если КадровыеДанныеСотрудника.Количество() > 0 Тогда
			Строка.Подразделение 	= КадровыеДанныеСотрудника[0].Подразделение;
			Строка.Должность 		= КадровыеДанныеСотрудника[0].Должность;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОтрисовкаФормы

&НаСервере
Процедура УстановитьДоступностьЭлементовФормы()
	УстановитьДоступностьСписковСотрудников();
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьСписковСотрудников()
	ДоступностьСписков = Объект.ЗаседанияКомиссии.Количество()>0;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		ЭтотОбъект.Элементы,
		"АттестуемыеСотрудники",
		"Доступность",
		ДоступностьСписков);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		ЭтотОбъект.Элементы,
		"Сотрудники",
		"Доступность",
		ДоступностьСписков);
КонецПроцедуры

#Область НастроитьФормуПоВидуАттестации

&НаСервере
Процедура НастроитьФормуПоВидуАттестации()
	
	УстановитьВидимостьКомиссии();
	УстановитьТекущуюСтраницуПериодаАттестации();
	
	НастроитьФормуПоТипуАттестации();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКомиссии()
	ОтображатьКомиссию = НеобходимоОтображатьКомиссию();
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "ПриказОФормированииКомиссии", "Видимость", ОтображатьКомиссию);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "СтраницаСоставКомиссии",	"Видимость", ОтображатьКомиссию);
КонецПроцедуры

&НаСервере
Процедура УстановитьТекущуюСтраницуПериодаАттестации()
	Если АттестацииСотрудниковФормы.ДлительностьАттестационнойКампанииГод(Объект.ВидАттестации) Тогда
		Элементы.СтраницыПериодаАттестации.ТекущаяСтраница = Элементы.СтраницыПериодаАттестации.ПодчиненныеЭлементы.СтраницаПериодАттестацииГод;
	Иначе
		Элементы.СтраницыПериодаАттестации.ТекущаяСтраница = Элементы.СтраницыПериодаАттестации.ПодчиненныеЭлементы.СтраницаПроизвольныйПериодАттестации;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция НеобходимоОтображатьКомиссию()
	Возврат НЕ АттестацииСотрудников.ЭтоВнешняяАттестация(Объект.ВидАттестации);
КонецФункции

&НаСервере
Процедура НастроитьФормуПоТипуАттестации()
	
	УстановитьВидимостьДолжности();
	УстановитьВидимостьСпециальности();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДолжности()
	ОтображатьДолжность = НеобходимоОтображатьДолжность();
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "СотрудникиДолжность", "Видимость", ОтображатьДолжность);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "АттестуемыеСотрудникиДолжность", "Видимость", ОтображатьДолжность);
КонецПроцедуры

&НаСервере
Функция НеобходимоОтображатьДолжность()
	Возврат ТипАттестации = Перечисления.ТипыАттестацииСотрудников.ПодтверждениеСоответствияДолжности;
КонецФункции

&НаСервере
Процедура УстановитьВидимостьСпециальности()
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "СотрудникиСпециальность", "Видимость", ТипАттестацииИспользуетСпециальность);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "СотрудникиКатегория", "Видимость", ТипАттестацииИспользуетСпециальность);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "АттестуемыеСотрудникиСпециальность", "Видимость", ТипАттестацииИспользуетСпециальность);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "АттестуемыеСотрудникиКатегория", "Видимость", ТипАттестацииИспользуетСпециальность);
КонецПроцедуры

#КонецОбласти 

#КонецОбласти

#Область КлючевыеРеквизитыЗаполненияФормы

// Функция возвращает описание таблиц формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыТаблицыОчищаемыеПриИзменении() Экспорт
	Массив = Новый Массив;
	Массив.Добавить("Объект.Сотрудники");
	Массив.Добавить("Объект.ГрафикПредоставленияДокументов");
	Возврат Массив
КонецФункции 

// Функция возвращает массив реквизитов формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыОписаниеКлючевыхРеквизитов() Экспорт
	Массив = Новый Массив;
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "Организация",					Нстр("ru = 'организации'")));
	Возврат Массив
КонецФункции

#КонецОбласти

&НаСервере
Процедура ОбновитьАттестуемыхСотрудников()
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) 
		Или НЕ ЗначениеЗаполнено(Объект.ВидАттестации)  Тогда
		Возврат;
	КонецЕсли; 
	
	СотрудникиПодлежащиеАттестации = АттестацииСотрудников.СотрудникиПодлежащиеАттестации(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.ВидАттестации), Объект.Организация, Объект.НачалоПериода, Объект.КонецПериода);
	
	АттестуемыеСотрудники.Загрузить(СотрудникиПодлежащиеАттестации);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТипАттестации()
	
	Если НЕ ЗначениеЗаполнено(Объект.ВидАттестации)  Тогда
		Возврат;
	КонецЕсли; 
	
	ТипАттестации = АттестацииСотрудников.ТипАттестации(Объект.ВидАттестации);
	ТипАттестацииИспользуетСпециальность = ТипАттестации = Перечисления.ТипыАттестацииСотрудников.ПодтверждениеКвалификацииПоСпециальности;
	ТипАттестацииИспользуетДолжность = ТипАттестации = Перечисления.ТипыАттестацииСотрудников.ПодтверждениеСоответствияДолжности;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДатуВремяКомиссииАттестуемыхСотрудников(Форма)
	
	Если Форма.АттестуемыеСотрудники.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	Для каждого АттестуемыйСотрудник Из Форма.АттестуемыеСотрудники Цикл
		АттестуемыйСотрудник.ДатаЗаседания 		= Неопределено;
		АттестуемыйСотрудник.ВремяАттестации 	= Неопределено;
		
		СтрокаСотрудника = Неопределено;
		
		Для каждого Строка Из Форма.Объект.Сотрудники Цикл
			Если Строка.Сотрудник = АттестуемыйСотрудник.Сотрудник 
				И ((Форма.ТипАттестацииИспользуетСпециальность И Строка.Специальность = АттестуемыйСотрудник.Специальность) 
				ИЛИ (Форма.ТипАттестацииИспользуетДолжность И Строка.Должность = АттестуемыйСотрудник.Должность)) Тогда
				СтрокаСотрудника = Строка;
			КонецЕсли;
		КонецЦикла;
		
		Если СтрокаСотрудника = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Для каждого Строка Из Форма.Объект.ЗаседанияКомиссии Цикл
			Если Строка.ИдентификаторСтрокиЗаседаниеКомиссии = СтрокаСотрудника.ИдентификаторСтрокиЗаседаниеКомиссии Тогда
				АттестуемыйСотрудник.ДатаЗаседания 		= Строка.ДатаЗаседания;
				АттестуемыйСотрудник.ВремяАттестации 	= СтрокаСотрудника.ВремяАттестации;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#Область УстановитьНовыйИдентификаторЗаседания

&НаКлиенте
Процедура УстановитьНовыйИдентификаторЗаседания()
	Строка =  Элементы.ЗаседанияКомиссии.ТекущиеДанные;
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Строка.ИдентификаторСтрокиЗаседаниеКомиссии = СледующийИдентификаторЗаседания(); 
КонецПроцедуры

&НаКлиенте
Функция СледующийИдентификаторЗаседания()
	Возврат МаксимальныйИдентификаторЗаседания() + 1;
КонецФункции

&НаКлиенте
Функция МаксимальныйИдентификаторЗаседания()
	МаксимальныйИдентификатор = 0;
	Для каждого Заседание Из Объект.ЗаседанияКомиссии Цикл
		МаксимальныйИдентификатор = Макс(МаксимальныйИдентификатор, Заседание.ИдентификаторСтрокиЗаседаниеКомиссии); 
	КонецЦикла;
	Возврат МаксимальныйИдентификатор;
КонецФункции

#КонецОбласти

&НаКлиенте
Процедура УстановитьПродолжительностьАттестации()
	Строка = Элементы.ЗаседанияКомиссии.ТекущиеДанные;
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Строка.ПродолжительностьАттестации = ПродолжительностьАттестацииПоУмолчанию; 
КонецПроцедуры

&НаКлиенте
Функция НачалоСледующейАттестации(ТекущаяАттестация = Неопределено)
	
	НачалоПоследнейАттестации = Элементы.ЗаседанияКомиссии.ТекущиеДанные.ВремяНачала;
	
	Если Объект.Сотрудники.Количество() = 0 Тогда
		Возврат НачалоПоследнейАттестации;
	КонецЕсли;
	
	ЕстьАттестация = Ложь;
	
	Для каждого Аттестация Из Объект.Сотрудники Цикл
		Если Аттестация.ИдентификаторСтрокиЗаседаниеКомиссии = Элементы.ЗаседанияКомиссии.ТекущиеДанные.ИдентификаторСтрокиЗаседаниеКомиссии Тогда
			
			Если Аттестация = ТекущаяАттестация Тогда
			    Продолжить;
			КонецЕсли;
			
			НачалоПоследнейАттестации = Макс(НачалоПоследнейАттестации, Аттестация.ВремяАттестации); 
			ЕстьАттестация = Истина;
			
		КонецЕсли;
	КонецЦикла;
	
	НачалоСледующейАттестации = НачалоПоследнейАттестации + ?(ЕстьАттестация, Элементы.ЗаседанияКомиссии.ТекущиеДанные.ПродолжительностьАттестации * 60, 0);
	
	Возврат НачалоСледующейАттестации;
	
КонецФункции

&НаКлиенте
Процедура СдвинутьНачалоПоследующихАттестаций(НомерСтроки, НачалоСледующейАттестации)
	
	Если НЕ Объект.Сотрудники.Количество() > НомерСтроки Тогда
		Возврат
	КонецЕсли;
	
	Объект.Сотрудники[НомерСтроки].ВремяАттестации = НачалоСледующейАттестации;
	
	НачалоСледующейАттестации = НачалоСледующейАттестации + ПродолжительностьАттестацииТекущегоЗаседания();
	
	СдвинутьНачалоПоследующихАттестаций(НомерСтроки + 1, НачалоСледующейАттестации);
	
КонецПроцедуры

&НаКлиенте
Функция ПродолжительностьАттестацииТекущегоЗаседания()
	
	ПродолжительностьАттестации = 0;
	Если НЕ Элементы.ЗаседанияКомиссии.ТекущиеДанные = Неопределено Тогда
		ПродолжительностьАттестации = Элементы.ЗаседанияКомиссии.ТекущиеДанные.ПродолжительностьАттестации;
	КонецЕсли;
	
	Возврат ПродолжительностьАттестации * 60;
	
КонецФункции

&НаКлиенте
Процедура УстановитьОтборСтрокПоЗаседанию()
	
	Если Элементы.ЗаседанияКомиссии.ТекущиеДанные = Неопределено Тогда
		ОтборСтрок = Неопределено;
	Иначе	
		ОтборСтрок = Новый ФиксированнаяСтруктура("ИдентификаторСтрокиЗаседаниеКомиссии", Элементы.ЗаседанияКомиссии.ТекущиеДанные.ИдентификаторСтрокиЗаседаниеКомиссии);
	КонецЕсли;
	Элементы.Сотрудники.ОтборСтрок = ОтборСтрок;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВыбранныхСотрудниковКЗаседанию(МассивСтрок, СтандартнаяОбработка)
	
	Если Элементы.ЗаседанияКомиссии.ТекущиеДанные = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Нстр("ru = 'Необходимо сначала выбрать заседание'"));
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	НачалоСледующейАттестации = НачалоСледующейАттестации();
	
	Для каждого Строка Из МассивСтрок Цикл
		Если ТипЗнч(Строка) = Тип("Число") Тогда
			СтрокаАттестуемого = АттестуемыеСотрудники.НайтиПоИдентификатору(Строка);
		Иначе
			СтрокаАттестуемого = Строка;
		КонецЕсли;
		
		// Если сотрудник уже добавлен к какому-то заседанию в документе - пропускаем его.
		Если ЗначениеЗаполнено(СтрокаАттестуемого.ДатаЗаседания) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока 									= Объект.Сотрудники.Добавить();
		НоваяСтрока.Сотрудник 							= СтрокаАттестуемого.Сотрудник;
		НоваяСтрока.Подразделение 						= СтрокаАттестуемого.Подразделение;
		НоваяСтрока.Должность 							= СтрокаАттестуемого.Должность;
		НоваяСтрока.Специальность 						= СтрокаАттестуемого.Специальность;
		НоваяСтрока.Категория 							= СтрокаАттестуемого.Категория;
		НоваяСтрока.ВремяАттестации 					= НачалоСледующейАттестации;
		НоваяСтрока.ИдентификаторСтрокиЗаседаниеКомиссии = Элементы.ЗаседанияКомиссии.ТекущиеДанные.ИдентификаторСтрокиЗаседаниеКомиссии;		
		
		НачалоСледующейАттестации = НачалоСледующейАттестации + ПродолжительностьАттестацииТекущегоЗаседания();
		
		ПодходящаяСтрокаОтветственного = Неопределено;
		
		Для каждого СтрокаОтветственного Из Объект.ГрафикПредоставленияДокументов Цикл
			Если НЕ СтрокаОтветственного.ИдентификаторСтрокиЗаседаниеКомиссии = Элементы.ЗаседанияКомиссии.ТекущиеДанные.ИдентификаторСтрокиЗаседаниеКомиссии Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрокаОтветственного.Подразделение = СтрокаАттестуемого.Подразделение Тогда
				ПодходящаяСтрокаОтветственного = СтрокаОтветственного;
			КонецЕсли;
		КонецЦикла;
		
		Если ПодходящаяСтрокаОтветственного = Неопределено Тогда
			ПодходящаяСтрокаОтветственного = Объект.ГрафикПредоставленияДокументов.Добавить();
			ПодходящаяСтрокаОтветственного.Подразделение = СтрокаАттестуемого.Подразделение;
			ПодходящаяСтрокаОтветственного.ИдентификаторСтрокиЗаседаниеКомиссии = Элементы.ЗаседанияКомиссии.ТекущиеДанные.ИдентификаторСтрокиЗаседаниеКомиссии;  
			ПодходящаяСтрокаОтветственного.ДатаПредставленияДокументов = ДобавитьМесяц(Элементы.ЗаседанияКомиссии.ТекущиеДанные.ДатаЗаседания, -1);  
		КонецЕсли;
		
	КонецЦикла;
	
	ОбновитьДатуВремяКомиссииАттестуемыхСотрудников(ЭтотОбъект);
	УстановитьОтборСтрокПоЗаседанию();
	
КонецПроцедуры

#КонецОбласти
