
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Организация") Тогда
		Организация = Параметры.Организация;
	Иначе
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Сотрудник") Тогда
		Сотрудник = Параметры.Сотрудник;
	Иначе
		Возврат;
	КонецЕсли;
	
	ТаблицаДоговоров = Документы.АктПриемкиВыполненныхРаботОказанныхУслуг.ДоговорыСотрудника(Организация, Сотрудник);
	СписокДоговоров.Загрузить(ТаблицаДоговоров);
	
	Если Параметры.Свойство("Договор") Тогда
		Отбор = Новый Структура;
		Отбор.Вставить("Договор", Параметры.Договор);
		МассивСтрок = СписокДоговоров.НайтиСтроки(Отбор);
		Если МассивСтрок.Количество() > 0  Тогда
			ИндексСтроки = СписокДоговоров.Индекс(МассивСтрок[0]);
			Элементы.СписокДоговоров.ТекущаяСтрока = ИндексСтроки;
		КонецЕсли;
	КонецЕсли;
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Договоры (%1)'"), Сотрудник);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокДоговоровВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОбработатьВыбор();
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	ОбработатьВыбор();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьСписокНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьВыбор()

	ТекущиеДанные = Элементы.СписокДоговоров.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущийДоговор = ТекущиеДанные.Договор;
		ОповеститьОВыборе(ТекущийДоговор);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокНаСервере()

	ТаблицаДоговоров = Документы.АктПриемкиВыполненныхРаботОказанныхУслуг.ДоговорыСотрудника(Организация, Сотрудник);
	СписокДоговоров.Загрузить(ТаблицаДоговоров);

КонецПроцедуры

#КонецОбласти
