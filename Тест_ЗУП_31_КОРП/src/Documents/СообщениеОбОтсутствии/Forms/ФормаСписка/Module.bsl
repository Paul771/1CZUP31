
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.РежимВыбора Тогда
		Элементы.Список.РежимВыбора = Истина;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.КоманднаяПанельФормы;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "Список");
	
	ОтбиратьПоДате = Истина;
	ДатаОтсутствия = ТекущаяДатаСеанса();
	УстановитьОтборПоДате(Список.Отбор, ОтбиратьПоДате, ДатаОтсутствия);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтбиратьПоДатеПриИзменении(Элемент)
	
	УстановитьОтборПоДате(Список.Отбор, ОтбиратьПоДате, ДатаОтсутствия);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОтсутствияПриИзменении(Элемент)
	
	ОтбиратьПоДате = Истина;
	УстановитьОтборПоДате(Список.Отбор, ОтбиратьПоДате, ДатаОтсутствия);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если ЗначениеЗаполнено(ФизическоеЛицо) Тогда 
		Отказ = Истина;
		ЗначенияЗаполнения = Новый Структура("ФизическоеЛицо", ФизическоеЛицо);
		ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
		ОткрытьФорму("Документ.СообщениеОбОтсутствии.ФормаОбъекта", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, Настройки);
КонецПроцедуры

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, , СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Вчера(Команда)
	
	ОтбиратьПоДате = Истина;
	ДатаОтсутствия = ОбщегоНазначенияКлиент.ДатаСеанса() - 86400;
	УстановитьОтборПоДате(Список.Отбор, ОтбиратьПоДате, ДатаОтсутствия);
	
КонецПроцедуры

&НаКлиенте
Процедура Завтра(Команда)
	
	ОтбиратьПоДате = Истина;
	ДатаОтсутствия = ОбщегоНазначенияКлиент.ДатаСеанса() + 86400;
	УстановитьОтборПоДате(Список.Отбор, ОтбиратьПоДате, ДатаОтсутствия);
	
КонецПроцедуры

&НаКлиенте
Процедура Сегодня(Команда)
	
	ОтбиратьПоДате = Истина;
	ДатаОтсутствия = ОбщегоНазначенияКлиент.ДатаСеанса();
	УстановитьОтборПоДате(Список.Отбор, ОтбиратьПоДате, ДатаОтсутствия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоДате(ГруппаОтбора, ОтбиратьПоДате, ДатаОтсутствия)
	
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(ГруппаОтбора, "ДатаНачала");
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(ГруппаОтбора, "ДатаОкончания");
	
	Если ОтбиратьПоДате Тогда 
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаОтбора, "ДатаНачала", КонецДня(ДатаОтсутствия), ВидСравненияКомпоновкиДанных.МеньшеИлиРавно, , Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаОтбора, "ДатаОкончания", ДатаОтсутствия, ВидСравненияКомпоновкиДанных.БольшеИлиРавно, , Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
