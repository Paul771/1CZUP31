
#Область ОбработкаСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая() Тогда
		ЗначенияДляЗаполнения = Новый Структура("Ответственный, Месяц", "Объект.Ответственный", "Объект.Период");
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
		
		ПриПолученииДанныхНаСервере();
	КонецЕсли;
	
	// Обработчик подсистемы "ВерсионированиеОбъектов".
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ПриПолученииДанныхНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриПолученииДанныхНаСервере();
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Запись_ПланПоказателейЭффективностиСотрудника", Новый Структура("Сотрудники", Объект.Сотрудник));
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	УстановитьПараметрыВыбораСотрудника();
КонецПроцедуры

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	ЗаполнитьПоказателиНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПоказателиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	КлючевыеПоказателиЭффективностиКлиентСервер.ЗаполнитьВторичныеРеквизитыСтрокиПоказателя(ЭтаФорма, Элемент.ТекущиеДанные);
	
КонецПроцедуры

#Область МесяцСтрокой

&НаКлиенте
Процедура МесяцСтрокойПриИзменении(Элемент)
	МесяцСтрокойПриИзмененииНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтаФорма, ЭтаФорма, "Объект.Период", "МесяцСтрокой",, Новый ОписаниеОповещения("МесяцСтрокойПриИзмененииНаКлиенте", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокойРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(ЭтаФорма, "Объект.Период", "МесяцСтрокой", Направление);
	МесяцСтрокойПриИзмененииНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокойАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокойОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПоказатели(Команда)
	ЗаполнитьПоказателиНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриПолученииДанныхНаСервере()
	
	ЗаполнитьВторичныеРеквизитыФормы();
	
	УстановитьСвойстваЭлементовФормы();

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВторичныеРеквизитыФормы()
	
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтаФорма, "Объект.Период", "МесяцСтрокой");
	
	ЗаполнитьТаблицуПоказателей();
	КлючевыеПоказателиЭффективностиФормы.УстановитьПараметрыОтбораПоказателям(ЭтаФорма);
	КлючевыеПоказателиЭффективностиФормы.ЗаполнитьВторичныеРеквизитыТаблицыПоказателей(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовФормы()
	УстановитьПараметрыВыбораСотрудника();
КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокойПриИзмененииНаКлиенте(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	МесяцСтрокойПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура МесяцСтрокойПриИзмененииНаСервере()
	ЗаполнитьПоказателиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоказателиНаСервере()
	
	ЗаполнитьТаблицуПоказателей();
	
	КлючевыеПоказателиЭффективностиФормы.УстановитьПараметрыОтбораПоказателям(ЭтаФорма);
	КлючевыеПоказателиЭффективностиФормы.ЗагрузитьПоказатели(ЭтаФорма);
	КлючевыеПоказателиЭффективностиФормы.ЗаполнитьВторичныеРеквизитыТаблицыПоказателей(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуПоказателей()
	
	ПоказателиСотрудника = КлючевыеПоказателиЭффективности.ПоказателиСотрудников(Объект.Сотрудник, Объект.Период);
	
	Если ПоказателиСотрудника = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПлановыеПоказателиСотрудника = ПоказателиСотрудника.Скопировать(Новый Структура("ТребуетсяВводПлана", Истина));
	
	КлючевыеПоказателиЭффективностиФормы.ЗаполнитьСоответствиеПоказателей(ЭтаФорма, ПлановыеПоказателиСотрудника);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораСотрудника()
	КлючевыеПоказателиЭффективностиФормы.УстановитьПараметрыВыбораЭлементаПоМестуВСтруктуреПредприятия(Элементы.Сотрудник, Объект.Подразделение);
КонецПроцедуры

#КонецОбласти