
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ДанныеДляПроведения = Документы.ПланПоказателейЭффективностиСотрудника.ПолучитьДанныеДляПроведения(Ссылка);
	КлючевыеПоказателиЭффективности.СформироватьДвиженияПлановыеЗначенияПоказателейЭффективностиСотрудников(Движения, ДанныеДляПроведения.ПланСотрудников);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ПоказателиСтрокой = КлючевыеПоказателиЭффективности.ПоказателиСтрокой(Показатели);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ПараметрыПоказатели = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеЗаполнения, "Показатели");
	Если ПараметрыПоказатели <> Неопределено Тогда
		Для каждого Показатель Из ПараметрыПоказатели Цикл
			НоваяСтрока = Показатели.Добавить();
			НоваяСтрока.Показатель = Показатель;
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ТаблицаПланаСотрудников = РегистрыСведений.ПлановыеЗначенияПоказателейЭффективностиСотрудников.ТаблицаИзмеренийПланаСотрудников();
	
	Для каждого СтрокаПоказателя Из Показатели Цикл
		НоваяСтрока = ТаблицаПланаСотрудников.Добавить();
		НоваяСтрока.ПериодДействия = Период;
		НоваяСтрока.Сотрудник = Сотрудник;
		НоваяСтрока.Показатель = СтрокаПоказателя.Показатель;
	КонецЦикла; 
	
	ТаблицаВведеныхЗначений = КлючевыеПоказателиЭффективности.ВведенныеПлановыеЗначенияПоказателейСотрудников(ТаблицаПланаСотрудников, Ссылка);
	
	Для каждого СтрокаВведеныхЗначений Из ТаблицаВведеныхЗначений Цикл
		НайденныеСтроки = Показатели.НайтиСтроки(Новый Структура("Показатель", СтрокаВведеныхЗначений.Показатель));
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Для сотрудника %1 уже введено плановое значение показателя %2.'"), Сотрудник, СтрокаВведеныхЗначений.Показатель);
		Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				ЭтотОбъект,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Показатели[%1].Показатель", Показатели.Индекс(НайденнаяСтрока)),, Отказ);
		КонецЦикла; 
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	
	Возврат ОбменДаннымиЗарплатаКадрыРасширенный.ОграниченияРегистрацииПоПодразделениюИСотруднику(
				ЭтотОбъект, Подразделение, Сотрудник, Период);
				
КонецФункции

#КонецОбласти

#КонецЕсли
