
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники") Тогда
		ЗарплатаКадры.ЗаполнитьПоОснованиюСотрудником(ЭтотОбъект, ДанныеЗаполнения, , Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	ДанныеДляПроведения = ДанныеДляПроведенияДокумента();
	
	УчетНДФЛ.СформироватьУдержанныйНалогПоВременнойТаблице(Движения, Отказ, Организация, КонецМесяца(Месяц), ДанныеДляПроведения.МенеджерВременныхТаблиц);
	Для каждого Движение Из Движения.РасчетыНалогоплательщиковСБюджетомПоНДФЛ Цикл
		Движение.ВариантУдержания = Перечисления.ВариантыУдержанияНДФЛ.ВозвращеноНалоговымАгентом
	КонецЦикла;
	
	ТаблицаНДФЛ = Движения.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Выгрузить();
	ТаблицаНДФЛ.Колонки.Удалить("Подразделение");
	ТаблицаНДФЛ.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Для каждого СтрокаТЗ Из ТаблицаНДФЛ Цикл
		СтрокаТЗ.Подразделение = ДанныеДляПроведения.ПодразделенияФизическихЛиц[СтрокаТЗ.ФизическоеЛицо];
		СтрокаТЗ.Сумма = - СтрокаТЗ.Сумма;
	КонецЦикла;
	УчетНачисленнойЗарплаты.ЗарегистрироватьВозвратНДФЛ(Движения, Отказ, Организация, Месяц, ТаблицаНДФЛ, ХарактерВыплаты());
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Для Каждого СтрокаТаблицы Из СуммыВозврата Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ПоСтавке09) 
			И НЕ ЗначениеЗаполнено(СтрокаТаблицы.ПоСтавке13)
			И НЕ ЗначениеЗаполнено(СтрокаТаблицы.ПоСтавке35) Тогда
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='В строке %1 таблицы должен быть заполнен хотя бы один налог к возврату.'"), СтрокаТаблицы.НомерСтроки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке,,,,Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	Возврат ОбменДаннымиЗарплатаКадры.ОграниченияРегистрацииПоОрганизацииИФизическомуЛицу(ЭтотОбъект, Организация, Сотрудник, Месяц);
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведенияДокумента()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(Месяц));
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВозвратНДФЛСуммыВозврата.Ссылка.Сотрудник КАК ФизическоеЛицо,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение,
	|	СУММА(ВозвратНДФЛСуммыВозврата.ПоСтавке13) КАК ПоСтавке13,
	|	СУММА(ВозвратНДФЛСуммыВозврата.ПоСтавке09) КАК ПоСтавке09,
	|	СУММА(ВозвратНДФЛСуммыВозврата.ПоСтавке35) КАК ПоСтавке35,
	|	ВозвратНДФЛСуммыВозврата.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	ВозвратНДФЛСуммыВозврата.КатегорияДохода КАК КатегорияДохода,
	|	ВозвратНДФЛСуммыВозврата.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	КОНЕЦПЕРИОДА(ВозвратНДФЛСуммыВозврата.Ссылка.Месяц, МЕСЯЦ) КАК Период
	|ПОМЕСТИТЬ ВТСтрокиДокумента
	|ИЗ
	|	Документ.ВозвратНДФЛ.СуммыВозврата КАК ВозвратНДФЛСуммыВозврата
	|ГДЕ
	|	ВозвратНДФЛСуммыВозврата.Ссылка = &Ссылка
	|	И ВозвратНДФЛСуммыВозврата.ПоСтавке13 + ВозвратНДФЛСуммыВозврата.ПоСтавке09 + ВозвратНДФЛСуммыВозврата.ПоСтавке35 > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВозвратНДФЛСуммыВозврата.Ссылка.Сотрудник,
	|	ВозвратНДФЛСуммыВозврата.МесяцНалоговогоПериода,
	|	ВозвратНДФЛСуммыВозврата.РегистрацияВНалоговомОргане,
	|	ВозвратНДФЛСуммыВозврата.Ссылка.Месяц,
	|	ВозвратНДФЛСуммыВозврата.КатегорияДохода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТФизическиеЛица
	|ИЗ
	|	ВТСтрокиДокумента КАК Сотрудники
	|ГДЕ
	|	Сотрудники.ФизическоеЛицо <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)";
	Результаты = Запрос.Выполнить();
	
	КадровыйУчет.СоздатьВТОсновныеСотрудникиФизическихЛиц(Запрос.МенеджерВременныхТаблиц, Ложь, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник), Организация, КонецМесяца(Месяц));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Сотрудники.Сотрудник КАК Сотрудник,
	|	&КонецМесяца КАК Период
	|ПОМЕСТИТЬ ВТПериодыСотрудников
	|ИЗ
	|	ВТОсновныеСотрудникиФизическихЛиц КАК Сотрудники";
	Запрос.Выполнить();
	
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(Запрос.МенеджерВременныхТаблиц, "ВТПериодыСотрудников");
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, Ложь, "Подразделение");
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтрокиДокумента.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЕСТЬNULL(ДанныеОСотруднике.Подразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК Подразделение
	|ИЗ
	|	ВТСтрокиДокумента КАК СтрокиДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОсновныеСотрудникиФизическихЛиц КАК Сотрудники
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК ДанныеОСотруднике
	|			ПО Сотрудники.Сотрудник = ДанныеОСотруднике.Сотрудник
	|		ПО СтрокиДокумента.ФизическоеЛицо = Сотрудники.ФизическоеЛицо
	|
	|СГРУППИРОВАТЬ ПО
	|	СтрокиДокумента.ФизическоеЛицо,
	|	ЕСТЬNULL(ДанныеОСотруднике.Подразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтрокиДокумента.ФизическоеЛицо КАК ФизическоеЛицо,
	|	-СтрокиДокумента.ПоСтавке13 КАК Сумма,
	|	СтрокиДокумента.Подразделение КАК Подразделение,
	|	ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13) КАК СтавкаНалогообложенияРезидента,
	|	СтрокиДокумента.МесяцНалоговогоПериода КАК Период,
	|	СтрокиДокумента.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	СтрокиДокумента.КатегорияДохода КАК КатегорияДохода,
	|	ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.ПустаяСсылка) КАК КодДохода,
	|	СтрокиДокумента.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ ВТНалогУдержанный
	|ИЗ
	|	ВТСтрокиДокумента КАК СтрокиДокумента
	|ГДЕ
	|	СтрокиДокумента.ПоСтавке13 <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтрокиДокумента.ФизическоеЛицо,
	|	-СтрокиДокумента.ПоСтавке09,
	|	СтрокиДокумента.Подразделение,
	|	ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09),
	|	СтрокиДокумента.МесяцНалоговогоПериода,
	|	СтрокиДокумента.МесяцНалоговогоПериода,
	|	ВЫБОР
	|		КОГДА СтрокиДокумента.ПоСтавке13 <> 0
	|				ИЛИ СтрокиДокумента.ПоСтавке35 <> 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.Дивиденды)
	|		ИНАЧЕ СтрокиДокумента.КатегорияДохода
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.Код1010),
	|	СтрокиДокумента.РегистрацияВНалоговомОргане
	|ИЗ
	|	ВТСтрокиДокумента КАК СтрокиДокумента
	|ГДЕ
	|	СтрокиДокумента.ПоСтавке09 <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтрокиДокумента.ФизическоеЛицо,
	|	-СтрокиДокумента.ПоСтавке35,
	|	СтрокиДокумента.Подразделение,
	|	ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35),
	|	СтрокиДокумента.МесяцНалоговогоПериода,
	|	СтрокиДокумента.МесяцНалоговогоПериода,
	|	ВЫБОР
	|		КОГДА СтрокиДокумента.ПоСтавке13 <> 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеДоходы)
	|		ИНАЧЕ СтрокиДокумента.КатегорияДохода
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.ПустаяСсылка),
	|	СтрокиДокумента.РегистрацияВНалоговомОргане
	|ИЗ
	|	ВТСтрокиДокумента КАК СтрокиДокумента
	|ГДЕ
	|	СтрокиДокумента.ПоСтавке35 <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 0
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо,
	|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
	|	0 КАК СуммаДохода,
	|	0 КАК СуммаВычета,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение
	|ПОМЕСТИТЬ ВТДанныеДляРасчетаНДФЛ";
	Результаты = Запрос.ВыполнитьПакет();
	
	ПодразделенияФизическихЛиц = Новый Соответствие;
	Выборка = Результаты[0].Выбрать();
	Пока Выборка.Следующий() Цикл
		ПодразделенияФизическихЛиц.Вставить(Выборка.ФизическоеЛицо, Выборка.Подразделение);	
	КонецЦикла;
	
	Возврат Новый Структура("ПодразделенияФизическихЛиц, МенеджерВременныхТаблиц", ПодразделенияФизическихЛиц, Запрос.МенеджерВременныхТаблиц);

КонецФункции

Функция ХарактерВыплаты()
	
	ХарактерВыплаты = Перечисления.ХарактерВыплатыЗарплаты.Зарплата;
	
	Если Метаданные().Реквизиты.Найти("ПорядокВыплаты") <> Неопределено Тогда
		ХарактерВыплаты = ЭтотОбъект["ПорядокВыплаты"]
	КонецЕсли;
	
	Возврат ХарактерВыплаты
	
КонецФункции

#КонецОбласти

#КонецЕсли