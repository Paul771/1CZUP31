
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Основная форма печати
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "ЗарплатаКадрыКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Идентификатор = "ПФ_MXL_РасходыНаИсточникИнформацииОКандидатах";
	КомандаПечати.Представление = НСтр("ru = 'Справка о расходах на источник'");
	КомандаПечати.Порядок = 10;
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры

// Сформировать печатные формы объектов.
//
// ВХОДЯЩИЕ:
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы.
//   ОшибкиПечати          - Список значений  - Ошибки печати  (значение - ссылка на объект, представление - текст
//                           ошибки).
//   ОбъектыПечати         - Список значений  - Объекты печати (значение - ссылка на объект, представление - имя
//                           области в которой был выведен объект).
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_РасходыНаИсточникИнформацииОКандидатах") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
						КоллекцияПечатныхФорм, 
						"ПФ_MXL_РасходыНаИсточникИнформацииОКандидатах", 
						НСтр("ru = 'Расходы на источники информации о кандидатах'"), 
						ПечатнаяФормаРасходыНаИсточникИнформацииОКандидатах(МассивОбъектов, ОбъектыПечати), ,
						"Документ.РасходыНаИсточникИнформацииОКандидатах.ПФ_MXL_РасходыНаИсточникИнформацииОКандидатах");
	КонецЕсли;					
						
КонецПроцедуры								

Функция ПечатнаяФормаРасходыНаИсточникИнформацииОКандидатах(МассивОбъектов, ОбъектыПечати)
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РасходыНаИсточникИнформацииОКандидатах";
	
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	Макет 		  = УправлениеПечатью.МакетПечатнойФормы("Документ.РасходыНаИсточникИнформацииОКандидатах.ПФ_MXL_РасходыНаИсточникИнформацииОКандидатах");
	Заголовок 	  = Макет.ПолучитьОбласть("Заголовок");
	Шапка 		  = Макет.ПолучитьОбласть("Шапка");
	ШапкаТаблицы  = Макет.ПолучитьОбласть("ВакансииШапка");
	СтрокаТаблицы = Макет.ПолучитьОбласть("ВакансииСтрока");
	Подвал 		  = Макет.ПолучитьОбласть("Подвал");
	
	ВыводимыеОбласти = Новый Массив;
	ВыводимыеОбласти.Добавить(СтрокаТаблицы);
	ВыводимыеОбласти.Добавить(Подвал);
	
	ДанныеДляПечати = ДанныеДляПечатиДокументаРасходыНаИсточникИнформацииОКандидатах(МассивОбъектов);
	
	ВыборкаПоДокументам = ДанныеДляПечати.РезультатПоШапке.Выбрать();
	ВыборкаПоСтрокам 	= ДанныеДляПечати.РезультатПоТабличнойЧасти.Выбрать();
	
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
		
		Заголовок.Параметры.Дата = Формат(ВыборкаПоДокументам.Дата, "ДЛФ=D");
		Заголовок.Параметры.Номер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаПоДокументам.Номер, Ложь, Истина);
		ТабДокумент.Вывести(Заголовок);
		
		ЗаполнитьЗначенияСвойств(Шапка.Параметры, ВыборкаПоДокументам);
		ТабДокумент.Вывести(Шапка);
		
		СтруктураПоиска = Новый Структура("Ссылка", ВыборкаПоДокументам.Ссылка);
		ВыборкаПоСтрокам.Сбросить();
		Если ВыборкаПоСтрокам.НайтиСледующий(СтруктураПоиска) Тогда 
			ТабДокумент.Вывести(ШапкаТаблицы);
			ВыборкаПоСтрокам.Сбросить();
			Пока ВыборкаПоСтрокам.НайтиСледующий(СтруктураПоиска) Цикл 
				
		    	ЗаполнитьЗначенияСвойств(СтрокаТаблицы.Параметры, ВыборкаПоСтрокам);
				
				Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабДокумент, ВыводимыеОбласти) Тогда
					ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				КонецЕсли;
				
				ТабДокумент.Вывести(СтрокаТаблицы);
				
			КонецЦикла;	
		КонецЕсли;	
			
		Если ЗначениеЗаполнено(ВыборкаПоДокументам.Комментарий) Тогда 
			ЗаполнитьЗначенияСвойств(Подвал.Параметры, ВыборкаПоДокументам);
			ТабДокумент.Вывести(Подвал);
		КонецЕсли;	
		
	    ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаПоДокументам.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДокумент;
	
КонецФункции	

Функция ДанныеДляПечатиДокументаРасходыНаИсточникИнформацииОКандидатах(МассивСсылок)

	// Запрос по шапкам документов.
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РасходыНаИсточникИнформацииОКандидатах.Ссылка КАК Ссылка,
	|	РасходыНаИсточникИнформацииОКандидатах.Дата КАК Дата,
	|	РасходыНаИсточникИнформацииОКандидатах.Номер,
	|	РасходыНаИсточникИнформацииОКандидатах.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ПОДСТРОКА(РасходыНаИсточникИнформацииОКандидатах.Организация.НаименованиеПолное, 1, 10) <> """"
	|			ТОГДА РасходыНаИсточникИнформацииОКандидатах.Организация.НаименованиеПолное
	|		ИНАЧЕ РасходыНаИсточникИнформацииОКандидатах.Организация.Наименование
	|	КОНЕЦ КАК ПолноеНаименованиеОрганизации,
	|	РасходыНаИсточникИнформацииОКандидатах.НачалоПериодаОказанияУслуги,
	|	РасходыНаИсточникИнформацииОКандидатах.Сумма,
	|	РасходыНаИсточникИнформацииОКандидатах.Источник,
	|	РасходыНаИсточникИнформацииОКандидатах.Комментарий
	|ИЗ
	|	Документ.РасходыНаИсточникИнформацииОКандидатах КАК РасходыНаИсточникИнформацииОКандидатах
	|ГДЕ
	|	РасходыНаИсточникИнформацииОКандидатах.Ссылка В(&МассивСсылок)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ДанныеДляПечати = Новый Структура;
	ДанныеДляПечати.Вставить("РезультатПоШапке", РезультатЗапроса);
	
	// Запрос по табличным частям
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РасходыНаИсточникИнформацииОКандидатахВакансии.Ссылка КАК Ссылка,
	|	РасходыНаИсточникИнформацииОКандидатахВакансии.НомерСтроки КАК НомерСтроки,
	|	РасходыНаИсточникИнформацииОКандидатахВакансии.Вакансия КАК Вакансия
	|ИЗ
	|	Документ.РасходыНаИсточникИнформацииОКандидатах.Вакансии КАК РасходыНаИсточникИнформацииОКандидатахВакансии
	|ГДЕ
	|	РасходыНаИсточникИнформацииОКандидатахВакансии.Ссылка В(&МассивСсылок)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ДанныеДляПечати.Вставить("РезультатПоТабличнойЧасти", РезультатЗапроса);
	
	Возврат ДанныеДляПечати;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли


