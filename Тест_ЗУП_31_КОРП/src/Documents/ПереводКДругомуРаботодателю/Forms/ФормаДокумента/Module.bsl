
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	КадровыйУчетФормы.ФормаКадровогоДокументаПриСозданииНаСервере(ЭтаФорма);
	Если Не ЗначениеЗаполнено(Параметры.Ключ) Тогда
		
		// Создается новый документ, заполним организацию, ответственного, дату увольнения, статью ТК РФ.  
		ЗначенияДляЗаполнения = Новый Структура("Организация, Ответственный", 
		"Объект.Организация", "Объект.Ответственный");
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
		
		Объект.ДатаУвольнения = ТекущаяДатаСеанса();
		Объект.ДатаПриема = КонецДня(Объект.ДатаУвольнения) + 1;
		Объект.СтатьяТКРФ = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ОснованияУвольнения.Статья77п5");
		
		// Если передан сотрудник - заполним.
		Если Параметры.Свойство("Сотрудник") И ЗначениеЗаполнено(Параметры.Сотрудник) Тогда 
			Объект.Сотрудник = Параметры.Сотрудник;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Объект.Сотрудник) Тогда
			СотрудникПриИзмененииНаСервере();
		КонецЕсли; 
		
		ЗаполнитьДанныеФормыПоОрганизации();
		ЗаполнитьДанныеФормыПоНовойОрганизации();
		ПриПолученииДанныхНаСервере(Объект);
		
	КонецЕсли;
	
	КадровыйУчетРасширенный.УстановитьПараметрыВыбораНачисленийПоКатегории(
		ЭтаФорма,
		ОписаниеТаблицыНачислений(),
		КадровыйУчетРасширенный.ПараметрыВыбораКатегорииНачислений());
		
	ЗарплатаКадрыРасширенный.УстановитьВидимостьКомандПечатиМногофункциональногоДокумента(ЭтаФорма);
	
	Элементы.РазрядКатегория.Заголовок = РазрядыКатегорииДолжностей.ИнициализироватьЗаголовокФормыИРеквизитов("РеквизитРазрядКатегорияВКадровыхДокументах");	
	
	РазмерАвансаПоУмолчанию = РасчетЗарплатыФормы.РазмерАвансаВПроцентахПоУмолчанию(Объект.Организация);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужбаФормы");
		Модуль.УстановитьПараметрыВыбораСотрудников(ЭтаФорма, "Сотрудник");
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ПриПолученииДанныхНаСервере(ТекущийОбъект);
	
	ОбменДаннымиЗарплатаКадры.ПриЧтенииНаСервереДокумента(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если (ИмяСобытия = "ПослеЗаписиОбъектаСвязанногоСПереводомКДругомуРаботодателю" И (Источник <> Неопределено 
		И ТипЗнч(Источник) = Тип("УправляемаяФорма") И Источник.ВладелецФормы = ЭтаФорма)) Или Источник = ЭтаФорма  Тогда
		ОбновитьДанныеФормыПоРезультату(ЭтаФорма, Параметр);
		Записать(Новый Структура("РежимЗаписи ", РежимЗаписиДокумента.Запись));
		ОбновитьНадписиНаФорме(ЭтаФорма);
	КонецЕсли;
	
	Если ИмяСобытия = "ИзмененСтажФизическогоЛица" И Источник.ВладелецФормы = ЭтаФорма Тогда
		УстановитьПредставленияКомандВводаСтажей();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	РеквизитВДанные(ТекущийОбъект);
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	РасчетЗарплатыФормы.ЗапомнитьРазмерАвансаПоУмолчанию(
		ТекущийОбъект.Аванс, ТекущийОбъект.НоваяОрганизация, ТекущийОбъект.СпособРасчетаАванса);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ДанныеНачисленийВРеквизит(ТекущийОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Запись_ПереводКДругомуРаботодателю", ПараметрыЗаписи, Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТекущаяОрганизацияПриИзменении(Элемент)
	
	ТекущаяОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	СотрудникПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДатаУвольненияПриИзменении(Элемент)
	ДатаУвольненияПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура НоваяОрганизацияПриИзменении(Элемент)
	
	НоваяОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	
	ПодразделениеПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПозицияШтатногоРасписанияПриИзменении(Элемент)
	
	ПозицияШтатногоРасписанияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьПриИзменении(Элемент)
	
	ДолжностьПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриемаПриИзменении(Элемент)
	ДатаПриемаПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СпособРасчетаАвансаПриИзменении(Элемент)
	
	Если Объект.СпособРасчетаАванса = ПредопределенноеЗначение("Перечисление.СпособыРасчетаАванса.ПроцентомОтТарифа") Тогда
		Объект.Аванс = РазмерАвансаПоУмолчанию;
	Иначе
		Объект.Аванс = 0;
	КонецЕсли; 
	
	УстановитьПоказРазмераАванса(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоСтавокПредставлениеНажатие(Элемент, СтандартнаяОбработка)

	Оповещение = Новый ОписаниеОповещения("ПриИзмененииКоличестваСтавок", ЭтотОбъект);
	КадровыйУчетРасширенныйКлиент.ИзменитьКоличествоСтавок(ЭтаФорма, "Объект.КоличествоСтавок", СтандартнаяОбработка, "КоличествоСтавокПредставление", Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПКУПриИзменении(Элемент)
	
	Если ПолучитьФункциональнуюОпциюФормы("ИспользоватьТарифныеСеткиПриРасчетеЗарплаты") Тогда
		ПКУПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазрядКатегорияПриИзменении(Элемент)
	
	Если ПолучитьФункциональнуюОпциюФормы("ИспользоватьТарифныеСеткиПриРасчетеЗарплаты") Тогда
		РазрядКатегорияПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОЙ ЧАСТИ НАЧИСЛЕНИЯ

&НаКлиенте
Процедура НачисленияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ЗарплатаКадрыРасширенныйКлиент.РедактированиеСоставаНачисленийВыбор(
		ЭтаФорма, Элемент, Поле, СтандартнаяОбработка, 1, Объект.Сотрудник, Объект.ДатаПриема);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ЗарплатаКадрыРасширенныйКлиент.РедактированиеСоставаНачисленийПриНачалеРедактирования(ЭтаФорма, "Начисления", 1,  Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияНачислениеПриИзменении(Элемент)
	
	ОписаниеТаблицыВидовРасчета = ОписаниеТаблицыНачислений();
	
	ДополнительныеПараметры = ЗарплатаКадрыРасширенныйКлиентСервер.ПараметрыЗаполненияЗначенийПоказателейТарифныхСеток();
	ДополнительныеПараметры.ДатаСведений = Объект.ДатаПриема;
	ДополнительныеПараметры.ТарифнаяСетка = ТарифнаяСетка;
	ДополнительныеПараметры.ТарифнаяСеткаНадбавки = ТарифнаяСеткаНадбавки;
	ДополнительныеПараметры.РазрядКатегория = РазрядКатегория;
	ДополнительныеПараметры.РазрядКатегорияНадбавки = Объект.РазрядКатегория;
	
	ЗарплатаКадрыРасширенныйКлиент.ВводПлановыхНачисленийНачислениеПриИзменении(ЭтаФорма, ОписаниеТаблицыВидовРасчета, 1, Объект.Сотрудник, ДополнительныеПараметры);
		
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗначениеПоказателяПриИзмененииНачисления(Элемент)
	
	ОписаниеТаблицыНачислений = ОписаниеТаблицыНачислений();
	ЗарплатаКадрыРасширенныйКлиентСервер.УстановитьКомментарииДействийСНачислением(Элементы.Начисления.ТекущиеДанные,
		ЗарплатаКадрыРасширенныйКлиентСервер.МаксимальноеКоличествоПоказателейПоОписаниюТаблицы(ЭтаФорма, ОписаниеТаблицыНачислений,, 1), 1, ОписаниеТаблицыНачислений, Объект.ДатаПриема);		
	
КонецПроцедуры	

#КонецОбласти


#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ОбновитьСсылкиНаРезультаты(Команда)
	ОбновитьСсылкиНаРезультатыНаСервере();
	ОбновитьНадписиНаФорме(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура НадписьУвольнениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;	
	
	СсылкаУвольнение = СтрокаПоТипуЗначения(ЭтаФорма, Тип("ДокументСсылка.Увольнение")).Результат;
	
	Если ЗначениеЗаполнено(СсылкаУвольнение) Тогда
		
		ОткрытьФорму("Документ.Увольнение.Форма.ФормаДокумента", Новый Структура("Ключ", СсылкаУвольнение), ЭтаФорма);
		
	Иначе		
		
		Если МожноСоздатьУвольнение() Тогда
			СоздатьУвольнение();
		Иначе
			СообщитьОНевозможностиСозданияУвольнения();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьИсходящаяСправкаОЗаработкеДляРасчетаПособийНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;	
	
	СсылкаИсходящаяСправка = СтрокаПоТипуЗначения(ЭтаФорма, Тип("ДокументСсылка.ИсходящаяСправкаОЗаработкеДляРасчетаПособий")).Результат;
	
	Если ЗначениеЗаполнено(СсылкаИсходящаяСправка) Тогда
		
		ОткрытьФорму("Документ.ИсходящаяСправкаОЗаработкеДляРасчетаПособий.Форма.ФормаДокумента", Новый Структура("Ключ", СсылкаИсходящаяСправка), ЭтаФорма);
		
	Иначе
		
		Если МожноСоздатьИсходящуюСправкуОЗаработке() Тогда
			СоздатьИсходящуюСправкуОЗаработке();
		Иначе
			СообщитьОНевозможностиСозданияИсходящейСправкиОЗаработке();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьПриемНаРаботуНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;	
	
	СсылкаПриемНаРаботу = СтрокаПоТипуЗначения(ЭтаФорма, Тип("ДокументСсылка.ПриемНаРаботу")).Результат;
	
	Если ЗначениеЗаполнено(СсылкаПриемНаРаботу) Тогда
		ОткрытьФорму("Документ.ПриемНаРаботу.Форма.ФормаДокумента", Новый Структура("Ключ", СсылкаПриемНаРаботу), ЭтаФорма);		
	Иначе
		Если МожноСоздатьПриемНаРаботу() Тогда
			СоздатьПриемНаРаботу();
		Иначе
			СообщитьОНевозможностиСозданияПриемаНаРаботу();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьВходящаяСправкаОЗаработкеДляРасчетаПособийНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;	
	
	СсылкаВходящаяСправка = СтрокаПоТипуЗначения(ЭтаФорма, Тип("ДокументСсылка.ВходящаяСправкаОЗаработкеДляРасчетаПособий")).Результат;
		
	Если ЗначениеЗаполнено(СсылкаВходящаяСправка) Тогда
		
		ОткрытьФорму("Документ.ВходящаяСправкаОЗаработкеДляРасчетаПособий.Форма.ФормаДокумента", Новый Структура("Ключ", СсылкаВходящаяСправка), ЭтаФорма);
	Иначе
		
		Если МожноСоздатьВходящуюСправкуОЗаработке() Тогда
			СоздатьВходящуюСправкуОЗаработке();
		Иначе
			СообщитьОНевозможностиСозданияВходящейСправкиОЗаработке();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьВводНачальныхОстатковОтпускаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;	
	
	СсылкаВводНачальныхОстатковОтпусков = СтрокаПоТипуЗначения(ЭтаФорма, Тип("ДокументСсылка.ВводНачальныхОстатковОтпусков")).Результат;

	Если ЗначениеЗаполнено(СсылкаВводНачальныхОстатковОтпусков) Тогда
		ОткрытьФорму("Документ.ВводНачальныхОстатковОтпусков.Форма.ФормаДокумента", Новый Структура("Ключ", СсылкаВводНачальныхОстатковОтпусков), ЭтаФорма);
	Иначе

		Если МожноСоздатьВводНачальныхОстатковОтпусков() Тогда
			СоздатьВводНачальныхОстатковОтпусков();
		Иначе
			СообщитьОНевозможностиСозданияВводаНачальныхОстатковОтпусков();
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриПолученииДанныхНаСервере(ТекущийОбъект)
	
	УстановитьОграниченияНаУровнеЗаписей();
	
	ДоступноДобавлениеВходящейСправкиОЗаработкеДляРасчетаПособий = 
	Пользователи.РолиДоступны("ПолныеПрава,ДобавлениеИзменениеПособийФСС");
	
	ДоступноДобавлениеИсходящейСправкиОЗаработкеДляРасчетаПособий = 
	Пользователи.РолиДоступны("ПолныеПрава,ДобавлениеИзменениеИсходящейСправкиОЗаработке,ДобавлениеИзменениеНачисленнойЗарплатыРасширенная");
	
	ДоступноДобавлениеИзменениеКадровыхПриказов = 
	Пользователи.РолиДоступны("ПолныеПрава,ДобавлениеИзменениеКадровогоСостоянияРасширенная,ДобавлениеИзменениеДанныхДляНачисленияЗарплатыРасширенная");
	
	ПараметрыФО = Новый Структура("Организация", Объект.Организация);
	УстановитьПараметрыФункциональныхОпцийФормы(ПараметрыФО);
	
	ДоступенРасчетЗарплаты 		= ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная") И Пользователи.РолиДоступны("ЧтениеНачисленнойЗарплатыРасширенная, ДобавлениеИзменениеНачисленнойЗарплатыРасширенная");
	ДоступныПлановыеНачисления 	= ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная") И Пользователи.РолиДоступны("ЧтениеДанныхДляНачисленияЗарплатыРасширенная, ДобавлениеИзменениеДанныхДляНачисленияЗарплатыРасширенная");
	
	ИспользоватьШтатноеРасписание =  ПолучитьФункциональнуюОпцию("ИспользоватьШтатноеРасписание");
	
	КоличествоСтавокПредставление = КадровыйУчетРасширенныйКлиентСервер.ПредставлениеКоличестваСтавок(Объект.КоличествоСтавок);
	
	ДанныеДолжности(Истина);
	
	ПрочитатьТекущиеКадровыеДанныеСотрудника();
	ПрочитатьТарифнуюСетку();
	УстановитьТипОснованияУвольнения();
	
	ОбновитьСсылкиНаРезультатыНаСервере();
	ДополнитьФорму();
	
	Если ДоступныПлановыеНачисления Тогда
		ДанныеНачисленийВРеквизит(ТекущийОбъект);
		УстановитьПоказРазмераАванса(ЭтаФорма);
	КонецЕсли;
	
	ОбновитьНадписиНаФорме(ЭтаФорма);
	
	УстановитьВидимостьРасчетныхПолей();
	
	РазрядыКатегорииДолжностей.УстановитьСвязиПараметровВыбораРазрядаКадровогоПриказа(ЭтотОбъект);
	
	УстановитьДоступныеОрганизации();
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьФорму()
	Если ДоступныПлановыеНачисления Тогда
		ОписаниеТаблицыВидовРасчета = ОписаниеТаблицыНачислений();
		ЗарплатаКадрыРасширенный.РедактированиеСоставаНачисленийДополнитьФорму(ЭтаФорма, ОписаниеТаблицыВидовРасчета, "Начисления", 3);
	КонецЕсли;
КонецПроцедуры	

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеТаблицыНачислений()
	
	ОписаниеТаблицыВидовРасчета = РасчетЗарплатыРасширенныйКлиентСервер.ОписаниеТаблицыРасчета();
	ОписаниеТаблицыВидовРасчета.Вставить("ЗапретитьИзменениеПоказателяТарифнойСетки", Истина);
	ОписаниеТаблицыВидовРасчета.Вставить("ЗапретитьИзменениеПоказателяТарифнойСетки", Истина);
	Возврат ОписаниеТаблицыВидовРасчета;
	
КонецФункции

&НаСервере
Процедура ДанныеНачисленийВРеквизит(ТекущийОбъект)
	
	Если ДоступныПлановыеНачисления Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		ТаблицаСотрудников = ТаблицаСотрудников(Объект.ДатаУвольнения - 1);
		
		Если ТаблицаСотрудников.Количество() > 0 Тогда
			
			ОписаниеТаблицыВидовРасчета = ОписаниеТаблицыНачислений();
			ЗарплатаКадрыРасширенный.РедактированиеСоставаНачисленийДанныеВРеквизит(ТекущийОбъект.Ссылка, ТаблицаСотрудников, ЭтаФорма, ОписаниеТаблицыВидовРасчета, 1, , Объект.ДатаПриема);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РеквизитВДанные(ТекущийОбъект)
	Если ДоступныПлановыеНачисления Тогда
		ОписаниеТаблицыВидовРасчета = ОписаниеТаблицыНачислений();
		ЗарплатаКадрыРасширенный.ВводНачисленийРеквизитВДанные(ЭтаФорма, ТекущийОбъект, ОписаниеТаблицыВидовРасчета, 1);
	КонецЕсли;
КонецПроцедуры	

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьНадписиНаФорме(Форма)
	
	Если Форма.ДоступноДобавлениеИзменениеКадровыхПриказов Тогда
		Форма.НадписьУвольнение = Форма.Элементы.НадписьУвольнение.Заголовок;
		Форма.НадписьПриемНаРаботу = Форма.Элементы.НадписьПриемНаРаботу.Заголовок;
		Форма.НадписьВводНачальныхОстатковОтпуска = Форма.Элементы.НадписьВводНачальныхОстатковОтпуска.Заголовок;
		
	Иначе
		
		Форма.НадписьУвольнение = НСТр("ru='Приказ об увольнении еще не вводился'");
		Форма.НадписьПриемНаРаботу = НСТр("ru='Приказ о приеме еще не вводился'");
		Форма.НадписьВводНачальныхОстатковОтпуска = НСТр("ru='Начальные остатки отпуска не вводились'");
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "НадписьУвольнение", "Гиперссылка", Форма.ДоступноДобавлениеИзменениеКадровыхПриказов);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "НадписьПриемНаРаботу", "Гиперссылка", Форма.ДоступноДобавлениеИзменениеКадровыхПриказов);	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "НадписьВводНачальныхОстатковОтпуска", "Гиперссылка", Форма.ДоступноДобавлениеИзменениеКадровыхПриказов);
	
	Если Форма.ДоступенРасчетЗарплаты Тогда
		Если Форма.ДоступноДобавлениеИсходящейСправкиОЗаработкеДляРасчетаПособий Тогда
			Форма.НадписьИсходящаяСправкаОЗаработкеДляРасчетаПособий = Форма.Элементы.НадписьИсходящаяСправкаОЗаработкеДляРасчетаПособий.Заголовок;
		Иначе
			Форма.НадписьИсходящаяСправкаОЗаработкеДляРасчетаПособий = НСТр("ru='Исходящая справка о заработке еще не вводилась'");
			
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			"НадписьИсходящаяСправкаОЗаработкеДляРасчетаПособий",
			"Гиперссылка",
			Ложь);
			
		КонецЕсли;
		
		Если Форма.ДоступноДобавлениеВходящейСправкиОЗаработкеДляРасчетаПособий Тогда
			Форма.НадписьВходящаяСправкаОЗаработкеДляРасчетаПособий = Форма.Элементы.НадписьВходящаяСправкаОЗаработкеДляРасчетаПособий.Заголовок;
		Иначе
			Форма.НадписьВходящаяСправкаОЗаработкеДляРасчетаПособий = НСТр("ru='Входящая справка о заработке еще не вводилась'");
			
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			"НадписьВходящаяСправкаОЗаработкеДляРасчетаПособий",
			"Гиперссылка",
			Ложь);
		КонецЕсли;
	КонецЕсли;
	
	Для каждого Строка Из Форма.ДействияПриПереводеСотрудника Цикл
		ОбновитьНадписьНаФормеПоСтрокеТЧДействияПриПереводеСотрудника(Форма, Строка)
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДанныеФормыПоРезультату(Форма, Результат, ИмяРеквизитаСсылки = "Результат")
	Строка = СтрокаПоТипуЗначения(Форма, ТипЗнч(Результат[ИмяРеквизитаСсылки]));
	
	Если Строка = Неопределено Тогда
	   Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Строка, Результат);	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьНадписьНаФормеПоСтрокеТЧДействияПриПереводеСотрудника(Форма, Строка)
	
	ТипРезультата = ТипЗнч(Строка.Результат);
	
	Если ТипРезультата = Тип("ДокументСсылка.Увольнение") Тогда
		УстановитьНадписьСтроки(Форма.НадписьУвольнение, Нстр("ru = 'Приказ об увольнении'"), Строка, ТипРезультата);
		УстановитьКартинкуСтроки(Форма.НадписьУвольнениеКартинка, Строка, ТипРезультата);
		УстановитьОтветственногоСтроки(Форма.НадписьУвольнениеОтветственный, Строка);
	ИначеЕсли ТипРезультата = Тип("ДокументСсылка.ПриемНаРаботу") Тогда
		УстановитьНадписьСтроки(Форма.НадписьПриемНаРаботу, Нстр("ru = 'Приказ о приеме на работу'"), Строка, ТипРезультата);
		УстановитьКартинкуСтроки(Форма.НадписьПриемНаРаботуКартинка, Строка, ТипРезультата);
		УстановитьОтветственногоСтроки(Форма.НадписьПриемНаРаботуОтветственный, Строка);
	ИначеЕсли ТипРезультата = Тип("ДокументСсылка.ВводНачальныхОстатковОтпусков") Тогда
		УстановитьНадписьСтроки(Форма.НадписьВводНачальныхОстатковОтпуска, Нстр("ru = 'Документ ввода остатков отпуска'"), Строка, ТипРезультата);
		УстановитьКартинкуСтроки(Форма.НадписьВводНачальныхОстатковОтпускаКартинка, Строка, ТипРезультата);
		УстановитьОтветственногоСтроки(Форма.НадписьВводНачальныхОстатковОтпускаОтветственный, Строка);
	ИначеЕсли Форма.ДоступенРасчетЗарплаты И Форма.ДоступноДобавлениеИсходящейСправкиОЗаработкеДляРасчетаПособий 
		И ТипРезультата = Тип("ДокументСсылка.ИсходящаяСправкаОЗаработкеДляРасчетаПособий") Тогда
		УстановитьНадписьСтроки(Форма.НадписьИсходящаяСправкаОЗаработкеДляРасчетаПособий, Нстр("ru = 'Исходящая справка о заработке'"), Строка, ТипРезультата);
		УстановитьКартинкуСтроки(Форма.НадписьИсходящаяСправкаОЗаработкеДляРасчетаПособийКартинка, Строка, ТипРезультата);
		УстановитьОтветственногоСтроки(Форма.НадписьИсходящаяСправкаОЗаработкеДляРасчетаПособийОтветственный, Строка);
	ИначеЕсли Форма.ДоступенРасчетЗарплаты И Форма.ДоступноДобавлениеВходящейСправкиОЗаработкеДляРасчетаПособий 
		И ТипРезультата = Тип("ДокументСсылка.ВходящаяСправкаОЗаработкеДляРасчетаПособий") Тогда 
		УстановитьНадписьСтроки(Форма.НадписьВходящаяСправкаОЗаработкеДляРасчетаПособий, Нстр("ru = 'Входящая справка о заработке'"), Строка, ТипРезультата);
		УстановитьКартинкуСтроки(Форма.НадписьВходящаяСправкаОЗаработкеДляРасчетаПособийКартинка, Строка, ТипРезультата);
		УстановитьОтветственногоСтроки(Форма.НадписьВходящаяСправкаОЗаработкеДляРасчетаПособийОтветственный, Строка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьКартинкуСтроки(Картинка, Строка, ТипРезультата)
	
	Если НЕ ЗначениеЗаполнено(Строка.Результат) Тогда
		Картинка = 0;
	ИначеЕсли Строка.Проведен Или ТипРезультата = Тип("СправочникСсылка.Сотрудники") 
		Или ТипРезультата = Тип("ДокументСсылка.ИсходящаяСправкаОЗаработкеДляРасчетаПособий") Тогда
		Картинка =  1;
	ИначеЕсли НЕ Строка.Проведен И НЕ Строка.ПомеченНаУдаление Тогда
		Картинка =  3;
	ИначеЕсли Строка.ПомеченНаУдаление Тогда
		Картинка =  2;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьНадписьСтроки(Надпись, НачалоНадписи, Строка, Тип)
	Если ЗначениеЗаполнено(Строка.Результат) Тогда
		ПредставлениеРезультата = ПредставлениеРезультата(Строка);
		ДополнениеПредставления = ДополнениеПредставленияРезультата(Строка, Тип);
		Надпись = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 %2 %3", НачалоНадписи, ПредставлениеРезультата, ДополнениеПредставления);	
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтветственногоСтроки(Ответственный, Строка)
	Если ЗначениеЗаполнено(Строка.Ответственный) Тогда
		Ответственный = Нстр("ru = 'Отв'") + ".: " + Строка.Ответственный;
	Иначе
		Ответственный = "";
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеРезультата(Строка)
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("(%1 от %2)", Строка.НомерДокумента, Формат(Строка.ДатаДокумента, "ДФ='dd MMM yyyy'"))	
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ДополнениеПредставленияРезультата(Строка, ТипРезультата)
	
	Если НЕ ЗначениеЗаполнено(Строка.Результат) 
		Или (Строка.Проведен Или ТипРезультата = Тип("СправочникСсылка.Сотрудники") 
				Или ТипРезультата = Тип("ДокументСсылка.ИсходящаяСправкаОЗаработкеДляРасчетаПособий")) Тогда
		ДополнительноеПредставление =  "";
	ИначеЕсли НЕ Строка.Проведен И НЕ Строка.ПомеченНаУдаление Тогда
		ДополнительноеПредставление = Нстр("ru = '(не проведен)'");
	ИначеЕсли Строка.ПомеченНаУдаление Тогда
		ДополнительноеПредставление = Нстр("ru = '(помечен на удаление)'");
	КонецЕсли;
	
	Возврат ДополнительноеПредставление	
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтрокаПоТипуЗначения(Форма, Тип)
	
	Для каждого Строка Из Форма.ДействияПриПереводеСотрудника Цикл
		ТипРезультата = ТипЗнч(Строка.Результат);
		Если ТипРезультата = Тип Тогда
			Возврат Строка
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции


#Область СоздатьУвольнение

&НаКлиенте
Процедура СоздатьУвольнение()
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполненияУвольнения());
	
	ОткрытьФорму("Документ.Увольнение.Форма.ФормаДокумента", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Функция МожноСоздатьУвольнение()
	
	Возврат ЗначениеЗаполнено(Объект.Сотрудник) И
			ЗначениеЗаполнено(Объект.Организация) И
			ЗначениеЗаполнено(Объект.ДатаУвольнения) И
			ЗначениеЗаполнено(Объект.СтатьяТКРФ);
			
КонецФункции

&НаКлиенте
Процедура СообщитьОНевозможностиСозданияУвольнения()
	ТекстСообщения = Нстр("ru = 'Перед созданием увольнения необходимо заполнить организацию, сотрудника, дату увольнения и статью ТК РФ.'");
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
КонецПроцедуры

&НаКлиенте
Функция ЗначенияЗаполненияУвольнения()
	
	ЗначенияЗаполненияУвольнения = Новый Структура;
	
	ЗначенияЗаполненияУвольнения.Вставить("Организация", Объект.Организация);
	ЗначенияЗаполненияУвольнения.Вставить("Сотрудник", Объект.Сотрудник);
	ЗначенияЗаполненияУвольнения.Вставить("ДатаУвольнения", Объект.ДатаУвольнения);
	ЗначенияЗаполненияУвольнения.Вставить("СтатьяТКРФ", Объект.СтатьяТКРФ);
	ОснованиеУвольнения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Перевод к другому работодателю %1'"), ?(ЗначениеЗаполнено(Объект.НоваяОрганизация), "(" + Объект.НоваяОрганизация+ ")" , ""));
	ЗначенияЗаполненияУвольнения.Вставить("ОснованиеУвольнения", ОснованиеУвольнения);	
	
	Возврат ЗначенияЗаполненияУвольнения;
	
КонецФункции 

#КонецОбласти 


#Область СоздатьИсходящуюСправкуОЗаработке

&НаКлиенте
Процедура СоздатьИсходящуюСправкуОЗаработке()
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполненияИсходящейСправкиОЗаработке());
	
	ОткрытьФорму("Документ.ИсходящаяСправкаОЗаработкеДляРасчетаПособий.Форма.ФормаДокумента", ПараметрыФормы, ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Функция МожноСоздатьИсходящуюСправкуОЗаработке()
	
	Возврат ЗначениеЗаполнено(Объект.Сотрудник) И
			ЗначениеЗаполнено(Объект.Организация) И
			ЗначениеЗаполнено(Объект.ДатаУвольнения);
			
КонецФункции

&НаКлиенте
Процедура СообщитьОНевозможностиСозданияИсходящейСправкиОЗаработке()
	ТекстСообщения = Нстр("ru = 'Перед созданием исходящей справки о заработке необходимо заполнить организацию, сотрудника и дату увольнения'");
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
КонецПроцедуры

&НаКлиенте
Функция ЗначенияЗаполненияИсходящейСправкиОЗаработке()
	
	ЗначенияЗаполненияИсходящейСправки = Новый Структура;
	ЗначенияЗаполненияИсходящейСправки.Вставить("Организация", Объект.Организация);
	ЗначенияЗаполненияИсходящейСправки.Вставить("Сотрудник", Объект.Сотрудник);	
	ЗначенияЗаполненияИсходящейСправки.Вставить("ГодС", Год(Объект.ДатаУвольнения)-2);
	ЗначенияЗаполненияИсходящейСправки.Вставить("ГодПо", Год(Объект.ДатаУвольнения));
	
	Возврат ЗначенияЗаполненияИсходящейСправки;
	
КонецФункции 

#КонецОбласти 


#Область СоздатьПриемНаРаботу

&НаКлиенте
Процедура СоздатьПриемНаРаботу()
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполненияПриемаНаРаботу());
	
	ОткрытьФорму("Документ.ПриемНаРаботу.Форма.ФормаДокумента", ПараметрыФормы, ЭтаФорма);		
	
КонецПроцедуры

&НаКлиенте
Функция МожноСоздатьПриемНаРаботу()
	
	Возврат ЗначениеЗаполнено(Объект.Сотрудник)
			И ЗначениеЗаполнено(Объект.НоваяОрганизация)
			И ЗначениеЗаполнено(Объект.Подразделение)
			И ЗначениеЗаполнено(Объект.Должность)
			И ЗначениеЗаполнено(Объект.ДатаПриема)
			И ЗначениеЗаполнено(Объект.КоличествоСтавок);
			
КонецФункции

&НаКлиенте
Процедура СообщитьОНевозможностиСозданияПриемаНаРаботу()
	ТекстСообщения = Нстр("ru = 'Перед созданием приема на работу необходимо заполнить сотрудника, новую организацию, подразделение, должность, дату увольнения, дату приема, количество ставок и график работы.'");
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
КонецПроцедуры

&НаСервере
Функция ЗначенияЗаполненияПриемаНаРаботу()
	
	ЗначенияЗаполненияПриемаНаРаботу = Новый Структура;
	ЗначенияЗаполненияПриемаНаРаботу.Вставить("Организация", 					Объект.НоваяОрганизация);
	ЗначенияЗаполненияПриемаНаРаботу.Вставить("ДатаПриема", 					Объект.ДатаПриема);
	ЗначенияЗаполненияПриемаНаРаботу.Вставить("ФизическоеЛицо", 				Объект.ФизическоеЛицо);
	ЗначенияЗаполненияПриемаНаРаботу.Вставить("Подразделение", 					Объект.Подразделение);
	ЗначенияЗаполненияПриемаНаРаботу.Вставить("Должность", 						Объект.Должность);
	ЗначенияЗаполненияПриемаНаРаботу.Вставить("ДолжностьПоШтатномуРасписанию", 	Объект.ДолжностьПоШтатномуРасписанию);
	ЗначенияЗаполненияПриемаНаРаботу.Вставить("КоличествоСтавок", 				Объект.КоличествоСтавок);
	ЗначенияЗаполненияПриемаНаРаботу.Вставить("ГрафикРаботы", 					Объект.ГрафикРаботы);
	ЗначенияЗаполненияПриемаНаРаботу.Вставить("ПКУ", 							Объект.ПКУ);
	ЗначенияЗаполненияПриемаНаРаботу.Вставить("РазрядКатегория", 				Объект.РазрядКатегория);
	ЗначенияЗаполненияПриемаНаРаботу.Вставить("СпособРасчетаАванса", 			Объект.СпособРасчетаАванса);
	ЗначенияЗаполненияПриемаНаРаботу.Вставить("Аванс", 							Объект.Аванс);
	ЗначенияЗаполненияПриемаНаРаботу.Вставить("Начисления", 					Объект.Начисления);
	ЗначенияЗаполненияПриемаНаРаботу.Вставить("Показатели", 					Объект.Показатели);
	
	ЗначенияЗаполненияПриемаНаРаботу.Вставить("Сотрудник", 						СотрудникНовойОрганизации());
	
	Возврат ЗначенияЗаполненияПриемаНаРаботу;
	
КонецФункции 

&НаСервере
Функция СотрудникНовойОрганизации()
	
	Сотрудник = СтрокаПоТипуЗначения(ЭтаФорма, Тип("СправочникСсылка.Сотрудники")).Результат;
	
	Если НЕ ЗначениеЗаполнено(Сотрудник) Тогда
		СоздатьСотрудника();
		Сотрудник = СтрокаПоТипуЗначения(ЭтаФорма, Тип("СправочникСсылка.Сотрудники")).Результат;
	КонецЕсли;
	
	Возврат Сотрудник;
	
КонецФункции

&НаСервере
Процедура СоздатьСотрудника()
	
	СписокФизическихЛиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.ФизическоеЛицо);
	ГоловнаяОрганизация = ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(Объект.НоваяОрганизация);
	
	Сотрудник = КадровыйУчет.НеоформленныеСотрудникиФизическихЛиц(СписокФизическихЛиц, ГоловнаяОрганизация, Ложь).Получить(Объект.ФизическоеЛицо).ПолучитьОбъект(); 
	
	КадровыеДанные = КадровыйУчет.КадровыеДанныеФизическихЛиц(Ложь, СписокФизическихЛиц, "Фамилия,Имя,Отчество", ТекущаяДатаСеанса()); 
	
	Если КадровыеДанные.Количество() > 0 Тогда
		КадровыеДанные 	= КадровыеДанные[0];
		Сотрудник.Наименование = КадровыйУчетКлиентСервер.ПолноеНаименованиеСотрудника(КадровыеДанные.Фамилия, КадровыеДанные.Имя, КадровыеДанные.Отчество, "");
	Иначе
		Сотрудник.Наименование = Объект.ФизическоеЛицо.Наименование;
	КонецЕсли;
	
	Сотрудник.Записать();
	
	Если ДоступныПлановыеНачисления Тогда
		УстановитьПривилегированныйРежим(Истина);
		ДоходыНовогоСотрудникаСПредыдущегоМестаРаботы(Сотрудник.Ссылка, Объект.Организация, Объект.ДатаУвольнения);
		ОтражениеЗарплатыНовогоСотрудникаВБухУчете(Сотрудник.Ссылка);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Строка = СтрокаПоТипуЗначения(ЭтаФорма, ТипЗнч(Сотрудник.Ссылка));
	Строка.Результат = Сотрудник.Ссылка;
	Строка.Ответственный = "";
КонецПроцедуры	

&НаСервере
Процедура ДоходыНовогоСотрудникаСПредыдущегоМестаРаботы(НовыйСотрудник, Организация, ДатаПолученияДоходов)
	
	ДоходСНарастающимИтогом = 0;
	
	НаборЗаписей = РегистрыСведений.ДоходыПредыдущегоМестаРаботыНДФЛ.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ФизическоеЛицо.Значение      = НовыйСотрудник.ФизическоеЛицо;
	НаборЗаписей.Отбор.ФизическоеЛицо.ВидСравнения  = ВидСравнения.Равно;
	НаборЗаписей.Отбор.ФизическоеЛицо.Использование = Истина;
	НаборЗаписей.Отбор.ГоловнаяОрганизация.Значение     = НовыйСотрудник.ГоловнаяОрганизация;
	НаборЗаписей.Отбор.ГоловнаяОрганизация.ВидСравнения = ВидСравнения.Равно;                   
	НаборЗаписей.Отбор.ГоловнаяОрганизация.Использование = Истина;
	
	Сотрудники = Новый Массив;	
	Сотрудники.Добавить(НовыйСотрудник.ФизическоеЛицо);
	
	ДанныеДоходахНалогахВычетах = УчетНДФЛ.ДанныеОДоходахНалогахВычетах(Сотрудники, ГОД(ДатаПолученияДоходов), НовыйСотрудник.ГоловнаяОрганизация, , ДатаПолученияДоходов);
	
	Доходы = ДанныеДоходахНалогахВычетах.Доходы;
	Доходы.Свернуть("МесяцНалоговогоПериода","СуммаДохода");
	Доходы.Сортировать("МесяцНалоговогоПериода");
	
	Для Каждого СтрокаДохода Из Доходы Цикл
		
		ДоходСНарастающимИтогом = ДоходСНарастающимИтогом + СтрокаДохода.СуммаДохода;
		ЗаписьОДоходах  = НаборЗаписей.Добавить();
		ЗаписьОДоходах.МесяцНалоговогоПериода = Дата(Год(ДатаПолученияДоходов), СтрокаДохода.МесяцНалоговогоПериода, 1);
		ЗаписьОДоходах.ФизическоеЛицо         = НовыйСотрудник.ФизическоеЛицо;
		ЗаписьОДоходах.ГоловнаяОрганизация    = НовыйСотрудник.ГоловнаяОрганизация;
		ЗаписьОДоходах.Размер                 = ДоходСНарастающимИтогом;
		
	КонецЦикла;
	
	НаборЗаписей.Записать(истина);
КонецПроцедуры 

#КонецОбласти 


#Область СоздатьВходящуюСправкуОЗаработке

&НаКлиенте
Процедура СоздатьВходящуюСправкуОЗаработке()
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполненияВходящейСправкиОЗаработке());
	
	ОткрытьФорму("Документ.ВходящаяСправкаОЗаработкеДляРасчетаПособий.Форма.ФормаДокумента", ПараметрыФормы, ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Функция МожноСоздатьВходящуюСправкуОЗаработке()
	
	Возврат ЗначениеЗаполнено(Объект.НоваяОрганизация) И
			ЗначениеЗаполнено(СтрокаПоТипуЗначения(ЭтаФорма, Тип("СправочникСсылка.Сотрудники")).Результат);
			
КонецФункции

&НаКлиенте
Процедура СообщитьОНевозможностиСозданияВходящейСправкиОЗаработке()
	ТекстСообщения = Нстр("ru = 'Перед созданием входящей справки о заработке необходимо заполнить новую организацию и ввести документ приема на новое место работы.'");
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
КонецПроцедуры

&НаКлиенте
Функция ЗначенияЗаполненияВходящейСправкиОЗаработке()
	
	ЗначенияЗаполненияВходящейСправки = Новый Структура;
	ЗначенияЗаполненияВходящейСправки.Вставить("Организация", Объект.НоваяОрганизация);
	ЗначенияЗаполненияВходящейСправки.Вставить("Страхователь", РаботодательПоОрганизации(Объект.Организация));
	
	Сотрудник = СтрокаПоТипуЗначения(ЭтаФорма, Тип("СправочникСсылка.Сотрудники")).Результат;
	ЗначенияЗаполненияВходящейСправки.Вставить("Сотрудник", Сотрудник);
	
	ДополнитьДаннымиИсходящейСправкиОЗаработке(ЗначенияЗаполненияВходящейСправки);
	
	Возврат ЗначенияЗаполненияВходящейСправки;
	
КонецФункции 

&НаСервереБезКонтекста
Функция РаботодательПоОрганизации(Организация)
	Возврат Справочники.Работодатели.РаботодательПоОрганизации(Организация);
КонецФункции

&НаКлиенте
Процедура ДополнитьДаннымиИсходящейСправкиОЗаработке(ЗначенияЗаполненияВходящейСправки)
	
	ИсходящаяСправка = СтрокаПоТипуЗначения(ЭтаФорма, Тип("ДокументСсылка.ИсходящаяСправкаОЗаработкеДляРасчетаПособий")).Результат;
	
	Если НЕ ИсходящаяСправка.Пустая() Тогда
		
		ДанныеИсходящейСправки = ДанныеИсходящейСправкиОЗаработке(ИсходящаяСправка);
		
		Если ЗначениеЗаполнено(ДанныеИсходящейСправки.АдресДанныхОЗаработкеВХранилище) Тогда
			ЗначенияЗаполненияВходящейСправки.Вставить("АдресДанныхОЗаработкеВХранилище", ДанныеИсходящейСправки.АдресДанныхОЗаработкеВХранилище);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДанныеИсходящейСправки.ПериодРаботыС) Тогда
			ЗначенияЗаполненияВходящейСправки.Вставить("ПериодРаботыС", ДанныеИсходящейСправки.ПериодРаботыС);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДанныеИсходящейСправки.ПериодРаботыПо) Тогда
			ЗначенияЗаполненияВходящейСправки.Вставить("ПериодРаботыПо", ДанныеИсходящейСправки.ПериодРаботыПо);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДанныеИсходящейСправкиОЗаработке(Ссылка)
	
	ДанныеВходящейСправки = Новый Структура("АдресДанныхОЗаработкеВХранилище,ПериодРаботыС,ПериодРаботыПо");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИсходящаяСправка.РасчетныйГод,
	|	ИсходящаяСправка.Заработок
	|ИЗ
	|	Документ.ИсходящаяСправкаОЗаработкеДляРасчетаПособий.ДанныеОЗаработке КАК ИсходящаяСправка
	|ГДЕ
	|	ИсходящаяСправка.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ИсходящаяСправкаОЗаработкеДляРасчетаПособийПериодыРаботы.ПериодРаботыС) КАК ПериодРаботыС,
	|	МАКСИМУМ(ИсходящаяСправкаОЗаработкеДляРасчетаПособийПериодыРаботы.ПериодРаботыПо) КАК ПериодРаботыПо
	|ИЗ
	|	Документ.ИсходящаяСправкаОЗаработкеДляРасчетаПособий.ПериодыРаботы КАК ИсходящаяСправкаОЗаработкеДляРасчетаПособийПериодыРаботы
	|ГДЕ
	|	ИсходящаяСправкаОЗаработкеДляРасчетаПособийПериодыРаботы.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	
	Если НЕ ПакетРезультатов[0].Пустой() Тогда
		ДанныеОЗаработке = ПакетРезультатов[0].Выгрузить();
		Если ДанныеОЗаработке.Количество() > 0 Тогда
			АдресДанныхОЗаработкеВХранилище = ПоместитьВоВременноеХранилище(ДанныеОЗаработке, ЭтаФорма.УникальныйИдентификатор);
			ДанныеВходящейСправки.Вставить("АдресДанныхОЗаработкеВХранилище", АдресДанныхОЗаработкеВХранилище);
		КонецЕсли;
	КонецЕсли;
	
	ПериодыРаботы = ПакетРезультатов[1].Выбрать();
	
	Если ПериодыРаботы.Следующий() 
		И (ЗначениеЗаполнено(ПериодыРаботы.ПериодРаботыС) 
			ИЛИ ЗначениеЗаполнено(ПериодыРаботы.ПериодРаботыПо)) Тогда
		ДанныеВходящейСправки.Вставить("ПериодРаботыС", ПериодыРаботы.ПериодРаботыС);
		ДанныеВходящейСправки.Вставить("ПериодРаботыПо", ПериодыРаботы.ПериодРаботыПо);
	КонецЕсли;
	
	Возврат ДанныеВходящейСправки;
	
КонецФункции 

#КонецОбласти 


#Область СоздатьВводНачальныхОстатковОтпусков

&НаКлиенте
Процедура СоздатьВводНачальныхОстатковОтпусков()
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполненияВводаНачальныхОстатковОтпусков());
	
	ОткрытьФорму("Документ.ВводНачальныхОстатковОтпусков.Форма.ФормаДокумента", ПараметрыФормы, ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Функция МожноСоздатьВводНачальныхОстатковОтпусков()
	
	Возврат ЗначениеЗаполнено(Объект.Организация) И 
			ЗначениеЗаполнено(Объект.Сотрудник) И
			ЗначениеЗаполнено(Объект.ДатаПриема) И
			ЗначениеЗаполнено(СтрокаПоТипуЗначения(ЭтаФорма, Тип("СправочникСсылка.Сотрудники")).Результат);
			
КонецФункции

&НаКлиенте
Процедура СообщитьОНевозможностиСозданияВводаНачальныхОстатковОтпусков()
	ТекстСообщения = Нстр("ru = 'Перед вводом начальных остатков отпусков необходимо заполнить организацию, сотрудника, дату приема на работу и ввести документ приема на новое место работы.'");
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
КонецПроцедуры

&НаКлиенте
Функция ЗначенияЗаполненияВводаНачальныхОстатковОтпусков()
		
	ЗначенияЗаполненияВводаНачальныхОстатковОтпусков = Новый Структура;
	ЗначенияЗаполненияВводаНачальныхОстатковОтпусков.Вставить("Организация", Объект.НоваяОрганизация);
	ЗначенияЗаполненияВводаНачальныхОстатковОтпусков.Вставить("ДатаОстатков", Объект.ДатаПриема);
	
	Сотрудник = СтрокаПоТипуЗначения(ЭтаФорма, Тип("СправочникСсылка.Сотрудники")).Результат;
	ЗначенияЗаполненияВводаНачальныхОстатковОтпусков.Вставить("Сотрудник", Сотрудник);
	
	ДополнитьОстаткамиОтпусковСотрудника(ЗначенияЗаполненияВводаНачальныхОстатковОтпусков);
	
	Возврат ЗначенияЗаполненияВводаНачальныхОстатковОтпусков;
	
КонецФункции 

&НаКлиенте
Процедура ДополнитьОстаткамиОтпусковСотрудника(ЗначенияЗаполненияВводаНачальныхОстатковОтпусков)
	
	ЗначенияЗаполненияВводаНачальныхОстатковОтпусков.Вставить("АдресОстатковОтпускаВХранилище", ОстаткиОтпусковСотрудникаНаДату(Объект.Сотрудник, Объект.ДатаПриема));
		
КонецПроцедуры

&НаСервере
Функция ОстаткиОтпусковСотрудникаНаДату(Сотрудник, Дата)
	
	Адрес = Неопределено;
	
	ОстаткиОтпусковСотрудника 				= ОстаткиОтпусков.ОстатокОтпускаСотрудникаНаДату(Сотрудник, Дата, Объект.Ссылка, Истина);
	ОстаткиВРазрезеРабочихЛетИВидовОтпусков = ОстаткиОтпусковСотрудника.ОстаткиВРазрезеРабочихЛетИВидовОтпусков;
	
	Если ОстаткиВРазрезеРабочихЛетИВидовОтпусков.Количество() > 0 Тогда
		ТаблицаОстатков = Новый ТаблицаЗначений;
		ТаблицаОстатков.Колонки.Добавить("ВидЕжегодногоОтпуска");
		ТаблицаОстатков.Колонки.Добавить("РабочийГодНачало");
		ТаблицаОстатков.Колонки.Добавить("РабочийГодОкончание");
		ТаблицаОстатков.Колонки.Добавить("Остаток");
		
		Для каждого ОстатокПоВидуОтпуска Из ОстаткиВРазрезеРабочихЛетИВидовОтпусков Цикл
			Если ОстатокПоВидуОтпуска.КоличествоДней > 0 Тогда
				ОстатокНовогоСотрудника  						= ТаблицаОстатков.Добавить();
				ОстатокНовогоСотрудника.ВидЕжегодногоОтпуска 	= ОстатокПоВидуОтпуска.ВидЕжегодногоОтпуска;
				ОстатокНовогоСотрудника.РабочийГодНачало 		= ОстатокПоВидуОтпуска.РабочийГодС;
				ОстатокНовогоСотрудника.РабочийГодОкончание 	= ОстатокПоВидуОтпуска.РабочийГодПо;
				ОстатокНовогоСотрудника.Остаток				 	= ОстатокПоВидуОтпуска.КоличествоДней;
			КонецЕсли;
		КонецЦикла; 
		
		ТаблицаОстатков.Сортировать("ВидЕжегодногоОтпуска, РабочийГодНачало");
		Адрес = ПоместитьВоВременноеХранилище(ТаблицаОстатков, ЭтаФорма.УникальныйИдентификатор);
		
	КонецЕсли;
	
	Возврат Адрес;
	
КонецФункции 

#КонецОбласти 


#Область ПодобратьНовыеКадровыеДанные

&НаСервере
Процедура ПодобратьНовыеКадровыеДанные()
	
	Поля = "Подразделение,Должность,КоличествоСтавок,ГрафикРаботы,Организация";
	ДанныеСотрудников = КадровыйУчет.КадровыеДанныеСотрудников(Ложь, Объект.Сотрудник, Поля, НачалоДня(Объект.ДатаУвольнения) - 1);
	Если ДанныеСотрудников.Количество() > 0 Тогда
		КадровыеДанныеСотрудника = ДанныеСотрудников[0];
		// Если дата приема не заполнена, установим следующую после даты увольнения.
		Если НЕ ЗначениеЗаполнено(Объект.ДатаПриема) И ЗначениеЗаполнено(Объект.ДатаУвольнения) Тогда
			Объект.ДатаПриема = КонецДня(Объект.ДатаУвольнения)+1;
		КонецЕсли;
		
		// Подберем по наименованию и заполним новое подразделение.
		Если ЗначениеЗаполнено(КадровыеДанныеСотрудника.Подразделение) И НЕ ЗначениеЗаполнено(Объект.Подразделение) Тогда
			Объект.Подразделение = ПодобратьНовоеПодразделение(Объект.НоваяОрганизация, КадровыеДанныеСотрудника.Подразделение);
		КонецЕсли;
		
		// Заполним новую должность
		Если ЗначениеЗаполнено(КадровыеДанныеСотрудника.Должность) И НЕ  ЗначениеЗаполнено(Объект.Должность) Тогда
			Объект.Должность = КадровыеДанныеСотрудника.Должность;
		КонецЕсли;
		
		// Заполним новую позицию штатного расписания.
		ИспользоватьШтатноеРасписание = ПолучитьФункциональнуюОпцию("ИспользоватьШтатноеРасписание");
		Если ИспользоватьШтатноеРасписание И НЕ ЗначениеЗаполнено(Объект.ДолжностьПоШтатномуРасписанию) И ЗначениеЗаполнено(Объект.Подразделение) И ЗначениеЗаполнено(Объект.Должность) Тогда
			Объект.ДолжностьПоШтатномуРасписанию = ПодобратьНовуюДолжностьПоШтатномуРасписанию(Объект.Подразделение, Объект.Должность);
		КонецЕсли;
		
		// Заполним новый график
		Если ЗначениеЗаполнено(КадровыеДанныеСотрудника.ГрафикРаботы) И НЕ ЗначениеЗаполнено(Объект.ГрафикРаботы) Тогда
			Объект.ГрафикРаботы = КадровыеДанныеСотрудника.ГрафикРаботы;
		КонецЕсли;
		
		// Заполним новое количество ставок.
		Если ЗначениеЗаполнено(КадровыеДанныеСотрудника.КоличествоСтавок) И НЕ ЗначениеЗаполнено(Объект.КоличествоСтавок) Тогда
			Объект.КоличествоСтавок = КадровыеДанныеСотрудника.КоличествоСтавок;
		КонецЕсли;
	КонецЕсли;
	
	ПодобратьНовыеНачисленияСотрудника();
	ПодобратьНовыйАвансСотрудника();
	
КонецПроцедуры	

&НаСервере
Функция ПодобратьНовоеПодразделение(НоваяОрганизация, СтароеПодразделение)
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ
	|	ПодразделенияОрганизаций.Ссылка КАК Подразделение
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|ГДЕ
	|	ПодразделенияОрганизаций.Владелец = &Владелец
	|	И ПодразделенияОрганизаций.Наименование = &Наименование";
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Владелец", НоваяОрганизация);
	Запрос.УстановитьПараметр("Наименование", СтароеПодразделение.Наименование);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Подразделение;
	КонецЕсли;
	
	Возврат Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
КонецФункции	

&НаСервере
Функция ПодобратьНовуюДолжностьПоШтатномуРасписанию(Подразделение, Должность)
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ
	|	ШтатноеРасписание.Ссылка КАК ДолжностьПоШтатномуРасписанию
	|ИЗ
	|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
	|ГДЕ
	|	ШтатноеРасписание.Подразделение = &Подразделение
	|	И ШтатноеРасписание.Должность = &Должность";
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("Должность", Должность);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ДолжностьПоШтатномуРасписанию;
	КонецЕсли;
	
	Возврат Справочники.ШтатноеРасписание.ПустаяСсылка();
КонецФункции	
	
#КонецОбласти


&НаСервере
Процедура ПодобратьНовыеНачисленияСотрудника()
	
	Если ДоступныПлановыеНачисления Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		Объект.Начисления.Очистить();
		Объект.Показатели.Очистить();
		
		ТаблицаСотрудников = ТаблицаСотрудников(Объект.ДатаУвольнения - 1);
		
		ОписаниеТаблицыВидовРасчета = РасчетЗарплатыРасширенныйКлиентСервер.ОписаниеТаблицыРасчета();
		
		ЗарплатаКадрыРасширенный.РедактированиеСоставаНачисленийДействующиеНачисленияВРеквизит(Объект.Ссылка, ТаблицаСотрудников, ЭтаФорма, ОписаниеТаблицыВидовРасчета, 1, Объект.ДатаПриема, , Истина);
		
		ИспользоватьШтатноеРасписание = ПолучитьФункциональнуюОпцию("ИспользоватьШтатноеРасписание");
		Если ИспользоватьШтатноеРасписание И ЗначениеЗаполнено(Объект.ДолжностьПоШтатномуРасписанию) Тогда
			
			ПараметрыЗаполнения = КадровыйУчетРасширенный.ПараметрыЗаполненияНачисленийПоПозицииШтатногоРасписания();
			ПараметрыЗаполнения.ДатаСобытия = Объект.ДатаПриема;
			ПараметрыЗаполнения.Сотрудник = Объект.Сотрудник;
			ПараметрыЗаполнения.РазрядКатегорияНадбавки = Объект.РазрядКатегория;
			ПараметрыЗаполнения.ОписаниеТаблицыНачислений = ОписаниеТаблицыНачислений();
			ПараметрыЗаполнения.СохранятьТекущиеЗначения = Истина;
			ПараметрыЗаполнения.ДолжностьПоШтатномуРасписанию = Объект.ДолжностьПоШтатномуРасписанию;
			
			Если ТекущиеКадровыеДанныеСотрудника <> Неопределено Тогда
				ПараметрыЗаполнения.ТекущаяДолжностьПоШтатномуРасписанию = ТекущиеКадровыеДанныеСотрудника.ДолжностьПоШтатномуРасписанию;
			КонецЕсли;
			
			КадровыйУчетРасширенный.ЗаполнитьНачисленияПоПозиции(ЭтаФорма, ПараметрыЗаполнения);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПодобратьНовыйАвансСотрудника()
	Если ДоступныПлановыеНачисления Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		ТаблицаСотрудников = ТаблицаСотрудников(Объект.ДатаУвольнения - 1);
		
		ДанныеОбАвансе = РасчетЗарплатыРасширенный.АвансыСотрудников(ТаблицаСотрудников, Объект.Ссылка);
		
		Если ДанныеОбАвансе.Количество() > 0 Тогда
			Объект.СпособРасчетаАванса = ДанныеОбАвансе[0].СпособРасчетаАванса;
			Объект.Аванс = ДанныеОбАвансе[0].Аванс;
		КонецЕсли;
		
	КонецЕсли
КонецПроцедуры	

&НаСервере
Процедура ОтражениеЗарплатыНовогоСотрудникаВБухУчете(НовыйСотрудник)
	
	БухучетСотрудника = ОтражениеЗарплатыВБухучетеРасширенный.НастройкаБухучетаЗарплатыСотрудника(Объект.Сотрудник.Ссылка, Объект.ДатаПриема);
	Если ЗначениеЗаполнено(БухучетСотрудника.Период) Тогда
		
		НаборЗаписейБухУчета  = РегистрыСведений.БухучетЗарплатыСотрудников.СоздатьНаборЗаписей();
		НаборЗаписейБухУчета.Отбор.Сотрудник.Установить(НовыйСотрудник, Истина);
		Запись = НаборЗаписейБухУчета.Добавить();
		ЗаполнитьЗначенияСвойств(Запись,БухучетСотрудника);
		Запись.Сотрудник = НовыйСотрудник;
		Запись.Период = Объект.ДатаПриема;
		НаборЗаписейБухУчета.Записать();
		
	КонецЕсли;
	
КонецПроцедуры	

#Область СервернаяЧастьОбработчиковСобытийЭлементовФормы

&НаСервере
Процедура ТекущаяОрганизацияПриИзмененииНаСервере()
	
	ЗаполнитьДанныеФормыПоОрганизации();
	ПриИзмененииРеквизитовОпределяющихОграниченияНаУровнеЗаписей();
	
	УстановитьДоступныеОрганизации();
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьТекущиеКадровыеДанныеСотрудника()
	
	Поля = "ФизическоеЛицо,РазрядКатегория,ДолжностьПоШтатномуРасписанию,ВидДоговора";
	
	ДанныеСотрудников = КадровыйУчет.КадровыеДанныеСотрудников(Ложь, Объект.Сотрудник, Поля, ?(ЗначениеЗаполнено(Объект.ДатаУвольнения),Объект.ДатаУвольнения,ТекущаяДатаСеанса()));
	Если ДанныеСотрудников.Количество() > 0 Тогда
		
		КадровыеДанныеСотрудника = Новый Структура(Поля);
		ЗаполнитьЗначенияСвойств(КадровыеДанныеСотрудника, ДанныеСотрудников[0]);
		ТекущиеКадровыеДанныеСотрудника = Новый ФиксированнаяСтруктура(КадровыеДанныеСотрудника);
		ВидДоговора = ТекущиеКадровыеДанныеСотрудника.ВидДоговора;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СотрудникПриИзмененииНаСервере()
	
	ПриИзмененииРеквизитовОпределяющихОграниченияНаУровнеЗаписей();
	
	ПрочитатьТекущиеКадровыеДанныеСотрудника();
	Если ТекущиеКадровыеДанныеСотрудника <> Неопределено Тогда
		// Заполним физическое лицо
		Объект.ФизическоеЛицо = ТекущиеКадровыеДанныеСотрудника.ФизическоеЛицо;
		Объект.РазрядКатегория = ТекущиеКадровыеДанныеСотрудника.РазрядКатегория;
		
	КонецЕсли;
	
	УстановитьТипОснованияУвольнения();
	
	ОбновитьСсылкиНаРезультатыНаСервере();
	
	ОбновитьНадписиНаФорме(ЭтаФорма);
	
	ПодобратьНовыеКадровыеДанные();
	
	УстановитьПоказРазмераАванса(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ДатаУвольненияПриИзмененииНаСервере()
	Если НЕ ЗначениеЗаполнено(Объект.ДатаПриема) И ЗначениеЗаполнено(Объект.ДатаУвольнения) Тогда
		Объект.ДатаПриема = КонецДня(Объект.ДатаУвольнения)+1;
	КонецЕсли;
	ПодобратьНовыеКадровыеДанные(); 
КонецПроцедуры

&НаСервере
Процедура НоваяОрганизацияПриИзмененииНаСервере()
	
	ПриИзмененииРеквизитовОпределяющихОграниченияНаУровнеЗаписей();
	ПодобратьНовыеКадровыеДанные();
	ОбновитьСсылкиНаРезультатыНаСервере();
	ОбновитьНадписиНаФорме(ЭтаФорма);
	ЗаполнитьДанныеФормыПоНовойОрганизации();
	
	ЗаполнитьГрафикработы();
	
	РазмерАвансаПоУмолчанию = РасчетЗарплатыФормы.РазмерАвансаВПроцентахПоУмолчанию(Объект.НоваяОрганизация);
	
КонецПроцедуры

&НаСервере
Функция ДанныеДолжности(Перезаполнить = Ложь)
	
	Данные = Неопределено;
	Если Перезаполнить Или ДанныеДолжности = Неопределено Тогда
		
		Если ТекущиеКадровыеДанныеСотрудника = Неопределено Тогда
			ТекущаяДолжностьПоШтатномуРасписанию = Неопределено;
		Иначе
			ТекущаяДолжностьПоШтатномуРасписанию = ТекущиеКадровыеДанныеСотрудника.ДолжностьПоШтатномуРасписанию;
		КонецЕсли;
		
		КадровыйУчетФормыРасширенный.УстановитьДанныеДолжностиВФорме(
			ЭтаФорма, Объект.ДатаПриема, Объект.Должность, Объект.ДолжностьПоШтатномуРасписанию, ТекущаяДолжностьПоШтатномуРасписанию);
		
	КонецЕсли;
	
	Если ДанныеДолжности <> Неопределено Тогда
		
		Если ПолучитьФункциональнуюОпциюФормы("ИспользоватьШтатноеРасписание") Тогда
			Данные = ДанныеДолжности.Получить(Объект.ДолжностьПоШтатномуРасписанию);
		Иначе
			Данные = ДанныеДолжности.Получить(Объект.Должность);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

&НаСервере
Процедура ПозицияШтатногоРасписанияПриИзмененииНаСервере()
	
	ДанныеПозиции = ДанныеДолжности(Истина);
	Если ЗначениеЗаполнено(Объект.ДолжностьПоШтатномуРасписанию)
		И (Не ЗначениеЗаполнено(Объект.НоваяОрганизация)
			Или Не ЗначениеЗаполнено(Объект.Подразделение)) Тогда
		
		Если ДанныеПозиции <> Неопределено Тогда
			
			Если Не ЗначениеЗаполнено(Объект.НоваяОрганизация) Тогда
				Объект.НоваяОрганизация = ДанныеПозиции.Организация;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(Объект.Подразделение) Тогда
				Объект.Подразделение = ДанныеПозиции.Подразделение;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПрочитатьТарифнуюСетку();
	ЗаполнитьПКУПоУмолчанию();
	ЗаполнитьРазрядПоУмолчанию();
	ПодобратьНовыеНачисленияСотрудника();
	ПодобратьНовыйАвансСотрудника();
	
	ЗаполнитьГрафикработы();
	
КонецПроцедуры

&НаСервере
Процедура ДолжностьПриИзмененииНаСервере()
	
	ДанныеДолжности(Истина);
	
	ПрочитатьТарифнуюСетку();
	ЗаполнитьРазрядПоУмолчанию();
	ЗаполнитьДанныеТарифнойСетки();
	
КонецПроцедуры

&НаСервере
Процедура ДатаПриемаПриИзмененииНаСервере()
	Если ПолучитьФункциональнуюОпциюФормы("ИспользоватьШтатноеРасписание")
		И ЗначениеЗаполнено(Объект.ДолжностьПоШтатномуРасписанию) Тогда
		Если Объект.ДатаПриема < ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДолжностьПоШтатномуРасписанию, "ДатаУтверждения") Тогда
			Объект.ДолжностьПоШтатномуРасписанию = Справочники.ШтатноеРасписание.ПустаяСсылка();
		КонецЕсли; 
	КонецЕсли;
	ЗаполнитьДанныеТарифнойСетки();
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииКоличестваСтавок(Результат, ДополнительныеПараметры) Экспорт
	
	ПодобратьНовыеНачисленияСотрудника();
	
КонецПроцедуры

&НаСервере
Процедура ПКУПриИзмененииНаСервере()
	
	ЗаполнитьДанныеТарифнойСетки();
	
КонецПроцедуры

&НаСервере
Процедура РазрядКатегорияПриИзмененииНаСервере()
	
	ЗаполнитьДанныеТарифнойСетки();
	
КонецПроцедуры

#КонецОбласти 

&НаСервере
Процедура ЗаполнитьДанныеФормыПоОрганизации()
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		Возврат;
	КонецЕсли; 
	
	ЗапрашиваемыеЗначения = Новый Структура;
	ЗапрашиваемыеЗначения.Вставить("Организация", "Объект.Организация");
	
	ЗапрашиваемыеЗначения.Вставить("Руководитель", "Объект.Руководитель");
	ЗапрашиваемыеЗначения.Вставить("ДолжностьРуководителя", "Объект.ДолжностьРуководителя");
	
	ЗарплатаКадры.ЗаполнитьЗначенияВФорме(ЭтаФорма, ЗапрашиваемыеЗначения, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("Организация"));	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеФормыПоНовойОрганизации()
	
	Если НЕ ЗначениеЗаполнено(Объект.НоваяОрганизация) Тогда
		Возврат;
	КонецЕсли; 
	
	ЗапрашиваемыеЗначения = Новый Структура;
	ЗапрашиваемыеЗначения.Вставить("Организация", "Объект.НоваяОрганизация");
	
	ЗапрашиваемыеЗначения.Вставить("Руководитель", "Объект.РуководительНовойОрганизации");
	ЗапрашиваемыеЗначения.Вставить("ДолжностьРуководителя", "Объект.ДолжностьРуководителяНовойОрганизации");
	
	ЗарплатаКадры.ЗаполнитьЗначенияВФорме(ЭтаФорма, ЗапрашиваемыеЗначения, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("Организация"));	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеТарифнойСетки()
	
	ДополнительныеПараметры = ЗарплатаКадрыРасширенныйКлиентСервер.ПараметрыЗаполненияЗначенийПоказателейТарифныхСеток();
	ДополнительныеПараметры.ДатаСведений = Объект.ДатаПриема;
	ДополнительныеПараметры.ТарифнаяСетка = ТарифнаяСетка;
	ДополнительныеПараметры.ТарифнаяСеткаНадбавки = ТарифнаяСеткаНадбавки;
	ДополнительныеПараметры.РазрядКатегория = РазрядКатегория;
	ДополнительныеПараметры.РазрядКатегорияНадбавки = Объект.РазрядКатегория;
	ДополнительныеПараметры.ПКУ = Объект.ПКУ;
	
	ЗарплатаКадрыРасширенныйКлиентСервер.ЗаполнитьЗначенияПоказателейТарифныхСеток(ЭтаФорма, Объект.Начисления, ОписаниеТаблицыНачислений(), 1, ДополнительныеПараметры);
		
	ПерезаполнитьДанныеТарифнойСетки = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьТарифнуюСетку()
	
	РазрядыКатегорииДолжностей.ПрочитатьДанныеТарифныхСетокДолжностиВФорму(ЭтаФорма, Объект.Должность, Объект.ДолжностьПоШтатномуРасписанию, Объект.ДатаПриема);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПКУПоУмолчанию()

	РазрядыКатегорииДолжностей.ЗаполнитьПКУСотрудникаПоУмолчанию(
		ЭтаФорма, "Объект.ПКУ", Объект.ДатаПриема, Объект.ДолжностьПоШтатномуРасписанию);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРазрядПоУмолчанию()

	РазрядыКатегорииДолжностей.ЗаполнитьРазрядКатегориюСотрудникаПоУмолчанию(
		ЭтаФорма, "Объект.РазрядКатегория", ТарифнаяСетка, Объект.Должность, 
		Объект.ДолжностьПоШтатномуРасписанию, Объект.Сотрудник, Объект.ДатаПриема);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПоказРазмераАванса(Форма)
	Если Форма.ОграниченияНаУровнеЗаписей.ЧтениеБезОграничений Тогда
		РасчетЗарплатыКлиентСервер.УстановитьПоказРазмераАванса(Форма);
	КонецЕсли;
КонецПроцедуры	

&НаСервере
Процедура ОбновитьСсылкиНаРезультатыНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИнициализироватьТаблицуРезультатов();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ФизическоеЛицо", 		Объект.ФизическоеЛицо);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", 	ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(Объект.НоваяОрганизация));
	Запрос.УстановитьПараметр("Сотрудник", 				Объект.Сотрудник);
	Запрос.УстановитьПараметр("Дата", 					Объект.ДатаУвольнения);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Сотрудник КАК Сотрудник,
	|	&Дата КАК Период
	|ПОМЕСТИТЬ ВТСотрудники";
	Запрос.Выполнить();
	
	ОписательВременныхТаблиц = 
		КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
    		Запрос.МенеджерВременныхТаблиц,
    		"ВТСотрудники");

	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, Истина, "ПриказОбУвольнении");
	
	ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудниковОрганизаций.Организация 			= Объект.НоваяОрганизация;
	ПараметрыПолученияСотрудниковОрганизаций.СписокФизическихЛиц 	= ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.ФизическоеЛицо);
	ПараметрыПолученияСотрудниковОрганизаций.НачалоПериода 			= Объект.ДатаУвольнения;
	ПараметрыПолученияСотрудниковОрганизаций.КадровыеДанные 		= "ПриказОПриеме";
	КадровыйУчет.СоздатьВТСотрудникиОрганизации(Запрос.МенеджерВременныхТаблиц, Истина, ПараметрыПолученияСотрудниковОрганизаций);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВТСотрудникиОрганизации.Сотрудник,
	|	ВТСотрудникиОрганизации.ПриказОПриеме
	|ПОМЕСТИТЬ ВТСотрудникНовойОрганизации
	|ИЗ
	|	ВТСотрудникиОрганизации КАК ВТСотрудникиОрганизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВТКадровыеДанныеСотрудников.Сотрудник,
	|	ВТКадровыеДанныеСотрудников.ПриказОбУвольнении
	|ПОМЕСТИТЬ ВТСотрудникСтаройОрганизации
	|ИЗ
	|	ВТКадровыеДанныеСотрудников КАК ВТКадровыеДанныеСотрудников
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Увольнение.Ссылка КАК Результат,
	|	Увольнение.Ответственный,
	|	Увольнение.Проведен,
	|	Увольнение.ПометкаУдаления КАК ПомеченНаУдаление,
	|	Увольнение.Дата КАК ДатаДокумента,
	|	Увольнение.Номер КАК НомерДокумента
	|ИЗ
	|	Документ.Увольнение КАК Увольнение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникСтаройОрганизации КАК ВТСотрудникСтаройОрганизации
	|		ПО Увольнение.Сотрудник = ВТСотрудникСтаройОрганизации.Сотрудник
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИсходящаяСправка.Ссылка,
	|	ИсходящаяСправка.Ответственный,
	|	ИсходящаяСправка.Проведен,
	|	ИсходящаяСправка.ПометкаУдаления,
	|	ИсходящаяСправка.Дата,
	|	ИсходящаяСправка.Номер
	|ИЗ
	|	Документ.ИсходящаяСправкаОЗаработкеДляРасчетаПособий КАК ИсходящаяСправка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникСтаройОрганизации КАК ВТСотрудникСтаройОрганизации
	|		ПО ИсходящаяСправка.Сотрудник = ВТСотрудникСтаройОрганизации.Сотрудник
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Сотрудники.Ссылка,
	|	"""",
	|	ИСТИНА,
	|	ЛОЖЬ,
	|	ДАТАВРЕМЯ(1, 1, 1),
	|	0
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникНовойОрганизации КАК ВТСотрудникНовойОрганизации
	|		ПО Сотрудники.Ссылка = ВТСотрудникНовойОрганизации.Сотрудник
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПриемНаРаботу.Ссылка,
	|	ПриемНаРаботу.Ответственный,
	|	ПриемНаРаботу.Проведен,
	|	ПриемНаРаботу.ПометкаУдаления,
	|	ПриемНаРаботу.Дата,
	|	ПриемНаРаботу.Номер
	|ИЗ
	|	Документ.ПриемНаРаботу КАК ПриемНаРаботу
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникНовойОрганизации КАК ВТСотрудникНовойОрганизации
	|		ПО ПриемНаРаботу.Ссылка = ВТСотрудникНовойОрганизации.ПриказОПриеме
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВходящаяСправка.Ссылка,
	|	ВходящаяСправка.Ответственный,
	|	ВходящаяСправка.Проведен,
	|	ВходящаяСправка.ПометкаУдаления,
	|	ВходящаяСправка.Дата,
	|	ВходящаяСправка.Номер
	|ИЗ
	|	Документ.ВходящаяСправкаОЗаработкеДляРасчетаПособий КАК ВходящаяСправка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникНовойОрганизации КАК ВТСотрудникНовойОрганизации
	|		ПО ВходящаяСправка.Сотрудник = ВТСотрудникНовойОрганизации.Сотрудник
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВводНачальныхОстатковОтпусков.Ссылка,
	|	ВводНачальныхОстатковОтпусков.Ответственный,
	|	ВводНачальныхОстатковОтпусков.Проведен,
	|	ВводНачальныхОстатковОтпусков.ПометкаУдаления,
	|	ВводНачальныхОстатковОтпусков.Дата,
	|	ВводНачальныхОстатковОтпусков.Номер
	|ИЗ
	|	Документ.ВводНачальныхОстатковОтпусков КАК ВводНачальныхОстатковОтпусков
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникНовойОрганизации КАК ВТСотрудникНовойОрганизации
	|		ПО ВводНачальныхОстатковОтпусков.Сотрудник = ВТСотрудникНовойОрганизации.Сотрудник";
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ОбновитьДанныеФормыПоРезультату(ЭтаФорма, Выборка);
		КонецЦикла;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьТаблицуРезультатов()
	ДействияПриПереводеСотрудника.Очистить();
	// Инициализируем табличную часть ДействияПриПереводеСотрудника.
	МассивПустыхРезультатов = Новый Массив;
	МассивПустыхРезультатов.Добавить(Документы.Увольнение.ПустаяСсылка());
	МассивПустыхРезультатов.Добавить(Документы.ИсходящаяСправкаОЗаработкеДляРасчетаПособий.ПустаяСсылка());
	МассивПустыхРезультатов.Добавить(Документы.ВходящаяСправкаОЗаработкеДляРасчетаПособий.ПустаяСсылка());
	МассивПустыхРезультатов.Добавить(Справочники.Сотрудники.ПустаяСсылка());
	МассивПустыхРезультатов.Добавить(Документы.ПриемНаРаботу.ПустаяСсылка());
	МассивПустыхРезультатов.Добавить(Документы.ВводНачальныхОстатковОтпусков.ПустаяСсылка());
	Для каждого ПустойРезультат Из МассивПустыхРезультатов Цикл
		НоваяСтрока = ДействияПриПереводеСотрудника.Добавить();
		НоваяСтрока.Результат = ПустойРезультат;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция ТаблицаСотрудников(Период)
	
	ТаблицаСотрудников = Новый ТаблицаЗначений;
	ТаблицаСотрудников.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаСотрудников.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	
	Если ЗначениеЗаполнено(Объект.Сотрудник) Тогда
		
		СтрокаСотрудник = ТаблицаСотрудников.Добавить();
		СтрокаСотрудник.Сотрудник = Объект.Сотрудник;
		СтрокаСотрудник.Период = Период;
		
	КонецЕсли;
	
	Возврат ТаблицаСотрудников;
	
КонецФункции

&НаСервере
Процедура УстановитьПредставленияКомандВводаСтажей()
	
	ОписаниеТаблицыВидовРасчета = ОписаниеТаблицыНачислений();
	
	ТаблицаСотрудников = ТаблицаСотрудников(Объект.ДатаПриема);
	
	ЗарплатаКадрыРасширенный.УстановитьПредставленияКомандВводаСтажей(ЭтаФорма, ТаблицаСотрудников, ОписаниеТаблицыВидовРасчета, 1);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьРасчетныхПолей()

	Если Не ДоступныПлановыеНачисления Тогда 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОплатаТруда", "Видимость", Ложь);
	Иначе 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОплатаТруда", "Видимость", ОграниченияНаУровнеЗаписей.ЧтениеБезОграничений);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОплатаТруда", "ТолькоПросмотр", Не ОграниченияНаУровнеЗаписей.ИзменениеБезОграничений);
	КонецЕсли;
	
	Если Не ДоступенРасчетЗарплаты Тогда 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаНадписьИсходящаяСправкаОЗаработкеДляРасчетаПособий", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаНадписьВходящаяСправкаОЗаработкеДляРасчетаПособий", "Видимость", Ложь);
	Иначе 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаНадписьИсходящаяСправкаОЗаработкеДляРасчетаПособий", "Видимость", ОграниченияНаУровнеЗаписей.ЧтениеБезОграничений);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаНадписьВходящаяСправкаОЗаработкеДляРасчетаПособий", "Видимость", ОграниченияНаУровнеЗаписей.ЧтениеБезОграничений);
		
	КонецЕсли;
	
	Если Не ОграниченияНаУровнеЗаписей.ИзменениеКадровыхДанных Тогда 
		ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииРеквизитовОпределяющихОграниченияНаУровнеЗаписей()
	
	БылиОграничения = ОграниченияНаУровнеЗаписей;
	УстановитьОграниченияНаУровнеЗаписей();
	
	Если ОграниченияИзменились(БылиОграничения) Тогда 
		
		УстановитьВидимостьРасчетныхПолей();
		
		Если ОграничениеНаЧтениеИзменилось(БылиОграничения) Тогда 
			ЗарплатаКадрыРасширенный.УстановитьВидимостьКомандПечатиМногофункциональногоДокумента(ЭтаФорма);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОграниченияИзменились(БылиОграничения)
	
	ОграниченияИзменились = Истина;	
	
	Если НЕ БылиОграничения = Неопределено Тогда
		
		ОграниченияИзменились = Ложь;
		
		Если БылиОграничения.ЧтениеБезОграничений <> ОграниченияНаУровнеЗаписей.ЧтениеБезОграничений
			Или БылиОграничения.ИзменениеБезОграничений <> ОграниченияНаУровнеЗаписей.ИзменениеБезОграничений
			Или БылиОграничения.ИзменениеКадровыхДанных <> ОграниченияНаУровнеЗаписей.ИзменениеКадровыхДанных Тогда 
			
			ОграниченияИзменились = Истина;
			
		КонецЕсли;
	КонецЕсли;	
	
	Возврат ОграниченияИзменились;
	
КонецФункции

&НаСервере
Функция ОграничениеНаЧтениеИзменилось(БылиОграничения)
	
	ОграничениеНаЧтениеИзменилось = Истина;	
	
	Если НЕ БылиОграничения = Неопределено Тогда
		ОграничениеНаЧтениеИзменилось = Ложь;		
		Если БылиОграничения.ЧтениеБезОграничений <> ОграниченияНаУровнеЗаписей.ЧтениеБезОграничений Тогда 
			ОграничениеНаЧтениеИзменилось = Истина;
		КонецЕсли;
	КонецЕсли;	
	
	Возврат ОграничениеНаЧтениеИзменилось;
	
КонецФункции

&НаСервере
Процедура УстановитьОграниченияНаУровнеЗаписей()

	ИспользуетсяРасчетЗарплаты = ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная");
	
	ПолныеПраваПоРолямТекущаяОрганизация = ИспользуетсяРасчетЗарплаты И Документы.Увольнение.ПолныеПраваНаДокумент();
	ПолныеПраваПоРолямНоваяОрганизация = ИспользуетсяРасчетЗарплаты И Документы.ПриемНаРаботу.ПолныеПраваНаДокумент();
	
	ФизическоеЛицо = ?(ЗначениеЗаполнено(Объект.Сотрудник), ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Сотрудник, "ФизическоеЛицо"), Справочники.ФизическиеЛица.ПустаяСсылка());
	МассивФизическихЛиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо);
	ДанныеДляПроверкиОграничений = ЗарплатаКадрыРасширенный.ОписаниеСтруктурыДанныхДляПроверкиОграниченийНаУровнеЗаписей();
	
	// Текущая организация
	ДанныеДляПроверкиОграничений.Организация = Объект.Организация;
	ДанныеДляПроверкиОграничений.МассивФизическихЛиц = МассивФизическихЛиц;
	
	ЧтениеБезОграниченийТекущаяОрганизация = Ложь;
	ИзменениеБезОграниченийТекущаяОрганизация = Ложь;
	
	Если ПолныеПраваПоРолямТекущаяОрганизация Тогда 
		ИзменениеБезОграниченийТекущаяОрганизация = Не ЗарплатаКадрыРасширенный.ЕстьОграниченияНаУровнеЗаписейПоВидуУчета("Изменение", "НачисленнаяЗарплата", ДанныеДляПроверкиОграничений);
		ЧтениеБезОграниченийТекущаяОрганизация = ?(ИзменениеБезОграниченийТекущаяОрганизация, Истина, Не ЗарплатаКадрыРасширенный.ЕстьОграниченияНаУровнеЗаписейПоВидуУчета("Чтение", "НачисленнаяЗарплата", ДанныеДляПроверкиОграничений));
	КонецЕсли;	
	
	ИзменениеКадровыхДанныхТекущаяОрганизация = ?(ИзменениеБезОграниченийТекущаяОрганизация, Истина, Не ЗарплатаКадрыРасширенный.ЕстьОграниченияНаУровнеЗаписейПоВидуУчета("Изменение", "КадровыеДанные", ДанныеДляПроверкиОграничений));
	
	Если Не ЧтениеБезОграниченийТекущаяОрганизация И Не ИзменениеКадровыхДанныхТекущаяОрганизация Тогда 
		ЧтениеКадровыхДанныхТекущаяОрганизация = Не ЗарплатаКадрыРасширенный.ЕстьОграниченияНаУровнеЗаписейПоВидуУчета("Чтение", "КадровыеДанные", ДанныеДляПроверкиОграничений);
		Если Не ЧтениеКадровыхДанныхТекущаяОрганизация Тогда 
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТекущаяОрганизация", "Видимость", Ложь);
			ЗарплатаКадрыРасширенный.УстановитьОтображениеГруппыФормы(Элементы, "ГруппаНадписьУвольнение", "Видимость", Ложь);
			ЗарплатаКадрыРасширенный.УстановитьОтображениеГруппыФормы(Элементы, "ГруппаНадписьИсходящаяСправкаОЗаработкеДляРасчетаПособий", "Видимость", Ложь);
		КонецЕсли;
	КонецЕсли;
	
	// Новая организация
	ДанныеДляПроверкиОграничений.Организация = Объект.НоваяОрганизация;
	ДанныеДляПроверкиОграничений.МассивФизическихЛиц = МассивФизическихЛиц;
	
	ЧтениеБезОграниченийНоваяОрганизация = Ложь;
	ИзменениеБезОграниченийНоваяОрганизация = Ложь;
	
	Если ПолныеПраваПоРолямНоваяОрганизация Тогда 
		ИзменениеБезОграниченийНоваяОрганизация = Не ЗарплатаКадрыРасширенный.ЕстьОграниченияНаУровнеЗаписейПоВидуУчета("Изменение", "ПлановыеНачисления", ДанныеДляПроверкиОграничений);
		ЧтениеБезОграниченийНоваяОрганизация = ?(ИзменениеБезОграниченийНоваяОрганизация, Истина, Не ЗарплатаКадрыРасширенный.ЕстьОграниченияНаУровнеЗаписейПоВидуУчета("Чтение", "ПлановыеНачисления", ДанныеДляПроверкиОграничений));
	КонецЕсли;	

	ИзменениеКадровыхДанныхНоваяОрганизация = ?(ИзменениеБезОграниченийНоваяОрганизация, Истина, Не ЗарплатаКадрыРасширенный.ЕстьОграниченияНаУровнеЗаписейПоВидуУчета("Изменение", "КадровыеДанные", ДанныеДляПроверкиОграничений));
	
	Если Не ЧтениеБезОграниченийНоваяОрганизация И Не ИзменениеКадровыхДанныхНоваяОрганизация Тогда 
		ЧтениеКадровыхДанныхНоваяОрганизация = Не ЗарплатаКадрыРасширенный.ЕстьОграниченияНаУровнеЗаписейПоВидуУчета("Чтение", "КадровыеДанные", ДанныеДляПроверкиОграничений);
		Если Не ЧтениеКадровыхДанныхНоваяОрганизация Тогда 
			ЗарплатаКадрыРасширенный.УстановитьОтображениеГруппыФормы(Элементы, "НовоеМестоРаботы", "Видимость", Ложь);
			ЗарплатаКадрыРасширенный.УстановитьОтображениеГруппыФормы(Элементы, "ГруппаНадписьПриемНаРаботу", "Видимость", Ложь);
			ЗарплатаКадрыРасширенный.УстановитьОтображениеГруппыФормы(Элементы, "ГруппаНадписьВводНачальныхОстатковОтпуска", "Видимость", Ложь);
			ЗарплатаКадрыРасширенный.УстановитьОтображениеГруппыФормы(Элементы, "ГруппаНадписьВходящаяСправкаОЗаработкеДляРасчетаПособий", "Видимость", Ложь);
		КонецЕсли;
	КонецЕсли;
	
	ЧтениеБезОграничений = ЧтениеБезОграниченийТекущаяОрганизация И ЧтениеБезОграниченийНоваяОрганизация;
	ИзменениеБезОграничений = ЧтениеБезОграниченийТекущаяОрганизация И ИзменениеБезОграниченийНоваяОрганизация;
	ИзменениеКадровыхДанных = ИзменениеКадровыхДанныхТекущаяОрганизация И ИзменениеКадровыхДанныхНоваяОрганизация;
	
	ОграниченияНаУровнеЗаписей = Новый ФиксированнаяСтруктура("ЧтениеБезОграничений, ИзменениеБезОграничений, ИзменениеКадровыхДанных", ЧтениеБезОграничений, ИзменениеБезОграничений, ИзменениеКадровыхДанных);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТипОснованияУвольнения()
	
	ОграничениеТипа = ОграничениеТипаОснованияУвольнения();
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "СтатьяТКРФ", "ОграничениеТипа", ОграничениеТипа);
	
КонецПроцедуры

&НаСервере
Функция ОграничениеТипаОснованияУвольнения()
	
	ТипОснования = ТипОснованияУвольнения();
	ОграничениеТипа = Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТипОснования));
	
	Возврат ОграничениеТипа;
	
КонецФункции

&НаСервере
Функция ТипОснованияУвольнения()
	
	ТипОснования = Тип("СправочникСсылка.ОснованияУвольнения");
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужбаФормы");
		ТипОснования = Модуль.ТипОснованияУвольнения(ВидДоговора);
	КонецЕсли;
	
	Возврат ТипОснования;
	
КонецФункции

&НаСервере
Процедура УстановитьДоступныеОрганизации()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	НЕ Организации.ОбособленноеПодразделение";
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		
		Запрос.УстановитьПараметр("Организация", Объект.Организация);
		Запрос.Текст = Запрос.Текст + Символы.ПС + "
			|	И НЕ Организации.Ссылка В
			|			(ВЫБРАТЬ
			|				Организации.ГоловнаяОрганизация
			|			ИЗ
			|				Справочник.Организации КАК Организации
			|			ГДЕ
			|				Организации.Ссылка = &Организация)";
		
	КонецЕсли;
	
	ДоступныеОрганизации.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

&НаСервере
Процедура ПодразделениеПриИзмененииНаСервере()
	
	ЗаполнитьГрафикработы();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьГрафикработы()
	
	ЗначенияДляЗаполнения = Новый Структура;
	
	ЗначенияДляЗаполнения.Вставить("Организация",					"Объект.НоваяОрганизация");
	ЗначенияДляЗаполнения.Вставить("Подразделение", 				"Объект.Подразделение");
	ЗначенияДляЗаполнения.Вставить("ДолжностьПоШтатномуРасписанию",	"Объект.ДолжностьПоШтатномуРасписанию");
	ЗначенияДляЗаполнения.Вставить("ГрафикРаботы",					"Объект.ГрафикРаботы");
	
	ФиксированныеЗначения = Новый Массив;
	ФиксированныеЗначения.Добавить("Организация");
	ФиксированныеЗначения.Добавить("Подразделение");
	ФиксированныеЗначения.Добавить("ДолжностьПоШтатномуРасписанию");
	ЗарплатаКадры.ЗаполнитьЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения, ФиксированныеЗначения);
	
КонецПроцедуры

#КонецОбласти
