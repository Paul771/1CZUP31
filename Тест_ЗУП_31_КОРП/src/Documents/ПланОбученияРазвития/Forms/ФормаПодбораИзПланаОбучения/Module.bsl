
#Область ОбработчикиСобытийФормы


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Подразделение") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ЗапланированныеКурсы,"Подразделение", Параметры.Подразделение);
	КонецЕсли;
	Если Параметры.Свойство("Мероприятие") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ЗапланированныеКурсы,"Мероприятие", Параметры.Мероприятие);
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти 


#Область ОбработчикиСобытийЭлементовТаблицыФормыЗапланированныеКурсы


&НаКлиенте
Процедура ЗапланированныеКурсыВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОбработатьВыборМероприятия();
КонецПроцедуры

&НаКлиенте
Процедура ЗапланированныеКурсыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОбработатьВыборМероприятия();
КонецПроцедуры


#КонецОбласти 


#Область СлужебныеПроцедурыИФункции


&НаКлиенте
Процедура ОбработатьВыборМероприятия()

	Если Элементы.ЗапланированныеКурсы.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ЗапланированныеКурсы.ТекущиеДанные;
	
	ПередаваемаяСтруктура = Новый Структура;
	ПередаваемаяСтруктура.Вставить("Подразделение", ТекущиеДанные.Подразделение);
	ПередаваемаяСтруктура.Вставить("Мероприятие", ТекущиеДанные.Мероприятие);
	ПередаваемаяСтруктура.Вставить("ДатаНачала", ТекущиеДанные.ДатаНачала);
	ПередаваемаяСтруктура.Вставить("ДатаОкончания", ТекущиеДанные.ДатаОкончания);
	ПередаваемаяСтруктура.Вставить("Сотрудники", ТаблицаСотрудниковЗапланированногоМероприятия(ТекущиеДанные));
	ПередаваемаяСтруктура.Вставить("КоличествоУчебныхЧасов", ТекущиеДанные.КоличествоУчебныхЧасов);
	ПередаваемаяСтруктура.Вставить("СуммаРасходов", ТекущиеДанные.СуммаРасходов);
	
	Закрыть(ПередаваемаяСтруктура);

КонецПроцедуры

&НаСервере
Функция ТаблицаСотрудниковЗапланированногоМероприятия(ТекущиеДанные)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПланОбученияСотрудников.Сотрудник КАК Сотрудник
		|ИЗ
		|	РегистрСведений.ПланОбученияСотрудников КАК ПланОбученияСотрудников
		|ГДЕ
		|	ПланОбученияСотрудников.Подразделение = &Подразделение
		|	И ПланОбученияСотрудников.Мероприятие = &Мероприятие
		|	И ПланОбученияСотрудников.ДатаНачала = &ДатаНачала
		|	И ПланОбученияСотрудников.ДатаОкончания = &ДатаОкончания";
	
	Запрос.УстановитьПараметр("ДатаНачала", ТекущиеДанные.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ТекущиеДанные.ДатаОкончания);
	Запрос.УстановитьПараметр("Мероприятие", ТекущиеДанные.Мероприятие);
	Запрос.УстановитьПараметр("Подразделение", ТекущиеДанные.Подразделение);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Сотрудник");

КонецФункции

#КонецОбласти 