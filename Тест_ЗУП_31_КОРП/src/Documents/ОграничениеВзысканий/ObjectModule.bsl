#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СтруктураВидовУчета = ПроведениеРасширенныйСервер.СтруктураВидовУчета();
	ПроведениеРасширенныйСервер.ПодготовитьНаборыЗаписейКРегистрацииДвиженийПоВидамУчета(РежимПроведения, Ссылка, СтруктураВидовУчета, Неопределено, Движения, ЭтотОбъект, Отказ);
	
	ДанныеДляПроведения = ДанныеДляПроведения();
	
	РасчетЗарплатыРасширенный.СформироватьДвиженияУдержаний(Движения, Отказ, Организация, КонецМесяца(МесяцНачисления), ДанныеДляПроведения.Удержания, ДанныеДляПроведения.ПоказателиУдержаний);
	ИсполнительныеЛисты.СформироватьУдержанияПоИсполнительнымДокументам(Движения, ДанныеДляПроведения.УдержанияПоИсполнительнымДокументам);
	РасчетЗарплатыРасширенный.СформироватьЗадолженностьПоУдержаниямФизическихЛиц(Движения, ДанныеДляПроведения.ЗадолженностьПоУдержаниям);
	
	ОтражениеЗарплатыВБухучетеРасширенный.СформироватьДвиженияБухучетНачисленияУдержанияПоСотрудникам(
		Движения, Отказ, Организация, МесяцНачисления, Неопределено, ДанныеДляПроведения.УдержанияПоСотрудникам, Неопределено);
	
	УчетНачисленнойЗарплаты.ЗарегистрироватьНачисленияУдержания(Движения, Отказ, Организация, МесяцНачисления, Неопределено, 
		ДанныеДляПроведения.УдержанияПоСотрудникам, Неопределено, Неопределено, Перечисления.ХарактерВыплатыЗарплаты.Зарплата, , Истина);
	
	ПроведениеРасширенныйСервер.ЗаписьДвиженийПоУчетам(Движения, СтруктураВидовУчета);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Новый Структура("Удержания", "ФизическоеЛицо"));
	
	Возврат ОбменДаннымиЗарплатаКадры.ОграниченияРегистрацииПоОрганизацииИФизическимЛицам(ЭтотОбъект, Организация, МассивПараметров, МесяцНачисления);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведения()
	
	ДанныеДляПроведения = РасчетЗарплаты.СоздатьДанныеДляПроведенияНачисленияЗарплаты();
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = ДанныеДляПроведения.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МесяцНачисления", МесяцНачисления);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОграничениеВзысканийУдержания.Ссылка.МесяцНачисления КАК Период,
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	               |	ОграничениеВзысканийУдержания.Ссылка.Организация.ГоловнаяОрганизация КАК Организация,
	               |	ОграничениеВзысканийУдержания.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	ОграничениеВзысканийУдержания.Удержание КАК Удержание,
	               |	ОграничениеВзысканийУдержания.ДокументОснование КАК ДокументОснование,
	               |	ОграничениеВзысканийУдержания.Задолженность КАК Сумма
	               |ИЗ
	               |	Документ.ОграничениеВзысканий.Удержания КАК ОграничениеВзысканийУдержания
	               |ГДЕ
	               |	ОграничениеВзысканийУдержания.Ссылка = &Ссылка
	               |	И ОграничениеВзысканийУдержания.Задолженность <> 0
	               |	И ОграничениеВзысканийУдержания.Удержание.ЯвляетсяВзысканием";
				   
	РезультатЗапроса = Запрос.Выполнить();
	
	ДанныеДляПроведения.Вставить("ЗадолженностьПоУдержаниям", РезультатЗапроса.Выгрузить());
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	Удержания.Ссылка КАК ДокументСсылка,
	               |	Удержания.Ссылка.Организация.ГоловнаяОрганизация КАК Организация,
	               |	Удержания.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	Удержания.Сотрудник КАК Сотрудник,
	               |	Удержания.Удержание КАК Удержание,
	               |	Удержания.Удержание.КатегорияУдержания КАК КатегорияУдержания,
	               |	Удержания.Результат - Удержания.Рассчитано КАК Сумма,
	               |	Удержания.Результат - Удержания.Рассчитано КАК Результат,
	               |	Удержания.ДокументОснование КАК ДокументОснование,
	               |	Удержания.Получатель КАК Получатель,
	               |	Удержания.ПлатежныйАгент КАК ПлатежныйАгент,
	               |	ВЫБОР
	               |		КОГДА Удержания.Удержание.КатегорияУдержания = ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ИсполнительныйЛист)
	               |			ТОГДА Удержания.Получатель
	               |		КОГДА Удержания.Удержание.КатегорияУдержания = ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ВознаграждениеПлатежногоАгента)
	               |			ТОГДА Удержания.ПлатежныйАгент
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	               |	КОНЕЦ КАК Контрагент,
	               |	НАЧАЛОПЕРИОДА(Удержания.ДатаНачала, МЕСЯЦ) КАК ПериодДействия,
	               |	Удержания.ДатаНачала КАК ДатаНачала,
	               |	Удержания.ДатаОкончания КАК ДатаОкончания,
	               |	Удержания.ИдентификаторСтрокиВидаРасчета КАК ИдентификаторСтроки,
	               |	ВЫБОР
	               |		КОГДА Удержания.Рассчитано = 0
	               |			ТОГДА 0
	               |		ИНАЧЕ (Удержания.Результат - Удержания.Рассчитано) / Удержания.Рассчитано
	               |	КОНЕЦ КАК Коэффициент,
	               |	ИСТИНА КАК ОграничениеВзыскания,
	               |	ИСТИНА КАК ФиксСторно
	               |ПОМЕСТИТЬ ВТЗаписиУдержаний
	               |ИЗ
	               |	Документ.ОграничениеВзысканий.Удержания КАК Удержания
	               |ГДЕ
	               |	Удержания.Ссылка = &Ссылка
	               |	И Удержания.Результат <> Удержания.Рассчитано";
	
	Запрос.Выполнить();
	
	// Если используются источники финансирования дополняем результатом распределения.
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	НачисленияУдержания.Сотрудник КАК Сотрудник,
		               |	НачисленияУдержания.Подразделение КАК Подразделение,
		               |	НачисленияУдержания.СтатьяФинансирования КАК СтатьяФинансирования,
		               |	НачисленияУдержания.СтатьяРасходов КАК СтатьяРасходов,
		               |	СУММА(НачисленияУдержания.Сумма * ЗаписиУдержаний.Коэффициент) КАК Сумма,
		               |	ЗаписиУдержаний.ИдентификаторСтроки КАК ИдентификаторСтроки
		               |ПОМЕСТИТЬ ВТРаспределениеРезультатовУдержаний
		               |ИЗ
		               |	РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК НачисленияУдержания
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЗаписиУдержаний КАК ЗаписиУдержаний
		               |		ПО НачисленияУдержания.Организация = ЗаписиУдержаний.Организация
		               |			И НачисленияУдержания.ФизическоеЛицо = ЗаписиУдержаний.ФизическоеЛицо
		               |			И НачисленияУдержания.НачислениеУдержание = ЗаписиУдержаний.Удержание
		               |			И НачисленияУдержания.ДокументОснование = ЗаписиУдержаний.ДокументОснование
		               |			И НачисленияУдержания.ДатаНачала = ЗаписиУдержаний.ДатаНачала
		               |			И НачисленияУдержания.ДатаОкончания = ЗаписиУдержаний.ДатаОкончания
		               |			И НачисленияУдержания.Контрагент = ЗаписиУдержаний.Контрагент
		               |			И (НачисленияУдержания.Период = &МесяцНачисления)
		               |			И (НачисленияУдержания.Регистратор <> &Ссылка)
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	НачисленияУдержания.Сотрудник,
		               |	НачисленияУдержания.Подразделение,
		               |	НачисленияУдержания.СтатьяФинансирования,
		               |	НачисленияУдержания.СтатьяРасходов,
		               |	ЗаписиУдержаний.ИдентификаторСтроки";
		
		Запрос.Выполнить();
		
		// Дополнить данными о распределении удержаний по источникам финансирования.
		ОтражениеЗарплатыВУчетеРасширенный.СоздатьВТУдержанияПоСотрудникамКонтрагент(Запрос.МенеджерВременныхТаблиц, "ВТЗаписиУдержаний");
		Запрос.Текст = "ВЫБРАТЬ
		               |	РаспределениеУдержаний.Сотрудник КАК Сотрудник,
		               |	РаспределениеУдержаний.Подразделение КАК Подразделение,
		               |	РаспределениеУдержаний.СтатьяФинансирования КАК СтатьяФинансирования,
		               |	РаспределениеУдержаний.СтатьяРасходов КАК СтатьяРасходов,
		               |	РаспределениеУдержаний.Сумма КАК Сумма,
		               |	ЕСТЬNULL(УдержанияПоСотрудникамКонтрагент.Контрагент, ЗаписиУдержаний.Контрагент) КАК Контрагент,
		               |	ЗаписиУдержаний.*
		               |ПОМЕСТИТЬ ВТУдержанияСРаспределением
		               |ИЗ
		               |	ВТРаспределениеРезультатовУдержаний КАК РаспределениеУдержаний
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЗаписиУдержаний КАК ЗаписиУдержаний
		               |		ПО РаспределениеУдержаний.ИдентификаторСтроки = ЗаписиУдержаний.ИдентификаторСтроки
		               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТУдержанияПоСотрудникамКонтрагент КАК УдержанияПоСотрудникамКонтрагент
		               |		ПО (ЗаписиУдержаний.ДокументОснование = УдержанияПоСотрудникамКонтрагент.ДокументОснование)
		               |			И (ЗаписиУдержаний.ФизическоеЛицо = УдержанияПоСотрудникамКонтрагент.ФизическоеЛицо)
		               |			И (ЗаписиУдержаний.Удержание = УдержанияПоСотрудникамКонтрагент.Удержание)
		               |			И (ЗаписиУдержаний.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))";
		
	Иначе
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение,
		               |	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
		               |	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
		               |	ЗаписиУдержаний.Сумма КАК Сумма,
		               |	ЗаписиУдержаний.*
		               |ПОМЕСТИТЬ ВТУдержанияСРаспределением
		               |ИЗ
		               |	ВТЗаписиУдержаний КАК ЗаписиУдержаний";
		
	КонецЕсли;
	Запрос.Выполнить();
	
	// Удержания
	Запрос.Текст = 
		"ВЫБРАТЬ * ИЗ ВТЗаписиУдержаний";
	ДанныеДляПроведения.Удержания = Запрос.Выполнить().Выгрузить();
	
	// Удержания с возможным распределением по источникам финансирования.
	Запрос.Текст = 
		"ВЫБРАТЬ * ИЗ ВТУдержанияСРаспределением";
	ДанныеДляПроведения.УдержанияПоСотрудникам = Запрос.Выполнить().Выгрузить();
	
	// Удержания по исполнительным документам.
	Запрос.Текст = "ВЫБРАТЬ
	               |	УдержанияСРаспределением.СтатьяФинансирования КАК СтатьяФинансирования,
	               |	УдержанияСРаспределением.СтатьяРасходов КАК СтатьяРасходов,
	               |	УдержанияСРаспределением.ДокументОснование КАК ИсполнительныйДокумент,
	               |	УдержанияСРаспределением.Получатель КАК Получатель,
	               |	УдержанияСРаспределением.ПлатежныйАгент КАК ПлатежныйАгент,
	               |	УдержанияСРаспределением.ОграничениеВзыскания КАК ОграничениеВзыскания,
	               |	НАЧАЛОПЕРИОДА(УдержанияСРаспределением.ДатаНачала, МЕСЯЦ) КАК МесяцУдержания,
	               |	СУММА(ВЫБОР
	               |			КОГДА Удержания.КатегорияУдержания = ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ИсполнительныйЛист)
	               |				ТОГДА УдержанияСРаспределением.Сумма
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК СуммаУдержания,
	               |	СУММА(ВЫБОР
	               |			КОГДА Удержания.КатегорияУдержания = ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ВознаграждениеПлатежногоАгента)
	               |				ТОГДА УдержанияСРаспределением.Сумма
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК СуммаВознагражденияПлатежногоАгента
	               |ИЗ
	               |	ВТУдержанияСРаспределением КАК УдержанияСРаспределением
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Удержания КАК Удержания
	               |		ПО УдержанияСРаспределением.Удержание = Удержания.Ссылка
	               |			И (Удержания.КатегорияУдержания В (ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ИсполнительныйЛист), ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ВознаграждениеПлатежногоАгента)))
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	УдержанияСРаспределением.СтатьяФинансирования,
	               |	УдержанияСРаспределением.СтатьяРасходов,
	               |	УдержанияСРаспределением.ДокументОснование,
	               |	УдержанияСРаспределением.Получатель,
	               |	УдержанияСРаспределением.ПлатежныйАгент,
	               |	УдержанияСРаспределением.ОграничениеВзыскания,
	               |	НАЧАЛОПЕРИОДА(УдержанияСРаспределением.ДатаНачала, МЕСЯЦ)
	               |
	               |ИМЕЮЩИЕ
	               |	(СУММА(ВЫБОР
	               |				КОГДА Удержания.КатегорияУдержания = ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ИсполнительныйЛист)
	               |					ТОГДА УдержанияСРаспределением.Сумма
	               |				ИНАЧЕ 0
	               |			КОНЕЦ) <> 0
	               |		ИЛИ СУММА(ВЫБОР
	               |				КОГДА Удержания.КатегорияУдержания = ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ВознаграждениеПлатежногоАгента)
	               |					ТОГДА УдержанияСРаспределением.Сумма
	               |				ИНАЧЕ 0
	               |			КОНЕЦ) <> 0)";
		
	РезультатЗапроса = Запрос.Выполнить();
	ДанныеДляПроведения.УдержанияПоИсполнительнымДокументам = РезультатЗапроса.Выгрузить();
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

#КонецОбласти

#КонецЕсли