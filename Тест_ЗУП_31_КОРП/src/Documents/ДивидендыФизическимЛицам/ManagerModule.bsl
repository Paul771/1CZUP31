#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаДокумента.
Функция ОписаниеСоставаДокумента() Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.ДивидендыФизическимЛицам;
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаДокументаПоМетаданнымФизическиеЛицаВТабличныхЧастях(МетаданныеДокумента);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
	
// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры


#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Справка о списании депонированной зарплаты.
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "ЗарплатаКадрыКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.МенеджерПечати = "Документ.ДивидендыФизическимЛицам";
	КомандаПечати.Идентификатор = "ПФ_MXL_СправкаОДивидендах";
	КомандаПечати.Представление = НСтр("ru = 'Справка о дивидендах'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнитьКомплектВнешнимиПечатнымиФормами = Истина;

КонецПроцедуры

// Формирует печатные формы
//
// Параметры:
//  (входные)
//    МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//    ПараметрыПечати - Структура - дополнительные настройки печати;
//  (выходные)
//   КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы.
//   ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                             представление - имя области в которой был выведен объект;
//   ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	НужноПечататьСправку = УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_СправкаОДивидендах");
	
	Если НужноПечататьСправку Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПФ_MXL_СправкаОДивидендах",
			НСтр("ru = 'Справка о дивидендах'"), ПечатьСправки(МассивОбъектов, ОбъектыПечати), ,
			"Документ.ДивидендыФизическимЛицам.ПФ_MXL_СправкаОРегистрацииПрочихДоходов");
	КонецЕсли;
						
КонецПроцедуры								

Функция ПечатьСправки(МассивОбъектов, ОбъектыПечати)
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РегистрацияПрочихДоходов_СправкаОДивидендах";
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ДокументРезультат.АвтоМасштаб = Истина;
	
	ДанныеПечатиОбъектов = ДанныеПечатиДокументов(МассивОбъектов);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ДивидендыФизическимЛицам.ПФ_MXL_СправкаОДивидендах");
	
	ОбластьМакетаШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакетаШапкаПовторятьПриПечати = Макет.ПолучитьОбласть("ШапкаПовторятьПриПечати");
	ОбластьМакетаСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал");
	
	ПервыйДокумент = Истина;
	
	Для Каждого ДанныеПечати Из ДанныеПечатиОбъектов Цикл
		
		ДанныеДокумента = ДанныеПечати.Значение;
		
		// Документы нужно выводить на разных страницах.
		Если Не ПервыйДокумент Тогда
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		Иначе
			ПервыйДокумент = Ложь;
		КонецЕсли;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ДокументРезультат.ВысотаТаблицы + 1;
		
		ОбластьМакетаШапка.Параметры.Заполнить(ДанныеДокумента);
		ДокументРезультат.Вывести(ОбластьМакетаШапка);
		
		Для каждого ДанныеСотрудника Из ДанныеДокумента.ТабличнаяЧастьДокумента Цикл
			
			МассивВыводимыхОбластей = Новый Массив;
			МассивВыводимыхОбластей.Добавить(ОбластьМакетаСтрока);
			МассивВыводимыхОбластей.Добавить(ОбластьМакетаПодвал);
			Если НЕ ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, МассивВыводимыхОбластей) Тогда
				ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
				ДокументРезультат.Вывести(ОбластьМакетаШапкаПовторятьПриПечати);
			КонецЕсли;
			
			ОбластьМакетаСтрока.Параметры.Заполнить(ДанныеСотрудника);
			
			ДокументРезультат.Вывести(ОбластьМакетаСтрока);
			
		КонецЦикла;
		
		ОбластьМакетаПодвал.Параметры.Заполнить(ДанныеДокумента);
		ДокументРезультат.Вывести(ОбластьМакетаПодвал);
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ДокументРезультат, НомерСтрокиНачало, ОбъектыПечати, ДанныеДокумента.Ссылка);
				
	КонецЦикла;
	
	Возврат ДокументРезультат;
		
КонецФункции

Функция ДанныеПечатиДокументов(МассивОбъектов)
	
	ДанныеПечатиОбъектов = Новый Соответствие;
	
	ВалютаУчета = ЗарплатаКадры.ВалютаУчетаЗаработнойПлаты();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДивидендыФизическимЛицам.Ссылка.Организация.НаименованиеПолное КАК Организация,
		|	ДивидендыФизическимЛицам.Ссылка КАК Ссылка,
		|	ДивидендыФизическимЛицам.Ссылка.Номер,
		|	НАЧАЛОПЕРИОДА(ДивидендыФизическимЛицам.Ссылка.Дата, ДЕНЬ) КАК Период,
		|	ДивидендыФизическимЛицам.Акционер КАК ФизическоеЛицо,
		|	ДивидендыФизическимЛицам.НДФЛ КАК НДФЛ,
		|	ДивидендыФизическимЛицам.Начислено КАК Начислено,
		|	ДивидендыФизическимЛицам.ВычетПоНДФЛ КАК СуммаВычета,
		|	ДивидендыФизическимЛицам.Удержано КАК Удержано,
		|	ДивидендыФизическимЛицам.КВыплате КАК СуммаКВыплате,
		|	ДивидендыФизическимЛицам.Ссылка.Исполнитель.ФИО КАК Исполнитель,
		|	ДивидендыФизическимЛицам.Ссылка.ДолжностьИсполнителя КАК ДолжностьИсполнителя
		|ПОМЕСТИТЬ ВТДанныеДокументов
		|ИЗ
		|	Документ.ДивидендыФизическимЛицам.Начисления КАК ДивидендыФизическимЛицам
		|ГДЕ
		|	ДивидендыФизическимЛицам.Ссылка В(&МассивОбъектов)";
	
	Запрос.Выполнить();
	
	ОписательВТ = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеФизическихЛиц(
		Запрос.МенеджерВременныхТаблиц, "ВТДанныеДокументов");
	КадровыйУчет.СоздатьВТКадровыеДанныеФизическихЛиц(ОписательВТ, Истина, "ФИОПолные");
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеДокументов.Организация КАК Организация,
		|	ДанныеДокументов.Ссылка КАК Ссылка,
		|	ДанныеДокументов.Номер КАК Номер,
		|	ДанныеДокументов.Период КАК Дата,
		|	ЕСТЬNULL(КадровыеДанныеФизическихЛиц.ФИОПолные, ДанныеДокументов.ФизическоеЛицо) КАК Акционер,
		|	ДанныеДокументов.НДФЛ КАК НДФЛ,
		|	ДанныеДокументов.Начислено КАК Начислено,
		|	ДанныеДокументов.СуммаВычета КАК СуммаВычета,
		|	ДанныеДокументов.Удержано КАК Удержано,
		|	ДанныеДокументов.СуммаКВыплате КАК СуммаКВыплате,
		|	ЕСТЬNULL(ДанныеДокументов.Исполнитель, """") КАК Исполнитель,
		|	ДанныеДокументов.ДолжностьИсполнителя КАК ДолжностьИсполнителя
		|ИЗ
		|	ВТДанныеДокументов КАК ДанныеДокументов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеФизическихЛиц КАК КадровыеДанныеФизическихЛиц
		|		ПО ДанныеДокументов.Период = КадровыеДанныеФизическихЛиц.Период
		|			И ДанныеДокументов.ФизическоеЛицо = КадровыеДанныеФизическихЛиц.ФизическоеЛицо
		|ИТОГИ
		|	МАКСИМУМ(Организация),
		|	МАКСИМУМ(Номер),
		|	МАКСИМУМ(Дата),
		|	СУММА(НДФЛ),
		|	СУММА(Начислено),
		|	СУММА(СуммаВычета),
		|	СУММА(Удержано),
		|	СУММА(СуммаКВыплате),
		|	МАКСИМУМ(Исполнитель),
		|	МАКСИМУМ(ДолжностьИсполнителя)
		|ПО
		|	Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	ГруппировкаПоДокументу = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ГруппировкаПоДокументу.Следующий() Цикл
		
		ДанныеПечати = Новый Структура;
		ДанныеПечати.Вставить("Ссылка", ГруппировкаПоДокументу.Ссылка);
		ДанныеПечати.Вставить("НазваниеОрганизации", ГруппировкаПоДокументу.Организация);
		ДанныеПечати.Вставить("ЕдиницаИзмерения", ВалютаУчета.НаименованиеПолное);
		ДанныеПечати.Вставить("ДатаДокумента", Формат(ГруппировкаПоДокументу.Дата, "ДЛФ=D"));
		ДанныеПечати.Вставить("НомерДокумента", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ГруппировкаПоДокументу.Номер, Истина, Истина));
		ДанныеПечати.Вставить("Начислено", ГруппировкаПоДокументу.Начислено);
		ДанныеПечати.Вставить("СуммаВычета", ГруппировкаПоДокументу.СуммаВычета);
		ДанныеПечати.Вставить("НДФЛ", ГруппировкаПоДокументу.НДФЛ);
		ДанныеПечати.Вставить("Удержано", ГруппировкаПоДокументу.Удержано);
		ДанныеПечати.Вставить("СуммаКВыплате", ГруппировкаПоДокументу.СуммаКВыплате);
		ДанныеПечати.Вставить("ДолжностьИсполнителя", ГруппировкаПоДокументу.ДолжностьИсполнителя);
		ДанныеПечати.Вставить("ИсполнительРасшифровкаПодписи", ФизическиеЛицаЗарплатаКадры.РасшифровкаПодписи(ГруппировкаПоДокументу.Исполнитель));
		ДанныеПечати.Вставить("ТабличнаяЧастьДокумента", Новый Массив);
		
		ГруппировкаПоСотрудникам = ГруппировкаПоДокументу.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
		Пока ГруппировкаПоСотрудникам.Следующий() Цикл
			
			СтрокаДанныхПечати = Новый Структура;
			СтрокаДанныхПечати.Вставить("Акционер", ГруппировкаПоСотрудникам.Акционер);
			СтрокаДанныхПечати.Вставить("Начислено", ГруппировкаПоСотрудникам.Начислено);
			СтрокаДанныхПечати.Вставить("СуммаВычета", ГруппировкаПоСотрудникам.СуммаВычета);
			СтрокаДанныхПечати.Вставить("НДФЛ", ГруппировкаПоСотрудникам.НДФЛ);
			СтрокаДанныхПечати.Вставить("Удержано", ГруппировкаПоСотрудникам.Удержано);
			СтрокаДанныхПечати.Вставить("СуммаКВыплате", ГруппировкаПоСотрудникам.СуммаКВыплате);
			
			ДанныеПечати.ТабличнаяЧастьДокумента.Добавить(СтрокаДанныхПечати);
			
		КонецЦикла;		
		
		ДанныеПечатиОбъектов.Вставить(ГруппировкаПоДокументу.Ссылка, ДанныеПечати);
		
	КонецЦикла;
	
	Возврат ДанныеПечатиОбъектов;
		
КонецФункции

#КонецОбласти

Процедура ЗаполнитьРеквизитыКВыплате() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДивидендыФизическимЛицамНачисления.Ссылка КАК Ссылка,
		|	ДивидендыФизическимЛицамНачисления.Ссылка.ВсегоВыплачено КАК ВсегоВыплачено,
		|	СУММА(ДивидендыФизическимЛицамНачисления.Начислено) КАК Начислено,
		|	СУММА(ДивидендыФизическимЛицамНачисления.НДФЛ) КАК НДФЛ,
		|	СУММА(ДивидендыФизическимЛицамНачисления.КВыплате) КАК КВыплате,
		|	СУММА(ДивидендыФизическимЛицамНачисления.Удержано) КАК Удержано
		|ПОМЕСТИТЬ ВТДокументыСИтогами
		|ИЗ
		|	Документ.ДивидендыФизическимЛицам.Начисления КАК ДивидендыФизическимЛицамНачисления
		|
		|СГРУППИРОВАТЬ ПО
		|	ДивидендыФизическимЛицамНачисления.Ссылка,
		|	ДивидендыФизическимЛицамНачисления.Ссылка.ВсегоВыплачено
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТДокументыСИтогами.Ссылка КАК Ссылка
		|ИЗ
		|	ВТДокументыСИтогами КАК ВТДокументыСИтогами
		|ГДЕ
		|	(ВТДокументыСИтогами.КВыплате <> ВТДокументыСИтогами.Начислено - (ВТДокументыСИтогами.НДФЛ + ВТДокументыСИтогами.Удержано)
		|			ИЛИ ВТДокументыСИтогами.ВсегоВыплачено <> ВТДокументыСИтогами.КВыплате)";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			Для каждого СтрокаНачислений Из ДокументОбъект.Начисления Цикл
				СтрокаНачислений.КВыплате = СтрокаНачислений.Начислено - СтрокаНачислений.НДФЛ - СтрокаНачислений.Удержано;
			КонецЦикла;
			
			ДокументОбъект.ВсегоВыплачено = ДокументОбъект.Начисления.Итог("КВыплате");
			
			ДокументОбъект.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
			ДокументОбъект.ОбменДанными.Загрузка = Истина;
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			
		КонецЦикла; 
		
	КонецЕсли;
		
КонецПроцедуры

Функция ДанныеДляПроведенияДивиденды(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Дивиденды.НомерСтроки КАК НомерСтроки,
	|	Дивиденды.ФизическоеЛицо КАК Акционер,
	|	Дивиденды.КатегорияДохода КАК КатегорияДохода,
	|	Дивиденды.Начислено КАК Начислено,
	|	Дивиденды.ВычетПоНДФЛ КАК ВычетПоНДФЛ,
	|	Дивиденды.НДФЛ КАК НДФЛ,
	|	Дивиденды.КВыплате КАК КВыплате,
	|	Дивиденды.Удержано КАК Удержано,
	|	ВЫБОР
	|		КОГДА ВТОсновныеСотрудникиФизическихЛиц.Сотрудник ЕСТЬ NULL
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.Дивиденды)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.ДивидендыСотрудников)
	|	КОНЕЦ КАК Начисление
	|ИЗ
	|	ВТДанныеДокумента КАК Дивиденды
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОсновныеСотрудникиФизическихЛиц КАК ВТОсновныеСотрудникиФизическихЛиц
	|		ПО Дивиденды.ФизическоеЛицо = ВТОсновныеСотрудникиФизическихЛиц.ФизическоеЛицо";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

Функция СпособВыплатыПрочихДоходов() Экспорт
	Возврат Перечисления.СпособыВыплатыПрочихДоходов.Дивиденды;
КонецФункции

#КонецОбласти

#КонецЕсли