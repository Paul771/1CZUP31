
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ПоказателиСтрокой = КлючевыеПоказателиЭффективности.ПоказателиСтрокой(Показатели);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ТаблицаПланаПозиций = РегистрыСведений.ПлановыеЗначенияПоказателейЭффективностиПозиций.ТаблицаИзмеренийПланаПозиций();
	
	Для каждого СтрокаПоказателя Из Показатели Цикл
		НоваяСтрока = ТаблицаПланаПозиций.Добавить();
		НоваяСтрока.ПериодДействия = Период;
		НоваяСтрока.Позиция = Позиция;
		НоваяСтрока.Показатель = СтрокаПоказателя.Показатель;
	КонецЦикла; 
	
	ТаблицаВведеныхЗначений = КлючевыеПоказателиЭффективности.ВведенныеПлановыеЗначенияПоказателейПозиций(ТаблицаПланаПозиций, Ссылка);
	
	Для каждого СтрокаВведеныхЗначений Из ТаблицаВведеныхЗначений Цикл
		НайденныеСтроки = Показатели.НайтиСтроки(Новый Структура("Показатель", СтрокаВведеныхЗначений.Показатель));
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Для позиции %1 уже введено плановое значение показателя %2.'"), Позиция, СтрокаВведеныхЗначений.Показатель);
		Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				ЭтотОбъект,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Показатели[%1].Показатель", Показатели.Индекс(НайденнаяСтрока)),, Отказ);
		КонецЦикла; 
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ДанныеДляПроведения = Документы.ПланПоказателейЭффективностиПозиции.ПолучитьДанныеДляПроведения(Ссылка);
	КлючевыеПоказателиЭффективности.СформироватьДвиженияПлановыеЗначенияПоказателейЭффективностиПозиций(Движения, ДанныеДляПроведения.ПланПозиций);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру параметров для ограничения регистрации объекта при обмене
// Вызывается ПередЗаписью объекта.
//
// Возвращаемое значение:
//	ОграниченияРегистрации - Структура - Описание см. ОбменДаннымиЗарплатаКадры.ОграниченияРегистрации.
//
Функция ОграниченияРегистрации() Экспорт
	Возврат ОбменДаннымиЗарплатаКадрыРасширенный.ОграниченияРегистрацииПоОрганизацииИПодразделению(ЭтотОбъект,, Подразделение, Позиция);
КонецФункции

#КонецОбласти

#КонецЕсли
