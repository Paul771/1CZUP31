
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОбластиДанных");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
	Блокировка.Заблокировать();
	
	Если Значение Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ОбластиДанных.Статус КАК Статус
		|ИЗ
		|	РегистрСведений.ОбластиДанных КАК ОбластиДанных";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Не Выборка.Следующий() Тогда
			ВызватьИсключение НСтр("ru = 'Не найдена область.'", Метаданные.ОсновнойЯзык.КодЯзыка);
		ИначеЕсли Выборка.Статус <> Перечисления.СтатусыОбластейДанных.Используется Тогда
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Область находится в статусе %1.'", Метаданные.ОсновнойЯзык.КодЯзыка), Выборка.Статус);
		КонецЕсли;
	КонецЕсли;
	
	ОчищаемыеПараметры = Новый Массив;
	ОчищаемыеПараметры.Добавить(Метаданные.ПараметрыСеанса.ВыполняетсяЗагрузкаДанных.Имя);
	ПараметрыСеанса.Очистить(ОчищаемыеПараметры);
	
КонецПроцедуры
