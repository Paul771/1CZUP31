
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПриПолученииДанныхНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура Выбрать(Команда)
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Выбрано", Истина);
	ВыбранныеЗначения = ЗначенияХарактеристики.НайтиСтроки(СтруктураПоиска);
	МассивЗначений = Новый Массив;
	Для Каждого ТекущаяСтрока Из ВыбранныеЗначения Цикл
		СтруктураЗначения = Новый Структура;
		СтруктураЗначения.Вставить("Значение", ТекущаяСтрока.ЗначениеХарактеристики);
		СтруктураЗначения.Вставить("ВесЗначения", ТекущаяСтрока.ВесЗначения);
		МассивЗначений.Добавить(СтруктураЗначения);
	КонецЦикла;
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Характеристика", Характеристика);
	СтруктураВозврата.Вставить("МассивЗначений", МассивЗначений);
	
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ХарактеристикаПриИзменении(Элемент)
	
	ЗаполнитьТаблицуХарактеристиками();
	
КонецПроцедуры

&НаКлиенте
Процедура ВесаЗначенийВыбраноПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ВесаЗначений.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Выбрано Тогда
		ТекущиеДанные.ВесЗначения = 1;
	Иначе
		ТекущиеДанные.ВесЗначения = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВесаЗначенийВесЗначенияПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ВесаЗначений.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Выбрано И ТекущиеДанные.ВесЗначения = 0 Тогда
		ТекущиеДанные.ВесЗначения = 1;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриПолученииДанныхНаСервере()

	УстановитьПризнакиОтображенияФормы();
	ПрочитатьПараметрыФормы();
	УстановитьСвойстваЭлементовФормы();

КонецПроцедуры

&НаСервере
Процедура УстановитьПризнакиОтображенияФормы()

	Если Не Параметры.Свойство("СкрытьРеквизиты") Тогда
		Возврат;
	КонецЕсли;
	
	СписокСкрываемыхРеквизитов = Параметры.СкрытьРеквизиты;
	
	Если СписокСкрываемыхРеквизитов.Свойство("Вес") Тогда
		СкрытьВесаЗначений = Истина;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПрочитатьПараметрыФормы()

	Если Не Параметры.Свойство("Характеристика") Тогда
		Возврат;
	КонецЕсли;
	
	Характеристика = Параметры.Характеристика;
	ЗаполнитьТаблицуХарактеристиками();
	
	Если Не Параметры.Свойство("МассивЗначений") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекущаяСтрока Из Параметры.МассивЗначений Цикл
		СтрокиЗначения = ЗначенияХарактеристики.НайтиСтроки(Новый Структура("ЗначениеХарактеристики", ТекущаяСтрока.Значение));
		Если СтрокиЗначения.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		СтрокиЗначения[0].Выбрано = Истина;
		Если ТекущаяСтрока.Свойство("ВесЗначения") Тогда
			СтрокиЗначения[0].ВесЗначения = ТекущаяСтрока.ВесЗначения;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовФормы()

	УстановитьЗаголовокФормы();
	УстановитьВидимостьВесЗначения();

КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокФормы()

	Если Не ЗначениеЗаполнено(Характеристика) Тогда
		Возврат;
	КонецЕсли;
	
	ЭтаФорма.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Настройка значений характеристики ""%1""'"),
		Характеристика);

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьВесЗначения()

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ВесаЗначенийВесЗначения",
		"Видимость",
		Не СкрытьВесаЗначений);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуХарактеристиками()
	
	ЗначенияХарактеристики.Очистить();
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗначенияХарактеристикПерсонала.Ссылка
		|ИЗ
		|	Справочник.ЗначенияХарактеристикПерсонала КАК ЗначенияХарактеристикПерсонала
		|ГДЕ
		|	ЗначенияХарактеристикПерсонала.Владелец = &Владелец
		|	И НЕ ЗначенияХарактеристикПерсонала.ПометкаУдаления";
	Запрос.УстановитьПараметр("Владелец", Характеристика);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ЗначенияХарактеристики.Добавить();
		НоваяСтрока.ЗначениеХарактеристики = Выборка.Ссылка;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписьХарактеристикиПерсонала" И Характеристика = Источник Тогда
		Элементы.ВесаЗначений.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти