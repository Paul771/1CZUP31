
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	РезультатПроверки = Параметры.РезультатПроверки;
	
	Для Каждого ДанныеСотрудника Из РезультатПроверки.ДанныеСотрудников Цикл 
		
		ТекстСообщения = ?(ДанныеСотрудника.Значение.Состояние = ПредопределенноеЗначение("Перечисление.СостоянияСотрудника.Увольнение"),
			НСтр("ru = 'Сотрудник %1 был уволен %2 (%3).'"),
			НСтр("ru = 'На период %2 сотруднику %1 уже зарегистрировано отсутствие документом %3.'"));
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ДанныеСотрудника.Ключ, ДанныеСотрудника.Значение.ПредставлениеПериода, ДанныеСотрудника.Значение.Регистратор);
		
		НоваяСтрока = Сообщения.Добавить();
		НоваяСтрока.Сообщение = ТекстСообщения;
		НоваяСтрока.Ссылка = ДанныеСотрудника.Значение.Регистратор;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСообщения

&НаКлиенте
Процедура СообщенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Сообщения.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(, ТекущиеДанные.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Закрыть(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

