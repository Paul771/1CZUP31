
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("СписокПредметов") Тогда
		ВызватьИсключение НСтр("ru = 'Не заполнен список физических лиц, для которых необходимо установить напоминание.'");
		Возврат;
	КонецЕсли; 
	
	ПриПолученииДанныхНаСервере(Параметры.СписокПредметов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НапомнитьОДнеРожденияПриИзменении(Элемент)
	УстановитьДоступностьКоличествоДней(ЭтаФорма);
	УстановитьВидимостьДекорацияКолокольчик(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура КоличествоДнейПриИзменении(Элемент)
	УстановитьЗаголовокКоличествоДней(ЭтаФорма);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	СоздатьУдалитьНапоминания();
	ПоказатьОповещениеОбОшибочных();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриПолученииДанныхНаСервере(СписокПредметов)

	ЗаполнитьПредметыНапоминаний(СписокПредметов);
	
	Если ПредметыНапоминаний.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Нет сотрудников с заполнеными датами рождения - напоминания не могут быть установлены.'");
	КонецЕсли;
	
	УстановитьРеквизитыНапоминанияОДнеРождения();
	
	УстановитьСвойстваЭлементовФормы();

КонецПроцедуры

&НаСервере
Процедура УстановитьРеквизитыНапоминанияОДнеРождения()
	
	Если ПредметыНапоминаний.Количество() = 0 Тогда
		НапомнитьОДнеРождения = Истина;
		Возврат;
	КонецЕсли;
	
	ФизическоеЛицо = ПредметыНапоминаний[0].ФизическоеЛицо;
	МассивКлючейРегистра = ПолучитьНапоминанияНаСервере(ФизическоеЛицо);
	
	НапомнитьОДнеРождения = (МассивКлючейРегистра.Количество() > 0);
	УстановитьКоличествоДнейНапоминания(?(МассивКлючейРегистра.Количество() > 0, МассивКлючейРегистра[0], Неопределено));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовФормы()
	
	УстановитьНадписьШапки();
	
	УстановитьЗаголовокКоличествоДней(ЭтаФорма);
	
	УстановитьВидимостьДекорацияКолокольчик(ЭтаФорма);
	УстановитьДоступностьКоличествоДней(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьДекорацияКолокольчик(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ДекорацияКолокольчикВкл",
		"Видимость",
		Форма.НапомнитьОДнеРождения);

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ДекорацияКолокольчикВыкл",
		"Видимость",
		Не Форма.НапомнитьОДнеРождения);
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьКоличествоДней(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"КоличествоДней",
		"Доступность",
		Форма.НапомнитьОДнеРождения);

КонецПроцедуры

&НаСервере
Процедура УстановитьНадписьШапки()

	Если ПредметыНапоминаний.Количество() = 0 Тогда
		Возврат;
	ИначеЕсли ПредметыНапоминаний.Количество() = 1 Тогда
		НадписьШапка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'У %1 день рождения %2.'"),
				ФизическиеЛицаЗарплатаКадрыКлиентСервер.ФамилияИнициалы(ПросклонятьФИОНаСервере(ПредметыНапоминаний[0].ФизическоеЛицо, 2, ПредметыНапоминаний[0].Пол)),
				Формат(ПредметыНапоминаний[0].ДатаРождения, "ДФ = ""д ММММ"""));
	Иначе
		НадписьШапка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'У %1 дни рождения.'"),
			СтрокаФизическихЛицИДнейРождений());
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокКоличествоДней(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"КоличествоДней",
		"Заголовок",
		СтроковыеФункцииКлиентСервер.ФормаМножественногоЧисла("день", "дня", "дней", Форма.КоличествоДней));

КонецПроцедуры

&НаКлиенте
Процедура СоздатьУдалитьНапоминания()
	
	// В любом случае очищаем уже сделанные напоминания.
	УдалитьНапоминания();
	
	Если НапомнитьОДнеРождения Тогда
		СоздатьНапоминания();
		СохранитьНастройкуНаСервере();
	КонецЕсли;
	
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.НапоминанияПользователя"));
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНапоминания()

	Для каждого ПредметНапоминания Из ПредметыНапоминаний Цикл
		НапоминанияПользователяКлиент.НапомнитьОЕжегодномСобытииПредмета(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 у %2 день рождения.'"),
				Формат(ПредметНапоминания.ДатаРождения, "ДФ = ""д ММММ"""),
				ПросклонятьФИОНаСервере(ПредметНапоминания.ФизическоеЛицо, 4, ПредметНапоминания.Пол)),
			КоличествоДней*24*60*60,
			ПредметНапоминания.ФизическоеЛицо,
			"ДатаРождения");
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьНапоминания()

	МассивКлючейРегистра = ПолучитьНапоминанияНаСервере();
	УдалитьНапоминанияНаСервере(МассивКлючейРегистра);
	
	Для каждого КлючЗаписиРегистра Из МассивКлючейРегистра Цикл
		НапоминанияПользователяКлиент.УдалитьЗаписьИзКэшаОповещений(КлючЗаписиРегистра);
		Оповестить("Запись_НапоминанияПользователя", Новый Структура, КлючЗаписиРегистра);
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОповещениеОбОшибочных()
	
	Если ОшибочныеПредметыНапоминаний.Количество() = 0 Тогда
		Возврат;
	ИначеЕсли ОшибочныеПредметыНапоминаний.Количество() = 1 Тогда
		ТекстОповещения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось создать напоминание для %1'"),
			ПросклонятьФИОНаСервере(ОшибочныеПредметыНапоминаний[0].ФизическоеЛицо, 2, ОшибочныеПредметыНапоминаний[0].Пол));
	Иначе
		ТекстОповещения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось создать напоминания для %1-х сотрудников'"),
			ОшибочныеПредметыНапоминаний.Количество());
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Не заполнена дата рождения.'"),, ТекстОповещения,БиблиотекаКартинок.Ошибка32);

КонецПроцедуры

&НаСервере
Функция ПолучитьНапоминанияНаСервере(ФизическоеЛицо = Неопределено)

	Если ФизическоеЛицо = Неопределено Тогда
		МассивПредметовНапоминаний = ПредметыНапоминаний.Выгрузить(,"ФизическоеЛицо").ВыгрузитьКолонку("ФизическоеЛицо");
	Иначе
		МассивПредметовНапоминаний = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо);
	КонецЕсли;
	
	Возврат КадровыйУчетРасширенный.НапоминанияОДняхРождения(МассивПредметовНапоминаний);
	
КонецФункции

&НаСервереБезКонтекста
Процедура УдалитьНапоминанияНаСервере(МассивКлючейРегистра)
	
	Для каждого КлючРегистраНапоминаний Из МассивКлючейРегистра Цикл
		НапоминанияПользователяСлужебный.ОтключитьНапоминание(КлючРегистраНапоминаний, Ложь);
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКоличествоДнейНапоминания(КлючРегистра)

	КоличествоДнейНапоминания = Неопределено;
	
	Если КлючРегистра <> Неопределено Тогда
		КоличествоДнейНапоминания = КоличествоДнейНапоминания(КлючРегистра);
	КонецЕсли;
	
	Если КоличествоДнейНапоминания = Неопределено Тогда
		КоличествоДней = ПоследнееУстановленноеКоличествоДней();
	Иначе
		КоличествоДней = КоличествоДнейНапоминания;
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция КоличествоДнейНапоминания(КлючРегистра)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НапоминанияПользователя.ИнтервалВремениНапоминания КАК ИнтервалВремениНапоминания
		|ИЗ
		|	РегистрСведений.НапоминанияПользователя КАК НапоминанияПользователя
		|ГДЕ
		|	НапоминанияПользователя.Пользователь = &Пользователь
		|	И НапоминанияПользователя.ВремяСобытия = &ВремяСобытия
		|	И НапоминанияПользователя.Источник = &Источник";
	
	Запрос.УстановитьПараметр("ВремяСобытия", КлючРегистра.ВремяСобытия);
	Запрос.УстановитьПараметр("Источник", КлючРегистра.Источник);
	Запрос.УстановитьПараметр("Пользователь", КлючРегистра.Пользователь);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.ИнтервалВремениНапоминания/86400;

КонецФункции

&НаСервереБезКонтекста
Функция ПоследнееУстановленноеКоличествоДней()
	Возврат ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкиПользователя", "ПоследнееУстановленноеКоличествоДней", 5);
КонецФункции

&НаСервере
Процедура ЗаполнитьПредметыНапоминаний(АдресПредметовНапоминаний)

	СписокПредметов = ПолучитьИзВременногоХранилища(АдресПредметовНапоминаний);
	Если СписокПредметов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(СписокПредметов[0]) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		Для каждого ФизичискоеЛицо Из СписокПредметов Цикл
			ПредметНапоминания = ПредметыНапоминаний.Добавить();
			ПредметНапоминания.ФизичискоеЛицо = ФизичискоеЛицо;
		КонецЦикла; 
	ИначеЕсли ТипЗнч(СписокПредметов[0]) = Тип("СправочникСсылка.Сотрудники") Тогда
		ЗаполнитьПредметыНапоминанийИзСотрудников(СписокПредметов);
	КонецЕсли;
	
	ЗаполнитьРеквизитыФизическизхЛиц(ПредметыНапоминаний.Выгрузить(, "ФизическоеЛицо").ВыгрузитьКолонку("ФизическоеЛицо"));
	
	ОтделитьОшибочноЗаполенныеПредметы();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПредметыНапоминанийИзСотрудников(СписокСотрудников)

	ФизическиеЛица = Новый Массив;
	
	ФизическиеЛицаСотрудников = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СписокСотрудников, "ФизическоеЛицо");
	Для каждого ФизическоеЛицо Из ФизическиеЛицаСотрудников Цикл
		ФизическиеЛица.Добавить(ФизическоеЛицо.Значение);
	КонецЦикла; 
	
	СписокПредметов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ФизическиеЛица);
	Для каждого ФизичискоеЛицо Из СписокПредметов Цикл
		ПредметНапоминания = ПредметыНапоминаний.Добавить();
		ПредметНапоминания.ФизическоеЛицо = ФизичискоеЛицо;
	КонецЦикла; 

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыФизическизхЛиц(МассивФизическихЛиц)

	РеквизитыФизическихЛиц = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивФизическихЛиц, "ДатаРождения, Пол");
	Для каждого ПредметНапоминания Из ПредметыНапоминаний Цикл
		ЗаполнитьЗначенияСвойств(ПредметНапоминания, РеквизитыФизическихЛиц[ПредметНапоминания.ФизическоеЛицо]);
	КонецЦикла; 

КонецПроцедуры

&НаСервере
Процедура ОтделитьОшибочноЗаполенныеПредметы()

	МассивОшибочныхСтрок = Новый Массив;
	
	Для каждого ПредметНапоминания Из ПредметыНапоминаний Цикл
		Если Не ЗначениеЗаполнено(ПредметНапоминания.ДатаРождения) Тогда
			МассивОшибочныхСтрок.Добавить(ПредметНапоминания);
		КонецЕсли;
	КонецЦикла; 
	
	Для каждого ОшибочнаяСтрока Из МассивОшибочныхСтрок Цикл
		ПредметыНапоминаний.Удалить(ОшибочнаяСтрока);
	    ЗаполнитьЗначенияСвойств(ОшибочныеПредметыНапоминаний.Добавить(), ОшибочнаяСтрока);
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкуНаСервере()
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("НастройкиПользователя", "ПоследнееУстановленноеКоличествоДней", КоличествоДней);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПросклонятьФИОНаСервере(ПредметНапоминания, Падеж, Пол)
	Возврат СклонениеПредставленийОбъектов.ПросклонятьФИО(ПредметНапоминания, Падеж,, ?(Пол = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.Женский"), 2, 1));
КонецФункции

&НаСервере
Функция СтрокаФизическихЛицИДнейРождений()

	ИтоговаяСтрока = "";
	
	КоличествоВыводимыхСотрудников = ?(ПредметыНапоминаний.Количество() > 3, 2, ПредметыНапоминаний.Количество());
	Для Сч = 0 По КоличествоВыводимыхСотрудников - 1 Цикл
		Если Сч = 1 ИЛИ (Сч = 2 И КоличествоВыводимыхСотрудников = 3) Тогда
			ИтоговаяСтрока = ИтоговаяСтрока + ", ";
		КонецЕсли;
		ИтоговаяСтрока = ИтоговаяСтрока + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 (%2)'"),
			ФизическиеЛицаЗарплатаКадрыКлиентСервер.ФамилияИнициалы(ПросклонятьФИОНаСервере(ПредметыНапоминаний[Сч].ФизическоеЛицо, 2, ПредметыНапоминаний[Сч].Пол)),
			Формат(ПредметыНапоминаний[Сч].ДатаРождения, "ДФ = ""д ММММ"""));
	КонецЦикла; 
	Если ПредметыНапоминаний.Количество() > 3 Тогда
		ИтоговаяСтрока = ИтоговаяСтрока + " " + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'и еще у %1 сотрудников'"), ПредметыНапоминаний.Количество() - 2);
	КонецЕсли;	

	Возврат ИтоговаяСтрока;
	
КонецФункции

#КонецОбласти