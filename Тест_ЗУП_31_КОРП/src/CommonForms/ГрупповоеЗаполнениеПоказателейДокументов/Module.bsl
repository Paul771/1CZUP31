
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	РасширенноеРедактирование = Параметры.РасширенноеРедактирование;
	МассивНачислений = Неопределено;
	Параметры.Свойство("МассивНачислений", МассивНачислений);
	Если МассивНачислений <> Неопределено Тогда 
		ДополнитьФормуНачислениями(МассивНачислений);
		ЗаполнитьНачисления(МассивНачислений);
		УстановитьДоступностьЗначенийНачислений(ЭтотОбъект);
	КонецЕсли;
	
	МассивПоказателей = Параметры.МассивПоказателей;
	Если МассивПоказателей.Количество() > 0 Или МассивНачислений = Неопределено Тогда
		ДополнитьФорму(МассивПоказателей);
		ЗаполнитьПоказатели(МассивПоказателей);
	КонецЕсли;
	
	КлючСохраненияПоложенияОкна = Строка(КоличествоПоказателей + КоличествоНачислений);
	
	УстановитьДоступностьЗначенийПоказателей(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Подключаемый_ИспользованиеПоказателяПриИзменении(Элемент)
	
	УстановитьДоступностьЗначенийПоказателей(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ИспользованиеНачисленияПриИзменении(Элемент)
	
	УстановитьДоступностьЗначенийНачислений(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПоказательДействие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НачислениеДействие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	НачисленияПоказатели = Новый Структура;
	
	ЗначенияПоказателей = Новый Соответствие;
	Для Сч = 1 По КоличествоПоказателей Цикл
		Если ЭтотОбъект["Показатель" + Сч + "Использование"] Тогда 
			Если РасширенноеРедактирование Тогда
				ДействиеСПоказателем = Новый Структура;
				ДействиеСПоказателем.Вставить("Действие", ЭтотОбъект["Показатель" + Сч + "Действие"]);
				ДействиеСПоказателем.Вставить("Значение", ЭтотОбъект["Показатель" + Сч + "Значение"]);
				ЗначенияПоказателей.Вставить(ЭтотОбъект["Показатель" + Сч], ДействиеСПоказателем);
			Иначе
				ЗначенияПоказателей.Вставить(ЭтотОбъект["Показатель" + Сч], ЭтотОбъект["Показатель" + Сч + "Значение"]);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	НачисленияПоказатели.Вставить("Показатели", ЗначенияПоказателей);
	
	РазмерНачислений = Новый Соответствие;
	Для Сч = 1 По КоличествоНачислений Цикл
		Если ЭтотОбъект["Начисление" + Сч + "Использование"] Тогда 
			Если РасширенноеРедактирование Тогда
				ДействиеСНачислением = Новый Структура;
				ДействиеСНачислением.Вставить("Действие", ЭтотОбъект["Начисление" + Сч + "Действие"]);
				ДействиеСНачислением.Вставить("Значение", ЭтотОбъект["Начисление" + Сч + "Значение"]);
				РазмерНачислений.Вставить(ЭтотОбъект["Начисление" + Сч], ДействиеСНачислением);
			Иначе
				РазмерНачислений.Вставить(ЭтотОбъект["Начисление" + Сч], ЭтотОбъект["Начисление" + Сч + "Значение"]);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	НачисленияПоказатели.Вставить("Начисления", РазмерНачислений);
	
	Оповестить("ИзмененыПоказателиДокумента", НачисленияПоказатели, ЭтотОбъект);
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДополнитьФорму(МассивПоказателей)

	КоличествоПоказателей = МассивПоказателей.Количество();
	ФиксированнаяСумма = КоличествоПоказателей = 0;
	
	РеквизитыПоказателей = Новый Соответствие;
	Если КоличествоПоказателей <> 0 Тогда 
		РеквизитыПоказателей = ЗарплатаКадрыРасширенный.СведенияОПоказателяхРасчетаЗарплаты(МассивПоказателей);
	КонецЕсли;
	
	КоличествоПоказателей = ?(ФиксированнаяСумма, 1, КоличествоПоказателей);
	
	МассивИменРеквизитовФормы = Новый Массив;
	ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(ЭтотОбъект, МассивИменРеквизитовФормы);
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	Для Сч = 1 По КоличествоПоказателей Цикл
		
		ПоказательРеквизит = Новый РеквизитФормы(
			"Показатель" + Сч, Новый ОписаниеТипов("СправочникСсылка.ПоказателиРасчетаЗарплаты"));
		ДобавляемыеРеквизиты.Добавить(ПоказательРеквизит);
		
		НаименованиеПоказателя = ?(ФиксированнаяСумма, 
			НСтр("ru = 'Сумма'"), ?(ЗначениеЗаполнено(РеквизитыПоказателей[МассивПоказателей[Сч-1]].КраткоеНаименование), 
			РеквизитыПоказателей[МассивПоказателей[Сч-1]].КраткоеНаименование, РеквизитыПоказателей[МассивПоказателей[Сч-1]].Наименование));
		
		ИспользованиеПоказателяРеквизит = Новый РеквизитФормы(
			"Показатель" + Сч + "Использование", Новый ОписаниеТипов("Булево"), , НаименованиеПоказателя);
		ДобавляемыеРеквизиты.Добавить(ИспользованиеПоказателяРеквизит);
		
		Если РасширенноеРедактирование Тогда
			ДействиеРеквизит = Новый РеквизитФормы("Показатель" + Сч + "Действие",
				Новый ОписаниеТипов("Строка"));
			ДобавляемыеРеквизиты.Добавить(ДействиеРеквизит);
		КонецЕсли;
			
		ЗначениеРеквизит = Новый РеквизитФормы("Показатель" + Сч + "Значение",
			Справочники.ПоказателиРасчетаЗарплаты.ОписаниеТиповЗначенияПоказателяРасчетаЗарплаты());
		ДобавляемыеРеквизиты.Добавить(ЗначениеРеквизит);
		
	КонецЦикла;
	
	ЗарплатаКадры.ИзменитьРеквизитыФормы(ЭтотОбъект, ДобавляемыеРеквизиты, МассивИменРеквизитовФормы);
	
	ТипЧисло = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2));
	
	Для Сч = 1 По КоличествоПоказателей Цикл
		
		Если Элементы.Найти("Показатель" + Сч + "Использование") = Неопределено Тогда 
			
			Использование = Элементы.Добавить("Показатель" + Сч + "Использование", Тип("ПолеФормы"), Элементы.ПоказателиЛеваяКолонка);
			Использование.Вид = ВидПоляФормы.ПолеФлажка;
			Использование.ПутьКДанным = "Показатель" + Сч + "Использование";
			Использование.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
			Использование.УстановитьДействие("ПриИзменении", "Подключаемый_ИспользованиеПоказателяПриИзменении");
			
			Если РасширенноеРедактирование Тогда
				Действие = Элементы.Добавить("Показатель" + Сч + "Действие", Тип("ПолеФормы"), Элементы.ПоказателиСредняяКолонка);
				Действие.Вид = ВидПоляФормы.ПолеВвода;
				Действие.ПутьКДанным = "Показатель" + Сч + "Действие";
				Действие.РастягиватьПоГоризонтали = Ложь;
				Действие.Ширина = 16;
				Действие.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
				Действие.РежимВыбораИзСписка = Истина;
				Действие.РедактированиеТекста = Ложь;
				ЗаполнитьСписокДействийСПоказателями(Действие.СписокВыбора);
				ЭтаФорма["Показатель" + Сч + "Действие"] = Действие.СписокВыбора[0].Значение;
				Действие.УстановитьДействие("Очистка", "Подключаемый_ПоказательДействие");
			КонецЕсли;
			
			Значение = Элементы.Добавить("Показатель" + Сч + "Значение", Тип("ПолеФормы"), Элементы.ПоказателиПраваяКолонка);
			Значение.Вид = ВидПоляФормы.ПолеВвода;
			Значение.ПутьКДанным = "Показатель" + Сч + "Значение";
			Значение.РастягиватьПоГоризонтали = Ложь;
			Значение.Ширина = 16;
			Значение.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
			
			ТипПоказателя = ?(ФиксированнаяСумма, ТипЧисло, РеквизитыПоказателей[МассивПоказателей[Сч-1]].ТипПоказателяПриРасчете);
			Если РасширенноеРедактирование Тогда
				ТипПоказателя = Новый ОписаниеТипов(ТипПоказателя, "Число",,Новый КвалификаторыЧисла(15, 4));
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы, "Показатель" + Сч + "Значение", "ОграничениеТипа", ТипПоказателя);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоказатели(МассивПоказателей)
	
	Сч = 1;
	Для Каждого Показатель Из МассивПоказателей Цикл 
		ЭтотОбъект["Показатель" + Сч] = Показатель;
		Сч = Сч + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьЗначенийПоказателей(Форма)
	
	Для Сч = 1 По Форма.КоличествоПоказателей Цикл
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, 
			"Показатель" + Сч + "Значение", "Доступность", Форма["Показатель" + Сч + "Использование"]);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, 
			"Показатель" + Сч + "Действие", "Доступность", Форма["Показатель" + Сч + "Использование"]);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьФормуНачислениями(МассивНачислений)

	КоличествоНачислений = МассивНачислений.Количество();
	
	Если КоличествоНачислений = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	РеквизитыНачислений = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивНачислений, "Наименование, КраткоеНаименование");
	
	МассивИменРеквизитовФормы = Новый Массив;
	ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(ЭтотОбъект, МассивИменРеквизитовФормы);
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	Для Сч = 1 По КоличествоНачислений Цикл
		
		НачислениеРеквизит = Новый РеквизитФормы("Начисление" + Сч, Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления"));
		ДобавляемыеРеквизиты.Добавить(НачислениеРеквизит);
		
		НаименованиеНачисления = ?(ЗначениеЗаполнено(РеквизитыНачислений[МассивНачислений[Сч-1]].КраткоеНаименование), 
			РеквизитыНачислений[МассивНачислений[Сч-1]].КраткоеНаименование, РеквизитыНачислений[МассивНачислений[Сч-1]].Наименование);
		
		ИспользованиеНачисленияРеквизит = Новый РеквизитФормы(
			"Начисление" + Сч + "Использование", Новый ОписаниеТипов("Булево"), , НаименованиеНачисления);
		ДобавляемыеРеквизиты.Добавить(ИспользованиеНачисленияРеквизит);
		
		Если РасширенноеРедактирование Тогда
			ДействиеРеквизит = Новый РеквизитФормы("Начисление" + Сч + "Действие", Новый ОписаниеТипов("Строка"));
			ДобавляемыеРеквизиты.Добавить(ДействиеРеквизит);
		КонецЕсли;
		
		ЗначениеРеквизит = Новый РеквизитФормы("Начисление" + Сч + "Значение", Новый ОписаниеТипов("Число"));
		ДобавляемыеРеквизиты.Добавить(ЗначениеРеквизит);
		
	КонецЦикла;
	
	ЗарплатаКадры.ИзменитьРеквизитыФормы(ЭтотОбъект, ДобавляемыеРеквизиты, МассивИменРеквизитовФормы);
	
	ТипЧисло = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2));
	
	Для Сч = 1 По КоличествоНачислений Цикл
		
		Если Элементы.Найти("Начисление" + Сч + "Использование") = Неопределено Тогда 
			
			Использование = Элементы.Добавить("Начисление" + Сч + "Использование", Тип("ПолеФормы"), Элементы.ПоказателиЛеваяКолонка);
			Использование.Вид = ВидПоляФормы.ПолеФлажка;
			Использование.ПутьКДанным = "Начисление" + Сч + "Использование";
			Использование.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
			Использование.УстановитьДействие("ПриИзменении", "Подключаемый_ИспользованиеНачисленияПриИзменении");
			
			Если РасширенноеРедактирование Тогда
				Действие = Элементы.Добавить("Начисление" + Сч + "Действие", Тип("ПолеФормы"), Элементы.ПоказателиСредняяКолонка);
				Действие.Вид = ВидПоляФормы.ПолеВвода;
				Действие.ПутьКДанным = "Начисление" + Сч + "Действие";
				Действие.РастягиватьПоГоризонтали = Ложь;
				Действие.Ширина = 16;
				Действие.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
				Действие.РежимВыбораИзСписка = Истина;
				Действие.РедактированиеТекста = Ложь;
				ЗаполнитьСписокДействийСПоказателями(Действие.СписокВыбора);
				ЭтаФорма["Начисление" + Сч + "Действие"] = Действие.СписокВыбора[0].Значение;
				Действие.УстановитьДействие("Очистка", "Подключаемый_НачислениеДействие");
			КонецЕсли;
			
			Значение = Элементы.Добавить("Начисление" + Сч + "Значение", Тип("ПолеФормы"), Элементы.ПоказателиПраваяКолонка);
			Значение.Вид = ВидПоляФормы.ПолеВвода;
			Значение.ПутьКДанным = "Начисление" + Сч + "Значение";
			Значение.РастягиватьПоГоризонтали = Ложь;
			Значение.Ширина = 16;
			Значение.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
			
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы, "Начисление" + Сч + "Значение", "ОграничениеТипа", ТипЧисло);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНачисления(МассивНачислений)
	
	Сч = 1;
	Для Каждого Начисление Из МассивНачислений Цикл 
		ЭтотОбъект["Начисление" + Сч] = Начисление;
		Сч = Сч + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьЗначенийНачислений(Форма)
	
	Для Сч = 1 По Форма.КоличествоНачислений Цикл
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, 
			"Начисление" + Сч + "Значение", "Доступность", Форма["Начисление" + Сч + "Использование"]);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, 
			"Начисление" + Сч + "Действие", "Доступность", Форма["Начисление" + Сч + "Использование"]);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокДействийСПоказателями(СписокДействий)
	
	СписокДействий.Добавить("ФиксЗначение", "Фикс. значение");
	СписокДействий.Добавить("Прибавить", "Прибавить");
	СписокДействий.Добавить("Умножить", "Умножить на");
	
КонецПроцедуры

#КонецОбласти
